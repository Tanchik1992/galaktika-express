!-------------------------------------------------------------------------------
!                                                       (c) корпорация Галактика
!  Галактика 7.12 - Макроопределения для вывода в поток ответственных лиц,
!  на базе объекта комиссий.
!-------------------------------------------------------------------------------
#ifndef __ResponsibleForm__
#define __ResponsibleForm__

! Макроопределение для вставки в секции .Fields базовой формы
#declare ResponsibleFields

  Ответственные_лица                     : boolean     // True, если выборка выбрана.
  cSelectionResponsibleNRec              : comp        // Ссылка на выбранную комиссию
  Имя_выборки_ответственных_лиц                        // имя выбранной комиссии

  // * Списки в формате установленном пользователем:
  Список_ответственных_лиц             // Вывод списка ответственных лиц, одной строкой, в формате установленном пользователем

  // * Списки ответственных лиц, одной строкой, с жёстко установленной структурой:
  ФИО_ответственных_лиц                                 // вывод только ФИО ответственных лиц
  Должность_и_ФИО_ответственных_лиц                     // вывод Должности и ФИО ответственных лиц, в такой последовательности
  ФИО_и_Должность_ответственных_лиц                     // вывод ФИО и Должности ответственных лиц, в такой последовательности

  // * Ответственные лица:
  Количество_ответственных_лиц           : word

  // * Цикл по ответственным лицам:
  Ответственное_лицо_nRec                : comp
  Ответственное_лицо_ФИО
  Ответственное_лицо_Табельный_Номер     : LongInt     // tTabN
  Ответственное_лицо_Должность
#end // ResponsibleFields

! Макроопределение для вставки в секции вывода данных базовой формы
#declare ResponsibleBody(FormName)

  Ответственные_лица                     - ^
  cSelectionResponsibleNRec              - ^
  Имя_выборки_ответственных_лиц          - ^

  *** Списки в формате установленном пользователем: ***
  Список_ответственных_лиц               - ^

  *** Списки ответственных лиц, одной строкой, с жёстко установленной структурой: ***
  ФИО_ответственных_лиц                  - ^
  Должность_и_ФИО_ответственных_лиц      - ^
  ФИО_и_Должность_ответственных_лиц      - ^

  *** Ответственные лица: ***
  Количество_ответственных_лиц           - ^

  { ***** цикл по ответственным лицам Responsible_#FormName *****
.{ CheckEnter Responsible_#FormName
    { -------------------
      Ответственное_лицо_nRec            - ^
      Ответственное_лицо_ФИО             - ^
      Ответственное_лицо_Табельный_Номер - ^
      Ответственное_лицо_Должность       - ^
    } -------------------
.}
  } ***** цикл по ответственным лицам *****
#end // ResponsibleBody

! Макроопределение для вставки в секции .var присоединенной формы
#declare ResponsibleLinkVar
.var
  // *** Объявление вспомогательных переменных ***
  Номер_ответственного_лица : word;

  // *** Объявление массивов *****
  Массив_Ответственное_лицо_nRec            : array[1..2] of comp;
  Массив_Ответственное_лицо_ФИО             : array[1..2] of string;
  Массив_Ответственное_лицо_Табельный_Номер : array[1..2] of string; // tTabN; LongInt;
  Массив_Ответственное_лицо_Должность       : array[1..2] of string;
.endvar
#end // ResponsibleBody

! Макроопределение для вставки в секции вывода данных присоединенной формы
#declare ResponsibleLinkInit(FormName)
.begin
  // *** Обнуление переменной ***
  Номер_ответственного_лица := 0;

  // *** Обнуление массивов по таблице ***
  for (Номер_ответственного_лица := 1; Номер_ответственного_лица <= Count(Массив_Ответственное_лицо_nRec); Номер_ответственного_лица := Номер_ответственного_лица + 1)
  {
    Массив_Ответственное_лицо_nRec            [Номер_ответственного_лица] := comp(0);
    Массив_Ответственное_лицо_ФИО             [Номер_ответственного_лица] := '';
    Массив_Ответственное_лицо_Табельный_Номер [Номер_ответственного_лица] := ''; // LongInt(0);
    Массив_Ответственное_лицо_Должность       [Номер_ответственного_лица] := '';
  }

  Номер_ответственного_лица := 0;
end.
.{ CheckEnter Responsible_#FormName
.begin
  Номер_ответственного_лица := Номер_ответственного_лица + 1;
  Массив_Ответственное_лицо_nRec            [Номер_ответственного_лица] := Ответственное_лицо_nRec;
  Массив_Ответственное_лицо_ФИО             [Номер_ответственного_лица] := Ответственное_лицо_ФИО;
  Массив_Ответственное_лицо_Табельный_Номер [Номер_ответственного_лица] := string(Ответственное_лицо_Табельный_Номер);
  Массив_Ответственное_лицо_Должность       [Номер_ответственного_лица] := Ответственное_лицо_Должность;
end.
.}
.begin
  Номер_ответственного_лица := 0;
end.
#end // ResponsibleLinkInit
#end // __ResponsibleForm__
