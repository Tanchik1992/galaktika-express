#include Commission.frn
.linkform 'GroupOs_02_DM_Bel' prototype is 'GroupOs'
.NameInList 'Форма N ОС-2дм (от 29.12.04) << Беларусь >>'
.group 'Внутреннее перемещение ОС <<Беларусь>>'
.hide
.defo portrait
.p 86
.var
  Массив_ИК_Дата_выпуска              : array[1..10] of date;   // Дата, выпуска(год)
  Массив_ИК_Дата_принятия             : array[1..10] of date;   // Дата, выпуска(год)
  Массив_СтрокаХарактеристики         : array[1..2] of string;  // Краткая индивидуальная характеристика объекта
  Массив_ИК_КоличествоХарактеристик   : array[1..10] of word;   //   Количество строк Характеристик в ИК
  Массив_ИК_КоличествоДрагМеталлов    : array[1..10] of word;   //   Количество Драг Металлов в ИК
  Массив_ИК_Количество                : array[1..10] of double; // Количество в конкретной ИК
  Характеристика_Наименование_Объекта : array[1..10] of string;
  Характеристика_Количество_Объектов  : array[1..10] of double;
  Характеристика_ИнвенНомер_Объектов  : array[1..10] of string;
  Характеристика_СтоимостьЕдиницы_Об  : array[1..10] of string;
  Характеристика_СтоимостьВсех_Объектов: array[1..10] of string;
  Характеристика_NameDrag             : array[1..10] of string;
  Характеристика_N_Edin               : array[1..10] of string;
  Характеристика_MassaDrag            : array[1..10] of string;
  Характеристика_Кол_во_Составляющих  : array[1..10] of string;
  // ***** Переменные проверки отличий общих полей *****
  Flag : boolean;
  Flag_Инвентарный_Номер                    : boolean;
  Flag_Структурное_подразделение_Поставщика : boolean;
  Flag_Структурное_подразделение_Получателя : boolean;
  Flag_Сдал : boolean;
  Flag_Принял : boolean;

  // ***** Переменные общих полей *****
  // Выводим, только если у всех одинаковое значение
  Итог_Инвентарный_Номер                      : string;
  Итог_Структурное_подразделение_Поставщика   : string;
  Итог_Структурное_подразделение_Получателя   : string;
  Итог_Сдал                                   : string;
  Итог_Сдал_Табельный_номер                   : string;
  Итог_Принял                                 : string;
  Итог_Принял_Табельный_номер                 : string;

  КолНомер_в_ИК                               : word;
  Maxi                                        : word;
  НомерПоПорядку                              : word;                  // Текущий номер элемента в цикле состава
  НомерХарактеристики                         : word;
  НомерДрагМеталла                            : word;                  // Текущий номер элемента в общем цикле драгметаллов

  i           : word;
  НомерИК     : word; // Текущий номер в цикле состава
  Хар2        : word; // Текущий номер в цикле драгметаллов
  НомерИК_Max : word; // Количество элементов в массиве данных характеристики
.endvar
#CommissionLinkVar
#include funcOs.frn
end.  // funcOs.frn - вынес из файла чтобы не создавалась пустая строка перед формой
.{
.begin
  for (i := 1; i <= Count(Характеристика_Наименование_Объекта); i := i + 1)
  {
    Массив_ИК_Дата_выпуска              [i] := date (0,0,0); // 06 - Дата, выпуска(год)
    Массив_ИК_Дата_принятия             [i] := date (0,0,0);
    Массив_ИК_КоличествоХарактеристик   [i] := 0;            //      Количество строк Характеристик в ИК
    Массив_ИК_КоличествоДрагМеталлов    [i] := 0;            //      Количество Драг Металлов в ИК
    Массив_ИК_Количество                [i] := 0;            // Количество в конкретной ИК
    Характеристика_Наименование_Объекта [i] := ' ';
    Характеристика_Количество_Объектов  [i] := 0.0;
  }

  for (i := 1; i <= Count(Массив_СтрокаХарактеристики); i := i + 1)
  {
    Массив_СтрокаХарактеристики [i] := '';
  }

  for (Хар2 := 1; Хар2 <= Count(Характеристика_NameDrag); Хар2 := Хар2 + 1)
  {
    Характеристика_NameDrag  [Хар2] := '';
    Характеристика_N_Edin    [Хар2] := '';
    Характеристика_MassaDrag [Хар2] := '';
  }
  // ***** Обнуление переменных проверки отличий общих полей *****
  Flag                                        := true;
  Flag_Инвентарный_Номер                      := true;
  Flag_Структурное_подразделение_Поставщика   := true;
  Flag_Структурное_подразделение_Получателя   := true;
  Flag_Сдал                                   := true;
  Flag_Принял                                 := true;
  Итог_Инвентарный_Номер                      := '';
  Итог_Структурное_подразделение_Поставщика   := '';
  Итог_Структурное_подразделение_Получателя   := '';
  Итог_Сдал                                   := '';
  Итог_Сдал_Табельный_номер                   := '';
  Итог_Принял                                 := '';
  Итог_Принял_Табельный_номер                 := '';

  НомерХарактеристики := 0;
  НомерДрагМеталла    := 0;

  i := 0;
  НомерПоПорядку := 0;
  НомерИК := 0;
  Хар2 := 0;
  НомерИК_Max := 0;

end.
! ***** Заполнение массивов *****
#CommissionLinkInit(GroupOs)
.{ CheckEnter CycleByGroupIK
.begin
  НомерИК := НомерИК + 1;
  НомерИК_Max := НомерИК_Max + 1;

  Массив_ИК_Дата_выпуска               [НомерИК] := Дата_выпуска;                  // Дата, выпуска(год)
  Массив_ИК_Дата_принятия              [НомерИК] := Дата_принятия_к_бухгалтерскому_учету;
  Массив_ИК_Количество                 [НомерИК] := Количество;                    // Количество в конкретной ИК
  Характеристика_Наименование_Объекта  [НомерИК] := string(Наименование);
  Характеристика_Количество_Объектов   [НомерИК] := Количество;
  Характеристика_ИнвенНомер_Объектов   [НомерИК] := string(Инвентарный_Номер);
  Характеристика_СтоимостьЕдиницы_Об   [НомерИК] := string(Стоимость_за_единицу);
  Характеристика_СтоимостьВсех_Объектов[НомерИК] := string(Стоимость_всего);

  if (НомерИК = 1)
  {
    Итог_Инвентарный_Номер                    := Инвентарный_Номер;
    Итог_Структурное_подразделение_Поставщика := Структурное_подразделение_Поставщика;
    Итог_Структурное_подразделение_Получателя := Структурное_подразделение_Получателя;

    Итог_Сдал                                 := Сдал;
    Итог_Сдал_Табельный_номер                 := Сдал_Табельный_номер;

    Итог_Принял                               := Принял;
    Итог_Принял_Табельный_номер               := Принял_Табельный_номер;
  }
  else
   if (НомерИК > 1)
   {
    if (Итог_Инвентарный_Номер <> Инвентарный_Номер)
    {
      Flag := false;
      Итог_Инвентарный_Номер := '';
      Flag_Инвентарный_Номер := false;
    }

    if (Итог_Структурное_подразделение_Поставщика <> Структурное_подразделение_Поставщика)
    {
      Flag := false;
      Итог_Структурное_подразделение_Поставщика := '';
      Flag_Структурное_подразделение_Поставщика := false;
    }

    if (Итог_Структурное_подразделение_Получателя <> Структурное_подразделение_Получателя)
    {
      Flag := false;
      Итог_Структурное_подразделение_Получателя := '';
      Flag_Структурное_подразделение_Получателя := false;
    }

   }
  Массив_ИК_КоличествоХарактеристик [НомерИК] := 0;
  Массив_ИК_КоличествоДрагМеталлов  [НомерИК] := 0;
end.
.{ CheckEnter FormGroupOsMemo
.begin
  НомерХарактеристики := НомерХарактеристики + 1;
  Массив_ИК_КоличествоХарактеристик [НомерИК] := Массив_ИК_КоличествоХарактеристик [НомерИК] + 1;

  Массив_СтрокаХарактеристики       [НомерХарактеристики] := СтрокаХарактеристики;
end.
.}
.{ CheckEnter FormGroupOs_NalDrag
.begin
  НомерДрагМеталла := НомерДрагМеталла + 1;

  Массив_ИК_КоличествоДрагМеталлов [НомерИК] := Массив_ИК_КоличествоДрагМеталлов [НомерИК] + 1;

  Характеристика_NameDrag  [НомерДрагМеталла] := string(ДрагМеталл_Наименование);
  Характеристика_N_Edin    [НомерДрагМеталла] := string(ДрагМеталл_ЕдИзм);
  Характеристика_MassaDrag [НомерДрагМеталла] := string(ДрагМеталл_Масса,18,12);
end.
.}
.}
! ***** Вывод в форму *****
.begin
  НомерПоПорядку := 0;
  НомерИК        := 1;
  КолНомер_в_ИК  := 0;

  if (not Flag)
  {
    Message ('Значения в карточках для полей:'#13 +
             if (not Flag_Структурное_подразделение_Поставщика, 'Структурное подразделение поставщика,'#13 , '') +
             if (not Flag_Структурное_подразделение_Получателя, 'Структурное подразделение получателя,'#13 , '') +
             if (not Flag_Инвентарный_Номер                   , 'Инвентарный номер,'#13                    , '') +
             if (not Flag_Сдал   , 'МОЛ сдавшее объекты основных средств и его табельный номер,'#13        , '') +
             if (not Flag_Принял , 'МОЛ принявшее объекты основных средств и его табельный номер,'#13      , '') +
             'отличаются.'                                    , Information+CancelButton);
  }
end.
.fields
  '(Метод учета: ' +Метод_Учета +')'
  Поставщик
  Поставщик_ОКПО
  Структурное_подразделение_Поставщика
  Структурное_подразделение_Получателя
  Номер_документа
  Дата_документа
  Номер_приказа
  if (Longint(Дата_приказа) <> 0, DateToStr(Дата_приказа, 'DD/MM/YYYY'),'')
 Основание_для_составления_акта
.endfields
 Ш@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
                                                                                                                                              БФорма № ОС-2дм Б

                                                                                                                                            ┌────────────┐
                                                                                                                                            │    Коды    │
                                                                                                                                            ├────────────┤
                                                                                                                              Форма по ОКУД │            │
   Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е                        ├────────────┤
                                         наименование организации                                                                  по ОКЮЛП │@~@@@@@@@@@@│
   Сдатчик     Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е                        ├────────────┤
                                         наименование структурного подразделения                                                            │            │
   Получатель  Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е                        ├────────────┤
                                        наименование структурного подразделения                                                             │            │
                                                                                                                                            └────────────┘

                                     БНАКЛАДНАЯ Б

                              БНА ВНУТРЕННЕЕ ПЕРЕМЕЩЕНИЕ Б
                                                                                                                ┌─────────────────┬────────────────┐
                             БОБЪЕКТОВ ОСНОВНЫХ СРЕДСТВ, Б                                                          │      Номер      │     Дата       │
                                                                                                                │    документа    │  составления   │
                               БСОДЕРЖАЩИХ ДРАГОЦЕННЫЕ Б                                                            ├─────────────────┼────────────────┤
                                                                                                                │ @~@@@@@@@@@@@@@ │ @~@@@@@@@@@@@@ │
                             БМЕТАЛЛЫ И ДРАГОЦЕННЫЕ КАМНИ Б                                                         └─────────────────┴────────────────┘
                                                                                                  ┌─────────────────┬────────────────┬────────────────┐
                                                                                                  │  Номер приказа  │     Дата       │ Основание для  │
                                                                                                  │  (распоряжения) │  составления   │   перемещения  │
                                                                                                  ├─────────────────┼────────────────┼────────────────┤
                                                                                                  │ @~@@@@@@@@@@@@@ │ @~@@@@@@@@@@@@ │ @@@@@@@@@@@@@@ │
                                                                                                  └─────────────────┴────────────────┴────────────────┘




   Сведения о состоянии объекта основных средств
   ┌─────┬─────────────────────────────────────────────────────────────────────────────────────┬────────────────┬────────────────────────────────────────┐
   │     │                          Объект основных средств                                    │                │           Стоимость, руб.              │
   │  №  ├──────────────────────────────────────────────────────┬───────────────┬──────────────┤  Количество,   ├─────────────────────┬──────────────────┤
   │ п/п │                                                      │      Дата     │              │      шт.       │                     │                  │
   │     │                  Наименование                        │ приобретения  │  инвентарный │                │      единицы        │     всего        │
   │     │                                                      │ (год выпуска, │     номер    │                │                     │                  │
   │     │                                                      │  постройки)   │              │                │                     │                  │
   ├─────┼──────────────────────────────────────────────────────┼───────────────┼──────────────┼────────────────┼─────────────────────┼──────────────────┤
   │  1  │                       2                              │       3       │     4        │       5        │         6           │       7          │
.{While (НомерИК <= НомерИК_Max)
   ├─────┼──────────────────────────────────────────────────────┼───────────────┼──────────────┼────────────────┼─────────────────────┼──────────────────┤
.begin
  НомерПоПорядку := НомерПоПорядку + 1;
  КолНомер_в_ИК  := КолНомер_в_ИК  + 1;
end.
.fields
  НомерПоПорядку
  Характеристика_Наименование_Объекта  [НомерИК]                                         //2
  if (Longint (Массив_ИК_Дата_принятия[НомерИК]) <> 0, DatetoStr(Массив_ИК_Дата_принятия[НомерИК], 'DD/MM/YYYY'), '') +
   if (Longint(Массив_ИК_Дата_выпуска [НомерИК]) <> 0, ' (' + DatetoStr(Массив_ИК_Дата_выпуска [НомерИК], 'YYYY') + ')','')
  Характеристика_ИнвенНомер_Объектов   [НомерИК]
    + if (AddNumIKToInvNum and DuplicateRecordInGroupIK and (Массив_ИК_Количество [НомерИК] > 1), ' / ' + КолНомер_в_ИК, '')                                         //4
  iExcelFormat.DoubleToStrFormatExcel( if (DuplicateRecordInGroupIK
                                          , 1
                                          , Характеристика_Количество_Объектов[НомерИК])
                                     , Разрядность_дробной_части_Количества) //5
  Характеристика_СтоимостьЕдиницы_Об   [НомерИК]                                        //6
  if (DuplicateRecordInGroupIK,  Характеристика_СтоимостьЕдиницы_Об [НомерИК], Характеристика_СтоимостьВсех_Объектов[НомерИК])                                        //7
.endfields
   │&&&&&│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@~@@@@@@@@@@@@@│@@@@@@@@@@@@@@│&&&&&&&&&&&&&&&&│&&&&&&&&&&&&&&&&&&.&&│&&&&&&&&&&&&&&&.&&│
.begin
  if (DuplicateRecordInGroupIK)
  {
    if (КолНомер_в_ИК >= Массив_ИК_Количество [НомерИК])
    {
      НомерИК := НомерИК + 1;
      КолНомер_в_ИК := 0;
    }
  }
  else
  {
    НомерИК := НомерИК + 1;
  }
end.
.}
   └─────┴──────────────────────────────────────────────────────┴───────────────┴──────────────┴────────────────┴─────────────────────┴──────────────────┘
.begin
  НомерИК := 1;
  КолНомер_в_ИК := 0;
  НомерПоПорядку := 0;
end.



                                                                                                                         Оборотная сторона формы № ОС-2дм

   Краткая индивидуальная характеристика объекта основных средств
   ┌─────┬─────────────────────────────────────────────────────────────┬─────────────────────────────────────────────────────────────────────────────────┐
   │     │ Объект основных средств, приспособления, принадлежности     │           Содержание драгоценных материалов (металлов, камней и т.д.)           │
   │  №  ├──────────────────────────────────────────────┬──────────────┼──────────────────────┬──────────────┬───────────┬────────────┬──────────────────┤
   │ п/п │                  наименование                │  количество  │    наименование      │номенклатурный│  единица  │ количество │       масса      │
   │     │                                              │              │драгоценных материалов│    номер     │ измерения │            │                  │
   ├─────┼──────────────────────────────────────────────┼──────────────┼──────────────────────┼──────────────┼───────────┼────────────┼──────────────────┤
   │  1  │                      2                       │      3       │           4          │       5      │     6     │     7      │        8         │
.begin
  НомерИК             := 1;
  КолНомер_в_ИК       := 0;

  НомерДрагМеталла    := 0;
  НомерХарактеристики := 0;
end.
.{WHILE НомерИК <= НомерИК_Max
   ├─────┼──────────────────────────────────────────────┼──────────────┼──────────────────────┼──────────────┼───────────┼────────────┼──────────────────┤
.begin
  КолНомер_в_ИК := КолНомер_в_ИК + 1;
  НомерПоПорядку := НомерПоПорядку + 1;
  i := 0;
end.
.{WHILE (i < Массив_ИК_КоличествоДрагМеталлов [НомерИК])
.begin
 i := i + 1;
end.
.fields
  if (i = 1, НомерПоПорядку, '')                                                                                // 1
  if (i = 1, Характеристика_Наименование_Объекта [НомерИК], '')                                                 // 2
  if (i = 1, iExcelFormat.DoubleToStrFormatExcel( if (DuplicateRecordInGroupIK
                                                     , 1
                                                     , Характеристика_Количество_Объектов[НомерИК])
                                                , Разрядность_дробной_части_Количества )
           , '')
  if (i <= Массив_ИК_КоличествоДрагМеталлов [НомерИК], Характеристика_NameDrag [НомерДрагМеталла + i],'')       // 4
  if (i <= Массив_ИК_КоличествоДрагМеталлов [НомерИК], Характеристика_N_Edin   [НомерДрагМеталла + i],'')       // 6
  if (i <= Массив_ИК_КоличествоДрагМеталлов [НомерИК], Характеристика_MassaDrag[НомерДрагМеталла + i],'')       // 8
.endfields
   │&&&&&│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│&&&&&&&&&&&&&&│@@@@@@@@@@@@@@@@@@@@@@│              │@@@@@@@@@@@│            │&&&&&.&&&&&&&&&&&&│
.}
.begin
  if (DuplicateRecordInGroupIK)
  {
    if (КолНомер_в_ИК >= Массив_ИК_Количество [НомерИК])
    {
      НомерХарактеристики := НомерХарактеристики + Массив_ИК_КоличествоХарактеристик [НомерИК];
      НомерДрагМеталла    := НомерДрагМеталла    + Массив_ИК_КоличествоДрагМеталлов  [НомерИК];

      НомерИК := НомерИК + 1;
      КолНомер_в_ИК := 0;
    }
  }
  else
  {
    НомерХарактеристики := НомерХарактеристики + Массив_ИК_КоличествоХарактеристик [НомерИК];
    НомерДрагМеталла    := НомерДрагМеталла    + Массив_ИК_КоличествоДрагМеталлов  [НомерИК];

    НомерИК := НомерИК + 1;
  }
end.
.}
.{?INTERNAL;(НомерДрагМеталла = 0)
   │     │                                              │              │                      │              │           │            │                  │
.}
   └─────┴──────────────────────────────────────────────┴──────────────┴──────────────────────┴──────────────┴───────────┴────────────┴──────────────────┘


   Другие характеристики

   ───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────


.fields
  Сдал_Должность
  Сдал
  Сдал_Табельный_номер

  Принял_Должность
  Принял
  Принял_Табельный_номер

  Номер_документа
  Дата_документа
  Главный_бухгалтер_Поставщика
.endfields

   Объекты основных средств

   Сдал                                                                                                                ┌────────────────────────────┐
    Д@@@@@@@@@@@@@@@@@@@@@@@@ Д   ____________________    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д          Табельный номер │@@@@@@@@@@@@@@@@@@@@@@@@@@@@│
           должность                 подпись                     фамилия, инициалы                                     └────────────────────────────┘
                                                                                                                   "____" __________________ 20____ г.


   Принял                                                                                                              ┌────────────────────────────┐
    Д@@@@@@@@@@@@@@@@@@@@@@@@ Д   ____________________    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д          Табельный номер │@@@@@@@@@@@@@@@@@@@@@@@@@@@@│
           должность                 подпись                     фамилия, инициалы                                     └────────────────────────────┘
                                                                                                                   "____" __________________ 20____ г.

  В инвентарной карточке учета объекта основных средств
  перемещение отмечено.        ┌─────────────────┬──────────────────┐
                               │ Номер документа │ Дата составления │
                               ├─────────────────┼──────────────────┤
                               │@~@@@@@@@@@@@@@@@│@~@@@@@@@@@@@@@@@@│
                               └─────────────────┴──────────────────┘

  Бухгалтер _____________________________________    Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                           подпись                             фамилия, инициалы



.}
.endform
