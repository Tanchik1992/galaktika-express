.linkform 'Form_OS6_Bel' prototype is 'FormOS6'
.NameInList 'Инвентарная карточка (форма N ОС-6) << Беларусь >>'
.group 'OS_Form6_Bel'
.hide
.defo portrait
.P 90
.var
  bWasPereoc : Boolean;  // проверка были ли мы в переоценке
  bWasOper : Boolean;    // проверка были ли мы в операциях
  Массив_Дата_Переоценка            : array[1..10] of date;
  Массив_Коэффициент_Переоценка     : array[1..10] of string;
  Массив_Восст_Стоимость_Переоценка : array[1..10] of string;
  N1 : word; // Текущий номер в цикле
  NN : word; // Количество элементов в массиве.
.endvar
#include funcOs.frn
end.  // funcOs.frn - вынес из файла чтобы не создавалась пустая строка перед формой
.fields
 '(Метод учета: ' +Метод_Учета +')'
 DateToStr(Отчетный_период,'Month YYYY')
 Center(Организация_Наименование, 65- Length(Организация_Наименование) )
 Организация_ОКПО
 Center(Подразделение, 65- Length(Подразделение) )

 Код_Нормы_Амортизации

 Инвентарный_Номер
 Отчетный_период

 Номер_Паспорта
 Заводской_Номер
 Инвентарный_Номер
 Center(Наименование_ОС, 60- Length(Наименование_ОС) )

 Дата_Документа_Получения
 Center(Тип_Марка, 60- Length(Тип_Марка) )
 Дата_Документа_Выбытия

 Счет_Субсчет+ ' ' + КАУ

 Center(Подразделение, 65- Length(Подразделение) )
 Center(Наименование_Изготовителя, 60- Length(Наименование_Изготовителя) )

.endfields
.{
.begin
  for (N1 := 1; N1 <= 10; N1 := N1 + 1)
  {
    Массив_Дата_Переоценка            [N1] := date(0,0,0);
    Массив_Коэффициент_Переоценка     [N1] := '';
    Массив_Восст_Стоимость_Переоценка [N1] := '';
  }
  bWasPereoc := false;
  bWasOper   := false;
  N1 := 0;
  NN := 0;
end.
 Р@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@@                                            Приложение 8
                                                                                                              к постановлению МФ РБ
                                                                                                                   08.12.2003 № 168
                                                                                                                       Б Форма № ОС-6 Б

                                                                                                                  ┌────────────────┐
                                                                                                                  │      Коды      │
                                                                                                                  ├────────────────┤
                                                                                                    Форма по ОКУД │     0501361    │
                                                                                                                  ├────────────────┤
    Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д   по ОКЮЛП │@~@@@@@@@@@@@@@@│
                                         наименование организации                                                 ├────────────────┤
    Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д            │                │
                                        структурное подразделение                                                 └────────────────┘

                          ┌──────────────────────────┬──────────────────┐ ┌───────────────────────────────────────┬────────────────┐
    Б      ИНВЕНТАРНАЯ      Б │      Номер документа     │ Дата составления │ │        Шифр нормативного срока службы │@~@@@@@@@@@@@@@@│
    БКАРТОЧКА УЧЕТА ОБЪЕКТА Б ├──────────────────────────┼──────────────────┤ ├─────┬─────────────────────────────────┼────────────────┤
    Б   ОСНОВНЫХ СРЕДСТВ    Б │ @~@@@@@@@@@@@@@@@@@@@@@@ │ @~@@@@@@@@@@@@@@ │ │     │       паспорта (регистрационный)│@~@@@@@@@@@@@@@@│
                          └──────────────────────────┴──────────────────┘ │     ├─────────────────────────────────┼────────────────┤
                                                                          │Номер│                       заводской │@~@@@@@@@@@@@@@@│
                                                                          │     ├─────────────────────────────────┼────────────────┤
                                                                          │     │                     инвентарный │@~@@@@@@@@@@@@@@│
                                                                          ├─────┼─────────────────────────────────┼────────────────┤
   Объект  Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д│     │ принятия к бухгалтерскому учету │@~@@@@@@@@@@@@@@│
                                                                          │Дата ├─────────────────────────────────┼────────────────┤
    Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д│     │ списания с бухгалтерского учета │@~@@@@@@@@@@@@@@│
                   серия, тип постройки или модель, марка                 └─────┴─────────────────────────────────┼────────────────┤
                                                                          Счет, субсчет, код аналитического учета │@~@@@@@@@@@@@@@@│
                                                                                                                  └────────────────┘
                                                                                                                  ┌────────────────┐
   Место нахождения объекта основных средств  Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д │                │
                                                                                                                  └────────────────┘
   Организация-изготовитель  Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                                                         наименование
.fields
 // Таблица 1
 if (longint(Дата_Выпуска) <> 0, DateToStr(Дата_Выпуска, 'DD/MM/YYYY'), '') // 01

  /*******************************************************************************************************************
   *                                                                                                                 *
   *  Комментарий к заполнению раздела "Документ о вводе в эксплуатацию" таблицы 1                                   *
   *  (т.е. к заполнению колонк 3 - 5), в форме ОС-6:                                                                *
   *                                                                                                                 *
   *  1. Если есть проведенная операция поступление и в инвентарной карточке есть дата ввода в эксплуатацию,         *
   *     которая равна дате проведения операции поступления, то:                                                     *
   *     - Поле 3 "наименование" - заполняется словом "Акт";                                                         *
   *     - Поле 4 "номер"        - заполняется номером операции поступление;                                         *
   *     - Поле 3 "дата"         - заполняется датой ввода в эксплуатацию из инвентарной карточки;                   *
   *  Т.к. это соответствует ситуации, когда основное средство введено в эксплуатацию при поступлении                *
   *  (и соответственно документом на поступление)...                                                                *
   *                                                                                                                 *
   *  2. Если есть проведенная операция поступление, но и в инвентарной карточке нет даты ввода в эксплуатацию, то:  *
   *     - Поле 3 "наименование" - не заполняется;                                                                   *
   *     - Поле 4 "номер"        - не заполняется;                                                                   *
   *     - Поле 3 "дата"         - не заполняется;                                                                   *
   *  Т.к. это соответствует ситуации, когда основное средство поступило, но не введено в эксплуатацию...            *
   *                                                                                                                 *
   *  3. Если есть проведенная операция поступление и в инвентарной карточке есть дата ввода в эксплуатацию,         *
   *  но дата ввода в эксплуатацию не равна дате проведения операции поступление, то:                                *
   *     - Поле 3 "наименование" - заполняется словом "Акт";                                                         *
   *     - Поле 4 "номер"        - не заполняется;                                                                   *
   *     - Поле 3 "дата"         - заполняется датой ввода в эксплуатацию из инвентарной карточки;                   *
   *  Т.к. это соответствует ситуации, когда основное средство поступило, но введено в эксплуатацию                  *
   *  не при поступлении (и соответственно не документом на поступление)...                                          *
   *                                                                                                                 *
   *  4. Если нет проведенной операции поступления, но в инвентарной карточке есть дата ввода в эксплуатацию -       *
   * KatOs.DatEk. То это соответствует ситуации, когда ввод в эксплуататцию был, но это не было отражено операцией   *
   * поступления в Галактике. К примеру в ситуации со ставрым ОС-ом, когда ОС был преобреьтён и введен в             *
   * эксплуатацию до покупки Галактики...                                                                            *
   * Поэтому в данной ситуации:                                                                                      *
   *     - Поле 3 "наименование" - заполняется словом "Акт";                                                         *
   *     - Поле 4 "номер"        - не заполняется;                                                                   *
   *     - Поле 3 "дата"         - заполняется датой ввода в эксплуатацию из инвентарной карточки;                   *
   *                                                                                                                 *
   *******************************************************************************************************************/

 // 03
 // Выводим слово 'Акт':
 //   - В соответствии с 102.59812 и 101.32852, если:
 //     - Есть проведенная операция поступление, и в инвентарной карточке есть дата ввода в эксплуатацию.
 //     - На совпадение с датой проведения операции не проверяем...
 //   - В соответствии с 101.34296, если:
 //     - Если в инвентарной карточке есть дата ввода в эксплуатацию.
 if (LongInt(Дата_Документа_Ввода) <> 0, 'Акт','')

 // 04
 // Номер документа ввода в эксплуатацию заполняем:
 //   - В соответствии с 102.59812 и 101.32852, если:
 //     - Есть проведенная операция поступление, и в инвентарной карточке есть дата ввода в эксплуатацию,
 //       которая должна совпадать с датой проведения операции.
 //     - В противном случае не заполняем...
 if (Проведенное_Поступление
       and LongInt(Дата_Документа_Ввода) <> 0
       and Дата_Документа_Ввода = Дата_Операции_Получения,
     Номер_Документа_Ввода,'')

 // 05
 // Дату ввода в эксплуатацию заполняем:
 //   - В соответствии с 102.59812 и 101.32852, если:
 //     - Есть проведенная операция поступление, и в инвентарной карточке есть дата ввода в эксплуатацию.
 //     - На совпадение с датой проведения операции не проверяем...
 //   - В соответствии с 101.34296, если:
 //     - Если в инвентарной карточке есть дата ввода в эксплуатацию.
 if (longint(Дата_Документа_Ввода) <> 0, DateToStr(Дата_Документа_Ввода, 'DD/MM/YYYY'), '')

 // 06
 if (WriteDivisionOne = 0,
   if ((Сумма_Амортизации_получения = 0), '', SrokIspEdIzmWord (Срок_Использования_До_Поступления  ) ),
   SrokIspEdIzmWord (Срок_Использования_на_заданную_дату) )

 // 07
 if (WriteDivisionOne = 0,
   if ((Сумма_Амортизации_получения = 0), '',  DoubleToStr(Сумма_Амортизации_получения, efSumFormatExcel)),
   DoubleToStr(Сумма_Амортизации,  efSumFormatExcel) )

 // 08
 if (WriteDivisionOne = 0,
   if ((Сумма_Амортизации_получения = 0), '', DoubleToStr((Стоимость_получения-Сумма_Амортизации_получения), efSumFormatExcel) ),
   DoubleToStr((Стоимость - Сумма_Амортизации), efSumFormatExcel) )

 // Таблица 2
 DoubleToStr(Стоимость_получения, efSumFormatExcel)                                                            // 01
 SrokIspEdIzmWord(Срок_Использования_При_поступлении - Срок_Использования_До_Поступления)        // 02
.endfields
                                                                                                          2. Сведения об объекте
                                                                                                          основных средств на дату
                                                                                                          принятия к бухгалтерскому
    1. Сведения о состоянии объекта основных средств на дату передачи                                     учету
   ┌─────────────────────┬─────────────────────────────────────────┬────────┬──────────────┬─────────────╥───────────────┬─────────┐
   │         Дата        │     Документ о вводе в эксплуатацию     │ Факти- │ Сумма начис- │ Остаточная  ║ Первоначальная│  Срок   │
   ├──────────┬──────────┼───────────────────┬──────────┬──────────┤ ческий │ ленной амор- │ стоимость,  ║ стоимость на  │ полез-  │
   │ выпуска  │последнего│                   │          │          │  срок  │    тизации,  │    руб.     ║ дату принятия │  ного   │
   │ (пост-   │ капремон-│    наименование   │  номер   │   дата   │  экс-  │      руб.    │             ║ к бухгалтерс- │ исполь- │
   │  ройки)  │та, модер-│                   │          │          │  плуа- │              │             ║  кому учету,  │ зования │
   │          │ низации, │                   │          │          │ тации  │              │             ║      руб.     │ (норма- │
   │          │  рекон-  │                   │          │          │        │              │             ║               │ тивный  │
   │          │ струкции │                   │          │          │        │              │             ║               │  срок)  │
   ├──────────┼──────────┼───────────────────┼──────────┼──────────┼────────┼──────────────┼─────────────╢───────────────┼─────────┤
   │     1    │     2    │         3         │     4    │     5    │    6   │       7      │      8      ║       1       │    2    │
   ├──────────┼──────────┼───────────────────┼──────────┼──────────┼────────┼──────────────┼─────────────╢───────────────┼─────────┤
   │@~@@@@@@@@│          │@~@@@@@@@@@@@@@@@@@│@~@@@@@@@@│@~@@@@@@@@│&&&&&&&&│&&&&&&&&&&&&&&│&&&&&&&&&&&&&║&&&&&&&&&&&&&&&│@@@@@@@@@│
   └──────────┴──────────┴───────────────────┴──────────┴──────────┴────────┴──────────────┴─────────────╨───────────────┴─────────┘
.{ FormOS6Amortization CheckEnter
.}
.{ FormOS6Pereoc CheckEnter
.begin
  N1:=N1+1;
  Массив_Дата_Переоценка           [N1] := Дата_Переоценка;
  Массив_Коэффициент_Переоценка    [N1] := String(Коэффициент_Переоценка    ,10,4);
  Массив_Восст_Стоимость_Переоценка[N1] := DoubleToStr(Восст_Стоимость_Переоценка, efSumFormatExcel);
end.
.}
.begin
  NN := N1;
  N1 := 1;
end.
    3. Переоценка
   ┌──────────┬──────────────┬────────────────╥──────────┬──────────────┬────────────────╥──────────┬─────────────┬────────────────┐
   │   Дата   │ Коэффициент  │  Восстанови-   ║   Дата   │ Коэффициент  │  Восстанови-   ║   Дата   │ Коэффициент │  Восстанови-   │
   │          │  пересчета   │    тельная     ║          │  пересчета   │    тельная     ║          │  пересчета  │    тельная     │
   │          │ (переоценки) │ стоимость,руб. ║          │ (переоценки) │ стоимость,руб. ║          │ (переоценки)│ стоимость,руб. │
   ├──────────┼──────────────┼────────────────╫──────────┼──────────────┼────────────────╫──────────┼─────────────┼────────────────┤
   │     1    │      2       │        3       ║     1    │      2       │        3       ║     1    │      2      │        3       │
   ├──────────┼──────────────┼────────────────╫──────────┼──────────────┼────────────────╫──────────┼─────────────┼────────────────┤
.{WHILE N1<=NN
.fields
  if (Longint(Массив_Дата_Переоценка [N1])<>0,DateToStr(Массив_Дата_Переоценка [N1], 'DD/MM/YYYY'),'')
  Массив_Коэффициент_Переоценка    [N1]
  Массив_Восст_Стоимость_Переоценка[N1]
  if (Longint(Массив_Дата_Переоценка [N1+1])<>0,DateToStr(Массив_Дата_Переоценка [N1+1], 'DD/MM/YYYY'),'')
  Массив_Коэффициент_Переоценка    [N1+1]
  Массив_Восст_Стоимость_Переоценка[N1+1]
  if (Longint(Массив_Дата_Переоценка [N1+2])<>0,DateToStr(Массив_Дата_Переоценка [N1+2], 'DD/MM/YYYY'),'')
  Массив_Коэффициент_Переоценка    [N1+2]
  Массив_Восст_Стоимость_Переоценка[N1+2]
.endfields
   │@@@@@@@@@@│&&&&&&&&&&&&&&│&&&&&&&&&&&&&&&&║@@@@@@@@@@│&&&&&&&&&&&&&&│&&&&&&&&&&&&&&&&║@@@@@@@@@@│&&&&&&&&&&&&&│&&&&&&&&&&&&&&&&│
.begin
  N1 :=N1+3;
  bWasPereoc := true;
end.
.}
.{?INTERNAL;(not bWasPereoc)
   ├──────────┼──────────────┼────────────────╫──────────┼──────────────┼────────────────╫──────────┼─────────────┼────────────────┤
   ├──────────┼──────────────┼────────────────╫──────────┼──────────────┼────────────────╫──────────┼─────────────┼────────────────┤
   ├──────────┼──────────────┼────────────────╫──────────┼──────────────┼────────────────╫──────────┼─────────────┼────────────────┤
.}
   └──────────┴──────────────┴────────────────╨──────────┴──────────────┴────────────────╨──────────┴─────────────┴────────────────┘
    4. Сведения о приемке, внутренних перемещениях, выбытии (списании) объекта основных средств
   ┌───────────────────────┬───────────────────────┬─────────────────────────────────┬────────────────┬────────────────────────────┐
   │ Документ, дата, номер │     Вид операции      │   Наименование структурного     │   Остаточная   │  Фамилия, инициалы лица,   │
   │                       │                       │          подразделения          │ стоимость, руб.│ ответственного за хранение │
   ├───────────────────────┼───────────────────────┼─────────────────────────────────┼────────────────┼────────────────────────────┤
   │           1           │           2           │                3                │        4       │              5             │
   ├───────────────────────┼───────────────────────┼─────────────────────────────────┼────────────────┼────────────────────────────┤
.{ FormOS6Oper CheckEnter
.fields
  /*********************************************************************************************************************************
   *                                                                                                                               *
   *  01 "Документ, дата, номер"                                                                                                   *
   *                                                                                                                               *
   *  - При наличии проведенной операций - поступление или выбытие, в начале выводится слова "Акт", а затем дата проведения        *
   *  операции и при наличии номер операции;                                                                                       *
   *  - При наличии проведенной операции - внутреннее перемещение, в начале выводится слово "Накладная", а затем дата проведения   *
   *  операции и и при наличии номер операции;                                                                                     *
   *                                                                                                                               *
   *********************************************************************************************************************************/
  Дата_Номер_Операции

  Вид_Операции
  Структ_Подраздел_Операции
  Ост_Стоимость_Операции
  МОЛ_Операции
.endfields
   │@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│&&&&&&&&&&&&&.&&│@@@@@@@@@@@@@@@@@@@@@@@@@@@@│
.begin
  bWasOper := true;
end.
.}
.{?INTERNAL;(not bWasOper)
   ├───────────────────────┼───────────────────────┼─────────────────────────────────┼────────────────┼────────────────────────────┤
   ├───────────────────────┼───────────────────────┼─────────────────────────────────┼────────────────┼────────────────────────────┤
   ├───────────────────────┼───────────────────────┼─────────────────────────────────┼────────────────┼────────────────────────────┤
.}
   └───────────────────────┴───────────────────────┴─────────────────────────────────┴────────────────┴────────────────────────────┘
   Справочно: Участники долевой собственности                                                  ┌──────────────────┬────────────────┐
                                                                                               │Доля в праве общей│                │
              ─────────────────────────────────────────────────────────────────────────────────│собственности, %  ├────────────────┤
                                                                                               │                  │                │
              ─────────────────────────────────────────────────────────────────────────────────└──────────────────┴────────────────┘
 Р
.{ FormOS6ModernNew CheckEnter
.}
#ifdef __YUKOS_OSSP__
.{ FormOS6OsMc   CheckEnter
.}
#end
.{ FormOS6Drag   CheckEnter
.}
.{ FormOS6Modern CheckEnter
.}
.{ FormOS6Memo   CheckEnter
.}

.}
.endform

.linkform 'Form_OS6_Bel_2' prototype is 'FormOS6'
.NameInList 'Инвентарная карточка (форма N ОС-6) << Беларусь >> (оборотная сторона)'
.group 'OS_Form6_Bel'
.hide
.defo landscape
.P 60
.var
  bWasFormOS6Modern : boolean // флаг того, что была выведена FormOS6Modern
  bWasMemo          : boolean // флаг того, что была выведена доп информации

  Массив_Основание_Документа_Модернизации : array[1..10] of string;
  Массив_Дата_Документа_Модернизации  : array[1..10] of string;
  Массив_Номер_Документа_Модернизации : array[1..10] of string;
  Массив_Сумма_Модернизации           : array[1..10] of string;

  Массив_Основание_Документа_Ремонт1  : array[1..10] of string;
  Массив_Дата_Документа_Ремонт1       : array[1..10] of string;
  Массив_Номер_Документа_Ремонт1      : array[1..10] of string;
  Массив_Сумма_Ремонт1                : array[1..10] of string;

  Массив_Наименование_Объекта         : array[1..10] of string;
  Массив_Количество_Объектов          : array[1..10] of string;
  Массив_Наименование_ДрагМеталлов    : array[1..10] of string;
  Массив_Номенклат_Номер_ДрагМеталлов : array[1..10] of string;
  Массив_ЕдИзм_ДрагМеталлов           : array[1..10] of string;
  Массив_Масса_ДрагМеталлов           : array[1..10] of string;
  Массив_ИнфоМемо                     : array[1..10] of string;

  N1 : word; // Текущий номер в цикле Модернизации
  N2 : word; // Текущий номер в цикле Ремонта
  N3 : word; // Текущий номер в наименованиях объекта и составляющих
  NN : word; // Количество элементов в массиве.
.endvar
.{
.begin
  for (N1 := 1; N1 <= Count(Массив_Дата_Документа_Модернизации); N1 := N1 + 1)
  {
    Массив_Основание_Документа_Модернизации [N1]:= '';
    Массив_Дата_Документа_Модернизации  [N1]:= '';
    Массив_Номер_Документа_Модернизации [N1]:= '';
    Массив_Сумма_Модернизации           [N1]:= '';
  }
  for (N1 := 1; N1 <= Count(Массив_Дата_Документа_Ремонт1); N1 := N1 + 1)
  {
    Массив_Основание_Документа_Ремонт1  [N1]:= '';
    Массив_Дата_Документа_Ремонт1       [N1]:= '';
    Массив_Номер_Документа_Ремонт1      [N1]:= '';
    Массив_Сумма_Ремонт1                [N1]:= '';
  }
  for (N1 := 1; N1 <= Count(Массив_Наименование_Объекта); N1 := N1 + 1)
  {
    Массив_Наименование_Объекта         [N1]:= '';
    Массив_Количество_Объектов          [N1]:= '';
  }
  for (N1 := 1; N1 <= Count(Массив_Наименование_ДрагМеталлов); N1 := N1 + 1)
  {
    Массив_Наименование_ДрагМеталлов    [N1]:= '';
    Массив_Номенклат_Номер_ДрагМеталлов [N1]:= '';
    Массив_ЕдИзм_ДрагМеталлов           [N1]:= '';
    Массив_Масса_ДрагМеталлов           [N1]:= '';
  }
  for (N1 := 1; N1 <= Count(Массив_ИнфоМемо); N1 := N1 + 1)
  {
    Массив_ИнфоМемо                     [N1]:= '';
  }
  bWasFormOS6Modern := false;
  bWasMemo          := false;
  N1 := 0;
  N2 := 0;
  N3 := 0;
  NN := 0;
end.
                                                                                Оборотная сторона формы № ОС-6

  5. Изменения первоначальной стоимости объекта           6. Затраты на ремонт
  основных средств
  ┌─────────────────────────────────────────────────────╥─────────────────────────────────────────────────────┐
  │      Реконструкция, достройка, дооборудование,      ║                        Ремонт                       │
  │         частичная ликвидация, модернизация          ║                                                     │
  ├──────┬─────────────────────────────────┬────────────╫──────┬─────────────────────────────────┬────────────┤
  │ вид  │             документ            │сумма затрат║ вид  │             документ            │сумма затрат│
  │опера-├────────────┬──────────┬─────────┤    руб.    ║ремон-├────────────┬──────────┬─────────┤    руб.    │
  │ ции  │наименование│   дата   │  номер  │            ║  та  │наименование│   дата   │  номер  │            │
  ├──────┼────────────┼──────────┼─────────┼────────────╫──────┼────────────┼──────────┼─────────┼────────────┤
  │  1   │      2     │    3     │    4    │     5      ║  1   │      2     │    3     │    4    │     5      │
  ├──────┼────────────┼──────────┼─────────┼────────────╫──────┼────────────┼──────────┼─────────┼────────────┤
.{ FormOS6Amortization CheckEnter
.}
.{ FormOS6Pereoc CheckEnter
.}
.{ FormOS6Oper CheckEnter
.}
.{ FormOS6ModernNew CheckEnter
.begin
   if (Mode = 0) OR (Mode = 2)// Модернизация или обособленные затраты
   {
     if (Abs(Сумма_Изменения) > 0.00001)
     {
       N1:=N1+1;
       Массив_Основание_Документа_Модернизации[N1] := Основание_Документа_Изменения;
       Массив_Дата_Документа_Модернизации[N1]      := Дата_Документа_Изменения;
       Массив_Номер_Документа_Модернизации[N1]     := Номер_Документа_Изменения;
       Массив_Сумма_Модернизации[N1]               := DoubleToStr(Сумма_Изменения, efSumFormatExcel);
     }
   }
   else
   {
     N2:=N2+1;
     Массив_Основание_Документа_Ремонт1[N2]      := Основание_Документа_Изменения;
     Массив_Дата_Документа_Ремонт1[N2]           := Дата_Документа_Изменения;
     Массив_Номер_Документа_Ремонт1[N2]          := Номер_Документа_Изменения;
     Массив_Сумма_Ремонт1[N2]                    := DoubleToStr(Сумма_Изменения, efSumFormatExcel);
   }
end.
.}
.begin
  if N2 > N1 NN := N2;
  else NN := N1;
  N1 :=0;
end.
.{WHILE N1<NN
.begin
 N1 :=N1+1;
end.
.fields
  Массив_Основание_Документа_Модернизации[N1] // Основание_Документа_Изменения
  Массив_Дата_Документа_Модернизации[N1]
  Массив_Номер_Документа_Модернизации[N1]
  Массив_Сумма_Модернизации[N1]

  Массив_Основание_Документа_Ремонт1[N1]      // Основание_Документа_Изменения
  Массив_Дата_Документа_Ремонт1[N1]
  Массив_Номер_Документа_Ремонт1[N1]
  Массив_Сумма_Ремонт1[N1]
.endfields
  │      │@@@@@@@@@@@@│@@@@@@@@@@│&&&&&&&&&│&&&&&&&&&&&&║      │@@@@@@@@@@@@│@@@@@@@@@@│&&&&&&&&&│&&&&&&&&&&&&│
.begin
  bWasFormOS6Modern := true;
end.
.}
.{?INTERNAL;(not bWasFormOS6Modern)
  │      │            │          │         │            ║      │            │          │         │            │
  ├──────┼────────────┼──────────┼─────────┼────────────╫──────┼────────────┼──────────┼─────────┼────────────┤
  │      │            │          │         │            ║      │            │          │         │            │
  ├──────┼────────────┼──────────┼─────────┼────────────╫──────┼────────────┼──────────┼─────────┼────────────┤
  │      │            │          │         │            ║      │            │          │         │            │
  ├──────┼────────────┼──────────┼─────────┼────────────╫──────┼────────────┼──────────┼─────────┼────────────┤
  │      │            │          │         │            ║      │            │          │         │            │
.}
  └──────┴────────────┴──────────┴─────────┴────────────╨──────┴────────────┴──────────┴─────────┴────────────┘
.begin
  N1 := 0;
  N2 := 0;
  N3 := 0;
  NN := 0;
end.
.begin
  N3 := N3 + 1;
  Массив_Наименование_Объекта [N3] := string(Наименование_ОС);
  Массив_Количество_Объектов  [N3] := string(Количество_Объектов,6,Десят_Знаков_Количества);
end.
#ifdef __YUKOS_OSSP__
.{ FormOS6OsMc   CheckEnter
.begin
  N3 := N3 + 1;
  Массив_Наименование_Объекта [N3] := string(Наименование_Составляющей);
  Массив_Количество_Объектов  [N3] := string(Кол_во_Составляющих,6,Десят_Знаков_Количества);
end.
.}
#end
.{ FormOS6Drag   CheckEnter
.begin
  N1:=N1+1;
  Массив_Наименование_ДрагМеталлов   [N1] := Наименование_ДрагМеталлов   ;
  Массив_Номенклат_Номер_ДрагМеталлов[N1] := Номенклат_Номер_ДрагМеталлов;
  Массив_ЕдИзм_ДрагМеталлов          [N1] := ЕдИзм_ДрагМеталлов          ;
  Массив_Масса_ДрагМеталлов          [N1] := string(Масса_ДрагМеталлов, 17, 12);
end.
.}
.{ FormOS6Modern CheckEnter
.}
.{ FormOS6Memo   CheckEnter
.begin
  N2:=N2+1;
  Массив_ИнфоМемо[N2] := InfoMemo;
end.
.}

  7. Краткая индивидуальная характеристика объекта основных средств
 Р    ┌────────────────────────────────┬────────────────────────────────────────────────────────┬───────────────────────────────────┬─────────────────────────────────────────────────┬───────┐
    │  Объект основных средств,при-  │    Содержание драгоценных и/или полудрагоценных        │Наименование конструктивных элемен-│   Качественные и количественные характеристики  │ Приме-│
    │  cпособления, принадлежности   │        материалов (металлов, камней и  т.д.)           │тов и других признаков, характери- │            (размеры, прочие сведения)           │ чание │
    ├─────────────────────────┬──────┼──────────────┬────────┬───────┬─────┬──────────────────┤           зующих объект           ├─────────┬───────────────────────────────────────┤       │
    │      наименование       │коли- │ наименование │номенк- │единица│коли-│      масса       │                                   │основного│   приспособлений, принадлежностей,    │       │
    │                         │чество│ драгоценных  │латурный│измере-│чест-│                  │                                   │ объекта │     пристроенных помещений и др.      │       │
    │                         │      │ и/или полу-  │ номер  │  ния  │ во  │                  │                                   │         ├─────────┬─────────┬─────────┬─────────┤       │
    │                         │      │ драгоценных  │        │       │     │                  │                                   │         │         │         │         │         │       │
    │                         │      │ материалов   │        │       │     │                  │                                   │         │         │         │         │         │       │
    ├─────────────────────────┼──────┼──────────────┼────────┼───────┼─────┼──────────────────┼───────────────────────────────────┼─────────┼─────────┼─────────┼─────────┼─────────┼───────┤
    │            1            │  2   │       3      │   4    │   5   │  6  │         7        │                 8                 │    9    │    10   │    11   │    12   │    13   │  14   │
    ├─────────────────────────┼──────┼──────────────┼────────┼───────┼─────┼──────────────────┼───────────────────────────────────┼─────────┼─────────┼─────────┼─────────┼─────────┼───────┤
.begin
  N2 := if(N1 > N2, N1, N2);

  if (N2 = 0)
    N2 := 1;

  NN := N2 + N3 - 1;
  N1 := 0;
end.
.{WHILE N1<NN
.begin
  N1 := N1 + 1;
end.
.fields
  if(N1 > N2, Массив_Наименование_Объекта [N1 - N2 + 1],
              if(N1 = 1, Массив_Наименование_Объекта [1], ''))
  if(N1 > N2, Массив_Количество_Объектов  [N1 - N2 + 1],
              if(N1 = 1, Массив_Количество_Объектов  [1], ''))

  Массив_Наименование_ДрагМеталлов   [N1]
  Массив_Номенклат_Номер_ДрагМеталлов[N1]
  Массив_ЕдИзм_ДрагМеталлов          [N1]
  Массив_Масса_ДрагМеталлов          [N1]

  Массив_ИнфоМемо[N1]
.endfields
    │@@@@@@@@@@@@@@@@@@@@@@@@@│&&&&&&│@@@@@@@@@@@@@@│&&&&&&&&│@~@@@@@│     │&&&&&.&&&&&&&&&&&&│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│         │         │         │         │         │       │
.begin
  bWasMemo := true;
end.
.}
    ├─────────────────────────┼──────┼──────────────┼────────┼───────┼─────┼──────────────────┼───────────────────────────────────┼─────────┼─────────┼─────────┼─────────┼─────────┼───────┤
    │                         │      │              │        │       │     │                  │В том числе:                       │         │         │         │         │         │       │
    │                         │      │              │        │       │     │                  │материалы                          │         │         │         │         │         │       │
    └─────────────────────────┴──────┴──────────────┴────────┴───────┴─────┴──────────────────┴───────────────────────────────────┴─────────┴─────────┴─────────┴─────────┴─────────┴───────┘

.fields
  ЛОзаВК_Должность
  ЛОзаВК
.endfields
     Лицо, ответственное за ведение инвентарной карточки  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д    ____________________________      Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                                                       должность                            подпись                                   расшифровка подписи
 Р

.}
.endform
