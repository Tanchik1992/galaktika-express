
!=============================================================================
.LINKFORM RepKr_04 PROTOTYPE IS RepKr
!=============================================================================
.NameInList 'Выплата вознаграждений по полученному кредиту '
.Group 'Выплата вознаграждений по полученному кредиту <<Казахстан>>'
.p 60
.defo landscape
.create view loVekslKred
var
  cVekslNRec : comp;
as select Veksl.*,KatOrg.*
from
  Veksl (ReadOnly),
  Refin (ReadOnly),
  Synonym KatOrg KatOrgCreditor (ReadOnly),
  Synonym KatOrg KatOrgDebtor   (ReadOnly)
where
  ((
    cVekslNRec  == Veksl.NRec          and
    Veksl.cPlat == KatOrgCreditor.NRec and
    Veksl.cPl   == KatOrgDebtor.NRec   
  ));
.create view loRefin
var
  wKOLMIN    : word;
  TopDate    : date;
as select Refin.*
from
  Refin (ReadOnly)
where
  ((
    wKOLMIN  ==  Refin.KOLMIN  and
    TopDate  >>= Refin.DATREC
  ))
order by Refin.Kolmin(DESC), Refin.DATREC
;
.create view loFirstStavka
var
  wKOLMIN    : word;
  TopDate    : date;
as select Refin.*
from
  Refin (ReadOnly)
where
  ((
    wKOLMIN  ==  Refin.KOLMIN  and
    TopDate  >>= Refin.DATREC
  ))
!order by Refin.Kolmin(DESC), Refin.DATREC(DESC)
;
.var
  Npp                               : word;
  Срок_Использования                : word;
  IsFirst                           : boolean;
  PrintRefin                        : boolean;
  LastPrint                         : boolean;
  Дата_Получ_Кредита                : date;
  Дата_Получ_Кредита_Тек            : date;
  Сумма_Кредита                     : double;
  Ставка_Рефинансирования           : double;
  Ставка_Рефин_Новая                : double;
  Ставка                            : double;
  Сумма_Вознагр_Ставка              : double;
  КРЕД_Итого_Сумма_Получ_Кред       : double;
  КРЕД_Итого_Сумма_Вознагр          : double;
  КРЕД_Итого_Сумма_Вознагр_Ставка   : double;
  КРЕД_Итого_Сумма_Вознагр_Подл_Выпл: double;
  Итого_Сумма_Получ_Кред            : double;
  Итого_Сумма_Вознагр               : double;
  Итого_Сумма_Вознагр_Ставка        : double;
  Итого_Сумма_Вознагр_Подл_Выпл     : double;
.endvar
.fields
  CommonFormHeader

  if( Дата_Состояния != date(0,0,0), 'ПО СОСТОЯНИЮ НА ' + String(Дата_Состояния), '')
  Настройка_Группировки
  Настройка_Сортировки
  Настройка_Фильтра
.endfields
^
.Function ProcKDay( dStDate : date; Srok : word) : word;
begin
  var dEndDate : date;
  dEndDate := Add_Day(dStDate, Srok);
  if ( Srok <= 0 )
    ProcKDay := 0;
  else
    {
    var MonthCnt : word; MonthCnt := Months_Between(dStDate,dEndDate);
    if MonthCnt <> 0
      ProcKDay := 30-day(dStDate)+day(dEndDate)+(MonthCnt-1)*30;
    else
      ProcKDay := day(dEndDate)-day(dStDate);
    }
end.
.begin
  Дата_Получ_Кредита                := date(0,0,0);
  Дата_Получ_Кредита_Тек            := date(0,0,0);
  Итого_Сумма_Получ_Кред            := 0;
  Итого_Сумма_Вознагр               := 0;
  Итого_Сумма_Вознагр_Ставка        := 0;
  Итого_Сумма_Вознагр_Подл_Выпл     := 0;
  IsFirst :=True;
  Npp := 0;
end.

                     ВЫПЛАТА ВОЗНАГРАЖДЕНИЙ ПО ПОЛУЧЕННОМУ КРЕДИТУ ^      

.{?Internal;Настройка_Группировки!='';
Группировка: ^
.}
.{?Internal;Настройка_Сортировки!='';
Сортировка:  ^
.}
.{ CheckEnter RepKrFilterLoop
.[h
Фильтры:
.]h
^
.}
.{ RepKrMainLoop
.[h
 Ш──────┬──────────────────────────────────────────┬───────────────┬─────────────────────┬───────────┬───────────────┬───────────────┬─────────┬─────────────────┬─────────┬────────┬───────────────┬───────────────
Номер │     Наименование организации-кредитора   │      РНН      │  Номер и дата зак-  │   Дата    │     Сумма     │     Сумма     │Срок исп.│Выплаченная сумма│ Ставка  │ Ставка,│Сумма вознагра-│Сумма вознагра-
строки│                                          │   кредитора   │  лючения кредитно-  │ получения │  полученного  │ задолженности │кредита, │ вознаграждения  │рефинан- │    %   │ждения при при-│ждения, подле-
      │                                          │               │  го договора        │  кредита  │    кредита    │               │   дней  │                 │сиров., %│        │менении ставки │жащая вычету 
──────┴──────────────────────────────────────────┴───────────────┴─────────────────────┴───────────┴───────────────┴───────────────┴─────────┴─────────────────┴─────────┴────────┴───────────────┴─────────────── Ш
.]h
.{ CheckEnter RepKrVekslTotalLoop
.}
.{ CheckEnter RepKrTotalLoop
.}  // RepKrTotalLoop
.{ CheckEnter RepKrVekslGroupLoop
.begin
  Npp := Npp + 1;
  КРЕД_Итого_Сумма_Получ_Кред       := 0;
  КРЕД_Итого_Сумма_Вознагр          := 0;
  КРЕД_Итого_Сумма_Вознагр_Ставка   := 0;
  КРЕД_Итого_Сумма_Вознагр_Подл_Выпл:= 0;
  IsFirst :=True;
  LastPrint := False;
  PrintRefin:= False;
  Сумма_Кредита   := 0;
  Ставка_Рефинансирования := 0;
  Ставка_Рефин_Новая := 0;
  Ставка := 0;
  loVekslKred.cVekslNRec := comp (VekslGrpNRec);
  loRefin.wKOLMIN := VekslGrpCVal;
  loRefin.TopDate := date(1,1,2100);
  loFirstStavka.wKOLMIN := VekslGrpCVal;
  loFirstStavka.TopDate := date(1,1,2100);
  if (loVekslKred.GetFirst Veksl = tsOk) {};
end.
.fields
  Npp
  loVekslKred.KatOrgCreditor.Name
  loVekslKred.KatOrgCreditor.UNN
  if (КРЕД_ГРУП_Номер<>'', КРЕД_ГРУП_Номер + ', ', '') + КРЕД_ГРУП_Дата_Выписки
.endfields
 Ш @~@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ &&&&&&&&&&&&&&& @@@@@@@@@@@@@@@@@@@ Ш
.}
.{ CheckEnter RepKrGroupLoop
.}  // RepKrGroupLoop
.{ CheckEnter RepKrGrafLoop
.{?INTERNAL;(not IsFirst) and (График_ТипОперацииЭтапа = 0)
.{table 'loRefin' by Refin.DatRec;
.begin
  PrintRefin := False;
  Дата_Получ_Кредита := Дата_Получ_Кредита_Тек;
  Ставка_Рефинансирования := Ставка_Рефин_Новая;
  if (VekslGrpCVal = 0) Ставка := Ставка_Рефинансирования * (1.5);
  else Ставка := Ставка_Рефинансирования * 2;
  if ((loRefin.Refin.DATREC < График_Дата) and (loRefin.Refin.DATREC >= Дата_Получ_Кредита))
  {
    if (loRefin.Refin.DATREC = Дата_Получ_Кредита)
    {
      Ставка_Рефин_Новая := loRefin.Refin.Proc;
      Дата_Получ_Кредита := loRefin.Refin.DATREC;
      PrintRefin := False;
    }
    else
    {
      Срок_Использования := 0;
      Сумма_Вознагр_Ставка := 0;
      if ((loRefin.Refin.DATREC <> date(0,0,0)) and (Дата_Получ_Кредита <> date(0,0,0)))
        Срок_Использования := To_Days(loRefin.Refin.DATREC) - To_Days(Дата_Получ_Кредита);
      if (Срок_Использования <> 0)
        Сумма_Вознагр_Ставка := Сумма_Кредита * Ставка * 
                                ProcKDay( Дата_Получ_Кредита,Срок_Использования) / 360/100;
      if (VekslGrpCVal = 0)
        КРЕД_Итого_Сумма_Вознагр_Ставка:= КРЕД_Итого_Сумма_Вознагр_Ставка + Сумма_Вознагр_Ставка;
      else
        КРЕД_Итого_Сумма_Вознагр_Ставка:= КРЕД_Итого_Сумма_Вознагр_Ставка + 
          oValFunc.CurrencyExchange(GrafCVal,Сумма_Вознагр_Ставка,0,График_Дата); 
      PrintRefin := True;
      Ставка_Рефин_Новая := loRefin.Refin.Proc;
      Дата_Получ_Кредита_Тек := loRefin.Refin.DATREC;
    }
  }
end.
.{?INTERNAL;(PrintRefin)
.fields
  Дата_Получ_Кредита
  DoubleToStr(Сумма_Кредита, Формат_Сумм)
  Срок_Использования
  Ставка_Рефинансирования
  Ставка
  DoubleToStr(Сумма_Вознагр_Ставка, Формат_Сумм)
  if (VekslGrpCVal = 0,'',КРЕД_ГРУП_Валюта)
.endfields
 Ш                                                                                        &&&&&&&&&&&                 &&&&&&&&&&&&&&& &&&&&&&&&                   &&&&&&.&& &&&&&.&& &&&&&&&&&&&&&&& @@@@@ Ш
.}
.} //end table
.begin
  Дата_Получ_Кредита := Дата_Получ_Кредита_Тек;
  Срок_Использования := 0;
  Сумма_Вознагр_Ставка := 0;
  if ((График_Дата <> date(0,0,0)) and (Дата_Получ_Кредита <> date(0,0,0)))
    Срок_Использования := To_Days(График_Дата) - To_Days(Дата_Получ_Кредита);
  if (Срок_Использования <> 0)
    Сумма_Вознагр_Ставка := Сумма_Кредита * Ставка * 
                            ProcKDay( Дата_Получ_Кредита,Срок_Использования) / 360/100;
  if (VekslGrpCVal = 0) 
    КРЕД_Итого_Сумма_Вознагр_Ставка:= КРЕД_Итого_Сумма_Вознагр_Ставка + Сумма_Вознагр_Ставка;
  else
    КРЕД_Итого_Сумма_Вознагр_Ставка:= КРЕД_Итого_Сумма_Вознагр_Ставка + 
      oValFunc.CurrencyExchange(GrafCVal,Сумма_Вознагр_Ставка,0,График_Дата); 
  Ставка_Рефинансирования := Ставка_Рефин_Новая;
  if (VekslGrpCVal = 0) Ставка := Ставка_Рефинансирования * (1.5);
  else Ставка := Ставка_Рефинансирования * 2;

end.
.fields
  Дата_Получ_Кредита
  DoubleToStr(Сумма_Кредита, Формат_Сумм)
  Срок_Использования
  Ставка_Рефинансирования
  Ставка
  DoubleToStr(Сумма_Вознагр_Ставка, Формат_Сумм)
  if (VekslGrpCVal = 0,'',КРЕД_ГРУП_Валюта) 
.endfields
 Ш                                                                                        &&&&&&&&&&&                 &&&&&&&&&&&&&&& &&&&&&&&&                   &&&&&&.&& &&&&&.&& &&&&&&&&&&&&&&& @@@@@ Ш
.begin
  Дата_Получ_Кредита_Тек := График_Дата;
  Дата_Получ_Кредита     := График_Дата;
  Сумма_Кредита      := Сумма_Кредита + 
   Double (if ( double(График_СуммаВыдачи)<>0, График_СуммаВыдачи, - График_СуммаПогашения)); 
end.
.}// end not is first
.begin
  if IsFirst // формирование данных для первой строки
  {
    IsFirst:=False;
    Дата_Получ_Кредита := График_Дата;
    Сумма_Кредита      := Сумма_Кредита + 
     Double (if ( double(График_СуммаВыдачи)<>0, График_СуммаВыдачи, - График_СуммаПогашения)); 
    Срок_Использования := 0;
    Ставка_Рефинансирования := 0;
    Сумма_Вознагр_Ставка := 0;
    loFirstStavka.TopDate := График_Дата;
    if (loFirstStavka.GetFirst Refin = tsOk) 
      Ставка_Рефинансирования := loFirstStavka.Refin.Proc;
    loFirstStavka.TopDate := date(1,1,2100);       
    Ставка_Рефин_Новая := Ставка_Рефинансирования;
    if (VekslGrpCVal = 0) Ставка := Ставка_Рефинансирования * (1.5);
    else Ставка := Ставка_Рефинансирования * 2;
    Дата_Получ_Кредита_Тек := Дата_Получ_Кредита;
    LastPrint := True;
  }
  if (VekslGrpCVal = 0)
  {
    КРЕД_Итого_Сумма_Получ_Кред := КРЕД_Итого_Сумма_Получ_Кред + double(График_СуммаВыдачи);
    КРЕД_Итого_Сумма_Вознагр    := КРЕД_Итого_Сумма_Вознагр    + double(График_СуммаПогашенияПроцентов);
  }
  else
  {
    КРЕД_Итого_Сумма_Получ_Кред := КРЕД_Итого_Сумма_Получ_Кред     + 
      oValFunc.CurrencyExchange(GrafCVal,double(График_СуммаВыдачи),0,График_Дата);
    КРЕД_Итого_Сумма_Вознагр    := КРЕД_Итого_Сумма_Вознагр     + 
      oValFunc.CurrencyExchange(GrafCVal,double(График_СуммаПогашенияПроцентов),0,График_Дата);
  }
end.
.}
.{?INTERNAL;((LastPrint)and(Сумма_Кредита <> 0) and (График_ТипОперацииЭтапа = 0))
.fields
  Дата_Получ_Кредита
  DoubleToStr(Сумма_Кредита, Формат_Сумм)
  Срок_Использования
  Ставка_Рефинансирования
  Ставка
  DoubleToStr(Сумма_Вознагр_Ставка, Формат_Сумм)
  if (VekslGrpCVal = 0,'',КРЕД_ГРУП_Валюта)
.endfields
 Ш                                                                                        &&&&&&&&&&&                 &&&&&&&&&&&&&&& &&&&&&&&&                   &&&&&&.&& &&&&&.&& &&&&&&&&&&&&&&& @@@@@ Ш
.}
.{?INTERNAL;(LastPrint)
.begin
  LastPrint := False;
  Срок_Использования := 0;
  if (КРЕД_Итого_Сумма_Вознагр < КРЕД_Итого_Сумма_Вознагр_Ставка)
    КРЕД_Итого_Сумма_Вознагр_Подл_Выпл := КРЕД_Итого_Сумма_Вознагр;
  else
    КРЕД_Итого_Сумма_Вознагр_Подл_Выпл := КРЕД_Итого_Сумма_Вознагр_Ставка;
  Итого_Сумма_Получ_Кред     := Итого_Сумма_Получ_Кред     + КРЕД_Итого_Сумма_Получ_Кред;  
  Итого_Сумма_Вознагр        := Итого_Сумма_Вознагр        + КРЕД_Итого_Сумма_Вознагр;
  Итого_Сумма_Вознагр_Ставка := Итого_Сумма_Вознагр_Ставка + КРЕД_Итого_Сумма_Вознагр_Ставка;
  Итого_Сумма_Вознагр_Подл_Выпл := Итого_Сумма_Вознагр_Подл_Выпл + КРЕД_Итого_Сумма_Вознагр_Подл_Выпл;
end.
.fields
  DoubleToStr( КРЕД_Итого_Сумма_Получ_Кред     , Формат_Сумм )
  DoubleToStr( КРЕД_Итого_Сумма_Вознагр        , Формат_Сумм )
  DoubleToStr( КРЕД_Итого_Сумма_Вознагр_Ставка , Формат_Сумм )
  DoubleToStr( КРЕД_Итого_Сумма_Вознагр_Подл_Выпл , Формат_Сумм )
.endfields
 Ш       Итого по кредиту                                                                             &&&&&&&&&&&&.&&                           &&&&&&&&&&&&&&.&&                    &&&&&&&&&&&&.&& &&&&&&&&&&&&.&& Ш
.}
.{ CheckEnter RepKrPlatLoop
.}
.}  // RepKrMainLoop
.fields
  DoubleToStr( Итого_Сумма_Получ_Кред    , Формат_Сумм )
  DoubleToStr( Итого_Сумма_Вознагр       , Формат_Сумм )
  DoubleToStr( Итого_Сумма_Вознагр_Ставка, Формат_Сумм )
  DoubleToStr( Итого_Сумма_Вознагр_Подл_Выпл , Формат_Сумм )
.endfields
 Ш       Итого                                                                                        &&&&&&&&&&&&.&&                           &&&&&&&&&&&&&&.&&                    &&&&&&&&&&&&.&& &&&&&&&&&&&&.&& Ш

.EndForm

