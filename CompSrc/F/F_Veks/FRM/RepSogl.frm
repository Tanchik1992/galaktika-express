//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 6.00 - Векселя и кредиты
// Печать ведомости по вексельным соглашениям
//------------------------------------------------------------------------------

#doc
Печать ведомости по вексельным соглашениям
#end

.FORM RepSogl
.HIDE

! ==============================================================================
.fields

nRec_UserDeskRep : comp
Настройка_Группировки
Настройка_Сортировки
Формат_Сумм
VOсVal                     : comp      // ссылка на каталог валют на валюту отчета
Валюта_Отчета
Соглашения_в_валюте_отчета : boolean   // true если надо выводить в валюте отчета
!{RepSoglFilterLoop
Настройка_Фильтра
!}

!{ Главный цикл

! ------------------------------------------------------------------------------
!{  Группировка с итогами
!{RepSoglTotalLoop
  ИТОГО_Группировка_нРек : comp
  ИТОГО_Наименование_Группировки
  ИТОГО_Сумма_Плановая_НДЕ    : double
  ИТОГО_Сумма_Плановая_ВО     : double // в валюте отчета
  ИТОГО_Сумма_Фактическая_НДЕ : double
  ИТОГО_Сумма_Фактическая_ВО  : double // в валюте отчета
!}

! ------------------------------------------------------------------------------
!{  Группировка без итогов
!{RepSoglGroupLoop
  Группировка_нРек : comp
  Наименование_Группировки
!}

! ------------------------------------------------------------------------------
!{  Соглашение
!{RepSoglLoop
  SoglVeksNRec    : comp
  SoglVeksCStat   : comp      // Ссылка на KatKlass (статус)
  SoglVeksCOrg    : comp      // Ссылка на каталог организаций (собственная организация)
  SoglVeksCOrgOut : comp      // Организация, передающая векселя собственной организации
  SoglVeksCOrgIn  : comp      // Организация, получающая векселя от собственной организации
  SoglVeksCOtv    : comp      // Ссылка на каталог МОЛ
  SoglVeksCVal    : comp      // Ссылка на каталог валют

  Номер_Внешний
  Номер_Внутренний
  Дата_Составления
  Статус_Соглашения
  Наименование_Организации
  Наименование_Организации_Передает
  Наименование_Организации_Получает
  Ответственный
  Сумма_Плановая_НДЕ : double
  Сумма_Плановая_Вал : double
  Сумма_Плановая_ВО  : double
  Сумма_Фактическая_НДЕ : double
  Сумма_Фактическая_Вал : double
  Сумма_Фактическая_ВО  : double
  Валюта

  Примечание_1
  Примечание_2
  Примечание_3

!{  АПП из соглашения
!{RepSoglAppLoop
  AppNRec  : comp
  AppCStat : comp           // Ссылка на KatKlass (статус АПП)
  AppCVal  : comp           // Ссылка на каталог валют
  AppCOrg  : comp           // Ссылка на каталог организаций (контрагент)

  АПП_Тип  : word
  АПП_Наименование_Типа
  АПП_Статус
  АПП_Номер
  АПП_Дата
  АПП_Сумма_НДЕ : double
  АПП_Сумма_Вал : double
  АПП_Валюта
  АПП_Контрагент

!{  Векселя из АПП
!{RepSoglVekslLoop
  VekslNRec  : comp
  VekslCStat : comp         // Ссылка на KatNotes (статус векселя)
  VekslCVal  : comp         // Ссылка на каталог валют
  VekslCPlat : comp         // Ссылка на каталог организаций (векселедатель)

  Векс_Статус
  Векс_Дата_Составления
  Векс_Серия
  Векс_Номер
  Векс_Сумма_НДЕ
  Векс_Сумма_Вал
  Векс_Валюта
  Векс_Векселедатель
!}
!}
!}
!}
.endfields
! ==============================================================================

^ ^ ^ ^ ^ ^ ^
.{ CheckEnter RepSoglFilterLoop
^
.}

.{ RepSoglMainLoop

.{ CheckEnter RepSoglTotalLoop
^ ^ ^ ^ ^ ^
.}  // RepSoglTotalLoop

.{ CheckEnter RepSoglGroupLoop
^ ^
.}  // RepSoglGroupLoop

.{ CheckEnter RepSoglLoop
^ ^ ^ ^ ^ ^ ^
^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^
^ ^ ^
.{ CheckEnter RepSoglAppLoop
^ ^ ^ ^
^ ^ ^ ^ ^ ^ ^ ^ ^
.{ CheckEnter RepSoglVekslLoop
^ ^ ^ ^
^ ^ ^ ^ ^ ^ ^ ^
.}  // RepSoglVekslLoop
.}  // RepSoglAppLoop
.}  // RepSoglLoop

.}  // RepSoglMainLoop

.ENDFORM

! ==============================================================================
.LINKFORM RepSogl_00 PROTOTYPE IS RepSogl
.NameInList 'Ведомость по вексельным соглашениям'
.p 60
.defo landscape
.var
 FirstLine : boolean
.endvar
.fields
  CommonFormHeader

  Настройка_Группировки
  Настройка_Сортировки
  Настройка_Фильтра
.endfields
^

                                                         БВЕДОМОСТЬ ПО ВЕКСЕЛЬНЫМ СОГЛАШЕНИЯМ Б

.{?Internal;Настройка_Группировки!='';
Группировка: ^
.}
.{?Internal;Настройка_Сортировки!='';
Сортировка:  ^
.}
.{ CheckEnter RepSoglFilterLoop
.[h
Фильтры:
.]h
^
.}

.{ RepSoglMainLoop
.[h
───────────┬──────────┬────────────┬─────────────────┬─────────────────┬──────┬───────────────────────────────────────────────────────────────────────────
   Номер   │   Дата   │   Статус   │Сумма фактическая│ Сумма плановая  │Валюта│               Акты приема-передачи по вексельным соглашениям
           │          │            │                 │                 │      ├───────────┬──────────┬─────────┬─────────────────┬─────────────────┬──────
           │          │            │                 │                 │      │   Номер   │   Дата   │   Тип   │Сумма поступления│ Сумма передачи  │Валюта
───────────┴──────────┴────────────┴─────────────────┴─────────────────┴──────┴───────────┴──────────┴─────────┴─────────────────┴─────────────────┴──────
.]h
.{ CheckEnter RepSoglTotalLoop
.fields
  ИТОГО_Наименование_Группировки
  DoubleToStr( ИТОГО_Сумма_Фактическая_НДЕ, Формат_Сумм )
  DoubleToStr( ИТОГО_Сумма_Плановая_НДЕ,    Формат_Сумм )
  DoubleToStr( ИТОГО_Сумма_Фактическая_ВО,  Формат_Сумм )
  DoubleToStr( ИТОГО_Сумма_Плановая_ВО,     Формат_Сумм )
  Валюта_Отчета
.endfields
 Б@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ &'&&&&&&&&&&&&.&& &'&&&&&&&&&&&&.&& Б
                                     Б&'&&&&&&&&&&&&.&& &'&&&&&&&&&&&&.&& @@@@@@ Б
.}
.{ CheckEnter RepSoglGroupLoop
.fields
  Наименование_Группировки
.endfields
 Б^ Б
.}
.{ CheckEnter RepSoglLoop
.begin
  firstLine := true;
end.
.{ CheckEnter RepSoglAppLoop
.fields
  if(firstLine, Номер_Внутренний, '')
  if(firstLine, Дата_Составления, '')
  if(firstLine, Статус_Соглашения, '')
  if(firstLine, DoubleToStr( if( SoglVeksCVal = 0, Сумма_Фактическая_НДЕ, if( Соглашения_в_валюте_отчета, Сумма_Фактическая_ВО, Сумма_Фактическая_Вал) ), Формат_Сумм), '')
  if(firstLine, DoubleToStr( if( SoglVeksCVal = 0, Сумма_Плановая_НДЕ,    if( Соглашения_в_валюте_отчета, Сумма_Плановая_ВО,    Сумма_Плановая_Вал) ), Формат_Сумм), '')
  if(firstLine, if( SoglVeksCVal = 0, '', if( Соглашения_в_валюте_отчета, Валюта_Отчета, Валюта)), '')

  АПП_Номер
  АПП_Дата
  АПП_Наименование_Типа
  if( АПП_Тип = word(82) or АПП_Тип = word(84), DoubleToStr( if(APPCVal = 0, АПП_Сумма_НДЕ, АПП_Сумма_Вал), Формат_Сумм), '')
  if( АПП_Тип = word(81) or АПП_Тип = word(83), DoubleToStr( if(APPCVal = 0, АПП_Сумма_НДЕ, АПП_Сумма_Вал), Формат_Сумм), '')
  АПП_Валюта
.endfields
@@@@@@@@@@@ @@@@@@@@@@ @@@@@@@@@@@@ &'&&&&&&&&&&&&.&& &'&&&&&&&&&&&&.&& @@@@@@ @@@@@@@@@@@ @@@@@@@@@@ @@@@@@@@@ &'&&&&&&&&&&&&.&& &'&&&&&&&&&&&&.&& @@@@@@
.begin
  firstLine := false;
end.
.{ CheckEnter RepSoglVekslLoop
.}
.}
! Если к соглашению не было АПП, то соглашение пока еще не выведено - надо выводить !
.fields
  if(firstLine, Номер_Внутренний, '')
  if(firstLine, Дата_Составления, '')
  if(firstLine, Статус_Соглашения, '')
  if(firstLine, DoubleToStr( if( SoglVeksCVal = 0, Сумма_Фактическая_НДЕ, if( Соглашения_в_валюте_отчета, Сумма_Фактическая_ВО, Сумма_Фактическая_Вал) ), Формат_Сумм), '')
  if(firstLine, DoubleToStr( if( SoglVeksCVal = 0, Сумма_Плановая_НДЕ,    if( Соглашения_в_валюте_отчета, Сумма_Плановая_ВО,    Сумма_Плановая_Вал) ), Формат_Сумм), '')
  if(firstLine, if( SoglVeksCVal = 0, '', if( Соглашения_в_валюте_отчета, Валюта_Отчета, Валюта)), '')
.endfields
.{?Internal;firstLine;
@@@@@@@@@@@ @@@@@@@@@@ @@@@@@@@@@@@ &'&&&&&&&&&&&&.&& &'&&&&&&&&&&&&.&& @@@@@@
.}
.}
.}
.ENDFORM

