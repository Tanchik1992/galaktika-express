//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 5.85 - Основные средства
// Разность данных по амортизации по методам учета
// Внимание! Существует slk-отчет в формате Excel
//------------------------------------------------------------------------------

#doc
Разность данных по амортизации по методам учета
#end

.set name = 'AmortSee'
.hide
.fields

  BegDate      : date
  EndDate      : date

  Фильтр
  Формат_Сумм : string // Формат содержащийся в рфыекщйках пользователя UserDeskRep.ResName[1]

  //---------------------------------------------
  fMetodName1 // исходный метод учета
  fMetodName2 // метод учета для сравнения

  //---------------------------------------------
  // группировки с итогами
  Node_Name

  Node_SIznM1  // сумма износа за месяц по исходному методу учета
  Node_Stoim1  // стоимость объекта по исходному методу учета
  Node_SumIzn1 // сумма износа объекта по исходному методу учета

  Node_SIznM2  // сумма износа за месяц по методу учета для сравнения
  Node_Stoim2  // стоимость объекта по методу учета для сравнения
  Node_SumIzn2 // сумма износа объекта по методу учета для сравнения

  Node_Raznost // общая разница по суммам износа за месяц
  Node_PR      // постоянная разность
  Node_VVRNach // начисление вычитаемой временной разности (ВВР)
  Node_VVRPog  // погашение ВВР
  Node_NVRNach // начисление налогооблагаемой временной разности (НВР)
  Node_NVRPog  // погашение НВР
  Node_ValName
  //---------------------------------------------
  // исходные строки отчета
  Leaf_Name

  Leaf_InvNumber
  Leaf_Period : date // период (месяц) сравниваемых данных

  Leaf_SIznM1     // сумма износа за месяц по исходному методу учета
  Leaf_Stoim1     // стоимость объекта по исходному методу учета
  Leaf_SumIzn1    // сумма износа объекта по исходному методу учета
  Leaf_Algoritm1  // Наименование алгоритма начисления амортизации
  Leaf_TXO1       // Наименование ТХО по амортизации

  Leaf_SIznM2     // сумма износа за месяц по методу учета для сравнения
  Leaf_Stoim2     // стоимость объекта по методу учета для сравнения
  Leaf_SumIzn2    // сумма износа объекта по методу учета для сравнения
  Leaf_Algoritm2  // Наименование алгоритма начисления амортизации
  Leaf_TXO2       // Наименование ТХО по амортизации

  Leaf_Raznost // общая разность
  Leaf_PR      // постоянная разность
  Leaf_VVRNach // начисление вычитаемой временной разности (ВВР)
  Leaf_VVRPog  // погашение ВВР
  Leaf_NVRNach // начисление налогооблагаемой временной разности (НВР)
  Leaf_NVRPog  // погашение НВР

  Leaf_ValName
  //---------------------------------------------
  Leaf_wTiDk     : word  // тип (ОС/НМА)
  Leaf_cKatOS    : comp  // ссылка на карточку ОС
  Leaf_DateK     : date  // дата ввода в эксплуатацию
!  // по исходному методу учета
  Leaf_cSpKatOS1 : comp     // ссылка на SpKatOS
  Leaf_SrokIsp1  : double   // Срок полезного использования (в месяцах)
  Leaf_STOIM_1   : double   // Первоначальная стоимость
  Leaf_SIZNM_1   : double   // Износ за месяц EndDate
  Leaf_SUMIZN_1  : double   // cумма износа за период BegDate-EndDate
  Leaf_OstSTOIM_1: double   // остаточная стоимость на EndDate
! // по методу учета для сравнения
  Leaf_cSpKatOS2 : comp     // ссылка на SpKatOS
  Leaf_SrokIsp2  : double   // Срок полезного использования (в месяцах)
  Leaf_STOIM_2   : double   // Первоначальная стоимость
  Leaf_SIZNM_2   : double   // Износ за месяц EndDate
  Leaf_SUMIZN_2  : double   // cумма износа за период BegDate-EndDate
  Leaf_OstSTOIM_2: double   // остаточная стоимость на EndDate

  Leaf_VVR_Stoim : double   // ВВР первоначальной стоимости
  Leaf_NVR_Stoim : double   // НВР первоначальной стоимости
  Leaf_VVR_Inp   : double   // ВВР на BegDate
  Leaf_NVR_Inp   : double   // НВР на BegDate
  Leaf_PR_Inp    : double   // ПР  на BegDate
  Leaf_VVR_Period: double   // ВВР за период BegDate-EndDate
  Leaf_NVR_Period: double   // НВР за период BegDate-EndDate
  Leaf_PR_Period : double   // ПР  за период BegDate-EndDate
  Leaf_Vyb_Period_1 : double   // выбытие по исходному методу учета за период BegDate-EndDate
  Leaf_Vyb_Period_2 : double   // выбытие по методу учета для сравнения за период BegDate-EndDate
  //---------------------------------------------
.endfields
! Подкачка Excel форматов
! Эта строчка нужна пока не будет решена 102.31867
#include ExcelFormat.frn
.{
     BegDate        ^
     EndDate        ^
.{ AmortSee_Filter CheckEnter
^
.}
     Формат_Сумм    ^
     fMetodName1    ^
     fMetodName2    ^
.{
.{ AmortSee_Header CheckEnter
     Node_Name      ^

     Node_SIznM1    ^
     Node_Stoim1    ^
     Node_SumIzn1   ^

     Node_SIznM2    ^
     Node_Stoim2    ^
     Node_SumIzn2   ^

     Node_Raznost   ^
     Node_PR        ^
     Node_VVRNach   ^
     Node_VVRPog    ^
     Node_NVRNach   ^
     Node_NVRPog    ^
     Node_ValName   ^
.}
.{ AmortSee_Body CheckEnter
     Leaf_Name      ^

     Leaf_InvNumber ^
     Leaf_Period    ^

     Leaf_SIznM1    ^
     Leaf_Stoim1    ^
     Leaf_SumIzn1   ^
     Leaf_Algoritm1 ^
     Leaf_TXO1      ^

     Leaf_SIznM2    ^
     Leaf_Stoim2    ^
     Leaf_SumIzn2   ^
     Leaf_Algoritm2 ^
     Leaf_TXO2      ^

     Leaf_Raznost   ^
     Leaf_PR        ^
     Leaf_VVRNach   ^
     Leaf_VVRPog    ^
     Leaf_NVRNach   ^
     Leaf_NVRPog    ^
     Leaf_ValName   ^
     //--------------------------
     Leaf_wTiDk     ^
     Leaf_cKatOS    ^
     Leaf_DateK     ^
     Leaf_cSpKatOS1 ^
     Leaf_SrokIsp1  ^
     Leaf_STOIM_1    ^
     Leaf_SIZNM_1    ^
     Leaf_SUMIZN_1   ^
     Leaf_OstSTOIM_1 ^
     Leaf_cSpKatOS_2 ^
     Leaf_SrokIsp_2  ^
     Leaf_STOIM_2    ^
     Leaf_SIZNM_2    ^
     Leaf_SUMIZN_2   ^
     Leaf_OstSTOIM_2 ^
     Leaf_VVR_Stoim  ^
     Leaf_NVR_Stoim  ^
     Leaf_VVR_Inp    ^
     Leaf_NVR_Inp    ^
     Leaf_PR_Inp     ^
     Leaf_VVR_Period ^
     Leaf_NVR_Period ^
     Leaf_PR_Period  ^
     Leaf_Vyb_Period_1 ^
     Leaf_Vyb_Period_2 ^
.}
.{ AmortSee_Footer CheckEnter
.}
.{ AmortSee_Line CheckEnter
────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
.}
.}
.}
.endform

//*******************************************************************
.linkform 'AmortSee01' prototype is 'AmortSee'
.nameinlist 'Разность данных по амортизации по методам учета'
.group 'Разность данных по амортизации'
.p 60
.defo landscape
.fields
  CommonFormHeader

  DateToStr(BegDate,'Mon YYYY')
  DateToStr(EndDate,'Month YYYY')

  Фильтр

  fMetodName1
  fMetodName2

  //---------------------------------------------
  // узел
  Node_Name

  DoubleToStr(double(Node_SIznM1), Формат_Сумм)
  DoubleToStr(double(Node_SIznM2), Формат_Сумм)

  DoubleToStr(double(Node_Raznost), Формат_Сумм)

  Node_ValName
  //---------------------------------------------
  // лист
  Leaf_Name

  Leaf_InvNumber // +
  Leaf_Period    // +

  DoubleToStr(double(Leaf_SIznM1), Формат_Сумм)
  Leaf_Algoritm1
  DoubleToStr(double(Leaf_SIznM2), Формат_Сумм)
  Leaf_Algoritm2

  DoubleToStr(double(Leaf_Raznost), Формат_Сумм)

  Leaf_ValName
  //---------------------------------------------

.endfields
 Р^
 Б                                                                  Разность данных по амортизации по методам учета С Р
.{
                                                                     за период с ^ по ^
                                                                                                                                                                                  лист @np@
Фильтры:
.{ AmortSee_Filter CheckEnter
 ^
.}
.{
.[H

─────────────────────────────────────────────┬───────────┬──────────┬─────────────────────────────────────────────┬─────────────────────────────────────────────┬───────────────────┬────────
                 Наименование                │Инвентарный│  Период  │@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│      Разность     │ Валюта
                                             │   номер   │          ├───────────────────┬─────────────────────────┼───────────────────┬─────────────────────────┤                   │
                                             │           │          │       Износ       │   Алгоритм начисления   │       Износ       │   Алгоритм начисления   │                   │
                                             │           │          │                   │       амортизации       │                   │       амортизации       │                   │
─────────────────────────────────────────────┴───────────┴──────────┴───────────────────┴─────────────────────────┴───────────────────┴─────────────────────────┴───────────────────┴────────
.]H
.{ AmortSee_Header CheckEnter
 Б@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                        &'&&&&&&&&&&&&&&&&&                           &'&&&&&&&&&&&&&&&&&                           &'&&&&&&&&&&&&&&&&& @@@@@@@ С Р
.}
.{ AmortSee_Body CheckEnter
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@ @~@@@@@@@@ &'&&&&&&&&&&&&&&&&& @@@@@@@@@@@@@@@@@@@@@@@@@ &'&&&&&&&&&&&&&&&&& @@@@@@@@@@@@@@@@@@@@@@@@@ &'&&&&&&&&&&&&&&&&& @@@@@@@
.}
.{ AmortSee_Footer CheckEnter

.}
.{ AmortSee_Line CheckEnter
─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
.}
.}
.[F
─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
.]F
.}
 Р
.endform

//*******************************************************************
//*******************************************************************
.linkform 'AmortSee02' prototype is 'AmortSee'
.nameinlist 'Постоянная и временная разница данных по амортизации'
.group 'Разность данных по амортизации <<Россия>>'
.p 60
.defo landscape
.var
  Node_VVRNachItog : double;
  Node_VVRPogItog  : double;
  Node_NVRNachItog : double;
  Node_NVRPogItog  : double;
.endvar
.fields
  CommonFormHeader

  DateToStr(BegDate,'Mon YYYY')
  DateToStr(EndDate,'Month YYYY')

  Фильтр

  SubStr(fMetodName1,1,19)
  SubStr(fMetodName2,1,19)

  //---------------------------------------------
  // узел
  Node_Name

  DoubleToStr(double(Node_SIznM1), Формат_Сумм)
  DoubleToStr(double(Node_SIznM2), Формат_Сумм)

  DoubleToStr(double(Node_Raznost), Формат_Сумм)
  DoubleToStr(double(Node_PR     ), Формат_Сумм)
  DoubleToStr(double(Node_VVRNach), Формат_Сумм)
  DoubleToStr(double(Node_VVRPog ), Формат_Сумм)
  DoubleToStr(double(Node_NVRNach), Формат_Сумм)
  DoubleToStr(double(Node_NVRPog ), Формат_Сумм)
  Node_ValName
  if(Node_VVRNachItog = 0,'',DoubleToStr(Node_VVRNachItog, Формат_Сумм))
  if(Node_VVRPogItog  = 0,'',DoubleToStr(Node_VVRPogItog,  Формат_Сумм))
  if(Node_NVRNachItog = 0,'',DoubleToStr(Node_NVRNachItog, Формат_Сумм))
  if(Node_NVRPogItog  = 0,'',DoubleToStr(Node_NVRPogItog,  Формат_Сумм))
  //---------------------------------------------
  // лист
  Leaf_Name

  Leaf_InvNumber
  DateToStr(Leaf_Period,'MM.YYYY')

  DoubleToStr(double(Leaf_SIznM1), Формат_Сумм)
  DoubleToStr(double(Leaf_SIznM2), Формат_Сумм)

  DoubleToStr(double(Leaf_Raznost), Формат_Сумм)
  DoubleToStr(double(Leaf_PR     ), Формат_Сумм)
  DoubleToStr(double(Leaf_VVRNach), Формат_Сумм)
  DoubleToStr(double(Leaf_VVRPog ), Формат_Сумм)
  DoubleToStr(double(Leaf_NVRNach), Формат_Сумм)
  DoubleToStr(double(Leaf_NVRPog ), Формат_Сумм)

  Leaf_ValName
  //---------------------------------------------

.endfields
 Ш^
 Й                                                           Разница данных по амортизации по методам учета
.{
                                                              за период с ^ по ^ Й
Фильтры:
.{ AmortSee_Filter CheckEnter
^
.}
.{
.[H
                                                                                                                                                                                                                       лист @np@
───────────────────────────────────────────┬───────────┬───────┬───────────────────┬───────────────────┬──────────────────┬──────────────────┬─────────────────────────────────────┬─────────────────────────────────────┬──────
              Наименование                 │Инвентарный│Период │@~@@@@@@@@@@@@@@@@@│@~@@@@@@@@@@@@@@@@@│     Разница      │    Постоянная    │        Вычитаемая временная         │     Налогооблагаемая временная      │Валюта
                                           │   номер   │       │                   │                   │                  │     разница      │               разница               │               разница               │
                                           │           │       │       Износ       │       Износ       │                  │                  ├──────────────────┬──────────────────┼──────────────────┬──────────────────┤
                                           │           │       │                   │                   │                  │                  │    Начисление    │    Погашение     │    Начисление    │    Погашение     │
───────────────────────────────────────────┴───────────┴───────┴───────────────────┴───────────────────┴──────────────────┴──────────────────┴──────────────────┴──────────────────┴──────────────────┴──────────────────┴──────
.]H
.{ AmortSee_Header CheckEnter
.begin
  if double(Node_VVRNach)*(double(Node_VVRNach)+double(Node_VVRPog)) > 0
  {
    Node_VVRNachItog := double(Node_VVRNach)+double(Node_VVRPog);
    Node_VVRPogItog  := 0;
  }
  else
  {
    Node_VVRNachItog := 0;
    Node_VVRPogItog  := double(Node_VVRNach)+double(Node_VVRPog);
  }

  if double(Node_NVRNach)*(double(Node_NVRNach)+double(Node_NVRPog)) > 0
  {
    Node_NVRNachItog := double(Node_NVRNach)+double(Node_NVRPog);
    Node_NVRPogItog  := 0;
  }
  else
  {
    Node_NVRNachItog := 0;
    Node_NVRPogItog  := double(Node_NVRNach)+double(Node_NVRPog);
  }
end.
 Б@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ &'&&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& @@@@@@
                                                                                                                                        сумма &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& Щ Ш
.}
.{ AmortSee_Body CheckEnter
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@ @~@@@@@ &'&&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& @@@@@@
.}
.{ AmortSee_Footer CheckEnter

.}
.{ AmortSee_Line CheckEnter
────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
.}
.}
.[F
───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
.]F
.} И Р
.endform

//*******************************************************************

//*******************************************************************
.linkform 'AmortSee03' prototype is 'AmortSee'
.nameinlist 'Реестр разниц данных по амортизации по методам учета'
.group 'Реестр разниц данных по амортизации'
.p 60
.defo landscape
.var
  MrkKatOS : longint;
  IndNo    : longint;
.endvar
.fields
  CommonFormHeader

  DateToStr(BegDate,'Mon YYYY')
  DateToStr(EndDate,'Month YYYY')

  Фильтр

  fMetodName1
  fMetodName2
.endfields
 Ш^
                                                                  Реестр разниц данных по амортизации по методам учета
.{
.begin
  MrkKatOS := InitMarker('PrnReestr_ByRazn', 8, 100, 100, True);
  ClearMarker(MrkKatOS);
end.
                                                                         за период с ^ по ^
Фильтры:
.{ AmortSee_Filter CheckEnter
 ^
.}
БУ - бухгалтерский учет, метод - @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
НУ - налоговый учет    , метод - @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
────────┬──────┬─────────────────────────┬─────────────────────────────────┬────────────────────────────┬──────────────────┬───────────────────────────┬─────────────────────────┬──────────────────────────────────────┬─────────────────────────
        │      │                         │    Первоначальная  стоимость    │Разница между первоначальной│  Срок полезного  │   Амортизация за период   │    На начало периода    │               За период              │    На конец периода
        │      │                         │                                 │    стоимостью за период    │использования(мес)│                           │                         │                                      │
  Дата  │ Инв. │      Наименование       ├────────────────┬────────────────┼──────────────┬─────────────┼─────────┬────────┼─────────────┬─────────────┼────────────┬────────────┼────────────┬────────────┬────────────┼────────────┬────────────
  ввода │  №   │                         │                │                │              │             │         │        │             │             │ вычитаемые │ налогообла │ вычитаемые │ налогообла │ постоянные │ вычитаемые │ налогообла
        │      │                         │       БУ       │       НУ       │      ВВР     │     НВР     │    БУ   │   НУ   │      БУ     │      НУ     │  временные │ гаемые вре │  временные │ гаемые вре │  разницы   │  временные │ гаемые вре
        │      │                         │                │                │              │             │         │        │             │             │  разницы   │ менные раз │  разницы   │ менные раз │            │  разницы   │ менные раз
        │      │                         │                │                │              │             │         │        │             │             │   (ВВР)    │ ницы (НВР) │   (ВВР)    │ ницы (НВР) │            │   (ВВР)    │ ницы (НВР)
────────┼──────┼─────────────────────────┼────────────────┼────────────────┼──────────────┼─────────────┼─────────┼────────┼─────────────┼─────────────┼────────────┼────────────┼────────────┼────────────┼────────────┼────────────┼────────────
    1   │   2  │            3            │        4       │        5       │       6      │       7     │    8    │    9   │      10     │      11     │      12    │     13     │      14    │     15     │     16     │     17     │     18
────────┴──────┴─────────────────────────┴────────────────┴────────────────┴──────────────┴─────────────┴─────────┴────────┴─────────────┴─────────────┴────────────┴────────────┴────────────┴────────────┴────────────┴────────────┴────────────
.{
.[H skip
.fields
  DateToStr(BegDate,'Mon YYYY')
  DateToStr(EndDate,'Month YYYY')
.endfields
                                                                  Реестр разниц данных по амортизации по методам учета
                                                                         за период с ^ по ^
                                                                                                                                                                                                           лист @np@
────────┬──────┬─────────────────────────┬────────────────┬────────────────┬──────────────┬─────────────┬─────────┬────────┬─────────────┬─────────────┬────────────┬────────────┬────────────┬────────────┬────────────┬────────────┬────────────
    1   │   2  │            3            │        4       │        5       │       6      │       7     │    8    │    9   │      10     │      11     │      12    │     13     │      14    │     15     │     16     │     17     │     18
────────┴──────┴─────────────────────────┴────────────────┴────────────────┴──────────────┴─────────────┴─────────┴────────┴─────────────┴─────────────┴────────────┴────────────┴────────────┴────────────┴────────────┴────────────┴────────────
.]H
.{ AmortSee_Header CheckEnter
.}
.{ AmortSee_Body CheckEnter
.{?Internal;(Leaf_cKatOS <> comp(0) AND not SearchMarker(MrkKatOS, Leaf_cKatOS, IndNo));
.begin
  InsertMarker(MrkKatOS, Leaf_cKatOS);
end.
.fields
  if ( longint(Leaf_DateK) > 0, DateToStr(Leaf_DateK,'DD/MM/YY'), '' )
  Leaf_InvNumber
  Leaf_Name     
  DoubleToStr(Leaf_Stoim_1  , Формат_Сумм)
  DoubleToStr(Leaf_Stoim_2  , Формат_Сумм)
  DoubleToStr(Leaf_VVR_Stoim, Формат_Сумм)
  DoubleToStr(Leaf_NVR_Stoim, Формат_Сумм)
  DoubleToStr(Leaf_SrokIsp1 , Формат_Сумм)
  DoubleToStr(Leaf_SrokIsp2 , Формат_Сумм)
  DoubleToStr(Leaf_SumIzn_1 , Формат_Сумм)
  DoubleToStr(Leaf_SumIzn_2 , Формат_Сумм)
  DoubleToStr(Leaf_VVR_Inp  , Формат_Сумм)
  DoubleToStr(Leaf_NVR_Inp  , Формат_Сумм)
  DoubleToStr(Leaf_VVR_Period, Формат_Сумм)
  DoubleToStr(Leaf_NVR_Period, Формат_Сумм)
  DoubleToStr(Leaf_PR_Period , Формат_Сумм)
  DoubleToStr((Leaf_VVR_Inp + Leaf_VVR_Period), Формат_Сумм)
  DoubleToStr((Leaf_NVR_Inp + Leaf_NVR_Period), Формат_Сумм)
.endfields
@@@@@@@@│@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@│&&&&&&&&&&&&&&&&│&&&&&&&&&&&&&&&&│&&&&&&&&&&&&&&│&&&&&&&&&&&&&│&&&&&&&&&│&&&&&&&&│&&&&&&&&&&&&&│&&&&&&&&&&&&&│&&&&&&&&&&&&│&&&&&&&&&&&&│&&&&&&&&&&&&│&&&&&&&&&&&&│&&&&&&&&&&&&│&&&&&&&&&&&&│&&&&&&&&&&&&
.}
.}
.{ AmortSee_Footer CheckEnter
.}
.{ AmortSee_Line CheckEnter
──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
.}
.}
.begin
  DoneMarker(MrkKatOS, '');
end.
.[F
──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
.]F
.} Ш

.endform
