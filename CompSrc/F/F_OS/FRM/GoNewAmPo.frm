//------------------------------------------------------------------------------
//                                                      (c) Корпорация ГАЛАКТИКА
// Галактика 8.1 - Бухгалтерский контур
// Ведомость перевода на новую амортизационную политику
//------------------------------------------------------------------------------
#doc
  Форма для печати ведомости перевода на новую амортизационную политику
#end
.set name='GoNewAmPo'
.nameinlist 'Общая базовая форма'
.hide //скрыт
.fields
   Тип_Документа                                          : string
   Дата_формирования_документа                            : date
   NastrOS_Nrec                                           : comp
!
!//////////////////////
!  Цикл по карточкам
   KatOS_Nrec                                             : comp    // Действует в текущем отчетном периоде
   ArcIznos_Nrec                                          : comp    // Действует в отчетном периоде из архива
   ArcIznos2_Nrec_Предыдущего_периода                     : comp    // Предыдущего периода
!
   Простой_объект                                         : word    //    isLeaf
   Инвентарный_номер                                      : string  //  1 GetKatOsInNum(KatOS.nRec)
   Наименование                                           : string  //  2 KatOS.NameOS
   Дата_ввода_в_эксплуатацию                              : date    //  3 KatOS.DatEK
   Стоимость_предыдущего_периода                          : double  //  4 ArcIznos2.Stoim
   Сумма_износа_предыдущего_периода                       : double  //  4 ArcIznos2.SumIzn или ArcIznos2.SumIzn + ArcIznos2.SIznM
   Стоимость_предыдущего_периода_2                        : double
   Сумма_износа_предыдущего_периода_2                     : double
   Код_предыдущего_периода                                : string  //  5 VidNorma2.Kod
   Норма_предыдущего_периода                              : double  //  6 VidNorma2.Norma
   Дата_поступления                                       : date    //  7 KatOS.DatOK
   Код                                                    : string  //  8 VidNorma.Kod
   Норма                                                  : double  //  9 VidNorma.Norma
   Нормативный_срок_использования                         : double  // 10 VidNorma.SrokIsp
   Сумма_аммортизации_за_месяц                            : double  // 11 SpKatOS.SIznM
!                                                                   // В зависимости от того текущий период или период из архива берем соответственно SpKatOS или Arciznos
   Первоначальная_стоимость                               : double    // SpKatOS.Stoim                  или Arciznos.Stoim
   Износ_за_месяц                                         : double    // SpKatOS.SIznM                  или Arciznos.SIznM
   Сумма_износа                                           : double    // SpKatOS.SumIzn + SpKatOS.SIznM или Arciznos.SumIzn + Arciznos.SIznM
!
   Поправочный_коофициент_из_KatOS_предыдущего_периода    : double  //    ArcIznos2.KoofSm
   Поправочный_коофициент_из_VidNorma_предыдущего_периода : double  //    ArcIznos2.KoofYsk
   Коофициент_индексации_предыдущего_периода              : double  //    ArcIznos2.KoofIz
   Поправочный_коофициент                                 : double  //    SpKatOS.PoprKoef или ArcIznos.KoofSm
   Поправочный_коофициент_из_вида                         : double  //    VidNorma.Norma   или ArcIznos.KoofYsk
   Коофициент_индексации_амортизационных_отчислений       : double  //    NastrOs.KIAO     или ArcIznos.KoofIz
   Код_группы                                             : string  //    GrOs.KodGr
   Имя_группы                                             : string  //    GrOs.NameGr
   Срок_полезногго_использования                          : double  //    SpKatOS.SrokIsp или ArcIznos.SrokIsp
   NameMol                                                : string
   Срок_использования_после_поступления                   : double  //  SpKastOS.IspCur
   Срок_использования_до_поступления                      : double  //  SpKastOS.IspPs
   ArcIznos_Срок_использования_до_поступления             : double  //  ArcIznos.IspCur
.endfields
! Подкачка Excel форматов
! Эта строчка нужна пока не будет решена 102.31867
#include ExcelFormat.frn
.{
  ^
  ^
  ^
.{
  ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^
.}
.}
.endform

.linkform 'GoNewAmPo_01' prototype is 'GoNewAmPo'
.nameinlist 'Ведомость перевода на новую амортизационную политику'
.fields
  'перевода '+ Тип_Документа + ' на новую амортизационную политику'
   Дата_формирования_документа

   Инвентарный_номер                                                       //  1
   Наименование                                                            //  2
   DateToStr(Дата_ввода_в_эксплуатацию, 'MM/YYYY')                         //  3
   Стоимость_предыдущего_периода - Сумма_износа_предыдущего_периода        //  4
   Код_предыдущего_периода                                                 //  5
   DoubleToStr(((1 / Норма_предыдущего_периода) * 100),'66666666')         //  6
   Months_Between (Дата_формирования_документа, Дата_поступления) - 1      //  7
   Код                                                                     //  8
   Нормативный_срок_использования                                          //  9
   Нормативный_срок_использования - (Months_Between (Дата_формирования_документа, Дата_поступления) - 1) // 10
   Сумма_аммортизации_за_месяц                                             // 11
.endfields
.{
 Б                                       Ведомость
           @~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
                при использовании линейного способа начисления амортизации Б
 Р───────────┬──────────────────────────────┬─────────┬──────────────────┬─────────────────────┬───────────┬─────────────────────┬─────────┬────────────
           │                              │   Дата  │                  │ По постановлению СМ │   Срок    │ По постановлению СМ │ Остато- │   Сумма
Инвентарный│         Наименование         │ ввода в │    Остаточная    │   от 3.01.91 г. N4  │  службы,  │ от 16.11.01г. N1668 │  чный   │  аморти-
   номер   │            обьекта           │ эксплуа-│   стоимость на   ├───────┬─────────────┤фактическое├───────┬─────────────┤  срок   │   зации
           │                              │  тацию, │   @@@@@@@@@@г.   │ Шифр  │ Нормативный │  кол-во   │ Шифр  │ Нормативный │ службы  │  за месяц
           │                              │месяц,год│                  │       │ срок службы │  месяцев  │       │ срок службы │ гр9-гр7 │  гр4.гр10
───────────┼──────────────────────────────┼─────────┼──────────────────┼───────┼─────────────┼───────────┼───────┼─────────────┼─────────┼────────────
    1      │               2              │    3    │        4         │   5   │       6     │     7     │   8   │      9      │   10    │     11
───────────┴──────────────────────────────┴─────────┴──────────────────┴───────┴─────────────┴───────────┴───────┴─────────────┴─────────┴──────────── Р
.{
 Р@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  @@@@@@@  &&&&&&&&&&&&&&&.&& @@@@@@@  &&&&&&&&&&&   &&&&&&&&&  @@@@@@@  &&&&&&&&&&&  &&&&&&&&& &&&&&&&&&.&& Р
.}
 Р────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────── Р
.}
.endform

.linkform 'GoNewAmPo_02' prototype is 'GoNewAmPo'
.nameinlist 'Ведомость перевода на новую амортизационную политику с учетом переоценки'
.fields
  'перевода '+ Тип_Документа + ' на новую амортизационную политику'
   Дата_формирования_документа

   Инвентарный_номер                                                                                     //  1
   Наименование                                                                                          //  2
   DateToStr(Дата_ввода_в_эксплуатацию, 'MM/YYYY')                                                       //  3
   Стоимость_предыдущего_периода_2 - Сумма_износа_предыдущего_периода_2                                  //  4
   Код_предыдущего_периода                                                                               //  5
   DoubleToStr(((1 / Норма_предыдущего_периода) * 100),'66666666')                                       //  6
   Months_Between (Дата_формирования_документа, Дата_поступления) - 1                                    //  7
   Код                                                                                                   //  8
   Нормативный_срок_использования                                                                        //  9
   Нормативный_срок_использования - (Months_Between (Дата_формирования_документа, Дата_поступления) - 1) // 10
   Сумма_аммортизации_за_месяц                                                                           // 11
   Срок_полезногго_использования                                                                         // 12
   Первоначальная_стоимость                                                                              // 13
   Срок_полезногго_использования - (Months_Between(Дата_формирования_документа,Дата_поступления) - 1)    // 14
   NameMol                                                                                               // 15
.endfields
.{
 Б                                       Ведомость
           @~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
                при использовании линейного способа начисления амортизации Б
 Р───────────┬──────────────────────────────┬─────────┬──────────────────┬─────────────────────┬───────────┬─────────────────────┬─────────┬────────────┬─────────┬──────────────────┬───────────┬──────────────────────────────
           │                              │   Дата  │                  │ По постановлению СМ │   Срок    │ По постановлению СМ │ Остато- │   Сумма    │         │                  │Фактический│
Инвентарный│         Наименование         │ ввода в │    Остаточная    │   от 3.01.91 г. N4  │  службы,  │ от 16.11.01г. N1668 │  чный   │  аморти-   │  Срок   │  Первоначальная  │остаточный │
   номер   │            обьекта           │ эксплуа-│   стоимость на   ├───────┬─────────────┤фактическое├───────┬─────────────┤  срок   │   зации    │полезного│    стоимость     │   срок    │               Мол
           │                              │  тацию, │   @@@@@@@@@@г.   │ Шифр  │ Нормативный │  кол-во   │ Шифр  │ Нормативный │ службы  │  за месяц  │ исполь- │                  │  службы   │
           │                              │месяц,год│                  │       │ срок службы │  месяцев  │       │ срок службы │ гр9-гр7 │  гр4.гр10  │ зования │                  │ гр12-гр7  │
───────────┼──────────────────────────────┼─────────┼──────────────────┼───────┼─────────────┼───────────┼───────┼─────────────┼─────────┼────────────┼─────────┼──────────────────┼───────────┼──────────────────────────────
    1      │               2              │    3    │        4         │   5   │       6     │     7     │   8   │      9      │   10    │     11     │   12    │        13        │    14     │                15
───────────┴──────────────────────────────┴─────────┴──────────────────┴───────┴─────────────┴───────────┴───────┴─────────────┴─────────┴────────────┴─────────┴──────────────────┴───────────┴────────────────────────────── Р
.{
 Р@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@  @@@@@@@  &&&&&&&&&&&&&&&.&& @@@@@@@  &&&&&&&&&&&   &&&&&&&&&  @@@@@@@  &&&&&&&&&&&  &&&&&&&&& &&&&&&&&&.&& &&&&&&&&& &&&&&&&&&&&&&&&.&& &&&&&&&&&&& @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Р
.}
 Р────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────── Р
.}
.endform
