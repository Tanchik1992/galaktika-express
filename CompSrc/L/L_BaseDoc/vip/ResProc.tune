//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Логистика - документы-основания
// Процедуры резервирования для продажи и отпуска на консигнацию
//******************************************************************************

#ifNdef _RESPROC_TUNE
#define _RESPROC_TUNE
//******************************************************************************

#declare TTuneRes(TTuneName, ResultType)
type #TTuneName = record
                    AlreadyInit: boolean;
                    Value: #ResultType;
                  end;
#end

//******************************************************************************

#TTuneRes(TwResTune, word)
#TTuneRes(TcoResTune, comp)
#TTuneRes(TboResTune, boolean)
#TTuneRes(TsResTune, string)

//******************************************************************************

Type
  TResTunes = record
              //-------------------------------------
                Tara_SkTara
              , Reserv_PrSchSklad
              , Reserv_PriorParty
              , Reserv_SrokReserv
              , Reserv_CalcSrokReserv
              , Reserv_OverOstatok
              , OnlyWithParty
                                        : TwResTune;
              //-------------------------------------
                User_cCurSklad
              , Nalog_cNalogNDS
              , ResRestrict_cSklad
              , ResRestrict_cMol
              , ResRestrict_cParty
                                        : TcoResTune;
              //-------------------------------------
                Netto_Tara_Control
              , Reserv_ReservWithoutOst
              , Reserv_PartReserv
              , Reserv_Razrez
              , Tara_SoprUslTara
                                        : TboResTune;
              //-------------------------------------
                Tara_KodGrMcTara
                                        : TsResTune;
              //-------------------------------------
              end;

var ResTunes: TResTunes;

//******************************************************************************

Procedure DoInitResTunes;
{
  ClearAdvRecord(ResTunes);
}

//******************************************************************************

#declare GetTuneRes(Prefix, ResultType)
Function #(Prefix)GetTuneRes(TuneName: string; var #(Prefix)Tune: T#(Prefix)ResTune): #ResultType;
{
  if NOT #(Prefix)Tune.AlreadyInit
  {
    #(Prefix)Tune.AlreadyInit := TRUE;
    #(Prefix)Tune.Value := #(Prefix)GetTune(TuneName);
  }
  #(Prefix)GetTuneRes := #(Prefix)Tune.Value
}
#end

//******************************************************************************

#GetTuneRes(w, word)
#GetTuneRes(co, comp)
#GetTuneRes(bo, boolean)
#GetTuneRes(s, string)

//******************************************************************************

HandleEvent
cmInit:
  DoInitResTunes;
end;

//******************************************************************************

#end //_RESPROC_TUNE
//******************************************************************************