//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Логистика - ДО
// Визуализация скидок документов-оснований
//******************************************************************************

{
  if ((BaseDoc.cNalog <> 0) AND ((tipDO = 201) OR (tipDO = 202) OR (tipDO = 1420)))
    {
      if (BaseDoc.cNalog = 1)
        SetFieldSelectable(#BaseDoc.Skidka, FALSE);

      SetFieldVisible(#KolKompl      , (BaseDoc.cNalog <> 1));
      SetFieldVisible(#BaseDoc.Skidka, (BaseDoc.cNalog  = 1));

      SetFieldSelectable(#SkidVar     , FALSE);
      SetFieldSelectable(#SpStep.Price, FALSE);
    }
  else
    {
      SetFieldVisible(#KolKompl         , FALSE);
      SetFieldVisible(#BaseDoc.Skidka   , TRUE );
      SetFieldSelectable(#BaseDoc.Skidka, TRUE );
      SetFieldSelectable(#SkidVar       , TRUE );

      var IsSkip: boolean;
      IsSkip := TRUE;

      #ifdef __vschet__
      cfsSetCurrentContainer('VSchet');
      cfsGetProp('c_BRSPSCHET_SPSTEP.PRICE_Цена_за_единицу', 'SKIP', IsSkip);
      #end

      #ifdef _vschetp_vpp
      cfsSetCurrentContainer('VSchetP');
      cfsGetProp('c_BRSPSCHET_SPSTEP.PRICE_Цена_за_единицу', 'SKIP', IsSkip);
      #end

      if IsSkip
        SetFieldSelectable(#SpStep.Price, TRUE);
    }

  #ifNdef __vschetr__
  if ((tipDO = 201 ) AND boGetTune('Doc.OnlyKatSkid') OR
      (tipDO = 1420) AND boGetTune('Doc.Wood.Schet.OnlyKatSkid'))
    {
      //Для ДО на продажу по настройке нельзя менять вручную скидки (только из каталогов скидок выбирать)
      #ifNdef __vschetb__
      #ifNdef _vschetpb_vpp
      SetFieldSelectable(#SkidFunc      , FALSE);
      #end
      #end
      SetFieldProtected (#BaseDoc.Skidka, TRUE );
      SetFieldSelectable(#SkidVar       , FALSE);
      SetFieldSelectable(#KolKompl      , FALSE);
      SetFieldProtected (#SpStep.VlSkid , TRUE );
      SetFieldSelectable(#SpStep.PrSkid , FALSE);
    }

  if ((tipDO = 201 ) AND boGetTune('Doc.NoEditCena') OR
      (tipDO = 1420) AND boGetTune('Doc.Wood.NoEditCena'))
    {
      //Для ДО на продажу по настройке нельзя менять вручную цену и скидки
      #ifNdef __vschetb__
      #ifNdef _vschetpb_vpp
      SetFieldSelectable(#SkidFunc      , FALSE);
      #end
      #end
      SetFieldSelectable(#BaseDoc.Skidka, FALSE);
      SetFieldSelectable(#SkidVar,        FALSE);
      SetFieldSelectable(#KolKompl,       FALSE);
      SetFieldSelectable(#SpStep.Price,   FALSE);
      SetFieldSelectable(#SummaTov,       FALSE);
      SetFieldSelectable(#SpStep.VlSkid,  FALSE);
      SetFieldSelectable(#SpStep.PrSkid,  FALSE);
    }
  #end

  #ifdef __vschet__
  #ifNdef __vschetb__
  if (wGetTune('Oper.AlgSkid') = 1) //если работаем по сложным скидкам для ДО на продажу
    {
      SetFieldSelectable(#BaseDoc.Skidka, FALSE);
      SetFieldSelectable(#SkidVar,        FALSE);
      SetFieldSelectable(#SpStep.VlSkid,  FALSE);
      SetFieldSelectable(#SpStep.PrSkid,  FALSE);
    }

  // если применяется жесткое резервирование в ДО на продажу, то проверяем возможность
  // модификации резерва по текущему документу для данного пользователя
  SetFieldSelectable(#SpStep.Reserv , isModifZhostkReserv(FALSE));
  SetFieldSelectable(#SpStep.SrokRes, isModifZhostkReserv(FALSE));
  #end
  #end

  if ( SpStep.PrMc <> 1 )
    {
      HideLabel4Field(#SpStep.Reserv);
      ClearFieldState(#SpStep.Reserv , sfVisible);
      HideLabel4Field(#SpStep.SrokRes);
      ClearFieldState(#SpStep.SrokRes, sfVisible);
      #ifdef __vschet__
      DisableCommand(cmValFld8);
      #end
    }
  else
    {
      SetFieldState(#SpStep.Reserv , sfVisible);
      ShowLabel4Field(#SpStep.Reserv);
      SetFieldState(#SpStep.SrokRes, sfVisible);
      ShowLabel4Field(#SpStep.SrokRes);
      #ifdef __vschet__
      EnableCommand(cmValFld8);
      #end

      if (
           (wGetTune('Reserv.PrSchSklad') = 2)
          AND
           ( (tipDO = 201) OR (tipDO = 202) OR (tipDO = 1420))
         )
        SetFieldSelectable(#SpStep.Reserv, FALSE);
    }
}

//******************************************************************************