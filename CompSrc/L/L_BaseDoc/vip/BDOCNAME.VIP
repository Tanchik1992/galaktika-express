//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - логистика
// Интерфейс заполнения каталога примечаний к документам
//******************************************************************************

#doc
Интерфейс заполнения каталога примечаний к документам
#end
Interface BDocName 'Список типовых примечаний к документам' escclose;
  show at (9,4,71,20);
Create View vBDocName
Var
  c1 : comp;
  w1 : word;
From
  BDocName (BDocName02)
Where
((
  word(w1) == BDocName.VidDoc
));

Parameters
  w1,
  c1;

Form frmBDocName('BDOCNAME.OUT','BDOCNAME');

Browse brBDocName;
Fields
  BDocName.name 'Типовое примечание к документу'('Типовое примечание к документу',hcOkatalIPrim,sci178Esc):[58];
end;

Browse brBDocNameIsk;
Fields
  BDocName.name 'Типовое определение суда'('Типовое определение суда',hcOkatalIPrim,sci178Esc):[58];
end;

HandleEvent

cmInit :
  {
    if (w1 = 701) // типовые определения суда по искам
      {
        SetTitle('Список типовых определений суда');
        SetFormat(brBDocNameIsk);
      }
    else
      {
        SetTitle('Список типовых примечаний к документам');
        SetFormat(brBDocName);
      }

    if (c1 = -1)
      PutCommand(cmInsert)
    else
      if( GetFirst BDocName where ((c1 == BDocName.NRec)) <> tsOK)
        if( GetFirst BDocName = 0)
          {};
  }

cmClose :
  {
    CloseInterface(cmDefault);
    stop;
  }

cmCancel :
  {
    c1 := BDocName.Nrec;
    if (not UpdateTable)
      abort;
    CloseInterface(cmDefault);
    stop;
  }

cmCheckRecord :
  if (BDocName.name = '')
    {
      message(''#3'Задайте '+ if(w1=701, 'определение', 'примечание') + ' или удалите запись',Okbutton);
      abort;
    }

cmInsertRecord :
  insert current BDocName;

cmDeleteRecord :
  if (message(''#3'Удалим текущую запись?',YesNo)=Yes)
    delete current BDocName;
  else
    abort;

cmUpdateRecord :
  Update current BDocName;

cmPickClass:
  if ((UpdateTable) and (BDocName.NRec <> 0))
    RunInterface(ExClassifier, word(coBDocName), BDocName.NRec);

cmPickAttr:
  if ((UpdateTable) and (BDocName.NRec <> 0))
    RunInterface(Attribute, word(coBDocName), BDocName.NRec);

cmPrintDoc :
  ProcessCommand(cmValue1);

cmValue1:
  if (UpdateTable)
    {
      PushPos(#BDocName);
      _LOOP BDocName
        frmBDocName.write(BDocName.Name);
      PopPos(#BDocName);

      frmBDocName.PutEvent(feBreak);

      if (not frmBDocName.error)
        frmBDocName.showFile('');
      else
        frmBDocName.abortForm;
      ReReadRecord;
    }

cmHotKeys :
  PutHotCommand(RunMenu('AttrClassPrn_HotKeys'));

end;
end.
