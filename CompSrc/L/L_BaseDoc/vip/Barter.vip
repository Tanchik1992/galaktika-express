//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - логистика
// Заполнение связей по бартерному договору
//******************************************************************************
#ifdef ATL51
  #include GetDBase.vih
#end

#doc
Интерфейс заполнения связей по бартерному договору
#end
Interface Barter 'Бартерные договоры' EscClose;
  Show at (, 2, , 16);

Create view vBarter
var
  p1: comp;
  CurEditFormat: word;

As select
  PrintNumber(BaseDoc.Descr, BaseDoc.NoDoc)
     (FieldName = NomDes1),

  PrintNumber(BaseDoc2.Descr, BaseDoc2.NoDoc)
     (FieldName = NomDes2),

  Barter.*,
  BaseDoc.NoDoc,
  BaseDoc2.NoDoc

From
  Barter,
  BaseDoc,
  BaseDoc BaseDoc2

Where
((
  Barter.cBaseDoc1 == BaseDoc.NRec  AND
  Barter.cBaseDoc2 == BaseDoc2.NRec
))
order byName by Barter.Name
order byKod  by Barter.NDoc
;

Parameters
  p1;


//******************************************************************************

Procedure SetDSKOrder;
var
  tmp_i: word;
{
  ReadMyDsk(tmp_i, 'Sort_Barter', FALSE);

  case tmp_i of
  //-----------
    1:
      {
        SetColumnSorting(-1, #Barter.NDoc, -1);
        SetTableBackOrderIn(tnBarter, TRUE);
        SetOrder(tiByKod);
      }
  //-----------
    2:
      {
        SetColumnSorting(-1, #Barter.Name, -1);
        SetTableBackOrderIn(tnBarter, TRUE);
        SetOrder(tiByName);
      }
  //-----------
    3:
      {
        SetColumnSorting(-1, #Barter.NDoc, 1);
        SetTableBackOrderIn(tnBarter, FALSE);
        SetOrder(tiByKod);
      }
  //-----------
    4:
      {
        SetColumnSorting(-1, #Barter.Name, 1);
        SetTableBackOrderIn(tnBarter, FALSE);
        SetOrder(tiByName);
      }
  //-----------
    else
      {
        SetColumnSorting(-1, #Barter.NDoc, 1);
        SetTableBackOrderIn(tnBarter, FALSE);
        SetOrder(tiByKod);
      }
  //-----------
  end; // case
}

//******************************************************************************

Browse brBarter (, hcZakupM1DocBarter, sci178Esc);

Fields
  Barter.NDoc #3'Номер'#13#3'договора'                  : [11], NoAutoSize;
  Barter.Name #3'Наименование бартерного договора'      : [30];
  NomDes1     #3'Договор'#13#3'на закупку'              (, , sci1378Esc): [16], NoAutoSize;
  NomDes2     #3'Договор'#13#3'на продажу'              (, , sci1378Esc): [16], NoAutoSize;
end;

Procedure SetMyEditFormat(_format: word); // 0 - Protect; 1 - edit
{
  CurEditFormat := _format;
  if ( _format = 0 )
    {
      SetHelpAndStatusContext(brBarter, -1, sci1478Esc, hcZakupM1DocBarter);
      SetHelpAndStatusContext(brBarter, #NomDes1, sci178Esc, hcZakupM1DocBarter);
      SetHelpAndStatusContext(brBarter, #NomDes2, sci178Esc, hcZakupM1DocBarter);

      SetFieldOption(#Barter.NDoc   , ofProtected);
      SetFieldOption(#Barter.Name   , ofProtected);
      SetFieldOption(#NomDes1       , ofProtected);
      SetFieldOption(#NomDes2       , ofProtected);

      SetTitle('Бартерные договоры');
      SetInterfaceOptions(opEscClose OR opCyanInterface);
    }
  else
    {
      SetHelpAndStatusContext(brBarter, -1           , sci178Esc , hcZakupM1DocBarter);

      SetHelpAndStatusContext(brBarter, #NomDes1, sci1378Esc, hcZakupM1DocBarter);
      SetHelpAndStatusContext(brBarter, #NomDes2, sci1378Esc, hcZakupM1DocBarter);

      ClearFieldOption(#Barter.NDoc   , ofProtected);
      ClearFieldOption(#Barter.Name   , ofProtected);
      ClearFieldOption(#NomDes1       , ofProtected);
      ClearFieldOption(#NomDes2       , ofProtected);

      SetTitle('Бартерные договоры (редактирование)');
      SetInterfaceOptions(opEscClose);
    }

  RereadRecord(#Barter);
}

//******************************************************************************

Panel p1;

HandleEvent

cmColumnClicked:
{
  var NewField: integer;

  NewField := Target;

  case GetColumnSorting(brBarter, NewField) of
  //------------------------------------------
    1:
    {
      case NewField of
      //--------------
        #Barter.NDoc:
        {
          SetColumnSorting(-1, NewField, -1);
          SetTableBackOrderIn(tnBarter, TRUE);
          SetOrder(tiByKod);
          RereadRecord(#Barter);
          SaveMyDsk(Word(1), 'Sort_Barter');
        }
      //--------------
        #Barter.Name:
        {
          SetColumnSorting(-1, NewField, -1);
          SetTableBackOrderIn(tnBarter, TRUE);
          SetOrder(tiByName);
          RereadRecord(#Barter);
          SaveMyDsk(Word(2), 'Sort_Barter');
        }
      //--------------
      end; // Case
    }
  //------------------------------------------
    0
  ,-1:
    {
      case NewField of
      //--------------
        #Barter.NDoc:
        {
          SetColumnSorting(-1, NewField, 1);
          SetTableBackOrderIn(tnBarter, FALSE);
          SetOrder(tiByKod);
          RereadRecord(#Barter);
          SaveMyDsk(Word(3), 'Sort_Barter');
        }
      //--------------
        #Barter.Name:
        {
          SetColumnSorting(-1, NewField, 1);
          SetTableBackOrderIn(tnBarter, FALSE);
          SetOrder(tiByName);
          RereadRecord(#Barter);
          SaveMyDsk(Word(4), 'Sort_Barter');
        }
      //--------------
      end; // Case
    }
  //------------------------------------------
  end; // Case
}

cmSetDefault:
  {
    var i1: integer;

    if (GetLast Barter ordered by Barter.NDoc = tsOk)
      {
        Barter.NDoc := NextNumStr(Barter.NDoc);
        i1 := #Barter.Name;
      }
    else
      {
        NDoc := '000000';
        i1 := #Barter.NDoc;
      }

    Barter.cBaseDoc1 := 0;
    Barter.cBaseDoc2 := 0;
    Barter.NRec := 0;
    Barter.Name := '';
    SelectField(i1);
  }

cmSetCurField:
  case Target of
  //------------
    #NomDes1:
      if (Barter.cBaseDoc1 = 0)
        PutCommand(cmPick);
  //------------
    #NomDes2:
      if (Barter.cBaseDoc2 = 0)
        PutCommand(cmPick);
  //------------
  end;

cmPick:
  case CurField of
  //--------------
    #NomDes1:
      if (RunInterface('GetDBase', TRUE, Word(2), Word(0), Barter.cBaseDoc1, Comp(0), Comp(0), Double(0)) <> cmCancel)
        SelectField(#NomDes2);
  //--------------
    #NomDes2:
      if (RunInterface('GetDBase', TRUE, Word(1), Word(201), Barter.cBaseDoc2, Comp(0), Comp(0), Double(0)) <> cmCancel){};
//        SelectField(#Barter.Name);
  //--------------
  end;

cmCheckRecord:
  if (Trim(Barter.Name) = '')
    {
      Message('Введите наименование договора или удалите запись', CancelButton);
      SelectField(#Barter.Name);
      Abort;
    }
  else
    Barter.Name := Trim(Barter.Name);

cmDeleteRecord:
  case CurField of
  //--------------
    #NomDes1: set Barter.cBaseDoc1 := 0;
  //--------------
    #NomDes2: set Barter.cBaseDoc2 := 0;
  //--------------
  end;

cmInsertRecord:
  Insert current Barter;

cmUpdateRecord:
  Update current Barter;

cmDeleteRecord:
  if (Message('Удалить текущий бартерный договор?', YesNo) = Yes)
    Delete current Barter;
  else
    Abort;

end;  // HandleEvent
end;  // Panel

//******************************************************************************

HandleEvent

cmInit:
  {
    SetDSKOrder;
    SetMyEditFormat(0);
    if (p1 = -1)
      PutCommand(cmInsert);
    else
      if (GetFirst Barter where ((p1 == Barter.NRec)) <> tsOk)
        if (GetFirst Barter = 0) {}
  }

cmCancel:
  if ( ( CurEditFormat = 1 ) AND UpdateTable )
    {
      SetMyEditFormat(0);
      Abort;
    }

cmClose:
  {
    Abort;
    PutCommand(cmCancel);
  }

cmDone:
  p1 := Barter.NRec;

cmEdit:
  if ( CurEditFormat = 0 )
    SetMyEditFormat(1);

cmInsert:
  if ( CurEditFormat = 0 )
    {
      PutCommand (cmInsert);
      SetMyEditFormat(1);
    }
//******************************************************************************

end;
End.
