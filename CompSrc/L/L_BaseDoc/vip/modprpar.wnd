//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - ДО_
// Окно ввода параметров пересчета
//******************************************************************************

var sFltKlPrice: string;
var pFltKlPrice: comp;
var sParKlPrice: string;
var Par1, Par2 : word;

#doc
Окно установки параметров пересчета
#end
Window ModPricePar 'Параметры пересчета' DoAccept, EscClose, Gray;
  Show at (, , 78, 13);

Panel pModPricePar

Screen scPar (, hcctxPriceIzmPar, sci1Esc)

Fields
  sFltKlPrice (,,sci13Esc): Protect;
  sParKlPrice (,,sci13Esc): Protect, NoDel;
  Par1 ('Вставка цены в НДЕ из прайс-листа в НДЕ-ДО', , ), NoProtect;
  Par2 ('Обнулять/Не обнулять цену для позиций спецификации, не найденных в прайс-листе', , ), NoProtect;

Buttons
  cmYes, Default, , 'Продолжить', , sci1Esc;
  cmCancel, , , 'Отмена', , sci1Esc;
<<

 Фильтр по позициям спецификации:.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Новые цены из прайс-листа:      .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

 `При вставке в ДО с типом по валюте НДЕ из прайса с типом по валюте НДЕ+ВАЛ`
 (.) Взять цену в НДЕ из прайс-листа`
 (.) Пересчитать цену из валюты прайс-листа в НДЕ по курсу на дату ДО`

 [.] Обнулять цену для позиций спецификации, не найденных в прайс-листе`

 <. Продолжить .>   <. Отмена     .>
>>
end;

HandleEvent

cmDelOnProtect:
{
  case CurField of
  //******************************************************************************
    #sFltKlPrice:
      {
        case pFltKlPrice of
        //******************************************************************************
           0:
             {
               set pFltKlPrice := -1;
               set sFltKlPrice := 'по всем позициям спецификации';
             }
        //******************************************************************************
        else
          {
            set pFltKlPrice := 0;
            set sFltKlPrice := 'у которых прайс-лист не указан';
          }
        end;
      }
  //******************************************************************************
  end;
}

end;

end; // Panel

HandleEvent

cmInit:
{
  set pFltKlPrice := -1;
  set sFltKlPrice := 'по всем позициям спецификации';

  if ( GetFirst KlPrice where (( PrNRec == KlPrice.NRec )) = tsOk )
    set sParKlPrice := KlPrice.Name;
}

cmPick:
{
  case CurField of
  //******************************************************************************
    #sFltKlPrice:
      {
        if ( RunInterface(GetKlPr, pFltKlPrice, RVhod, word(1), word(0)) <> cmCancel )
          if ( GetFirst KlPrice where (( pFltKlPrice == KlPrice.NRec )) = tsOk )
            set sFltKlPrice := 'по прайс-листу "' + KlPrice.Name + '"';
      }
  //******************************************************************************
    #sParKlPrice:
      {
        if ( RunInterface(GetKlPr, PrNrec, RVhod, word(1), word(0)) <> cmCancel )
          if ( GetFirst KlPrice where (( PrNRec == KlPrice.NRec )) = tsOk )
            set sParKlPrice := KlPrice.Name;
      }
  //******************************************************************************
  end;
}

cmYes:
{
  PutCommand(cmDefault);
}

end;

end; //Window


//******************************************************************************
