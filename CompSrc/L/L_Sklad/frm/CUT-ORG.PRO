/*
 ╔═══════════════════════════════════════════════════════════════════════════╗
 ║                     (c) 1994,97 корпорация ГАЛАКТИКА                      ║
 ║ Проект        : ГАЛАКТИКА                                                 ║
 ║ Система       : Основные средства                                         ║
 ║ Версия        : 5.00                                                      ║
 ║ Назначение    : Движение в разрезах по контрагентам                       ║
 ║ Ответственный : Крахотко Александр Валерьевич (КРОХА)                     ║
 ║ Параметры     :                                                           ║
 ╚═══════════════════════════════════════════════════════════════════════════╝
*/
#doc
Движение в разрезах по контрагентам
#end
.Set Name = 'CutOrg'
.hide
!.group 'cut_pp'
.var
  разрез_итого_колп  : double // суммы по разрезу склад/мол/партия
  разрез_итого_колр  : double
  разрез_итого_стмп  : double
  разрез_итого_стмр  : double

  конт_итого_колп  : double  // суммы по оппоненту
  конт_итого_колр  : double
  конт_итого_стмп  : double
  конт_итого_стмр  : double

  мц_итого_колп   : double // суммы по мц
  мц_итого_колр   : double
  мц_итого_кол    : double
  мц_итого_стмп   : double
  мц_итого_стмр   : double
  мц_итого_стм    : double

  ссо : double // сумма остатка по складу
  скп : double
  ссп : double
  скр : double
  сср : double

  ост_ц : double
  ост_к : double

  мц_и_кол : double
  мц_и_стм : double

  сумма_остатков_мц_на_складе : double
  сумма_остатков_мц_на_складе1 : double

  остаток_кол_d,количество_d : double
  остаток_кол_s,количество_s,fractional_part8 : string
  переоц_ордер: boolean
.endvar
.fields
 Кол_число_знаков:word
 начальная_дата:date
 конечная_дата:date
 склад склад_нрек:string ostatok_on_podr : double
  мол мол_нрек:string
   партия партия_нрек:string
    мц мц_нрек:string
        остаток_имя остаток_кол0:DOUBLE остаток_цена:DOUBLE
     тип
      склад2
       мол2
        дата
         вид
          номер_ордера
          тип_ордера:word
           накладная_номер
           накладная_дискриптр
           накладная_дата
           накладная_примечание
           накладная_цена
           накладная_сумма_налогов_к_позиции
           накладная_налоги_входят_в_цену:boolean
          цена:DOUBLE количество0:DOUBLE
           ордер_мц_нрек
           ордер_склад_нрек
           ордер_мол_нрек
           ордер_партия_нрек
.endfields
  Кол_число_знаков ^
  с ^ по ^
.begin
  Case Кол_число_знаков of
    2 : fractional_part8 := '88';
    3 : fractional_part8 := '888';
    4 : fractional_part8 := '8888';
    5 : fractional_part8 := '88888';
    6 : fractional_part8 := '888888';
    7 : fractional_part8 := '8888888';
    8 : fractional_part8 := '88888888';
    9 : fractional_part8 := '888888888';
  else
    fractional_part8 := '8';
  End;
end.
.{CutOrgPodr
  склад ^ : ^ - ^
.{CutOrgMol
  мол ^ : ^
.{CutOrgParty
.begin
  разрез_итого_колп := 0 ;
  разрез_итого_колр := 0 ;
  разрез_итого_стмп := 0 ;
  разрез_итого_стмр := 0 ;
 end.
   партия ^ : ^
.{CutOrgMC
    мц ^ : ^ остаток_имя ^ остаток_кол0 ^ остаток_цена ^
.begin
  остаток_кол_d := остаток_кол0;
  остаток_кол_s := DoubleToStr(остаток_кол_d,'[|-]3666`666`666`666.'+fractional_part8);
  мц_итого_кол  := остаток_кол_d ;
  мц_итого_колп := 0 ;
  мц_итого_колр := 0 ;
  мц_итого_стм  := if(abs(остаток_кол_d) < GetPresision_forKol, double(1),остаток_кол_d)*остаток_цена ;
  мц_итого_стмп := 0 ;
  мц_итого_стмр := 0 ;
 end.
.{CutOrgTip
     тип ^
.{CutOrgPodr2
      склад2 ^
.{CutOrgMol2
.begin
  конт_итого_колп := 0 ;
  конт_итого_стмп := 0 ;
  конт_итого_колр := 0 ;
  конт_итого_стмр := 0 ;
  скп := 0 ;
  ссп := 0 ;
  скр := 0 ;
  сср := 0 ;
 end.
       мол2 ^
.{CutOrgData
        дата ^
.{CutOrgVid
         вид ^
.{CheckEnter CutOrgSample
          ордер N ^
          тип ^
           накладная N ^ ^ от ^ на ^ (^ : ^ | ^)
           цена ^ количество0 ^
           (^ : ^ ^ ^)
.begin
  количество_d := количество0;
  количество_s := DoubleToStr(количество_d,'[|-]3666`666`666`666.'+fractional_part8);
  переоц_ордер:= (тип_ордера>=4) and (тип_ордера<=7);
  if (вид='приход')
    {
    конт_итого_колп := конт_итого_колп + количество_d ;
    конт_итого_стмп := конт_итого_стмп + цена*if(переоц_ордер, 1.0,количество_d) ;
    скп := скп + количество_d ;
    ссп := ссп + цена*if(переоц_ордер, 1.0,количество_d) ;
    }
  else
    {
    конт_итого_колр := конт_итого_колр + количество_d ;
    конт_итого_стмр := конт_итого_стмр + цена*if(переоц_ордер, 1.0,количество_d) ;
    скр := скр + количество_d ;
    сср := сср + цена*if(переоц_ордер, 1.0,количество_d) ;
    }
 end.
.} // sample
.}
.}
.begin
  мц_итого_колп := мц_итого_колп + конт_итого_колп ;
  мц_итого_колр := мц_итого_колр + конт_итого_колр ;
  мц_итого_кол  := мц_итого_кол  + конт_итого_колп  - конт_итого_колр ;
  мц_итого_стмп := мц_итого_стмп + конт_итого_стмп ;
  мц_итого_стмр := мц_итого_стмр + конт_итого_стмр ;
  мц_итого_стм  := мц_итого_стм  + конт_итого_стмп  - конт_итого_стмр ;
 end.
.} // mol2
.}
.}
.begin
  разрез_итого_колп := разрез_итого_колп + мц_итого_колп ;
  разрез_итого_колр := разрез_итого_колр + мц_итого_колр ;
  разрез_итого_стмп := разрез_итого_стмп + мц_итого_стмп ;
  разрез_итого_стмр := разрез_итого_стмр + мц_итого_стмр ;
 end.
.} // MC
.}
.}
.}

.endform
