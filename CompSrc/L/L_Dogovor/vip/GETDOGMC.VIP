/*
 ╔═══════════════════════════════════════════════════════════════════════════╗
 ║                     (c) 1987,98 корпорация ГАЛАКТИКА                      ║
 ║ Проект        : ГАЛАКТИКА                                                 ║
 ║ Система       : Оперативный контур                                        ║
 ║ Версия        : 5.73                                                      ║
 ║ Назначение    : Выбор МЦ из спецификации договора                         ║
 ║ Ответственный : Жданович Юрий Геннадьевич                                 ║
 ║ Параметры     : есть                                                      ║
 ║    pDocOwner  // ссылка на документ владелец                              ║
 ║    TiDkOwner  // TiDk документа-владельца                                 ║
 ║    wPrMc,     // тип позиции                                              ║
 ║    wDirect,   // направление                                              ║
 ║    p1         // возвращает ссылку на выбраную позицию                    ║
 ╚═══════════════════════════════════════════════════════════════════════════╝
*/

#ifdef __DO_PredOplata__

#doc
Выбор позиции из спецификации договора/соглашения/ПКП
#end
Interface GetDogovorMC 'Выбор МЦ из спецификации документа' EscClose, Cyan, DoAccept;
Show at (0,0,80,37);

Create View
Var
//--параметры
  pDocOwner: comp;
  TiDkOwner, wPrMc, wDirect: word;
  p1: comp;

(
  SpDocsName,
  SpDocsGrName
)
As Select
  if (SpDocs.cMCUsl = 0, '',
    if ((SpDocs.PrMC = 2) or (SpDocs.PrMC = 3),
        if (IsValid(tnKatUsl), KatUSL.Name, '? Усл ' + string(SpDocs.cMCUsl)),
        if (IsValid(tnKatMC) , KatMC.Name , '? MC '  + string(SpDocs.cMCUsl))
       )
     ),
  if (SpDocs.cGrMCUsl = 0,'',
    if ((SpDocs.PrMC = 2) or (SpDocs.PrMC = 3),
        if (IsValid(tnGroupUsl), GroupUSL.Name, '? Группа Усл ' + string(SpDocs.cGrMCUsl)),
        if (IsValid(tnGroupMC) , GroupMC.Name , '? Группа МЦ '  + string(SpDocs.cGrMCUsl))
       )
     )

From
  SpDocs    ,
  GroupMC   ,
  GroupUSL  ,
  KatMC     ,
  KatUSL    ,
  KatOtpEd
Where
((
  pDocOwner         ==  SpDocs.cDoc     and
  TiDkOwner         ==  SpDocs.TiDk     and
  wPrMc             ==  SpDocs.PrMC     and
  SpDocs.cGrMCUSL   ==  GroupMC.nRec    and
  SpDocs.cGrMCUSL   ==  GroupUsl.nRec   and
  SpDocs.cMCUSL     ==  KatMC.nRec      and
  SpDocs.cMCUSL     ==  KatUsl.nRec     and
  SpDocs.cOtpEd     ==  KatOtpEd.nRec
)) and ((wDirect = SpDocs.Direct) and (0 <> SpDocs.cMCUSL))
Order By SpDocs.Code;

Parameters
  pDocOwner,  // ссылка на документ из которого надо брать спецификацию
  TiDkOwner,  // тип документа из которого надо брать спецификацию
  wPrMc,      // тип позиции
  wDirect,    // направление
  p1          // ссылка на выбраную позицию при одиночном выборе
  ;

Panel panSpDocs;
 Table SpDocs;
Browse brSpDocs2 (,hcDogSSpecLinkPos,sci1EnEsc);
Fields
  SpDocs.CODE    'п/н' ('') : [5] , protect ;
  SpDocsGrName   'Группа' ('') : [15], protect;
  SpDocsName     'Продукция' ('') : [20], protect;
  KatOtpEd.Name  'ЕдИзм' ('') : [5], protect;
  SpDocs.Kol     'Количество'('',,): [10], protect;
  SpDocs.Price   'Цена за единицу'('',,): [12], protect;
end ; // browse

HandleEvent

cmPickClass: // Alt+C
  RunInterface('ExClassifier', word(coSpDocs), SpDocs.NRec);

cmPickAttr: // Alt+A
  RunInterface('Attribute', word(coSpDocs), SpDocs.NRec);

end; // HandleEvent

end; // panel

HandleEvent // Interface

cmInit: {
  if (getFirst SpDocs <> tsOK) {
    Message(''#3'У данного договора нет в спецификации '+
            if((wPrMc = 2) or (wPrMc = 3), 'услуг ', 'товаров ')+
#ifdef GAL7_1
            'с направлением '+if(wDirect = 1, '1->2', '2->1')+
#end
            '!', OkButton);
    abort;
    exit;
  }
}

cmDefault: {
  if ( not IsValid ( #SpDocs )) {
    abort;
    exit;
  }
  p1 := SpDocs.nRec;
}

end; // HandleEvent Interface

end. // Interface

#end
