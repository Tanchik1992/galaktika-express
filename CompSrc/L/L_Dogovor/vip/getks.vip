/*
 ╔═══════════════════════════════════════════════════════════════════════════╗
 ║                     (c) 1994,2000 корпорация ГАЛАКТИКА                    ║
 ║ Проект        : ГАЛАКТИКА                                                 ║
 ║ Система       : Оперативный контур                                        ║
 ║ Версия        : 5.73                                                      ║
 ║ Назначение    : УралСеверГаз-2. Выбор накладной определенного вида        ║
 ║ Ответственный : Жданович Юрий Геннадьевич (Yurgen)                        ║
 ╚═══════════════════════════════════════════════════════════════════════════╝
*/

#ifdef __USG__

#include EditDoc.vih

#doc
Интерфейс выбора накладной определенного вида
#end
Interface GetKatSopr 'Выбор накладной' DoAccept, EscClose, Cyan;
  Show at (,3,,20);

#include EditDoc.var

Create view
Var
  p1 : comp;
  w1 : word;
  s_SimvRub: string;
(
  KSSum,
  KsValut
)
As select
 if (KatSopr.cVal = 0, KatSopr.Summa, KatSopr.SumVal),
 if (KatSopr.cVal = 0,
   if (s_SimvRub <> '', s_SimvRub, 'руб.'), KlVal.SimVolV),
  KatSopr.*
from
  KatSopr(KatSopr02),
  KatNotes,
  KatOrg,
  KlVal

where
((
  w1            == KatSopr.VidSopr and
  KatSopr.cVal  == KlVal.nRec      and
  KatSopr.cNote == KAtNotes.nRec   and
  KatSopr.cOrg  == KatOrg.nRec
));

Parameters p1, w1;

Browse brKatSopr ('Выберите документ и нажмите ВВОД',,sci14EnEsc);
Table KatSopr
Fields
  KatSopr.dSopr 'Дата'       ('Дата формирования'): [10, 'DD/MM/YYYY'], protect;
  KatSopr.Descr 'Дескр.'     ('Дескриптор(идентификатор) пользователя') : [5],  protect;
  KatSopr.NSopr 'Номер'      ('Номер')            : [8],  protect;
  KatNotes.Name 'Статус'     ('Статус')           : [10], protect;
  KatOrg.Name   'Контрагент' ('Наименование контрагента')   : [22], protect;
  KSSum         'Сумма'      ('Сумма по документу')         : [17, '\2p[|-]36`666`666`666`666.88'], [brForSopr_Price], protect;
  KSValut       'Валюта'     ('Денежная единица')           : [6],  protect;
  KatSopr.Name  'Примечание' ('Примечание')     : [19], protect;
end;

HandleEvent
cmInit:
{
  SetTitle(GetTiDkGalName(w1));
  if getFirst KatSopr where (( p1 == KatSopr.nRec )) <> tsOk
    if getLast KatSopr <> tsOk {
      P1 := 0;
      Message(''#3''+GetTiDkGalName(w1)+' данного вида отсутствуют ...', CancelButton);
      Abort;
    }
  s_SimvRub := sGetTune('NDE.SimvRub');
  ReReadRecord(#KatSopr);
}

cmEdit :
  if (IsValid(#KatSopr))
  {
    iEditDoc.RunEditHozDoc(coKatSopr,
                           KatSopr.VidSopr,
                           KatSopr.VidSopr,
                           KatSopr.nRec);
    ReReadRecord(#KatSopr);
  }

cmDefault: p1 := KatSopr.nRec;

end;
End.
#end