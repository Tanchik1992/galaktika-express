/*
 ╔═══════════════════════════════════════════════════════════════════════════╗
 ║                     (c) 1994,97 корпорация ГАЛАКТИКА                      ║
 ║ Проект        : ГАЛАКТИКА                                                 ║
 ║ Система       : Оперативный контур                                        ║
 ║ Версия        : 5.70                                                      ║
 ║ Назначение    : Перегон выбора из маркера в Pick для GetSomKatNotes       ║
 ║ Ответственный : Глушаков Дмитрий Сергеевич                                ║
 ║ Параметры     : есть                                                      ║
 ║    pVidDoc    // фильтр вида документа ( 0 - фильтр не задан )            ║
 ║    pSysNum    // системная группа / работает если pVidDoc = 0 (см. ниже)  ║
 ║    pStatus    // 0 - выбирать все, иначе битовая маска запрещенных        ║
 ║    OkOne      // TRUE - груповой выбор FALSE - только один (+возвращает)  ║
 ║    pKatNotes  // входной-выходной - KatNotes.nRec                         ║
 ╚═══════════════════════════════════════════════════════════════════════════╝
*/

#doc
Интерфейс сохранения выбора статусов договоров из маркера в Pick
#end
Interface PickKatNotes 'Выбор статусов договоров' DoAccept,EscClose,cyan;
Create View
var
//--параметры
  pVidDoc,
  pSysNum,
  pStatus      : word;
  OkOne        : boolean;
  pKatNotes    : comp;
//--
  Marker         : longint; // указатель на маркер
  MarkerCount, i : longint; // счетчики записей в маркере
  pickrec        : comp;
As select
  Pick.wList
from
  Pick
where
  ((
    word(447) == Pick.wList
  ))
;
Parameters
  pVidDoc,   // фильтр вида документа ( 0 - фильтр не задан )
  pSysNum,   // системная группа: = word(11) для pVidDoc = 400,411,420,431;
             //                   = word(12) для pVidDoc = 412,432;
             // нужна для выбора статусов из нескольких видов документов (VidDoc)
  pStatus,   // 0 - выбирать все, иначе битовая маска запрещенных
  OkOne,     // TRUE - груповой выбор FALSE - только один/ ЕСЛИ возвращает TRUE-помечены все распоряжения!
  pKatNotes  // входной-выходной - KatNotes.nRec
  ;

HandleEvent
cmInit    :
{
var ret:word;
  Marker:=InitMarker('MKatNotes',8,100,10);
  ClearMarker(Marker);
  _loop Pick
  {
  InsertMarker(Marker,Pick.cRec);
  }
  DoneMarker(Marker,'MKatNotes');

  if RunInterface('GetSomKatNotes',
                  pVidDoc,   // фильтр вида документа
                  pSysNum,   // системная группа
                  pStatus,   // 0 - выбирать все, иначе битовая маска запрещенных
                  OkOne,     // TRUE - груповой выбор FALSE - только один/ ЕСЛИ возвращает TRUE-помечены все распоряжения!
                  pKatNotes  // входной-выходной - KatNotes.nRec
                  ) = cmCancel  // если там Cancel - то и здесь Cancel
    {
    Abort;
    Exit;
    }
  delete safe Pick;
  StartNewVisual(vtRotateVisual, vfTimer,
               ''#3'Формирование элементов выбора ...'#13#3, 1);
  Marker:=InitMarker('MKatNotes',8,100,10);
  MarkerCount:=GetMarkerCount(Marker);
  FOR ( i:=0; i<MarkerCount; i:=i+1 )
    {
    if GetMarker(Marker,i,pickrec)
      {
      Pick.wList := 447;
      Pick.PickKol:=i;
      Pick.cRec:=pickrec;
      insert current Pick;
      }
    NextVisual;
    }
  DoneMarker(Marker,'MKatNotes');
  StopVisual('',0);
  PutCommand(cmDefault);
}
end;  // HandleEvent
end.  // Interface