/*
 ╔═══════════════════════════════════════════════════════════════════════════╗
 ║                                               (c) корпорация ГАЛАКТИКА    ║
 ║ Назначение    : Выбор МЦ из ДО                                            ║
 ║ Параметры :                                                               ║
 ║    pDocOwner  // ссылка на документ владелец                              ║
 ║    p1         // возвращает ссылку на выбраную позицию                    ║
 ╚═══════════════════════════════════════════════════════════════════════════╝
*/

#doc
Выбор МЦ из спецификации ДО
#end
Interface GetDOMC 'Выбор МЦ из спецификации ДО' EscClose, Cyan, DoAccept;
Show at (,,,47);

Create View
Var
  pDocOwner: comp;
  p1: comp;

As Select
  *

From
  BaseDoc,
  StepDoc,
  SpStep,
  KatMC,
  KatOtpEd,
  KatParty
Where
((
  pDocOwner      ==  StepDoc.cBaseDoc  and
  StepDoc.Nrec   ==  SpStep.cStepDoc   and
  word(1)        ==  SpStep.prMC       and
  SpStep.cMCUSL  ==  KatMC.Nrec        and
  SpStep.cOtpEd  ==  KatOtpEd.Nrec     and
  SpStep.cParty  ==  KatParty.nRec
))
;

Parameters
  pDocOwner,  // ссылка на документ из которого надо брать спецификацию
  p1          // ссылка на выбраную позицию при одиночном выборе
  ;

Panel panSpStep;
 Table SpStep;
Browse brSpStep2 (,hcKouDocISpecSell,sci1EnEsc);
Fields
  KatMC.BarKod  'Ном.номер'      :[13], protect,NoAutoSize,NoDel;
  KatMC.Name    'Наименование'   :[16], protect,NoDel;
  KatOtpEd.Abbr 'ЕдИзм'          : [5], protect,NoAutoSize,NoDel;
  SpStep.KolSkl 'Количество'     :[10],[prSignsInKol], protect,NoAutoSize,NoDel;
  SpStep.Price  'Цена за единицу':[12],[brForPrice], protect,NoAutoSize,NoDel;
  KatParty.Name 'Партия'         :[10], protect,NoDel;
end ; // browse

end; // panel

HandleEvent // Interface

cmInit:
  if ( getFirst SpStep where (( p1            ==  SpStep.NRec(NoIndex) and
                                StepDoc.Nrec  ==  SpStep.cStepDoc  and
                                word(1)       ==  SpStep.prMC )) <> tsOK )
    if ( getFirst SpStep <> tsOK )
      {
        Message ( ''#3'У данного ДО нет МЦ в спецификации!', CancelButton );
        abort;
        exit;
      }

cmDefault:
  {
    if ( not IsValid ( #SpStep ))
      {
        abort;
        exit;
      }
    p1 := SpStep.nRec;
  }

end; // HandleEvent Interface

end. // Interface
