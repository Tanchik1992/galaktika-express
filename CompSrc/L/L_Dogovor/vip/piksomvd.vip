/*
 ╔═══════════════════════════════════════════════════════════════════════════╗
 ║                     (c) 1994,97 корпорация ГАЛАКТИКА                      ║
 ║ Проект        : ГАЛАКТИКА                                                 ║
 ║ Система       : Оперативный контур                                        ║
 ║ Версия        : 5.70                                                      ║
 ║ Назначение    : Перегон выбора из маркера в Pick для GetSomVidD           ║
 ║ Ответственный : Глушаков Дмитрий Сергеевич                                ║
 ║ Параметры     : есть                                                      ║
 ║    pVidDog    // возвращает вид документа                                 ║
 ║    pDirect    // возвращает направление                                   ║
 ║    OkOne      // TRUE - груповой выбор FALSE - только один (+возвращает)  ║
 ║    pKatVidD   // входной-выходной - KatVidD.nRec                          ║
 ║    wDirect    // фильтр на Direct                                         ║
 ╚═══════════════════════════════════════════════════════════════════════════╝
*/

#doc
Интерфейс сохранения выбора видов договоров из маркера в Pick
#end
Interface PickSomVidD 'Выбор видов договоров' DoAccept,EscClose,cyan;
  show at (4,2,76,23);
Create View
var
//--параметры
  pVidDog,
  wDirect,
  pDirect      : word;
  OkOne        : boolean;
  pKatVidD     : comp;
//--
  Marker         : longint; // указатель на маркер
  MarkerCount, i : longint; // счетчики записей в маркере
  pickrec        : comp;
As select
  Pick.wList
from
  Pick
where
  ((
    word(448) == Pick.wList
  ))
;
Parameters
  pVidDog,  // возвращает вид документа
  pDirect,  // возвращает направление
  OkOne,    // TRUE - груповой выбор FALSE - только один/ ЕСЛИ возвращает TRUE-помечены все распоряжения!
  pKatVidD, // входной-выходной - KatVidD.nRec
  wDirect   // фильтр на Direct
  ;

HandleEvent
cmInit :
{
var ret:word;
  Marker:=InitMarker('MKatVidD',8,100,10);
  ClearMarker(Marker);
  _loop Pick
  {
    InsertMarker(Marker,Pick.cRec);
  }
  DoneMarker(Marker,'MKatVidD');

  if RunInterface('GetSomVidD',
                  pVidDog,  // возвращает вид документа
                  pDirect,  // возвращает направление
                  OkOne,    // TRUE - груповой выбор FALSE - только один/ ЕСЛИ возвращает TRUE-помечены все распоряжения!
                  pKatVidD, // входной-выходной - KatVidD.nRec
                  wDirect   // фильтр на Direct
                  ) = cmCancel  // если там Cancel - то и здесь Cancel
    {
    Abort;
    Exit;
    }
  delete safe Pick;
  StartNewVisual(vtRotateVisual, vfTimer,
               ''#3'Формирование элементов выбора ...'#13#3, 1);
  Marker:=InitMarker('MKatVidD',8,100,10);
  MarkerCount:=GetMarkerCount(Marker);
  FOR ( i:=0; i<MarkerCount; i:=i+1 )
    {
    if GetMarker(Marker,i,pickrec)
      {
      Pick.wList := 448;
      Pick.PickKol:=i;
      Pick.cRec:=pickrec;
      insert current Pick;
      }
    NextVisual;
    }
  DoneMarker(Marker,'MKatVidD');
  StopVisual('',0);
  PutCommand(cmDefault);
}
end;  // HandleEvent
end.  // Interface