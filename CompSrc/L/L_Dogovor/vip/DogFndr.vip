//********************************************************************************
//                                                        (c) корпорация Галактика
// Галактика 5.7 - логистика
// Интерфейс поиска в договорах
//
//********************************************************************************

#doc
Интерфейс поиска в договорах
#end
Interface dogFinder doAccept, EscClose;
  Show at (,, 70, 19)

Table struct tblDogFinder ""
(
  cDogovor: comp        ""
, NRec    : comp        ""
, Name    : string[150] ""
)
With index
(
  tblDogFinder01 = Name
, tblDogFinder02 = cDogovor
);

Create view vDF

Var
  strFind
, strFind2
, strFilter
, s_SimvRub
            : string;

  wFindPar
, FldNo
            : word;

  pDogovor
            : comp;

Select

  if (Dogovor.cVal = 0, s_SimvRub, KlVal.SimVolV)
                                                  ( FieldName = ValDogovor )

, if (Dogovor.cDogovor = 0, '',
      'по '+if(DogOwner.cDogovor = 0, 'договору ', 'соглашению ')+
      '№ '+DogOwner.NoDoc+DateToStr(DogOwner.dDoc, ' от DD/MM/YYYY'))
                                                  ( FieldName = dogOwnerInfo )

, if(IsValid(tnDogZamena),
     'Отменяет '+if(DogZamena.cDogovor = 0, 'договор', 'соглашение')+
     ' № '+DogZamena.NoDoc+' от '+DateToStr(DogZamena.dDoc,'DD/MM/YYYY г.'),
     '')
                                                  ( FieldName = dogZamenaInfo )
, *

From
  tblDogFinder
, dogovor
, dogovor dogOwner
, CalPlan
, KatOrg
, KatOrg KatOrg1
, KlVal
, KatNotes
, dogovor dogZamena

Where
((
    tblDogFinder.nRec     == dogovor.nRec
and dogovor.cZamena       == dogZamena.nRec
and tblDogFinder.cDogovor == dogOwner.nRec
and dogovor.cVidDog       == KatVidD.nRec
and dogovor.cMyOrg        == KatOrg.nRec
and dogovor.cOrg          == KatOrg1.nRec
and dogovor.cVal          == KlVal.nRec
and dogovor.cNote         == KatNotes.nRec
))

Bounds ByName
    strFind                       <<= tblDogFinder.Name
and padch(strFind, chr(255), 255) >>= tblDogFinder.Name

Condition ByName
  (InStr(UpCase(strFind), Upcase(tblDogFinder.Name)) > 0)

Order by tblDogFinder.Name;

Parameters
  FldNo
, pDogovor
;

//-------------------------------------------------------------------------------

Screen scrDogovorFind (, hcdg_poiskLOKM, sci1Esc)
  Show at (,,, 6)

Fields
  strFind2  (,,) : NoProtect;
  wFindPar  (,,) : NoProtect;
  strFilter (,,) : Skip;

Buttons
  cmValue1,,,'Фильтр по подстроке',,;
  cmValue2,,,'Отмена установленного фильтра по подстроке',,;
  cmValue3,Default,,'Нажмите ENTER для перехода к данной организации в указанную группу',,;
<<
`Подстрока для фильтра:`                 <.Установить  ~ф~ильтр.>
 .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
`Фильтр на`                              <.     С~б~росить     .>
   (.) любое вхождение подстроки       `
   (.) вхождение подстроки как префикса` <.     ~П~ерейти      .>
.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
>>
end;

Browse brDogovorFind (, hcdg_poiskLOKM, sci1EnEsc)
  Show at (, 7,, 13)

Fields

  tblDogFinder.Name 'Поле поиска' ('Нажмите ENTER для перехода к данному документу',,)
                    : [25], Protect;

  if (Dogovor.cDogovor = 0,
    if (Dogovor.cZamena = 0, 'Договор', 'Отм. договор'),
    if (Dogovor.cZamena = 0, 'Уточн. согл.', 'Отм. согл.'))
                    'Документ' ('Нажмите ENTER для перехода к данному документу',,)
                    : [12], Skip;

  dogovor.NoDoc     'Номер' ('Внутренний номер. Нажмите ENTER для перехода к данному документу')
                    : [8], Protect;

  dogovor.dDoc      'Дата'  ('Дата заключения. Нажмите ENTER для перехода к данному документу')
                    : [10,'DD/MM/YYYY'], Protect;

end;

TabbedSheet BOTTOM tshMain
  Show at (, 14,,);

//-------------------------------------------------------------------------------

Screen scrDogovorFind2 'Информация по документу' (, hcdg_poiskLOKM, sci1EnEsc)

Fields

  KatVidD.Role1       ('Роль контрагента 1') : Protect;

  KatOrg.Name         ('Наименование контрагента') : Skip;

  KatVidD.Role2       ('Роль контрагента 2') : Skip;

  KatOrg1.Name        ('Наименование контрагента') : Skip;

  KatVidD.Name        ('Вид договора') : Skip;

  dogovor.Summa       ('Общая (планируемая) сумма по договору') : Skip;

  ValDogovor          ('Валюта договора') : Skip;

  dogovor.TipMoney    ('Тип договора')
                      : [LIST 1 'в НДЕ', 'вал-НДЕ', 'валютный'], Skip;

<<
 Контрагент 1.@@@@@@@@@@@@.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Контрагент 2.@@@@@@@@@@@@.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Вид         .@@@@@@@@@@@@@@@@@@@@@@@@@@Сумма.@@@@@@@@@@@@@@@@@@@.@@@@
 Тип         .@@@@@@@@@@@@@@@@@@@@@@@@@@
>>
end;

//-------------------------------------------------------------------------------

Screen scrDogovorFind3 'Доп. информация по документу' (, hcdg_poiskLOKM, sci1EnEsc)

Fields

  KatNotes.Name       ('Статус договора') : Protect;

  dogovor.DesGr       ('Код группы пользователей') : Skip;

  dogovor.Descr       ('Дескриптор(идентификатор) пользователя') : Skip;

  dogovor.NoDoc_Ext   ('Входящий номер договора') : Skip;

  dogovor.dInput      ('Дата создания договора') : Skip;

  dogovor.NoDoc       ('Номер договора') : Skip;

  dogovor.dDoc        ('Дата заключения договора') : Skip;

  dogOwnerInfo
  + dogZamenaInfo     : Skip;

  dogovor.dBeg        ('Дата начала договора') : Skip;

  dogovor.dEnd        ('Дата окончания договора',) : Skip;

<<
 Группа &Дескр.& Входящий № &Дата ввода & Статус.@@@@@@@@@@@@@@@@@@@@@
 .@@@@@@.@@@@@@ .@@@@@@@@@@@ .@@@@@@@@@@@
  Договор/Согл. .@@@@@@@@@@@ .@@@@@@@@@@@
 .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 На период с .@@@@@@@@@@@@по.@@@@@@@@@@@
>>
end;

end;

//-------------------------------------------------------------------------------

Procedure ClearFilter;
{
  if ( BoundActive(tbbyName) )
    PopBounds(tbbyName);

  if ( ConditionActive(tcbyName) )
    PopCondition(tcbyName);
}

//-------------------------------------------------------------------------------

HandleEvent

cmInit :
{
  s_SimvRub := sGetTune('NDE.SimvRub');

  SetFieldSelectable(#KatVidD.Role1, FALSE);
  SetFieldSelectable(#KatNotes.Name, FALSE);

  strFind := '';
  set strFilter := 'Фильтр не установлен'

  delete all tblDogFinder;

  Var f : word;
  f := FldNo;

  case FldNo of

    1, 2 :
      {
        SetTitle('Поиск договора по контрагенту ' + string(f));

        Insert visual ''
        into tblDogFinder (nRec, cDogovor, Name)
        select dogovor.nRec,
               dogovor.cDogovor,
               if(f = 1, KatOrg.Name, KatOrg1.Name)
        From dogovor, KatOrg, KatOrg KatOrg1
        Where
        ((
           dogovor.cMyOrg == KatOrg.nRec      and
           dogovor.cOrg   == KatOrg1.nRec
        ));
      }

    3 :
      {
        SetTitle('Поиск документа по номеру соглашения');
        ResetBounds(#Dogovor);

        if ( (GetFirst dogovor Where (comp(0) <> dogovor.cDogovor)) <> tsOk )
          {
            Message('Не найдено ни одного соглашения', Information);
            Abort;
            Exit;
          }

        SetBounds(#Dogovor);

        Insert visual ''
        into tblDogFinder (nRec, cDogovor, Name)
        select dogovor.nRec, dogovor.cDogovor, string(Dogovor.NoDoc)
        From dogovor;

        delete tblDogFinder where (( comp(0) == tblDogFinder.cDogovor ));
      }

    4 :
      {
        SetTitle('Поиск документа по номеру ПКП');

        if ( GetFirst CalPlan <> tsOk )
          {
            Message('Не найдено ни одного ПКП', Information);
            Abort;
            Exit;
          }

        Insert visual ''
        into tblDogFinder (nRec, cDogovor, Name)
        select CalPlan.cDogovor, dogovor.cDogovor, string(CalPlan.NoDoc)
        From CalPlan, dogovor where (( CalPlan.cDogovor == dogovor.nRec ));
      }

  end;

  PushBounds(tbbyName);

  if ( GetFirst tblDogFinder <> tsOk ) {}
}

cmValue1 :
{
  ClearFilter;
  strFind := strFind2;

  case wFindPar of

    0: PushCondition(tcbyName);
    1: PushBounds(tbbyName);

  end;

  set strFilter := 'Фильтр на '+
                   if(wFindPar = 0, 'любое ', '')+'вхождение "'+strFind+'"'+
                   if(wFindPar = 1, ' как префикса', '');

  RereadRecord(#tblDogFinder);
}

cmValue2 :
{
  ClearFilter;
  set strFilter := 'Фильтр не установлен';
  RereadRecord(#tblDogFinder);
}

cmValue3 :
  PutCommand(cmDefault);

cmDefault :
  if ( IsValid(#tblDogFinder) )
    {
      if ( FldNo = 3 )
        pDogovor := tblDogFinder.cDogovor;
      else
        pDogovor := tblDogFinder.nRec;
    }
  else
    Abort;

end;

end.
