/*
 ╔═══════════════════════════════════════════════════════════════════════════╗
 ║                     (c) 1987,98 корпорация ГАЛАКТИКА                      ║
 ║ Проект        : ГАЛАКТИКА                                                 ║
 ║ Система       : Оперативный контур                                        ║
 ║ Версия        : 5.50                                                      ║
 ║ Назначение    : Выбор ресурса отгрузки                                    ║
 ║ Ответственный : Володько Вадим Геннадьевич (VVG)                          ║
 ║ Параметры     : есть                                                      ║
 ╚═══════════════════════════════════════════════════════════════════════════╝
*/

#doc
Интерфейс выбора ресурса отгрузки
#end
Interface GetResOtgr 'Выберите ресурс отгрузки' (,hci_koudoc_Res_Otgryz, sci178Esc)
  doAccept,EscClose,cyan;
  show at (4,5,75,19);
Create View
Var
  c1 : comp;
From
  ResOtgr (ResOtgr02);

Parameters
  c1;          // NRec ресурса

QuickChoice qcTTNResOtgrName
{
  AcceptField = ResOtgr.Nrec;
  ViewField   = ResOtgr.Name;
  ViewField   = ResOtgr.Kod;
  SearchField = ResOtgr.Name;

  OnClose : c1 := ResOtgr.NRec
}

Form frmResOtgr('ResOtgr.OUT','Errr');
!-------------------------------------------------------------------------------

Procedure PrintKatalog;
{
  if (updatetable)
  {
    PushPos(#ResOtgr);

    if ((GetFirst ResOtgr) = tsOK)
    {
      frmResOtgr.write('         РЕСУРСЫ ОТГРУЗКИ' + chr(10));
      _loop ResOtgr
      {
        frmResOtgr.write('  ' + pad(ResOtgr.Kod,20)+'   '+ResOtgr.Name);
      }
    }
    PopPos(#ResOtgr);

    frmResOtgr.PutEvent(feBreak);

    if (not frmResOtgr.error)
      frmResOtgr.showFile('');
    else
    {
      frmResOtgr.abortForm;
      ReReadRecord;
    }
  }
  else
    message(''#3'Перед печатью заполните каталог',Information+CancelButton);
}

!-------------------------------------------------------------------------------

Window Win1 'Ресурсы отгрузки' escclose;
  show at (4,5,75,19);

browse brResOtgr ('',hci_koudoc_Res_Otgryz,sci178Esc);
Fields
  ResOtgr.Name 'Ресурс отгрузки':[60];
  ResOtgr.Kod 'Код':[6];
end;

HandleEvent

cmPrintDoc:
  PrintKatalog;

cmDone :
  if (not UpdateTable)
    abort;
  else
    if (not isvalid(#ResOtgr))
      PutCommand(cmClose);

end;
end; // window

!-------------------------------------------------------------------------------

browse ResOtgrbr ('Выберите ресурс и нажмите ВВОД',hci_koudoc_Res_Otgryz,sci147EnEsc)

fields
  ResOtgr.Name 'Ресурс отгрузки':[60],protect;
  ResOtgr.Kod 'Код':[6],protect;
end;

HandleEvent

cmInit :
  {
    if ( GetFirst ResOtgr where ((c1 == ResOtgr.NRec)) <> 0 )
      if ( GetFirst ResOtgr <> 0 )
        {
          if (message(''#3'Справочник ресурсов отгрузки пуст.'#13#3+
                          'Нажмите ВВОД и заполните его...',
                          OkButton) = cmCancel)
            {
              abort;
              exit;
            }
          PutCommand(cmInsert);
        }
  }

cmSetDefault :
  {
    SelectField(#ResOtgr.Name);
    PutCommand(cmEdit);
  }

cmDefault :
  c1 := ResOtgr.NRec;

cmEdit :
  RunWindow(Win1);

cmAddNewRec :
  {
    PutCommand(cmInsert);
    RunWindow(Win1);
    ReReadRecord;
  }

cmCheckRecord :
  if (trim(ResOtgr.Name) = '')
    {
      message(''#3'Укажите наименование ресурса или удалите запись...',
                CancelButton);
      abort;
    }

cmInsertRecord :
  insert current ResOtgr;

cmUpdateRecord :
  update current ResOtgr;

cmDeleteRecord :
  if (Message(''#3'Удалим ресурс отгрузки ?',YesNo)=Yes)
    if (Message(''#3'Отменить удаление ресурса отгрузки ?',YesNo)=No)
      delete current ResOtgr;
    else abort;
  else abort;

cmPickClass:
  if ((UpdateTable) and (ResOtgr.NRec <> 0))
    RunInterface(ExClassifier, word(CurTableCode), ResOtgr.NRec);

cmPickAttr:
  if ((UpdateTable) and (ResOtgr.NRec <> 0))
    RunInterface(Attribute, word(CurTableCode), ResOtgr.NRec);

cmPrintDoc:
  PrintKatalog;

end;
end.