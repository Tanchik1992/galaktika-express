//********************************************************************************
//                                                        (c) корпорация Галактика
// Галактика 7.12 - Управление договорами
// Документы "привязанные" к акту сверки по договору
//********************************************************************************

#doc
Документы "привязанные" к акту сверки по договору
#end
Interface GetDocSver 'Выбранные документы'
  ('Документы "привязанные" к акту', hcDogSelectionDoc, sci178EnEsc)
  DoAccept,
  EscClose,
  Cyan
;
  Show at (, 2, , 22);
create view loGetDocSver
var
  pTiDkSver : word;
  pAktSver  : comp;
  pSoprHoz  : comp;

As select
//******************************************************************************
  if ( DocSver.cVal = 0, sGetTune('NDE.SimvRub'), KlValDoc.SimvolV )
    ( FieldName = ValutName )
//******************************************************************************
, if ( SoprHoz.TiDkGal = 1 or
       SoprHoz.TiDkGal = 8 or
       SoprHoz.TiDkGal = 11 or
       SoprHoz.TiDkGal = 18 or
       SoprHoz.TiDkGal = 21 or
       SoprHoz.TiDkGal = 31 or
       SoprHoz.TiDkGal = 81 or
       SoprHoz.TiDkGal = 83 or
       SoprHoz.TiDkGal = 85 or
       SoprHoz.TiDkGal = 87 or
       SoprHoz.TiDkGal = 1032, KatOrgPol.Name,
       if ( SoprHoz.TiDkGal = 2 or
            SoprHoz.TiDkGal = 3 or
            SoprHoz.TiDkGal = 4 or
            SoprHoz.TiDkGal = 5 or
            SoprHoz.TiDkGal = 6 or
            SoprHoz.TiDkGal = 7 or
            SoprHoz.TiDkGal = 17 or
            SoprHoz.TiDkGal = 32 or
            SoprHoz.TiDkGal = 33 or
            SoprHoz.TiDkGal = 22 or
            SoprHoz.TiDkGal = 82 or
            SoprHoz.TiDkGal = 84 or
            SoprHoz.TiDkGal = 86 or
            SoprHoz.TiDkGal = 88 or
            SoprHoz.TiDkGal = 1011, KatOrgPlat.Name,
            if ( SoprHoz.TiDkGal = 201 or
                 SoprHoz.TiDkGal = 202 or
                 SoprHoz.TiDkGal = 203 or
                 SoprHoz.TiDkGal = 106 or
                 SoprHoz.TiDkGal = 211 or
                 SoprHoz.TiDkGal = 101 or
                 SoprHoz.TiDkGal = 102 or
                 SoprHoz.TiDkGal = 103 or
                 SoprHoz.TiDkGal = 108 or
                 SoprHoz.TiDkGal = 206 or
                 SoprHoz.TiDkGal = 110 or
                 SoprHoz.TiDkGal = 111, KatOrgSopr.Name,
                 if ( SoprHoz.TiDkGal = 51 or
                      SoprHoz.TiDkGal = 52 or
                      SoprHoz.TiDkGal = 41 or
                      SoprHoz.TiDkGal = 42, KatOrgDO.Name,
                      if ( SoprHoz.TiDkGal = 70,
                           if (PlPor.cPlat = PlPor.cPol,
                               KatOrgPlat.Name,
                               KatOrgPlat.Name+', '+ KatOrgPol.Name), '')))))
    ( FieldName = KontrNameSoprHoz )
//******************************************************************************
, if((SoprHoz.TiDkGal = 10) or (SoprHoz.TiDkGal = 21) or (SoprHoz.TiDkGal = 22),
    if((PlPor.ModeDoc and 1024) <> 0, 'продажа',
      if((PlPor.ModeDoc and 2048) <> 0, 'закупка',
        if((PlPor.ModeDoc and 4096) <> 0, 'не определено', '!? ошибка !?'))), '')
    ( FieldName = ModeDocDir )
//******************************************************************************
from
    AktSver
  , synonym AktSver AktSverOld
  , DocSver                            (Normal)
  , Dogovor
  , SoprHoz
  , KlVal
  , synonym KlVal KlValDoc
  , PlPor
  , KatSopr
  , KatOrg
  , synonym KatOrg  KatOrgPlat
  , synonym KatOrg  KatOrgPol
  , synonym KatOrg  KatOrgSopr
  , synonym KatOrg  KatOrgDO
  , IerDog
where
  ((
      pAktSver            ==   AktSver.nRec
  AND pAktSver            ==   DocSver.cAktSver
  AND DocSver.cSoprHoz    ==   SoprHoz.nRec
  AND pTiDkSver           ==   DocSver.TiDkSver
  AND AktSver.cDogovor    ==   Dogovor.nRec
  AND Dogovor.cValRas     ==   KlVal.nRec
  AND DocSver.cVal        ==   KlValDoc.nRec
  AND Dogovor.cOrg        ==   KatOrg.nRec
  AND SoprHoz.cSoprDoc    ==   PlPor.nRec
  AND PlPor.cPlat         ==   KatOrgPlat.nRec
  AND PlPor.cPol          ==   KatOrgPol.nRec
  AND SoprHoz.cSoprDoc    ==   KatSopr.nRec
  AND KatSopr.cOrgBase    ==   KatOrgSopr.nRec
  AND SoprHoz.cSoprDoc    ==   BaseDoc.nRec
  AND BaseDoc.cOrg        ==   KatOrgDO.nRec
  ))
;

Parameters
  pAktSver,                             // NRec акта сверки
  pTiDkSver                             // вид документов 11, 12, 13, 21, 22, 23
;

Browse brDocSver
  Show at (, , , 16);
  Table DocSver;
Fields
  GetTiDkGalName(SoprHoz.TiDkGal) #3'Тип' ('Тип документа'): [13], skip, Centered;
  ModeDocDir       #3'Направление': [11], protect, Centered, NoAutoSize;
  SoprHoz.DatOb    #3'Дата' ('Дата документа'): [10, 'DD/MM/YYYY'], protect, Centered, NoAutoSize;
  SoprHoz.NoDoc    #3'Номер' ('Номер документа'): [6], protect, Centered;
  KontrNameSoprHoz #3'Контрагент' ('Контрагент документа'): [14], protect;
  DocSver.Summa    #3'Сумма' ('Сумма по документу'): [12.2], protect;
  ValutName        #3'Вал' ('Валюта документа'): [4], protect;
  DocSver.SumVal   #3'Взаиморасчет' ('Сумма по взаиморасчетам', , sci1378EnEsc): [12.2], noprotect;
end; // Browse brDocSver
Screen scAktSver
  Show at (, 17, , 19);
  Table AktSver;
Fields
  (pTiDkSver div 10) ('Номер контрагента'): skip;
  KatOrg.Name ('Наименование контрагента договора'): skip;
  if (Dogovor.cValRas = 0, sGetTune('NDE.simvrub'), KlVal.SimvolV) ('Валюта взаиморасчетов по договору'): skip;
<<
 Контрагент.@@@.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Валюта взаиморасчетов по договору.@@@@@@@
>>
end; // Screen scAktSver

HandleEvent // Interface GetDocSver

cmInit:
  {
    oValFunc.InitVal;
    if (not IsValid(#DocSver))
      PutCommand(cmInsert);
  }

cmPick:
  {
    CASE CurField OF
      #DocSver.Sumval: set DocSver.Sumval := DocSver.Summa;
    end; // CASE
  }

cmInsert:
  {
    var newDocSver: word;

    pSoprHoz := 0;
    RunInterface ('GetSoprHoz', word(0),
                                pTiDkSver,
                                Dogovor.nRec,
                                pSoprHoz);

    if (GetFirst SoprHoz where (( pSoprHoz == SoprHoz.nRec )) <> tsOk)
      {
        Abort;
        Exit;
      }

 /*   newDocSver := TRUE;

    if (not newDocSver)
      {
        if (GetFirst AktSverOld
                     where ((DocSver.cAktSver == AktSverOld.nRec)) = tsOk)
          {
            if (AktSverOld.Status = 1)
              {
                newDocSver := TRUE;
                ClearBuffer(#DocSver);
              }
          }
      }
    else
      ClearBuffer(#DocSver);
   */

    newDocSver := 1; // insert DocSver

    if (SoprHoz.TiDkGal <> 70)
      {
        if (GetFirst DocSver where ((pSoprHoz == DocSver.cSoprHoz)) = tsOk)
          {
            _LOOP DocSver where ((pSoprHoz == DocSver.cSoprHoz))
              {
                if (GetFirst AktSverOld
                      where ((DocSver.cAktSver == AktSverOld.nRec)) = tsOk)
                  {
                    if (AktSverOld.Status = 1) //аннулированный
                      continue;

                    if (GetFirst FastFirstRow IerDog
                             where ((AktSver.cDogovor    == IerDog.cUpRec AND
                                     AktSverOld.cDogovor == IerDog.cRec)) <> tsOk)
                      {
                        if (GetFirst FastFirstRow IerDog
                             where ((AktSverOld.cDogovor == IerDog.cUpRec AND
                                     AktSver.cDogovor    == IerDog.cRec)) <> tsOk)
                          { // найденный акт сверки не находится на одной
                            // ветке с формируемым актом
                            // (имеется в виду дерево договоров)
                            if (AktSverOld.Status = 2)
                              { // проведенный
                                newDocSver := 2; // пропустить эту запись
                                ClearBuffer(tnDocSver);
                                Break;
                              }
                            else
                              { // оформляемый
                                newDocSver := 0; // update DocSver
                                Break;
                              }
                          }
                      }
                  }
              }
          }
      }

    DocSver.cAktSver := pAktSver;
    DocSver.TiDkSver := pTiDkSver;
    DocSver.cSoprHoz := pSoprHoz;
    DocSver.cVal     := SoprHoz.KodValut;
    DocSver.Summa    := if (SoprHoz.KodValut = 0, SoprHoz.Summa, SoprHoz.SumValut);

    if (Dogovor.dValCurse <> Date(0, 0, 0))
      {
        DocSver.SumVal := oValFunc.GetAnyCurrency(DocSver.cVal,
                                                  DocSver.Summa,
                                                  Dogovor.dValCurse,
                                                  Dogovor.cValRas)
      }
    else
      {
        if (SoprHoz.dValCurse <> Date(0, 0, 0))
          {
            DocSver.SumVal := oValFunc.GetAnyCurrency(DocSver.cVal,
                                                      DocSver.Summa,
                                                      SoprHoz.dValCurse,
                                                      Dogovor.cValRas);
          }
        else
          {
            if (SoprHoz.DatOb <> Date(0, 0, 0))
              {
                DocSver.SumVal := oValFunc.GetAnyCurrency(DocSver.cVal,
                                                          DocSver.Summa,
                                                          SoprHoz.DatOb,
                                                          Dogovor.cValRas);
              }
            else
              {
                DocSver.SumVal := oValFunc.GetAnyCurrency(DocSver.cVal,
                                                          DocSver.Summa,
                                                          Dogovor.dInput,
                                                          Dogovor.cValRas);
              }
          }
      }

    case newDocSver of
    0: {
          Message(''#3'Документ № '+SoprHoz.NoDoc+' от '+string(SoprHoz.DatOb)+
                  ' уже был ранее добавлен, '#13#3+
                  'Запись обновлена...', OkButton+Information);
          update current DocSver;
        }
    1: {
         DocSver.nRec := comp(0);
         insert current DocSver;
        }
    end; //case

    RereadRecord(#DocSver);
    Abort;
  }

cmDeleteRecord:
  {
    if (Message(''#3'Удалить запись?', YesNo) = Yes)
      delete current DocSver;
  }

cmUpDateRecord:
  {
    if (IsValid(#DocSver))
      update current DocSver;
  }

end; // HandleEvent Interface GetDocSver

End. // Interface GetDocSver
