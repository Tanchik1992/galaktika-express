//********************************************************************************
//                                                        (c) корпорация Галактика
// Галактика 7.12
// Выбор неотложного мероприятия
//********************************************************************************

#ifdef __NIOKR__

#doc
Интерфейс неотложных мероприятий
#end
Interface UrgentM 'Неотложные мероприятия' (, hcN_Klassif_Neotl_Meropr,) EscClose, DoAccept;//, Cyan;
  Show at (, , , 20);

//******************************************************************************
Create view vs_UrgentM

Var
  cDog
, cAppDog
, cCalPlan     : comp;

//----------------------
As Select
*
From
  UrgentM
, Measures
, CalPlan
, Dogovor

Where
((
    cCalPlan                         ==  UrgentM.cCalPlan
AND cCalPlan                         ==  CalPlan.NRec
AND CalPlan.cDogovor                 ==  Dogovor.NRec
AND UrgentM.cMeasures                ==  Measures.NRec
))
;

Parameters
  cCalPlan
;

//******************************************************************************
Function IconUrgentM : string;
var Prefix : string;
{
  IconUrgentM := 'BmpDogGreenCyrcle';

#ifdef ComponentVersion
  Prefix := 'L_DOGOVOR::';
#else
  Prefix := '';
#end

  if (UrgentM.dDoc = Date(0,0,0))
    if (UrgentM.dEnd < Cur_Date)
      IconUrgentM := Prefix + 'BmpDogRedCyrcle';
}

//******************************************************************************


Panel panMeasures;
  Table UrgentM;

Tree brwUrgentM ('', , sci178Esc);
  Table UrgentM;

Fields
  Measures.Name #3'Мероприятие'   (,, sci1378Esc) : [28], Protect;
  UrgentM.dEnd  #3'Дата'#13#3'исполнения'         : [11], NoProtect, NoAutoSize;
  UrgentM.NoDoc #3'Номер'#13#3'документа'         : [18], NoProtect;
  UrgentM.dDoc  #3'Дата'#13#3'документа'          : [11], NoProtect, NoAutoSize;
end;

HandleEvent
cmTreeNodeType  :
  TreeSetNodeType(brwUrgentM, 2, IconUrgentM);

cmPick:
  case CurField of
    #Measures.Name:
      RunInterface('GetMeasures', UrgentM.cMeasures, FALSE);
  end;

cmDelOnProtect:
  case CurField of
    #Measures.Name:
      set UrgentM.cMeasures := 0;
  end;

cmCheckRecord:
  {
    if (UrgentM.cMeasures = comp(0))
      {
        Message('Не задано мероприятие!'#13#13#3
              + 'Задайте или удалите запись!'#13
              , CancelButton+Warning);
        SelectField(#UrgentM.cMeasures);
        Abort;
      }

    if ((UrgentM.NoDoc <> '') AND UrgentM.dDoc = Date(0,0,0))
      {
        Message('Не введена дата подтверждающего документа!'#13#13#3
              + 'Задайте дату или удалите номер подтверждающего документа!'#13
              , CancelButton+Warning);
        SelectField(#UrgentM.dDoc);
        Abort;
      }

    if ((UrgentM.NoDoc = '') AND UrgentM.dDoc <> Date(0,0,0))
      {
        Message('Не введен номер подтверждающего документа!'#13#13#3
              + 'Задайте номер или удалите дату подтверждающего документа!'#13
              , CancelButton+Warning);
        SelectField(#UrgentM.NoDoc);
        Abort;
      }
  }

cmInsertRecord:
  {
    set UrgentM.cDogovor    := if (Dogovor.cDogovor = 0, Dogovor.NRec, Dogovor.cDogovor);
    set UrgentM.cAppDogovor := if (Dogovor.cDogovor = 0, 0, Dogovor.NRec);

    Insert current UrgentM;
  }
cmUpdateRecord:
  Update current UrgentM;

cmDeleteRecord:
  if (Message('Удалить это мероприятие?', Confirmation+YesNo) <> cmYes)
    Abort;
  else
    Delete current UrgentM;

cmDone :
  if (NOT UpdateTable)
    Abort;

end;
end;


HandleEvent
cmInit:
  if ( GetFirst UrgentM <> tsOk )
    { }

cmDefault:
  Abort;

cmAddNewRec:
  PutCommand(cmInsert);

end; // HandleEvent
End. // Interface

#end