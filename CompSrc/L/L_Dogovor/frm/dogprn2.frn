
#declare DogovorReport(LinkFormName, GroupName)
.LinkForm #LinkFormName Prototype is 'DogPrn2'
.NameInList 'Договор/Соглашение'
.Group #GroupName
.Defo Landscape
.Fields
Номер
ДатаС
ДатаПо
СуммаДог
ВалДог
Контрагент1
Контрагент2
Грузополучатель
Грузоотправитель
Назначение
Статус
Группа_ДО

Группа_МЦ
МЦ
Ед_измерения
Количество
Цена
Сумма

.EndFields
 Р
Договор N ^ на период с ^ по ^ на сумму ^ ^
Контрагент1      ^
Контрагент2      ^
Грузополучатель  ^
Грузоотправитель ^
Назначение       ^
Статус           ^
Документ входит в группы:
.{ CheckEnter DogGroup
  ^
.}

┌──────────────────────────┬──────────────────────────┬────────────┬──────────────┬────────────────┬────────────────┐
│          Группа          │ Наименование товара/усл. │Ед.измерения│  Количество  │      Цена      │    Стоимость   │
├──────────────────────────┼──────────────────────────┼────────────┼──────────────┼────────────────┼────────────────┤
.{ CheckEnter Spec                                                                 
│@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@│&'&&&&&&&&.&&&│&'&&&&&&&&&&&.&&│&'&&&&&&&&&&&.&&│
.}                                                                                 
└──────────────────────────┴──────────────────────────┴────────────┴──────────────┴────────────────┴────────────────┘
 Р
.EndForm
#end

#declare DogovorReportWithIer
.var
CurGrPrice, CurKlPrice, TekGrPrice, TekKlPrice: comp;
.endvar
.Fields
Номер
ДатаС
ДатаПо
СуммаДог
ВалДог
Контрагент1
Контрагент2
Грузополучатель
Грузоотправитель
Назначение
Статус
Группа_ДО
.EndFields

.{?INTERNAL; (TekGrPrice <> CurGrPrice);
.Fields
if (CurGrPrice = -1, 'Прайс-листы без группы', loIer.GrPrice.Name)
if (CurKlPrice = 0, 'Без привязки к прайс-листу', loIer.KlPrice.Name)
.EndFields
.} 

.{?INTERNAL; (TekGrPrice = CurGrPrice) and (TekKlPrice <> CurKlPrice);
.Fields
if (CurKlPrice = 0, 'Без привязки к прайс-листу', loIer.KlPrice.Name)
.EndFields
.}

.Fields
Группа_МЦ
МЦ
Ед_измерения
Количество
Цена
Сумма
.EndFields
 Р
Договор N ^ на период с ^ по ^ на сумму ^ ^
Контрагент1      ^
Контрагент2      ^
Грузополучатель  ^
Грузоотправитель ^
Назначение       ^
Статус           ^
Документ входит в группы:
.{ CheckEnter DogGroup
  ^
.}

┌──────────────────────────┬──────────────────────────┬──────────────────────────┬────────────┬──────────────┬────────────────┬────────────────┐
│  Группа ПЛ/Прайс-листы   │          Группа          │ Наименование товара/усл. │Ед.измерения│  Количество  │      Цена      │    Стоимость   │
├──────────────────────────┴──────────────────────────┴──────────────────────────┴────────────┴──────────────┴────────────────┴────────────────┤
.begin
  CurGrPrice := 0;
end.
.{ CheckEnter Spec
.begin
  loIer.GetFirst fastfirstrow SpDocs where (( nRecСтрочки == SpDocs.nRec ));
  if (loIer.GetFirst fastfirstrow KlPrice
               where (( loIer.SpDocs.cPriceLs == KlPrice.nRec )) = tsOK)
  {
    TekKlPrice := loIer.SpDocs.cPriceLs;
    if (loIer.GetFirst fastfirstrow GrPrice
                 where (( loIer.KlPrice.cGrPrice == GrPrice.nRec )) = tsOK)
      TekGrPrice := loIer.KlPrice.cGrPrice
    else
      TekGrPrice := -1;
  }
  else
  {
    TekKlPrice := 0;
    TekGrPrice := -1;
  }
end.
.{?INTERNAL; (TekGrPrice <> CurGrPrice) and (CurGrPrice != 0);
│ └────────────────────────┴──────────────────────────┴──────────────────────────┴────────────┴──────────────┴────────────────┴────────────────┤
.}
.{?INTERNAL; (TekGrPrice <> CurGrPrice);
.begin
  CurGrPrice := TekGrPrice;
  CurKlPrice := TekKlPrice;
end.
│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│
│ ├─┬─@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│
│ │ └──────────────────────┬──────────────────────────┬──────────────────────────┬────────────┬──────────────┬────────────────┬────────────────┤
.} 
.{?INTERNAL; (TekGrPrice = CurGrPrice) and (TekKlPrice <> CurKlPrice);
.begin
  CurKlPrice := TekKlPrice;
end.
│ │                        └──────────────────────────┴──────────────────────────┴────────────┴──────────────┴────────────────┴────────────────┤
│ ├─┬─@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│
│ │ └──────────────────────┬──────────────────────────┬──────────────────────────┬────────────┬──────────────┬────────────────┬────────────────┤
.}
│ │                        │@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@│&'&&&&&&&&.&&&│&'&&&&&&&&&&&.&&│&'&&&&&&&&&&&.&&│
.}
│ └────────────────────────┴──────────────────────────┴──────────────────────────┴────────────┴──────────────┴────────────────┴────────────────┤
└──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┘
 Р
#end

#doc
Продажа
Договор/Соглашение
#end
#DogovorReport(DogovorReport_01,'Продажа')

#doc
Закупка
Договор/Соглашение
#end
#DogovorReport(DogovorReport_02,'Закупка')

#doc
Продажа
Договор/Соглашение с иерархическим представлением спецификации
#end
.LinkForm DogovorReport_01_01 Prototype is 'DogPrn2'
.NameInList 'Договор/Соглашение с иерархическим представлением спецификации'
.Group 'Продажа'
.Defo Landscape
.create view loIer from SpDocs, KlPrice, GrPrice;
#DogovorReportWithIer
.EndForm

#doc
Закупка
Договор/Соглашение с иерархическим представлением спецификации
#end
.LinkForm DogovorReport_02_01 Prototype is 'DogPrn2'
.NameInList 'Договор/Соглашение с иерархическим представлением спецификации'
.Group 'Закупка'
.Defo Landscape
.create view loIer from SpDocs, KlPrice, GrPrice;
#DogovorReportWithIer
.EndForm

