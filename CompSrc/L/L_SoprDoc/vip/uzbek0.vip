!╔═══════════════════════════════════════════════════════════════════════════╗
!║ Назначение    : Выбор МЦ из разреза для документа.                        ║
!╚═══════════════════════════════════════════════════════════════════════════╝

//********************************************************************************

#doc
Назначение:
  Выбор МЦ из разреза для документа

Параметры:
  ParamcPodr - ссылка на подразделение
  ParamcMol  - ссылка на МОЛ
  ParamTiDk  - вид документа
  ParamcDoc  - ссылка на документ
#end

//********************************************************************************

Interface GetMCFromTekSaldo 'Выбор МЦ из разреза для документа. '
 ('Красным выделены МЦ недоступные для выбора.',, sci1EnEsc)
  EscClose,cyan;
  show at (,,,);
Create view
Var
  MarkerTS,MarkerSelect,MarkerTEMPPART : longint;
  IndexTS,IndexSelect,IndexTEMPPART    : longint;
  MarkerKol    : longint;
  IndexKol     : longint;

  ParamTiDk : word;
  ParamcDoc : comp;
  ParamcPodr: comp;
  ParamcMOL : comp;
  PNrec     : comp;
  myKol     : double;
  findflag  : boolean;

(
  picked
)
As select
  if(SearchMarker(MarkerSelect,TekSaldo.NRec,IndexSelect),'√',' '),
  TeKSaldo.*
From
  SpDocs,
  TekSaldo,
  TEMPPART,
  synonym SpDocs SpDocs1,
  synonym SpDocs SpDocs2,
  synonym SpDocs SpDocs3,
  SpStep
Where
((
  ParamcPodr       == TeKSaldo.cPodr and
  ParamcMol        == TekSaldo.cMol  and
  TekSaldo.cMC     ==  KatMC.Nrec     and
  TekSaldo.cParty  ==  KatParty.Nrec  and
  KatMC.cEd        ==  KatEd.NRec
))
;

Parameters
  ParamcPodr,
  ParamcMol,
  ParamTiDk,
  ParamcDoc ;

Procedure FindMakeSp;
{
  findflag:=true;

  case SpDocs.TiDk of

  400 :
     {

        IF (SpDocs.cMakeSp <> 0)
          { PNrec:=SpDocs.NRec; Exit;}

        IF GetFirst SpDocs1 where ((SpDocs.NRec == SpDocs1.cMakeSp)) =tsOK
          { PNrec:=SpDocs1.NRec; Exit;}

        IF GetFirst SpDocs1 where ((SpDocs.NRec == SpDocs1.cUpDoc and
                                     ( SpDocs1.TiDK=403) )) =tsOK
          {

            IF (SpDocs1.cMakeSp <> 0)
              { PNrec:=SpDocs1.NRec; Exit;}

            IF GetFirst SpDocs2 where ((SpDocs1.NRec == SpDocs2.cMakeSp)) =tsOK
              { PNrec:=SpDocs2.NRec; Exit;}
          }

        IF GetFirst SpDocs1 where ((SpDocs.NRec == SpDocs1.cUpDoc and
                                     ( SpDocs1.TiDK=401) )) =tsOK
          {
             IF (SpDocs1.cMakeSp <> 0)
               { PNrec:=SpDocs1.NRec; Exit;}

             IF GetFirst SpDocs2 where ((SpDocs1.NRec == SpDocs2.cMakeSp)) =tsOK
               { PNrec:=SpDocs2.NRec; Exit;}
          }
     }

  401,403:
     {
         IF (SpDocs.cMakeSp <> 0)
           { PNrec:=SpDocs.NRec; Exit;}

         IF GetFirst SpDocs1 where ((SpDocs.NRec == SpDocs1.cMakeSp)) =tsOK
           { PNrec:=SpDocs1.NRec; Exit;}

         IF GetFirst SpDocs1 where ((SpDocs.cUpDoc == SpDocs1.NRec)) =tsOK
           {
             IF (SpDocs1.cMakeSp <> 0)
               { PNrec:=SpDocs.NRec; Exit;}
             IF GetFirst SpDocs2 where ((SpDocs1.NRec == SpDocs2.cMakeSp)) =tsOK
               { PNrec:=SpDocs2.NRec; Exit;}
           }
         IF SpDocs.TiDk=403{
           IF GetFirst SpDocs1 where ((SpDocs.cUpDoc == SpDocs1.NRec)) =tsOK
             IF GetFirst SpDocs2 where ((SpDocs1.cUpDoc == SpDocs2.NRec)) =tsOK
              {
                IF (SpDocs2.cMakeSp <> 0)
                  { PNrec:=SpDocs2.NRec; Exit;}
                IF GetFirst SpDocs3 where ((SpDocs2.NRec == SpDocs3.cMakeSp)) =tsOK
                  { PNrec:=SpDocs3.NRec; Exit;}
              }
            } // IF 403
         IF SpDocs.TiDk=401
         {
           IF GetFirst SpDocs1 where ((SpDocs.NRec == SpDocs1.cUpDoc)) =tsOK
             {
               IF (SpDocs1.cMakeSp <> 0)
                 { PNrec:=SpDocs.NRec; Exit;}

               IF GetFirst SpDocs2 where ((SpDocs1.NRec == SpDocs2.cMakeSp)) =tsOK
                 { PNrec:=SpDocs2.NRec; Exit;}
             }
         } // IF 401
     }

  end; //case
  FindFlag:=false;
}


Procedure FindSpStep;
{
  IF FindFlag
    IF GetFirst SpDocs1 where ((PNrec == SpDocs1.NRec)) = tsOK
      Case SpDocs1.TiDK of
       400: {
             IF GetFirst SpStep where ((SpDocs1.NRec == SpStep.cSpDocs)) =tsOK
               Exit;

             IF GetFirst SpDocs2 where ((SpDocs1.NRec == SpDocs2.cUpDoc and
                                          ( SpDocs2.TiDK=403))) =tsOK
                IF GetFirst SpStep where ((SpDocs2.NRec == SpStep.cSpDocs)) =tsOK
                  {  PNrec:=SpDocs2.NRec ;  Exit; }
             IF GetFirst SpDocs2 where ((SpDocs1.NRec == SpDocs2.cUpDoc and
                                          ( SpDocs2.TiDK=401) )) =tsOK
                IF GetFirst SpStep where ((SpDocs2.NRec == SpStep.cSpDocs)) =tsOK
                  { PNrec:=SpDocs2.NRec ; Exit; }
            }

       403: {

             IF GetFirst SpStep where ((SpDocs1.NRec == SpStep.cSpDocs)) =tsOK
               Exit;
             //вверх на одну запись
             IF GetFirst SpDocs2 where ((SpDocs1.cUpDoc == SpDocs2.NRec)) =tsOK
                IF GetFirst SpStep where ((SpDocs2.NRec == SpStep.cSpDocs)) =tsOK
                  { PNrec:=SpDocs2.NRec  Exit; }

             // вверх на две записи
             IF GetFirst SpDocs2 where ((SpDocs1.cUpDoc == SpDocs2.NRec)) =tsOK
               IF GetFirst SpDocs3 where ((SpDocs2.cUpDoc == SpDocs3.NRec)) =tsOK
                IF GetFirst SpStep where ((SpDocs3.NRec == SpStep.cSpDocs)) =tsOK
                  { PNrec:=SpDocs3.NRec  Exit; }
             }
       401:{
             IF GetFirst SpStep where ((SpDocs1.NRec == SpStep.cSpDocs)) =tsOK
                                Exit;
             // вверх на оду
              IF GetFirst SpDocs2 where ((SpDocs1.cUpDoc == SpDocs2.NRec)) =tsOK
                 IF GetFirst SpStep where ((SpDocs2.NRec == SpStep.cSpDocs)) =tsOK
                   { PNrec:=SpDocs2.NRec  Exit; }
              // вниз на одну
              IF GetFirst SpDocs2 where ((SpDocs1.NRec == SpDocs2.cUpDoc)) =tsOK
                 IF GetFirst SpStep where ((SpDocs2.NRec == SpStep.cSpDocs)) =tsOK
                   { PNrec:=SpDocs2.NRec  Exit; }


           }

      end;

  FindFlag:=false;
}


Procedure MakeTEMPPART;
{

  _LOOP SpDocs where (( ParamTiDk == SpDocs.TiDk and
                        ParamcDoc == SpDocs.cDoc))
    {
      PushPos(#SpDocs);

      FindMakeSp;
      FindSpStep;
      IF FindFlag
        _LOOP SpStep where((Pnrec == SpStep.cSpDocs))
           _LOOP SpSopr where (( SpStep.Nrec == SpSopr.cSpStep ))
              {
                ClearBuffer(#TEMPPART);
                TEMPPART.cMC :=Pnrec;
                TEMPPART.cParty:=SpSopr.cParty;
                insert current TEMPPART;
              }
      PopPos(#SpDocs);
    }
}

Procedure MakeFinal;
{
  _LOOP TEMPPART
     _LOOP TekSaldo where ((TEMPPART.cParty == TekSaldo.cParty and
                            ( ParamcPodr = TekSaldo.cPodr and
                            ParamcMol  = TekSaldo.cMol  ) ))
        InsertMarker(MarkerTS,TekSaldo.NRec);
}

Window winGetPickKol 'Введите количество' (, , sci1Esc) EscClose, DoAccept, Gray;
  Show at (,7,,10);
Screen scrGetPickKol1;
  Table TekSaldo;
  Fields
   TekSaldo.Kol ('Остаток',, sci1Esc): [prSignsInKol], skip;
   myKol ('Количество',, sci1Esc): [prSignsInKol], NoProtect;
<<
                        Остаток       .@@@@@@@@@@@@
 Введите количество по данной позиции .@@@@@@@@@@@@
>>
end;

HandleEvent
cmDefault:
  if (myKol > TekSaldo.Kol)
    myKol:= TekSaldo.Kol;
end; // of HandleEvent Window winSerach

end; // of Window winSerach


Panel pnSelect table TekSaldo
Browse brTekSaldo ('Для выбора МЦ нажмите <Insert>',hcNoContext,sci1InsPM);
Fields
  {Font = {Color = if(SearchMarker(MarkerTS,TekSaldo.NRec,IndexTS),if(SearchMarker(MarkerSelect,TekSaldo.NRec,IndexSelect),ColorMark,0),ColorError)}};
  picked              '√'  : [1], skip,NoAutoSize;
  TekSaldo.Kol     'Имеется'      ('Количество МЦ, имеющееся в наличии по данному приходу ',,): [10],[prSignsInKol], protect;
  KatMC.Name       'Наименование' ('Наименование МЦ ',,): [25], protect;
  KatMC.BarKod     'Ном.номер'    ('Номенклатурный номер МЦ ',,): [12], protect,NoAutoSize;
  KatParty.Name    'Партия'       ('Партия МЦ данного остатка ',, ): [11], protect;
end; // browse

HandleEvent

cmMarkUnMark :
  {
    var tmpDouble : double;
    if SearchMarker(MarkerTS,TekSaldo.NRec,IndexTS)
      {
        if (SearchMarker(MarkerSelect,TekSaldo.NRec,IndexSelect))
          {
            DeleteMarker(MarkerSelect,TekSaldo.NRec);
            if (GetMarker(MarkerTEMPPART,IndexSelect,tmpDouble))
              AtDeleteMarker(MarkerTEMPPART,IndexSelect);
            if (GetMarker(MarkerKol,IndexSelect,tmpDouble))
              AtDeleteMarker(MarkerKol,IndexSelect);
#ifdef ATL51
            RedrawCurrentAndGo (GetCurrentFormat,true);
#else
            if (GetNext TekSaldo = tsOk)
              CallToNeighBours(cmPosDown,#TekSaldo);
            RescanPanel(#TekSaldo);
#end
          }
        else
          {
            InsertMarker(MarkerSelect,TekSaldo.NRec);
            IF GetFirst TEMPPART where ((TekSaldo.cParty == TEMPPART.cParty (NoIndex))) = tsOK
              InsertMarker(MarkerTEMPPART,TEMPPART.cMC);
            RunWindowModal(winGetPickKol);
            InsertMarker(MarkerKol,myKol);
#ifdef ATL51
            RedrawCurrentAndGo (GetCurrentFormat,true);
#else
            if (GetNext TekSaldo = tsOk)
              CallToNeighBours(cmPosDown,#TekSaldo);
            RescanPanel(#TekSaldo);
#end
          }
      }
  }

cmSelectAll :
  {
    PushPos(#TekSaldo);
    ClearMarker(MarkerSelect);
    ClearMarker(MarkerTEMPPART);
    ClearMarker(MarkerKol);
    StartNewVisual(vtNumericVisual, vfTimer+vfBreak+vfConfirm,
             ''#3'Помечаю документы: '#13#3, 1);
    _LOOP TekSaldo
      if SearchMarker(MarkerTS,TekSaldo.NRec,IndexTS)
        {
         if (not NextVisual)
           Break;
         else
           {
             if (not SearchMarker(MarkerSelect,TekSaldo.NRec,IndexSelect))
               {
                 InsertMarker(MarkerSelect,TekSaldo.NRec);
                 IF GetFirst TEMPPART where ((TekSaldo.cParty == TEMPPART.cParty (NoIndex) )) = tsOK
                   InsertMarker(MarkerTEMPPART,TEMPPART.cMC);
                 InsertMarker(MarkerKol,TekSaldo.Kol);
               }
           }
        }
    PopPos(#TekSaldo);
    ReReadRecord;
    RescanPanel(#TekSaldo);
    StopVisual('',0);
  }

cmUnSelectAll :
  {
    ClearMarker(MarkerSelect);
    ClearMarker(MarkerTEMPPART);
    ClearMarker(MarkerKol);
    ReReadRecord;
    RescanPanel(#TekSaldo);
  }

cmDefault :
  CloseInterface(cmDefault);

end;
end; //panel


HandleEvent
cmInit:
  {
    delete all TEMPPART;
    MarkerTS:=InitMarker('TekSaldo',8,100,10,false);
    MarKerSelect:=InitMarker('TekSaldo2',8,100,10,false);
    MarKerTEMPPART:=InitMarker('TEMPPART',8,100,10,false);
    MarkerKol:=InitMarker('TekSaldoKol',8,100,10,false);
    clearmarker(MarkerTS);
    clearmarker(MarkerSelect);
    clearmarker(MarkerTEMPPART);
    clearmarker(MarkerKol);
    MakeTEMPPART;
    MakeFINAL;
  }

cmDone:
  {
    DoneMarker(MarkerTS,'TekSaldo');
    DoneMarker(MarkerSelect,'TekSaldo2');
    DoneMarker(MarkerTEMPPART,'TEMPPART');
    DoneMarker(MarkerKol,'TekSaldoKol');
  }

end; // HandleEvent interface

END.
