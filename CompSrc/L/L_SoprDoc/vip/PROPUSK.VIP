/*
 ╔═══════════════════════════════════════════════════════════════════════════╗
 ║                     (c) 1994,97 корпорация ГАЛАКТИКА                      ║
 ║ Проект        : ГАЛАКТИКА                                                 ║
 ║ Система       : Оперативный контур                                        ║
 ║ Версия        : 5.70                                                      ║
 ║ Назначение    : Пропуск-отвес                                             ║
 ║ Ответственный : Шукан Александр Леонидович                                ║
 ║ Параметры     : есть                                                      ║
 ╚═══════════════════════════════════════════════════════════════════════════╝
*/

//******************************************************************************

#doc
Назначение:
  Пропуск-отвес

Параметры:
  pSopr - ссылка на сопр.документ
#end

//******************************************************************************

Interface Propusk_Otves ' Пропуск-отвес '
  (,hcSopr_PropOtves,),
   EscClose; //, Cyan;
   show at (8,4,71,17);
Create View
Var
  pSopr : comp;

From
  KatSopr,
  SpSopr ,
  KAtMC  ,
  Dover  ,
  DovFIO ,
  PassDoc

Where
((
  pSopr          == PassDoc.cSopr     and
  PassDoc.cDover == Dover.nRec        and
  PassDoc.cSopr  == KatSopr.nRec      and
  Dover.cDovFio  == DovFio.Nrec       and
  KatSopr.nRec   == SpSopr.cSopr      and
  word(1)        == SpSopr.PrMC       and
  SpSopr.cMCUsl  == KatMC.nRec
))
;

Parameters
  pSopr;

form frmPropusk('Propusk.out', 'Propusk') with NoVisual;

Panel pnPropusk;
  table PassDoc;
Screen scPropusk(,,sci1EnEsc);
Fields
  PassDoc.NoDoc   ('Номер пропуска-отвеса',,);
  PassDoc.dDoc    ('Дата выписки пропуска-отвеса',,) : [10,'DD/MM/YYYY'];
  KatSopr.NSopr   ('Номер накладной',,);
  Dover.NoDoc     ('Номер доверенности',,sci13EnEsc);
  Dover.dForm     ('Дата формирования доверенности',,sci13EnEsc) : [10,'DD/MM/YYYY'];
  PassDoc.Driver  ('Ф.И.О. водителя',,);
  PassDoc.Car     ('Автомашина',,);
  PassDoc.Comment ('Примечания к пропуску-отвесу',,);
buttons
  cmValue1 ,,,'Печать пропуск-отвеса',,sci1EnEsc;

<<

    Номер                  Выписан      По счету
  .@@@@@@@@@@@@@@@@@@@@@ .@@@@@@@@@@@ .@@@@@@@@@@@@@@@@@@@@@

 Доверенность .@@@@@@@@@@@@@@@@@@@@@@@ от .@@@@@@@@@@@
 Водитель .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Автомашина .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Примечание .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

                  <.Печать пропуск-отвеса.>
>>
end; // screen scPropusk

!HandleEvent
!
!end; // handleevent
end; // panel pnPropusk

HandleEvent

cmPick :
  {
    if (CurField = #Dover.NoDoc) or (CurField = #Dover.dForm)
      {
        RunInterface(GetDover, 3, PassDoc.cDover);
        if (PassDoc.Driver = '')
          PassDoc.Driver := DovFIO.Name;
        RescanPanel(#PassDoc);
      }
  }

cmValue1 :
  { var dStatus : word;

    frmPropusk.Write(PassDoc.NoDoc);
    frmPropusk.Write(KatSopr.NSopr);
    frmPropusk.Write(PassDoc.Driver);
    frmPropusk.Write(PassDoc.Car);
    frmPropusk.Write(Dover.NoDoc);
    frmPropusk.Write(Dover.dForm);
    frmPropusk.Write(PassDoc.Comment);

    _loop SpSopr
      {
        frmPropusk.Write(KatMC.Name);
        frmPropusk.Write(SpSopr.Kol,3);
      }

    frmPropusk.SkipFormat(2);
    frmPropusk.PutEvent(feBreak);

    frmPropusk.Write(PassDoc.dDoc);

    if (not frmPropusk.Error)
      frmPropusk.ShowFile('');
    else
      frmPropusk.AbortForm;
  }

end; // handleevent

HandleEvent

cmInit:
  {
    if (modifier getfirst PassDoc where ((pSopr == PassDoc.cSopr)) <> tsOk)
      {
        ClearBuffer(#PassDoc);
        PassDoc.cSopr := pSopr;
        insert current PassDoc;
      }
  }

cmDone :
  {
    update current PassDoc;
  }

end; // handleevent
end. // interface
