
#include ExtAttr.vih

//******************************************************************************

#doc
Назначение:
  Редактирование атрибутов "Акцизные товары"

Параметры:
  pNrec - ссылка на сопр.документ
#end

//******************************************************************************

Interface PrintNaklALK 'Редактирование атрибутов "Акцизные товары"' (,hcSkladInmovListLMAkciz,),EscClose;
  Show at (3,3,75,17);

Create View
Var
 pRec,coMyOrgKat : comp;
 DateOtpr, DatePoluch, DateRazresh, DateRazreshTo : date;
 NomerOtpr,NomerPoluch,RekvizitSkl, Razresh, RazreshTo : string;
 piExAttr  : iExtAttr;

From
  KatSopr,SpSopr,KatMC,KatPodr,KatOrg,SchFact,KatOtpEd,
  synonym KatOrg MyOrg,
  synonym KatPodr KatPodrto

Where
((
 pRec              == KatSopr.nRec and
 KatSopr.nRec      == SpSopr.cSopr and
 KatSopr.cPodrFrom == KatPodr.NRec and
 KatSopr.cPodrTo   == KatPodrTo.NRec and
 KatSopr.cOrgBase  == KatOrg.NRec  and
 word(1)           == SpSopr.PrMC  and
 SpSopr.cMcUsl     == KatMC.nRec   and
 SpSopr.cOtpEd     == KatOtpEd.NRec and
 coMyOrgKat        == MyOrg.nRec   and
 KatSopr.cSchFact  == SchFact.NRec
))
;

Parameters
 pRec
;

form PrintALK('PrintALK.out', 'PrintALK') with NoVisual;

Screen scPrintALK101 ('',hcSkladInmovListLMAkciz,sci1Esc);
  Show at (,,,);
!  Table KatSopr;
Fields
 DatePoluch ('Дата получения уведомления',,) : [,'DD/MM/YYYY'],noprotect;
 NomerPoluch('Номер получения уведомления',,) : noprotect;
 DateOtpr   ('Дата отправления уведомления',,) : [,'DD/MM/YYYY'],noprotect;
 NomerOtpr  ('Номер отправления уведомления',,) : noprotect;
 KATPODR.Name('',,) : skip;
 RekvizitSkl('Реквизиты акцизного склада отправителя',,):noprotect;
 Razresh ('Разрешение на учреждение акцизного склада отправителя',,) : noprotect;
 DateRazresh('Дата получения разрешения отправителя',,) : [,'DD/MM/YYYY'],noprotect;
 KATPODRTo.Name('',,) : skip;
 KATPODRTo.ADDR('Реквизиты акцизного склада получателя',,) : noprotect;
 RazreshTo('Разрешение на учреждение акцизного склада получателя',,) : noprotect;
 DateRazreshTo('Дата получения разрешения получателя',,) : [,'DD/MM/YYYY'],noprotect;
<<
   `Дата получ.уведомления`.@@@@@@@@@@ `Номер`.@@@@@@@@@@

   `Дата отпр.уведомления` .@@@@@@@@@@ `Номер`.@@@@@@@@@@

   `Реквизиты акцизного склада отправителя`.@@@@@@@@@@@@@@@@@@@@@@
    .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Разрешение`.@@@@@@@@@@@@@@@@ `Дата выдачи`.@@@@@@@@@@

   `Реквизиты акцизного склада получателя`.@@@@@@@@@@@@@@@@@@@@@@
    .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Разрешение`.@@@@@@@@@@@@@@@@ `Дата выдачи`.@@@@@@@@@@
>>
end;

Screen scPrintALK201 ('',hcSkladInmovListLMAkciz,sci1Esc);
  Show at (,,,);
!  Table KatSopr;
Fields
 DateOtpr   ('Дата отправления уведомления',,) : [,'DD/MM/YYYY'],noprotect;
 NomerOtpr  ('Номер отправления уведомления',,) : noprotect;
 DatePoluch ('Дата получения уведомления',,) : [,'DD/MM/YYYY'],noprotect;
 NomerPoluch('Номер получения уведомления',,) : noprotect;
 KATPODRTo.Name('',,) : skip;
 RekvizitSkl('Реквизиты акцизного склада получателя',,):noprotect;
 RazreshTo('Разрешение на учреждение акцизного склада получателя',,) : noprotect;
 DateRazreshTo('Дата получения разрешения получателя',,) : [,'DD/MM/YYYY'],noprotect;
 KATPODR.Name('',,) : skip;
 KATPODR.ADDR('Реквизиты акцизного склада отправителя',,) : noprotect;
 Razresh ('Разрешение на учреждение акцизного склада отправителя',,) : noprotect;
 DateRazresh('Дата получения разрешения отправителя',,) : [,'DD/MM/YYYY'],noprotect;
<<
   `Дата получ.уведомления`.@@@@@@@@@@ `Номер`.@@@@@@@@@@

   `Дата отпр.уведомления` .@@@@@@@@@@ `Номер`.@@@@@@@@@@

   `Реквизиты акцизного склада получателя`.@@@@@@@@@@@@@@@@@@@@@@
    .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Разрешение`.@@@@@@@@@@@@@@@@ `Дата выдачи`.@@@@@@@@@@

   `Реквизиты акцизного склада отправителя`.@@@@@@@@@@@@@@@@@@@@@@
    .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Разрешение`.@@@@@@@@@@@@@@@@ `Дата выдачи`.@@@@@@@@@@
>>
end;

Screen scPrintALK600 ('',hcSkladInmovListLMAkciz,sci1Esc);
  Show at (,,,);
!  Table KatSopr;
Fields
 DatePoluch ('Дата получения уведомления',,) : [,'DD/MM/YYYY'],noprotect;
 NomerPoluch('Номер получения уведомления',,) : noprotect;
 DateOtpr   ('Дата отправления уведомления',,) : [,'DD/MM/YYYY'],noprotect;
 NomerOtpr  ('Номер отправления уведомления',,) : noprotect;
 KATPODR.Name('',,) : skip;
 KATPODR.ADDR('Реквизиты акцизного склада отправителя',,) : noprotect;
 Razresh ('Разрешение на учреждение акцизного склада отправителя',,) : noprotect;
 DateRazresh('Дата получения разрешения отправителя',,) : [,'DD/MM/YYYY'],noprotect;
 KATPODRTo.Name('',,) : skip;
 KATPODRTo.ADDR('Реквизиты акцизного склада получателя',,) : noprotect;
 RazreshTo('Разрешение на учреждение акцизного склада получателя',,) : noprotect;
 DateRazreshTo('Дата получения разрешения получателя',,) : [,'DD/MM/YYYY'],noprotect;
<<
   `Дата получ.уведомления`.@@@@@@@@@@ `Номер`.@@@@@@@@@@

   `Дата отпр.уведомления` .@@@@@@@@@@ `Номер`.@@@@@@@@@@

   `Реквизиты акцизного склада отправителя`.@@@@@@@@@@@@@@@@@@@@@@
    .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Разрешение`.@@@@@@@@@@@@@@@@ `Дата выдачи`.@@@@@@@@@@

   `Реквизиты акцизного склада получателя`.@@@@@@@@@@@@@@@@@@@@@@
    .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
   `Разрешение`.@@@@@@@@@@@@@@@@ `Дата выдачи`.@@@@@@@@@@
>>
end;

Procedure PrintALK;
{
  case KatSopr.VidSopr of
   101,600,606:PrintALK.SetGroup('Nakl101');
   201:PrintALK.SetGroup('Nakl201');
  end;

  StartNewVisual(vtNumericVisual,vfTimer+vfBreak+vfConfirm,
   ''#3'Печать документа',1);

   PrintALK.write(KatSopr.nRec);

   PrintALK.write(DateOtpr);
   PrintALK.write(NomerOtpr);
   PrintALK.write(DatePoluch);
   PrintALK.write(NomerPoluch);

   PrintALK.write(MyOrg.Name);
   PrintALK.write(MyOrg.UNN);

   if katsopr.vidsopr=600 or katsopr.vidsopr=606 or katsopr.vidsopr=201
    PrintALK.write(KatPodr.ADDR);
   else
    PrintALK.write(RekvizitSkl);
   PrintALK.write(Razresh);
   PrintALK.write(DateRazresh);

   if katsopr.vidsopr=600 or katsopr.vidsopr=606 or katsopr.vidsopr=101
    PrintALK.write(KatPodrTo.ADDR);
   else
    PrintALK.write(RekvizitSkl);
   PrintALK.write(RazreshTo);
   PrintALK.write(DateRazreshTo);

   PrintALK.write(SchFact.dFact);
   PrintALK.write(SchFact.Num);
   PrintALK.write(KATSOPR.DSOPR);
   PrintALK.write(KATSOPR.NSOPR);

  if katsopr.vidsopr=600 or katsopr.vidsopr=606
  {
   PrintALK.write(MyOrg.Name);
   PrintALK.write(MyOrg.UNN);
  }
  else
  {
   PrintALK.write(KatOrg.Name);
   PrintALK.write(KatOrg.UNN);
  }
pushpos(#SpSopr);
 _loop SpSopr
 {
   if not NextVisual break;
   PrintALK.write(KatMC.name);
   PrintALK.write(SpSopr.KolOpl/1000);
   PrintALK.write(KatMC.Volume);//объем
   PrintALK.write(SpSopr.KolOpl*KatOtpEd.Koef/1000);//ДАЛ
 }
poppos(#SpSopr);
   PrintALK.putevent(feBreak);

    if PrintALK.Error
          PrintALK.AbortForm
    else  PrintALK.ShowFile;

  StopVisual('',0);
}


HandleEvent

cmInit :

 {
  coMyOrgKat := coGetTune('MyOrg');
  case KatSopr.VidSopr of
   101:
   {
    setformat(scPrintALK101);
    RazreshTo:=piExAttr.sGetAttr(coKatPodr,KatPodrTo.nRec,'РАЗРЕШЕНИЕ НА УЧРЕЖДЕНИЕ');
    DateRazreshTo:=piExAttr.dGetAttr(coKatPodr,KatPodrTo.nRec,'ДАТА ВЫДАЧИ РАЗРЕШЕНИЯ');
    Razresh:=piExAttr.sGetAttr(coKatOrg,KatOrg.nRec,'РАЗРЕШЕНИЕ НА УЧРЕЖДЕНИЕ');
    DateRazresh:=piExAttr.dGetAttr(coKatOrg,KatOrg.nRec,'ДАТА ВЫДАЧИ РАЗРЕШЕНИЯ');
   }
   201:
   {
    setformat(scPrintALK201);
    Razresh:=piExAttr.sGetAttr(coKatPodr,KatPodr.nRec,'РАЗРЕШЕНИЕ НА УЧРЕЖДЕНИЕ');
    DateRazresh:=piExAttr.dGetAttr(coKatPodr,KatPodr.nRec,'ДАТА ВЫДАЧИ РАЗРЕШЕНИЯ');
    RazreshTo:=piExAttr.sGetAttr(coKatOrg,KatOrg.nRec,'РАЗРЕШЕНИЕ НА УЧРЕЖДЕНИЕ');
    DateRazreshTo:=piExAttr.dGetAttr(coKatOrg,KatOrg.nRec,'ДАТА ВЫДАЧИ РАЗРЕШЕНИЯ');
   }
   600,606:
   {
    setformat(scPrintALK600);
    Razresh:=piExAttr.sGetAttr(coKatPodr,KatPodr.nRec,'РАЗРЕШЕНИЕ НА УЧРЕЖДЕНИЕ');
    DateRazresh:=piExAttr.dGetAttr(coKatPodr,KatPodr.nRec,'ДАТА ВЫДАЧИ РАЗРЕШЕНИЯ');
    RazreshTo:=piExAttr.sGetAttr(coKatPodr,KatPodrTo.nRec,'РАЗРЕШЕНИЕ НА УЧРЕЖДЕНИЕ');
    DateRazreshTo:=piExAttr.dGetAttr(coKatPodr,KatPodrTo.nRec,'ДАТА ВЫДАЧИ РАЗРЕШЕНИЯ');
   }
  end;

  DatePoluch :=piExAttr.dGetAttr(coKatSopr,KatSopr.nREC,'ДАТА ПОЛУЧ.УВЕДОМЛЕНИЯ');
  NomerPoluch:=piExAttr.sGetAttr(coKatSopr,KatSopr.nREC,'НОМЕР ПОЛУЧ.УВЕДОМЛЕНИЯ');
  DateOtpr   :=piExAttr.dGetAttr(coKatSopr,KatSopr.nREC,'ДАТА ОТПР.УВЕДОМЛЕНИЯ');
  NomerOtpr  :=piExAttr.sGetAttr(coKatSopr,KatSopr.nREC,'НОМЕР ОТПР.УВЕДОМЛЕНИЯ');
  RekvizitSkl:=piExAttr.sGetAttr(coKatOrg,KatOrg.nREC,'РЕКВИЗИТЫ АКЦИЗНОГО СКЛАДА');
 }

cmDone :
 {
  piExAttr.dSetAttr(coKatSopr,KatSopr.nREC,'ДАТА ПОЛУЧ.УВЕДОМЛЕНИЯ',DatePoluch);
  piExAttr.sSetAttr(coKatSopr,KatSopr.nREC,'НОМЕР ПОЛУЧ.УВЕДОМЛЕНИЯ',NomerPoluch);
  piExAttr.dSetAttr(coKatSopr,KatSopr.nREC,'ДАТА ОТПР.УВЕДОМЛЕНИЯ',DateOtpr);
  piExAttr.sSetAttr(coKatSopr,KatSopr.nREC,'НОМЕР ОТПР.УВЕДОМЛЕНИЯ',NomerOtpr);
  piExAttr.sSetAttr(coKatOrg,KatOrg.nRec,'РЕКВИЗИТЫ АКЦИЗНОГО СКЛАДА',RekvizitSkl);

  case KatSopr.VidSopr of
   101:
   {
    piExAttr.sSetAttr(coKatPodr,KatPodrTo.nRec,'РАЗРЕШЕНИЕ НА УЧРЕЖДЕНИЕ',RazreshTo);
    piExAttr.dSetAttr(coKatPodr,KatPodrTo.nRec,'ДАТА ВЫДАЧИ РАЗРЕШЕНИЯ',DateRazreshTo);
    piExAttr.sSetAttr(coKatOrg,KatOrg.nRec,'РАЗРЕШЕНИЕ НА УЧРЕЖДЕНИЕ',Razresh);
    piExAttr.dSetAttr(coKatOrg,KatOrg.nRec,'ДАТА ВЫДАЧИ РАЗРЕШЕНИЯ',DateRazresh);
   }
   201:
   {
    piExAttr.sSetAttr(coKatPodr,KatPodr.nRec,'РАЗРЕШЕНИЕ НА УЧРЕЖДЕНИЕ',Razresh);
    piExAttr.dSetAttr(coKatPodr,KatPodr.nRec,'ДАТА ВЫДАЧИ РАЗРЕШЕНИЯ',DateRazresh);
    piExAttr.sSetAttr(coKatOrg,KatOrg.nRec,'РАЗРЕШЕНИЕ НА УЧРЕЖДЕНИЕ',RazreshTo);
    piExAttr.dSetAttr(coKatOrg,KatOrg.nRec,'ДАТА ВЫДАЧИ РАЗРЕШЕНИЯ',DateRazreshTo);
   }
   600,606:
   {
    piExAttr.sSetAttr(coKatPodr,KatPodr.nRec,'РАЗРЕШЕНИЕ НА УЧРЕЖДЕНИЕ',Razresh);
    piExAttr.dSetAttr(coKatPodr,KatPodr.nRec,'ДАТА ВЫДАЧИ РАЗРЕШЕНИЯ',DateRazresh);
    piExAttr.sSetAttr(coKatPodr,KatPodrTo.nRec,'РАЗРЕШЕНИЕ НА УЧРЕЖДЕНИЕ',RazreshTo);
    piExAttr.dSetAttr(coKatPodr,KatPodrTo.nRec,'ДАТА ВЫДАЧИ РАЗРЕШЕНИЯ',DateRazreshTo);
   }
  end;
 }

cmCheckField:
{
 case curfield of
 #KATPODRTo.ADDR: if KATPODRTo.Name<>''
                   update current KatPodrTo;
                  else
                   message('Не определен склад ПОЛУЧАТЕЛЯ в документе.');

 #KATPODR.ADDR: if KATPODR.Name<>''
                   update current KatPodr;
                  else
                   message('Не определен склад ОТПРАВИТЕЛЯ в документе.');
 end;
}

cmPrintDoc: PrintALK;

cmHotkeys:PutHotCommand(RunMenu('MnuPrintNaklALK'));

end; // HandleEvent
end. // Interface


MnuPrintNaklALK Menu
{
-'Печать уведомления',cmPrintDoc,'Печать уведомления для текущего документа',hcSkladInmovListLMAkciz,'Ctrl-P',kbCtrlP,sci1Esc;
}
