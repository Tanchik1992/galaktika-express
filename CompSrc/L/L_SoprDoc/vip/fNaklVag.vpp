function ReCreateTmpTbl(VagNo: string): boolean;
{
  ReCreateTmpTbl := true;
  delete all DocOnVagon;
  StartNewVisual(vtNumericVisual, vfTimer + vfBreak + vfConfirm,
             ''#3'Поиск документов'#13#3, 1);
  _loop SpSopr where (( VagNo        == SpSopr.nVagon and
                        SpSopr.cSopr == KatSopr.NRec ))
    if not NextVisual 
      {
        ReCreateTmpTbl := false;
        Break;
      }
    else
      if (insert DocOnVagon set DocOnVagon.nRec   := KatSopr.nRec,
                                DocOnVagon.NoDoc  := KatSopr.NSopr,
                                DocOnVagon.VidDoc := KatSopr.VidSopr) <> tsOK {};
  StopVisual('',0);
}

procedure DoFindDocOnVagon;
var
  s: string;
  Res: word;
  fndVagNo: string[36];
{
  fndVagNo := '';
  s := '';
  while true do 
    {
      Res := RunDialog(GetVagonFnd, fndVagNo);
      if (s <> fndVagNo) and ((Res = cmValue1) or (Res = cmValue2)) 
        {
          if not ReCreateTmpTbl(fndVagNo) Break;
          s := fndVagNo;
          Res := cmValue1;
        }
      case Res of
        cmValue1: 
          {
            if GetFirst DocOnVagon where (( TypeSopr == DocOnVagon.VidDoc )) <> tsOK 
              {
                message('Документ не найден!', Information);
                Break
              }
            else 
              {
                GetFirst KatSopr where (( DocOnVagon.nRec == KatSopr.nRec ));
                ReReadRecord(#KatSopr);
                ReReadRecord(#SpSopr);
              }
          }
        cmValue2: 
          {
            if GetNext DocOnVagon where (( TypeSopr == DocOnVagon.VidDoc )) <> tsOK 
              {
                message('Поиск закончен!', Information);
                Break;
              }
            else 
              {
                GetFirst KatSopr where (( DocOnVagon.nRec == KatSopr.nRec ));
                ReReadRecord(#KatSopr);
                ReReadRecord(#SpSopr);
              }
          }
      else
        Break;
      end
    }
  ReReadRecord(#KatSopr);
  ReReadRecord(#SpSopr);
}