//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Логистика
//
//******************************************************************************

#include CopyExClass.vih

#doc
Назначение:
  Копирование внешего классификатора
#end

//******************************************************************************

Interface iCExClass;
  Show at (,,,);

//******************************************************************************

create view
var
  wTable_Is
, wTable_Copy   : word;

  cRec_Is
, cRec_Copy     : comp;

from
  ExClassVal
, ExClassVal  ExCVal_Is
, ExClassVal  ExCVal_Copy
, ExClassName
, ExClassName ExCName_Is
, ExClassName ExCName_Copy
, ExClassSeg
, ExClassSeg  ExCSeg_Is
, ExClassSeg  ExCSeg_Copy

where
((
     wTable_Is             == ExCVal_Is.wTable
AND  cRec_Is               == ExCVal_Is.cRec
AND  ExCVal_Is.wTable      == ExCName_Is.wTable
AND  ExCVal_Is.ClassCode   == ExCName_Is.ClassCode
))
;

Function CopyExClass4Table(_wTable_Is   : word;
                           _cRec_Is     : comp;
                           _wTable_Copy : word;
                           _cRec_Copy   : comp) : boolean;
{
  CopyExClass4Table := FALSE;

  wTable_Is   := _wTable_Is;
  wTable_Copy := _wTable_Copy;
  cRec_Is     := _cRec_Is;
  cRec_Copy   := _cRec_Copy;

  if (NOT IsValid(#ExCVal_Is))
    Exit;

  _LOOP ExCVal_Is
    if (GetFirst ExCName_Is = tsOk)
      _LOOP ExClassName where ((_wTable_Copy    == ExClassName.wTable
                            AND ExCName_Is.Name == ExClassName.Name))
        if (GetFirst ExCSeg_Is where ((ExCVal_Is.cClassSeg == ExCSeg_Is.NRec)) = tsOk)
          if (GetFirst ExClassSeg where ((ExClassName.ClassCode == ExClassSeg.ClassCode
                                      AND ExCSeg_Is.Name        == ExClassSeg.Name (NoIndex))) = tsOk)
            if (GetFirst ExClassVal where ((_wTable_Copy          == ExClassVal.wTable
                                        AND _cRec_Copy            == ExClassVal.cRec
                                        AND ExClassName.ClassCode == ExClassVal.ClassCode(NoIndex)
                                        AND ExClassSeg.NRec       == ExClassVal.cClassSeg(NoIndex))) <> tsOK)
              {
                ClearBuffer(#ExClassVal);
                ExClassVal.cRec      := _cRec_Copy;
                ExClassVal.wTable    := _wTable_Copy;
                ExClassVal.ClassCode := ExClassName.ClassCode;
                ExClassVal.cClassSeg := ExClassSeg.NRec;
                insert current ExClassVal;

                CopyExClass4Table := TRUE;
              }
}

End.

