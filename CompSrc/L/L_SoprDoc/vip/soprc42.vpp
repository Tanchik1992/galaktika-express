//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Логистика
// процедуры/функции для работы в сопроводительных документах
//******************************************************************************

#ifNdef soprc42_VPP
#define soprc42_VPP

Function CheckIsNewKatSopr: boolean;
{
  CheckIsNewKatSopr := FALSE;

  if CurTable<>#KatSopr
    Exit;

  if NOT IsNew
    Exit;

  CheckIsNewKatSopr := TRUE;
}

//******************************************************************************

Function CantSchFactMod: string;
{
  CantSchFactMod := '';

  if CheckIsNewKatSopr
    Exit;

#ifdef _AKTREC_VIP
  Exit;
#else
#ifdef _AKTUMC_VIP
  Exit;
#else
  if (KatSopr.cSchFact = 0)
    Exit;
  case wGetTune('Country') of
  //-------------------------
    2:
      CantSchFactMod := ''#13'Сопроводительный документ связан с документом для учета НДС '#13 + SCHFACTNUM;
  //-------------------------
    else
      CantSchFactMod := ''#13'Сопроводительный документ связан со счет-фактурой № '+SchFact.Num;
  //-------------------------
  end;

#end
#end
}

//******************************************************************************

Function CheckAktUsl: string;
{
  if CheckIsNewKatSopr
    Exit;

  CheckAktUsl := '';

  if (GetFirst KSopr where ((KatSopr.NRec == KSopr.cKatSopr)) <> tsOk)
    Exit;

  CheckAktUsl := ''#13'Сопроводительный документ связан с актом № '+KSopr.NSopr;
}

//******************************************************************************

Function CheckcStepDoc: string;
{
  CheckcStepDoc := '';

  if CheckIsNewKatSopr
    Exit;

  if (KatSopr.cStepDoc = 0)
    Exit;

  CheckcStepDoc := ''#13'Сопроводительный документ связан с ДО № ' + BDNomDes;
}

//******************************************************************************

Function CheckOborot: string;
{
  CheckOborot := '';

  if CheckIsNewKatSopr
    Exit;

  if (NOT oSdFuns.ExistsAnyOborotByVid(KatSopr.NRec, KatSopr.VidSopr))
    Exit;

  CheckOborot := ''#13'По сопроводительному документу созданы проводки';
}

//******************************************************************************

Function CheckDateOrd: string;
{
  CheckDateOrd := '';

  if CheckIsNewKatSopr
    Exit;

  if (KatSopr.VidSopr = 111) OR (KatSopr.VidSopr = 211) OR
     (KatSopr.VidSopr = 114) OR (KatSopr.VidSopr = 1610)
    Exit;

  if (KatSopr.dOpr = Date(0,0,0))
    Exit;

  CheckDateOrd := ''#13'По сопроводительному документу сформированы складские ордера';
}

//******************************************************************************

Function CheckValut: string;
{
  CheckValut := '';

  if CheckIsNewKatSopr
    Exit;

  if (not IsValid(tnSpSopr))
    Exit;

  CheckValut := ''#13'В сопроводительном документе есть спецификация';
}

//******************************************************************************

Function CheckNaklMod: string;
{
  CheckNaklMod := '';

  if CheckIsNewKatSopr
    Exit;

#ifdef _AKT111_VIP
  if (KatSopr.cKatSopr = 0)
    Exit;

  CheckNaklMod := ''#13#3'Сопроводительный документ связан с накладной №' + PrintNumber(KSSopr.Descr, KSSopr.NSopr);
#end
}


//******************************************************************************
#ifndef soprcomm_VPP

Function CanChangeDoc(flag: word): boolean;
{
  if CheckIsNewKatSopr
    {
      CanChangeDoc := TRUE;
      Exit;
    }

  CanChangeDoc := FALSE;

  case flag of
  //----------
   //#Valut
    1: if  CantSchFactMod<>'' OR CheckcStepDoc<>'' OR CheckValut<>''
         if Message(CheckValut+CantSchFactMod+CheckcStepDoc+''#13'Продолжить?', YesNo)<>cmYes
           Exit;
  //----------
   //#VhNal
    2: if  CantSchFactMod<>'' OR CheckcStepDoc<>'' OR CheckNaklMod<>''
         if Message(CantSchFactMod+CheckcStepDoc+CheckNaklMod+''#13'Продолжить?', YesNo)<>cmYes
           Exit;
  //----------
    3: if  CantSchFactMod<>''
         if Message(CantSchFactMod+''#13'Продолжить?', YesNo)<>cmYes
           Exit;
  //----------
  end;

  CanChangeDoc := TRUE;
}
#end

//******************************************************************************
#end