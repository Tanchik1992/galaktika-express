!╔═══════════════════════════════════════════════════════════════════════════╗
!║ Назначение    : Интерфейс-проверка для работы с нефтепродуктами           ║
!╚═══════════════════════════════════════════════════════════════════════════╝

#include ExtAttrClass.vih

//******************************************************************************

#doc
Назначение:
  Интерфейс-проверка для работы с нефтепродуктами

Параметры:
  pRec     - ссылку на позицию сопроводительного документа
  CanEdit  - результат проверки
#end

//******************************************************************************

Interface iCheckKodOil alwaysReturn,cacheable;

create view
var
  pRec        : comp;
  CanEdit     : boolean;
  piExAttr    : iExtAttr;
from
  SpSopr,
  KatMC,
  GroupMC
where
((
  pRec           == SpSopr.nRec and
  SpSopr.cMCUSL  == KatMC.nRec  and
  KatMC.cGroupMC == GroupMC.NRec
));

Parameters
  pRec,
  CanEdit;


function fCheckKodOil:boolean;
{
  fCheckKodOil := false;

  IF (not boGetTune('Doc.Capacity.VidSopr201'))
    Exit;//Учет топлива в сопроводительных документах=нет

  If (SpSopr.PrMC <> 1)
    {
      message(''#13#3+'Объемные ед.измерения используются только для учета МЦ');
      Exit;
    }

  var DocCapacityKodGR : string;  DocCapacityKodGR := sGetTune('Doc.Capacity.KodGR')


  If not isValid(#GroupMC)
    {
      message(''#13#3+'Для МЦ необходимо выбрать группу '+DocCapacityKodGR+' "нефтепродукты"'+
              ''#13#3+'или группу с дополнительной пометкой "нефтепродукты" в окне редактирования каталога групп МЦ'+
              ''#13#3+'(Каталог МЦ: группа МЦ "нефтепродукты")');
      Exit;
    }

  If (piExAttr.doGetAttr (coGroupMC,GroupMC.nREC,'КОД ГРУППЫ МЦ "НЕФТЕПРОДУКТЫ"')=1)
    {
      fCheckKodOil := true; //включена дополнительная настройка на внешнем атрибуте
      Exit;
    }

  If (DocCapacityKodGR='')
    {
      message(''#13#3+'Для учета МЦ в объемных ед.измерения необходимо определить код группы МЦ "нефтепродукты" в настройках'+
              ''#13#3+'или установить дополнительную пометку "нефтепродукты" в окне редактирования каталога групп МЦ'+
              ''#13#3+'(Настройка: Учет топлива=Код группы МЦ "нефтепродукты")');
      Exit;
    }

  if (KatMC.kGroupMC <> DocCapacityKodGR)
    {
      message(''#13#3+'Для работы с объемными ед.измерения необходимо, чтобы код группы '+DocCapacityKodGR+
              ''#13#3+'"нефтепродукты" в настройках, совпадал с кодом группы '+KatMC.kGroupMC+' в каталоге МЦ'+
              ''#13#3+'или установить дополнительную пометку "нефтепродукты" в окне редактирования каталога групп МЦ');
      Exit;
    }

  fCheckKodOil := true;
}

Handleevent
cmInit:
  {
    CanEdit:=fCheckKodOil;
    abort;
  }
end;
End.