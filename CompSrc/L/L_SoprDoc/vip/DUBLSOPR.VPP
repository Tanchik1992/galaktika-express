//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Логистика
// Отчет по дублирующимя позициям накладной
//******************************************************************************

#ifNdef __SDfuns_vip__
#info Вместо использования DublSopr из DublSopr.vpp необходимо использовать oSDfuns.DublSopr(KatSopr.NRec)
#end

//******************************************************************************

Form DublSprForm('dublsprs.OUT', 'dublsopr') with novisual;

//******************************************************************************

Procedure DublSopr(cKatSopr: comp);
{

  StartNewVisual(vtNumericVisual, vfTimer + vfBreak + vfConfirm,
    'Формирование справки по повторяющимся позициям в накладной', 1);

  var mcount: double;           mcount        := 0;
  var nnn: double;              nnn           := 0;

  var oldMC: comp;              oldMC         := 0;
  var oldParty: comp;           oldParty      := 0;
  var oldPrMC: double;          oldPrMC       := 0;

  var oldMCName: string;        oldMCName     := '';
  var oldBarKod: string;        oldBarKod     := '';
  var oldPartyName: string;     oldPartyName  := '';
  var oldOtpEd: string;         oldOtpEd      := '';
  var oldValut: string;         oldValut      := '';

  var oldKolFact: double;       oldKolFact    := 0;
  var oldCena: double;          oldCena       := 0;
  var oldStoim: double;         oldStoim      := 0;

  var OutFlag: boolean;         OutFlag       := FALSE;

  _LOOP SpSopr where ((cKatSopr == SpSopr.cSopr )) ordered by index SpSopr08
    if (oldMC = SpSopr.cMCUSL) AND (oldParty = SpSopr.cParty) AND (oldPrMC = SpSopr.PrMC)
      {
        inc(mcount);
        oldKolFact := SpSopr.KolFact + oldKolFact;
      }
    else
      {
        if (mcount > 1)
          {
            DublSprForm.Write(oldMCName);
            OutFlag := (not DublSprForm.Error) OR (NextVisual);
            if (not OutFlag)
              Break;

            DublSprForm.Write(oldBarKod);
            DublSprForm.Write(oldPartyName);
            DublSprForm.Write(oldKolFact);
            DublSprForm.Write(oldOtpEd);
            DublSprForm.Write(oldValut);
            DublSprForm.Write(mcount);
          }

        oldMC := SpSopr.cMCUSL;                
        oldParty := SpSopr.cParty;             
        oldPrMC := SpSopr.PrMC;                
                                               
        if SpSopr.PrMC = 1                     
          {                                    
            if GetFirst FastFirstRow KatMC where ((SpSopr.cMCUSL == KatMC.NRec)) = tsOK
              {
                oldMCName := KatMC.Name;           
                oldBarKod := KatMC.BarKod;         
              }
          }                                    
        else                                   
          {                                    
            if GetFirst FastFirstRow KatUSL where ((SpSopr.cMCUSL == KatUSL.NRec)) = tsOK
              {
                oldMCName := KatUSL.Name;          
                oldBarKod := KatUSL.Kod;           
              }
          }                                    
                                               
        if (GetFirst FastFirstRow KatParty where ((SpSopr.cParty == KatParty.NRec)) = tsOK)
          oldPartyName := KatParty.Name;         

        oldOtpEd := fEdIzm.GetNameOtpEd(SpSopr.cOtpEd);                     
        oldValut := oValFunc.GetValName(SpSopr.cVal);                     
        oldKolFact := SpSopr.KolFact;          
                                               
        mcount := 1;
      }

  if (mcount > 1)
    {
      DublSprForm.Write(oldMCName);
      DublSprForm.Write(oldBarKod);
      DublSprForm.Write(oldPartyName);
      DublSprForm.Write(oldKolFact);
      DublSprForm.Write(oldOtpEd);
      DublSprForm.Write(oldValut);
      DublSprForm.Write(mcount);
      mcount := 0;
    }

  StopVisual('', vfSilent);

  if (OutFlag)
    {
      DublSprForm.PutEvent(feBreak);
      DublSprForm.ShowFile('Справка по повторяющимся позициям спецификации');
    }
  else
    {
      DublSprForm.AbortForm;
      Message('Повторяющихся позиций в спецификации не обнаружено', Information);
    }
}

//******************************************************************************
