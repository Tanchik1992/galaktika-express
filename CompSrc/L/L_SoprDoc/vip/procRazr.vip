//********************************************************************************
//                                                        (c) корпорация Галактика
// Галактика 5.85 - логистика - снабжение
// Распределение позиций по разрезам
//********************************************************************************

//******************************************************************************

#doc
Назначение:
  Распределение позиций по разрезам

Параметры:
  Нет параметров
#end

//******************************************************************************

Interface ProcentRazrez 'Распределение МЦ в разрезы (в %)' (,hcPrihNaklLocMFormRasped,), EscClose, DoAccept, Cyan;
  Show At (10,4,70,15)

Create view
Var
  what: word;

From
  KoefPodr

where
((
       comp(0)        == KoefPodr.cMC
  AND  KoefPodr.cPodr == KatPodr.NRec
  AND  KoefPodr.comp1 == KatMOL.NRec
))
;

//******************************************************************************

Panel pnKoefPodr
Table KoefPodr;

Browse br1 ('', hcPrihNaklLocMFormRasped,sci1378EnEsc);
  Show At (,,,8)
Fields
  KatPodr.name  #3'Склад'('Укажите склад'): [25], protect;
  KatMOL.name   #3'МОЛ'  ('Укажите МОЛ')  : [30], protect;
  KoefPodr.dbl1 #3' %' ('Укажите процент распределения МЦ в данный разрез',,sci178Esc):
       {Font = {Bold = true}}, [5], noProtect;
end;

Screen scrProcentRazrez
  Show At (,9,,)
Fields
Buttons
  cmOk, Default,,'Сформировать распределение',,sci178Esc;
  cmCancel,,,'Отменить формирование',,sci178Esc;
<<
       <. Сформировать .>       <.    Отмена    .>
>>
end; // screen

//******************************************************************************

HandleEvent

cmInsertRecord:
  Insert Current KoefPodr;


cmUpdateRecord:
  Update Current KoefPodr;

cmDeleteRecord:
  if Message('Удалить запись ?', YesNo) <> cmYes
    Abort
  else
    Delete Current KoefPodr;


cmCheckRecord:
{
  if (KoefPodr.cPodr = 0)
  {
    Message('Укажите склад или удалите запись', CancelButton);
    SelectField(#KatPodr.name);
    Abort;
    Exit;
  }

  if (KoefPodr.dbl1 = 0)
  {
    Message('Укажите процент или удалите запись', CancelButton);
    SelectField(#KoefPodr.dbl1);
    Abort;
    Exit;
  }
}
end;
end; // panel

//******************************************************************************

HandleEvent

cmSetDefault:
{
  SelectField(#KatPodr.Name);
  PutCommand(cmPick);

  var d : double; d := 0;

  PushPos(#KoefPodr);

  _LOOP KoefPodr
    d := d + KoefPodr.dbl1;

  PopPos(#KoefPodr);

  KoefPodr.dbl1 := if (100 - d > 0, 100 - d, 0);
}

cmCheckField:
  if CurField = #KoefPodr.dbl1
  {
    if (KoefPodr.dbl1 < 0)
      KoefPodr.dbl1 := 0;

    var d : double; d := 0;

    PushPos(#KoefPodr);

    _LOOP KoefPodr
      d := d + KoefPodr.dbl1;

    PopPos(#KoefPodr);

    if (not IsNew)
      d := d - double(OldFieldValue);

    if (d + Koefpodr.dbl1 > 100)
      Koefpodr.dbl1 := 100 - d;

    if (d = 100)
    {
      Message('Уже распределено 100% МЦ. Удалите запись');
      Abort;
      Exit;
    }

    if (Koefpodr.dbl1 = 0)
    {
      Message('Укажите ненулевое значение или удалите запись');
      Abort;
      Exit;
    }
  }

cmPick:
  case CurField of
   #KatPodr.Name:
   {
     if RunInterface('GetAnyPodr', KoefPodr.cPodr, word(0)) = cmCancel
       Exit;
     set KoefPodr.cPodr := KoefPodr.cPodr;
   }
   #KatMOL.Name  :
   {
     if RunInterface('GetMOL', KoefPodr.comp1, KoefPodr.cPodr) = cmCancel
       Exit;
     set KoefPodr.comp1 := KoefPodr.comp1;
   }
  end;

cmOk:
{
  PutCommand(cmDefault);
}

end;
End. // Interface
