Window SHT_CE_GetSopr 'Введите номер распоряжение на отгрузку'  EscClose;
  show at (30,10,60,17) ;

Screen scrSHT_CE_GetSopr (,,sci1EnEsc)
Fields
  SHT_CE_nSopr ('Введите номер распоряжения на отгрузку') : noprotect ;
  SHT_CE_nDate ('Введите дату  распоряжения на отгрузку') : noprotect ;
  SHT_CE_dUse ('использовать дату  в поиске') : noprotect ;
Buttons
  cmOk,,,'Подтверждение ввода';
  cmCancel,,,'Выход';
<<

  Номер .@@@@@@@@@@@@@@@@@@@
  Дата  .@@@@@@@@@@@

  [.] использовать дату в поиске`

 <.~П~родолжить.>  <. ~О~тмена .>

>>
end ; // Screen scrSHT_CE_GetSopr

HandleEvent // Window SHT_CE_GetSopr
cmInit :
  {
    SHT_CE_nSopr := '' ;
    SHT_CE_nDate := Cur_Date;
    SHT_CE_dUse  := 1;
    PutCommand(cmValue1);
  }

cmValue1 :
  SelectField(#SHT_CE_nSopr) ;

cmOk :
  PutCommand(cmDefault) ;

cmDefault :
  {
    PackFields ;
    set SHT_CE_nSopr := trim(SHT_CE_nSopr) ;
    if (SHT_CE_dUse = 0)
      {
        if (GetFirst KatSopr where (( 201          == KatSopr.VidSopr
                                  and SHT_CE_nSopr == KatSopr.nSopr    (NoIndex)
                                  and ((SHT_CE_Vid and KatSopr.FilialNo)>0)
                               )) <> tsOk)
          {
            SelectField(#SHT_CE_nSopr);
            Message('Не найдено накладной с номером "'+SHT_CE_nSopr+'"',CancelButton) ;
            EXIT ;
          }
      }
    else
      {
        if (GetFirst KatSopr where (( 201          == KatSopr.VidSopr
                                  and SHT_CE_nDate == KatSopr.dSopr    
                                  and SHT_CE_nSopr == KatSopr.nSopr
                                  and ((SHT_CE_Vid and KatSopr.FilialNo)>0)
                               )) <> tsOk)
          {
            SelectField(#SHT_CE_nSopr);
            Message(''#3'Не найдено накладной с номером "'+SHT_CE_nSopr+'"'+
                    ''#13#3'на дату:"'+DateToStr(SHT_CE_nDate,'DD/MM/YYYY')+'"',CancelButton) ;
            EXIT ;
          }
      }
    RunWindowModal(SHT_CE_KatSopr_Full);
    set SHT_CE_nSopr := '' ;
  }
end ; // HandleEvent Window SHT_CE_GetSopr
end ; // Window SHT_CE_GetSopr