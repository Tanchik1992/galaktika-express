//********************************************************************************
//                                                        (c) корпорация Галактика
// Галактика 7.12 - _
// _
//********************************************************************************


Procedure MakeNewMTabReestrforKN2;
{
 delete all MTabReestrforKN2;

 If GetFirst KatSopr where ((c1 == KatSopr.nRec)) = tsOk{};

   StartNewVisual(vtNumericVisual, vfTimer+vfBreak+vfConfirm,
    'Формирую еще одну временную таблицу.', 1);
 _LOOP SpSopr
  _LOOP SpSopr3
  {
    If (SpSopr3.PrMC = 1) AND (SpSopr.cMcUsl = SpSopr3.cMcUsl)
    {
     ClearBuffer(#MTabReestrforKN2);
       MTabReestrforKN2.KatSopr2nRec   := KatSopr3.nRec;
       MTabReestrforKN2.KatSopr2VidSopr:= KatSopr3.VidSopr;
       MTabReestrforKN2.SpSopr2nRec    := SpSopr3.nRec;
       MTabReestrforKN2.KatSopr2nSopr  := KatSopr3.nSopr;
       MTabReestrforKN2.KatSopr2dSopr  := KatSopr3.dSopr;
       MTabReestrforKN2.KatSopr2dOpr   := KatSopr3.dOpr;
// == == == == == == == == == == == == == == == == == == == == == == == == == == == =
// разрез СКЛАД-МОЛ из приходной(КН) накладной
//   SoprOrdB3.cPodr                == KatPodr3.NRec     AND
//   SoprOrdB3.cMol                 == KatMol3.NRec      AND
       MTabReestrforKN2.KatPodrName    := KatPodr3.Name;
       MTabReestrforKN2.KatMolName     := KatMol3.Name;
       MTabReestrforKN2.cKatPodr       := KatPodr3.nRec;
       MTabReestrforKN2.cKatMol        := KatMol3.nRec;
// разрез СКЛАД-МОЛ из НВП
//   SpSopr3.cSopr                  == KatSopr3.nRec     AND
//   KatSopr3.cPodrFrom             == PodrFrom3.NRec    AND
//   KatSopr3.cMolFrom              == MolFrom3.NRec     AND
//   KatSopr3.cPodrTo               == PodrTo3.NRec      AND
//   KatSopr3.cMolTo                == MolTo3.NRec       AND
       MTabReestrforKN2.PodrFrom       := PodrFrom3.Name;
       MTabReestrforKN2.MolFrom        := MolFrom3.Name;
       MTabReestrforKN2.PodrTo         := PodrTo3.Name;
       MTabReestrforKN2.MolTo          := MolTo3.Name;
       MTabReestrforKN2.cPodrFrom      := KatSopr3.cPodrFrom;
       MTabReestrforKN2.cMolFrom       := KatSopr3.cMolFrom;
       MTabReestrforKN2.cPodrTo        := KatSopr3.cPodrTo;
       MTabReestrforKN2.cMolTo         := KatSopr3.cMolTo;
// == == == == == == == == == == == == == == == == == == == == == == == == == == == =
       MTabReestrforKN2.KatMCName      := KatMc3.Name;
       MTabReestrforKN2.KatPartyName   := Katparty3.Name;
       MTabReestrforKN2.KatSopr2cVal   := KatSopr3.cVal;
//       MTabReestrforKN2.NewPrice       := If(KatSopr3.cVal = 0, SpSopr.rprice, SpSopr.rvprice);
       MTabReestrforKN2.oldPrice       := SpSopr3.rprice;
       MTabReestrforKN2.NewPrice       := SpSopr.rprice;
       MTabReestrforKN2.KolOpl         := SpSopr3.KolOpl;
     If insert current MTabReestrforKN2<>tsOK{};
     if not NextVisual Break;
   }//If (SpSopr3.PrMC = 1) AND (SpSopr.cMcUsl = SpSopr3.cMcUsl)
  }//_LOOP SpSopr3
    StopVisual('', 0);
}//Procedure


Procedure SetFltBNSopr2;
{
 var str: string[20];
 str := MTabReestrforKN2.KatSopr2nSopr;

 if (RunDialog(DlgSetFltBNSopr, str) <> cmCancel)
    {
     BNSopr3 := str;
     PushBounds(tbnSoprBound3);
    }
  else
      if (Message('Снять установленный фильтр?', YesNo) = cmYes)
        {
          PopBounds(tbnSoprBound3);
        };

  RereadRecord;
}

Procedure SetSoprFlt2;
var aBegDate, aEndDate: Date;
    WhatDate: word;
{
  ReadMyDsk(aBEGdate, 'aBEGdate_MakePos201fromKN', FALSE);
  ReadMyDsk(aENDdate, 'aENDdate_MakePos201fromKN', FALSE);

!  if (RunDialog(GetInterval, abegdate, aenddate) <> cmCancel)
 if (RunDialog(GetInterval1, WhatDate, abegdate, aenddate) <> cmCancel)
    {
      if (abegdate > aenddate)
        {
          Message('Начальная дата фильтра больше конечной. Фильтр не изменен...', Information);
          Exit;
        }
      begdate := abegdate;
      enddate := aenddate;
      if WhatDate = 0
         PushBounds(tbdMainBound3);
      else
         PushBounds(tbdMainBound23);
    }
  else
      if (Message('Снять установленный фильтр?', YesNo) = cmYes)
        {
         if WhatDate = 0
            PopBounds(tbdMainBound3);
         else
            PopBounds(tbdMainBound23);
        };

  SaveMyDsk(aBEGdate, 'aBEGdate_MakePos201fromKN');
  SaveMyDsk(aENDdate, 'aENDdate_MakePos201fromKN');
  RereadRecord;
}


Procedure KatSopr600Default(TW: word);
{
!  ClearBuffer(#KatSopr);
  KatSopr.nRec := 0;
  KatSopr.VidSopr := 600;
  KatSopr.wADoc   := if (TW = 0, adSoprDocSNSell, adSoprDocKNSell);
  KatSopr.cADoc   := MTabReestrforKN2.KatSopr2nRec;
  KatSopr.NSopr    := if(TW = 0, KatSopr.NSopr+'(-КН)', KatSopr.NSopr+'(КН'+if(NN_KN>1, string(NN_KN), '')+')');

 if(TW = 0)//-КН
 {
  KatSopr.cPodrFrom := MTabReestrforKN2.cPodrTo;
  KatSopr.cMolFrom  := MTabReestrforKN2.cMolTo;
  KatSopr.cPodrTo   := MTabReestrforKN2.cPodrFrom;
  KatSopr.cMolTo    := MTabReestrforKN2.cMolFrom;
 }else
 {
  if (p1 = 1)//Склад-МОЛ из приходной накладной(КН)
  {
    KatSopr.cPodrFrom := MTabReestrforKN2.cKatPodr;
    KatSopr.cMolFrom  := MTabReestrforKN2.cKatMOL;
  }
 }

  KatSopr.cStepDoc := 0;
  KatSopr.reserved := 0;
  if not BoGetTune('Oper.CopyTXO')
    KatSopr.cHozOper := 0; //101.13575, 101.17360

  case wGetTune('Doc.Sell.Nakl.FacturDate') of
    0: { // дата приходной накладной
      PushPos(#KatSopr2);

      if GetFirst KatSopr2 where((c1 == KatSopr2.nRec)) = tsOk
        KatSopr.dSopr := KatSopr2.dSopr;
      PopPos(#KatSopr2);
    }

    1: { // дата накладной на внутреннее перемещение
      KatSopr.dSopr := MTabReestrforKN2.KatSopr2dSopr;
    }

    2: { // текущая дата
      KatSopr.dSopr := Cur_Date;
    }
  end;

  KatSopr.dOpr     := 0;
  KatSopr.cKatSopr := 0;
  KatSopr.YearSopr := Year(Cur_Date);
  KatSopr.cSchFact := 0;

  if (not boGetTune('Doc.ChangeDescr'))
  {
    KatSopr.Descr := sGetTune('User.Descr');
    KatSopr.DesGr := sGetTune('User.DesGr');
  }
  Insert current KatSopr;
  Curse := GetCursPrice;
}

Procedure SpSopr600Default(TW: word);
{
!  ClearBuffer(#SpSopr);
  var vcSpec : comp;
#ifndef __MTR_TO_SPEC__
  var vcZatr : comp;
  vcZatr := 0;
  if (IsValid(#SpecZatr))
    vcZatr := SpecZatr.cAddSumTune;
#end

  vcSpec := SpSopr.nRec;
  SpSopr.nRec := 0;
  SpSopr.cSopr := KatSopr.NRec;
  SpSopr.dSopr := KatSopr.dSopr;
  SpSopr.dOprTTN := 0;
  SpSopr.cSpOrder := 0;
  SpSopr.cSpStep := 0;
  SpSopr.cSpSopr := 0;
  SpSopr.cSpSchf := 0;//пир 103.1227
  SpSopr.Npp := oSpNext.GetSpNppNext(KatSopr.NRec, coSpSopr);

  SpSopr.wADocSp  := KatSopr.wADoc;
  SpSopr.cADocSp  := vcSpec;

!  If TW = 0
!  {
!   SpSopr.KolOpl := -SpSopr.KolOpl;
!   SpSopr.Kol := -SpSopr.Kol;
!   SpSopr.KolFact := -SpSopr.KolFact;
!  }

  If TW<>0
  {
    SpSopr.rprice := MTabReestrforKN2.NewPrice;
    SpSopr.rvprice := MTabReestrforKN2.NewPrice/Curse;
    SpSopr.price := SpSopr.rprice;
    SpSopr.vprice := SpSopr.rvprice;
  }

  Insert current SpSopr;
#ifndef __MTR_TO_SPEC__
  if (vcZatr <> 0)
    insert SpecZatr Set SpecZatr.coTable := coSpSopr, SpecZatr.cSpec := SpSopr.nRec, SpecZatr.cAddSumTune := vcZatr;
#else
  oMTRFun.CopySpecMTR(coSpSopr, vcSpec, coSpSopr, SpSopr.nRec);
#end
  RunInterface('CopySpDocSmeta', word(coSpSopr), vcSpec, word(coSpSopr), SpSopr.nRec);
//-- Копирование налогов по позиции
//в накладной на внутреннее перемещение нет налогов
}//SpSopr600Default


Procedure MTabReestrforKN2cmAlt1;
{
  If CurTable <> #MTabReestrforKN2 Exit;
  PushPos(#MTabReestrforKN2);
   If GetFirst MTabReestrforKN2 where(( 1 == MTabReestrforKN2.Pick)) ordered by index TMP08indexmem<>tsOk
    {
     Message('Необходимо пометить по <Ins> позиции для формирования по ним Накладных на внутреннее перемещение.');
     PopPos(#MTabReestrforKN2);
     Exit;
    }

    If RunDialog(DlgSetTypeWork, p1) = cmCancel
    {
     Exit;
    }

    var _KatSopr2nSopr  : string; _KatSopr2nSopr := MTabReestrforKN2.KatSopr2nSopr;
    var _KatPodrName    : string; _KatPodrName := MTabReestrforKN2.KatPodrName;
    var _KatMolName     : string; _KatMolName := MTabReestrforKN2.KatMolName;
    var TmpKatSoprnRec  : comp;   TmpKatSoprnRec := 0;
    var i               : word;   i := 0;

     ShowRep := FALSE;

   StartNewVisual(vtNumericVisual, vfTimer+vfBreak+vfConfirm, 'Создание Накладных на внутреннее перемещение.', 1);

     If GetFirst KatSopr where((MTabReestrforKN2.KatSopr2nRec == KatSopr.nRec)) = tsOk
     {
        KatSopr600Default(i);
        TmpKatSoprnRec := KatSopr.nRec; NN_KN := 1;
        WriteERRRMessage;
       For ( i := 0; i<2; i := i+1 )  //два прохода сначала делаю -КН потом КН документы
      {
       _LOOP MTabReestrforKN2 where(( 1 == MTabReestrforKN2.Pick)) ordered by index TMP08indexmem
       {
         if not NextVisual Break;
         If (_KatSopr2nSopr = MTabReestrforKN2.KatSopr2nSopr)
           AND CheckRazrez(i, _KatPodrName, MTabReestrforKN2.KatPodrName, _KatMolName, MTabReestrforKN2.KatMolName)
          {
             If GetFirst SpSopr where((MTabReestrforKN2.SpSopr2nRec == SpSopr.nRec)) = tsOk
             {
              SpSopr600Default(i);
             }
          }
          else
          {
            If (_KatSopr2nSopr <> MTabReestrforKN2.KatSopr2nSopr)
              NN_KN := 1;
            else
              NN_KN := NN_KN+1;
              //пересчитываю толькочто созданные документы
             If GetFirst KatSopr where((TmpKatSoprnRec == KatSopr.nRec)) = tsOk
             {
              RecalcSumm;
!               RunInterface( iChkSum, KatSopr.NRec, 0, FALSE, If(KatSopr.cVal = 0, 10, 20), TRUE);
               if not NextVisual Break;
             }

              _KatSopr2nSopr := MTabReestrforKN2.KatSopr2nSopr;
              _KatPodrName  := MTabReestrforKN2.KatPodrName;
              _KatMolName   := MTabReestrforKN2.KatMolName;

              If GetFirst KatSopr where((MTabReestrforKN2.KatSopr2nRec == KatSopr.nRec)) = tsOk
              {
                 KatSopr600Default(i);
                 TmpKatSoprnRec := KatSopr.nRec;
                 WriteERRRMessage;
                  If GetFirst SpSopr where((MTabReestrforKN2.SpSopr2nRec == SpSopr.nRec)) = tsOk
                  {
                    SpSopr600Default(i);
                  }
              }

          } //else
       }//_LOOP
         //пересчитываю толькочто созданные документы
        If GetFirst KatSopr where((TmpKatSoprnRec == KatSopr.nRec)) = tsOk
        {
         RecalcSumm;
!         RunInterface( iChkSum, KatSopr.NRec, 0, FALSE, If(KatSopr.cVal = 0, 10, 20), TRUE);
         if not NextVisual Break;
        }
         _KatSopr2nSopr := '';
     }//FOR ( i := 0; i<2; i := i+1 )
    }// If GetFirst KatSopr where((MTabReestrforKN2.KatSopr2nRec == KatSopr.nRec)) = tsOk

   StopVisual('', 0);
   PopPos(#MTabReestrforKN2);

  if ShowRep
    {
      errr.PutEvent(feBreak);
      errr.ShowFile('Реестр созданных Накладных на внутреннее перемещение.');
      MakeNewMTabReestrforKN2;
      RereadRecord;
    }
    else
    {
     errr.AbortForm;
     Message('Операция завершена. Новые документы не созданы.');
    }
   PutCommand(cmUnSelectAll);
}

Procedure MTabReestrforKN2cmMarkUnMark;
{
  If MTabReestrforKN2.Pick<>0
     MTabReestrforKN2.Pick := 0;
  else
     MTabReestrforKN2.Pick := 1;
    update current MTabReestrforKN2;

#ifdef ATL51
 RedrawCurrentAndGo (GetCurrentFormat, TRUE);
#else
 if (GetNext MTabReestrforKN2 = tsOk)
    CallToNeighbours(cmPosDown, #MTabReestrforKN2);
 RescanPanel(#MTabReestrforKN2);
#end
}

Procedure MTabReestrforKN2cmSelectAll;
{
  PushPos(#MTabReestrforKN2);
  _LOOP MTabReestrforKN2
   {
     If MTabReestrforKN2.Pick<>1
       {
        MTabReestrforKN2.Pick := 1;
        update current MTabReestrforKN2;
       }
   }//_LOOP MTabReestrforKN2
  PopPos(#MTabReestrforKN2);
  RescanPanel(#MTabReestrforKN2);
}

Procedure  MTabReestrforKN2cmUnSelectAll;
{
  PushPos(#MTabReestrforKN2);
  _LOOP MTabReestrforKN2
   {
     If MTabReestrforKN2.Pick<>0
       {
        MTabReestrforKN2.Pick := 0;
        update current MTabReestrforKN2;
       }
   }//_LOOP MTabReestrforKN2
  PopPos(#MTabReestrforKN2);
  RescanPanel(#MTabReestrforKN2);
}
