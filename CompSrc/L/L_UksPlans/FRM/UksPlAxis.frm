! ┌────────────────────────────────────────────────────────────────────────────┐
! │                    (c) 1995, 2004 корпорация ГАЛАКТИКА                     │
! │ Проект        : Галактика (УКС - Управление капитальным строительством)    │
! │ Версия        : 5.8                                                        │
! │ Назначение    : План капстроительства с настраиваемыми осями таблицы       │
! │ Ответственный : Окуневич Руслан Васильевич (RusLAN)                        │
! └────────────────────────────────────────────────────────────────────────────┘
#doc
План капстроительства с настраиваемыми осями таблицы
#end
.Form 'UksPlanAxis'
.Hide
.Fields
  cPlanVar План Вариант
  cUksAxis Оси
!.{
    ПарамНаим
    ПарамЗнач
!.}
  cPlanVar2:comp План2 Вариант2
!.{
    ПарамНаим2
    ПарамЗнач2
!.}
!.{
                  ВспомСимв11   ВспомСимв21
    ТипСтрок1     КолНаим11     КолНаим21
    ТипСтрок2     КолНаим12     КолНаим22
    ТипСтрок3     КолНаим13     КолНаим23
                  ВспомСимв12   ВспомСимв22
!.}
!.{
    НПП
    Наименование
    КодОбъекта
! .{
      Значение1
      Точность:word
      Значение2:double
! .}
!.}
.EndFields
^^^^^
.{ CheckEnter
^^
.}
^^^
.{ CheckEnter
^^
.}
      .{.?TableHeadAx11;^ .} .{.?TableHeadAx21;^ .}
    ^ .{.?TableHeadAx12;^ .} .{.?TableHeadAx22;^ .}
    ^ .{.?TableHeadAx13;^ .} .{.?TableHeadAx23;^ .}
    ^ .{.?TableHeadAx14;^ .} .{.?TableHeadAx24;^ .}
      .{.?TableHeadAx15;^ .} .{.?TableHeadAx25;^ .}
.{
^^^    .{.?TableValueAx1;^ .} .{.?TableValueAx2;^^ .}
.}
.EndForm


!▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓
#doc
План капитального строительства
#end
.LinkForm 'UksPlanAxis_01' prototype is 'UksPlanAxis'
.NameInList 'План капитального строительства'
.Fields
  План Вариант Оси
    ПарамНаим
    ПарамЗнач
.EndFields
    Б^ Б
   Вариант: ^
   Представление осей: "^"
.{ CheckEnter
     @@@@@@@@@@@@@@@:  Б"^" Б
.}
.{?Internal; (cPlanVar2<>0)
.Fields
  План2 Вариант2
    ПарамНаим2
    ПарамЗнач2
.EndFields

   Сравниваем с
    Б^ Б
   Вариант: ^
.{ CheckEnter
     @@@@@@@@@@@@@@@:  Б"^" Б
.}
.}// Internal
.Fields
                         ВспомСимв11          ВспомСимв21
    ТипСтрок1 : 'P:C'    КолНаим11 : 'P:C'    КолНаим21 : 'P:C'
    ТипСтрок2 : 'P:C'    КолНаим12 : 'P:C'    КолНаим22 : 'P:C'
    ТипСтрок3 : 'P:C'    КолНаим13 : 'P:C'    КолНаим23 : 'P:C'
                         ВспомСимв12          ВспомСимв22
    НПП : 'P:C'  
    Наименование : 'T:R'
    КодОбъекта
      Значение1 : 'P:C'
      if(Значение2=0, '',
        if(Точность<1, DoubleToStr(Значение2, '\0p[|-]3666`666`666`666'),
        if(Точность=1, DoubleToStr(Значение2, '\1p[|-]3666`666`666`666.8'),
        if(Точность=2, DoubleToStr(Значение2, '\2p[|-]3666`666`666`666.88'),
        if(Точность=3, DoubleToStr(Значение2, '\3p[|-]3666`666`666`666.888'),
        if(Точность=4, DoubleToStr(Значение2, '\4p[|-]3666`666`666`666.8888'),
                       DoubleToStr(Значение2, '\5p[|-]3666`666`666`666.88888'))))))) : 'P:R'
.EndFields
 Р
┌───┬────────────────────────────────────────┬────────────────────.{.?TableHeadAx11;┬──────────────────────@.}.{.?TableHeadAx21;┬─────────────@.}┐
│   │@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│                    .{.?TableHeadAx12;│@@@@@@@@@@@@@@@@@@@@@@@.}.{.?TableHeadAx22;│@@@@@@@@@@@@@@.}│
│ № │@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│    Код объекта     .{.?TableHeadAx13;│@@@@@@@@@@@@@@@@@@@@@@@.}.{.?TableHeadAx23;│@@@@@@@@@@@@@@.}│
│п/п│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│                    .{.?TableHeadAx14;│@@@@@@@@@@@@@@@@@@@@@@@.}.{.?TableHeadAx24;│@@@@@@@@@@@@@@.}│
└───┴────────────────────────────────────────┴────────────────────.{.?TableHeadAx15;┴──────────────────────@.}.{.?TableHeadAx25;┴─────────────@.}┘
.{                                                                
 @@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@.{.?TableValueAx1; @@@@@@@@@@@@@@@@@@@@@@@.}.{.?TableValueAx2; @@@@@@@@@@@@@@.}
.} Р
.EndForm
