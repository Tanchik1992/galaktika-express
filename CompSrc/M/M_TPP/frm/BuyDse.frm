//------------------------------------------------------------------------------
//                                                      (c) корпорация ГАЛАКТИКА
// ГАЛАКТИКА 7.11 - Производственное планирование
// Отчеты: Ведомость покупных ДСЕ;1-уровневый список ресурсов специф-й/маршрутов
//------------------------------------------------------------------------------

#doc
Базовая форма отчёта ведомости покупных ДСЕ</brief>
#end

.form PSBuy
.hide
.fields
// изделие
  Наименование_изделия
  Обозначение_изделия
  Номенкномер_изделие
// документ
  Документ_тип
  Документ
  Обозначение_документа
// параметры
  На_дату
  Volume: double
  VolumeUnit
// ресурсы
  Наименование
  Обозначение
  Обозначение_КУДА
  Количество            : double
  Количество_в_комплекты: double
  Всего
  Сокращение_единицы_измерения
.endfields
 ^ ^ ^
 ^ ^ ^
 ^ ^ ^
.{
 ^ ^ ^ ^ ^ ^ ^
.}
.endform

//******************************************************************************
// Ведомость покупных ДСЕ
.linkform 'PSBuy1' prototype is 'PSbuy'
.nameinlist 'Ведомость покупных ДСЕ'
.group 'Register'
.var
  N_строки: longint
.endvar
.begin
N_строки := 0;
end.


.fields
  Наименование_изделия
  Обозначение_изделия
  Номенкномер_изделие
  Документ_тип
  Документ
  Обозначение_документа
  На_дату
  Volume
  VolumeUnit
.endfields
ВЕДОМОСТЬ ПОКУПНЫХ ИЗДЕЛИЙ (ВП)

 Изделие (МЦ):
   наименование     : ^
   обозначение      : ^
   номенкл. номер   : ^
 Документ (^):
   наименование     : ^
   код              : ^
 Параметры расчета:
   на дату          : ^
   объем партии     : &&&&&&&&&&.&&&&&&& ^

────┬──────────────────┬──────────────────────────┬───────────────────────────┬─────────────────────────┬──────────
 N  │                  │                          │                           │      КОЛИЧЕСТВО         │
С-КИ│   ОБОЗНАЧЕНИЕ    │     НАИМЕНОВАНИЕ         │       КУДА ВХОДИТ         ├───────┬─────────┬───────│   ЕД.
    │                  │                          │                           │  НА   │    В    │ ВСЕГО │ИЗМЕРЕНИЯ
    │                  │                          │      (ОБОЗНАЧЕНИЕ)        │ИЗДЕЛИЕ│КОМПЛЕКТЫ│       │
────┼──────────────────┼──────────────────────────┼───────────────────────────┼───────┼─────────┼───────┼──────────
.{
.begin
N_строки := N_строки + 1;
end.
.fields
  N_строки
  Наименование
  Обозначение
  Обозначение_КУДА
  Количество
  Количество_в_комплекты
  Всего
  Сокращение_единицы_измерения
.endfields
&&&&│@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@@│&&&&.&&│ &&&&.&& │&&&&.&&│ @@@@@@@@
.}
.[F
────┴──────────────────┴──────────────────────────┴───────────────────────────┴───────┴─────────┴───────┴──────────
.]F
.endform

//******************************************************************************
// Одноуровневый список ресурсов спецификаций и маршрутов
.linkform 'ResourceList' prototype is 'PSbuy'
.nameinlist 'Список ресурсов'
.group 'ResourceList'
.defo landscape
.fields
  CommonFormHeader
  Документ_тип
  Документ
  Обозначение_документа
  Наименование_изделия
  Обозначение_изделия
  Номенкномер_изделие
  На_дату
  Volume
  VolumeUnit
.endfields
  ^
  СПИСОК РЕСУРСОВ ДОКУМЕНТА

 Документ (^):
   наименование    : ^
   код             : ^
 Изделие:
   наименование    : ^
   обозначение     : ^
   номенкл. номер  : ^
 Параметры расчета:
   на дату         : ^
   ед. нормирования: &&&&&&&&&&.&&&&&&& ^

────────┬────────────────────────────────┬─────────────────────┬──────────────────────┬──────────────────────────
 Тип    │Наименование                    │ Номенкл. номер      │ Обозначение          │ Количество
────────┼────────────────────────────────┼─────────────────────┼──────────────────────┼──────────────────────────
.{
.fields
  Обозначение_КУДА // тип ресурса
  Наименование
  Всего            // номенкл. номер
  Обозначение
  Количество
  Сокращение_единицы_измерения
.endfields
 @@@@@@ │ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ │ @@@@@@@@@@@@@@@@@@@ │ @@@@@@@@@@@@@@@@@@@@ │ &&&&&&&&&&.&&&&&&& @@@@@@
.}
.[F
────────┴────────────────────────────────┴─────────────────────┴──────────────────────┴──────────────────────────
.]F
.endform
