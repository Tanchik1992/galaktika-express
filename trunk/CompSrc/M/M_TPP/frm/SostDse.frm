//------------------------------------------------------------------------------
//                                                      (c) корпорация ГАЛАКТИКА
// ГАЛАКТИКА 7.11 - Производственное планирование
// Состав сборочной единицы
//------------------------------------------------------------------------------

#doc
Базовая форма 'Состав сборочной единицы'</brief>
#end

.FORM DSESost
.HIDE
.fields
  Наименование_изделия
  Обозначение_изделия
.endfields
  Наименование_изделия ^
  Обозначение_изделия  ^
.{PS_Razdel CheckEnter 
.FIELDS
  Порядковый_номер_раздела_спецификации
  Раздел_спецификации
  Наименование
  Обозначение
  Ед_измерения
  Количество:Double
.ENDFIELDS
  Порядковый_номер_раздела_спецификации             ^
  Раздел_спецификации                               ^
  Наименование                                      ^
  Обозначение                                       ^
  Ед_измерения                                      ^
  Количество:Double                                 ^
.}
.endform
!=============================================================================
.LinkForm 'DSESost1' Prototype is 'DSESost'
.NameInList 'Состав сборочной единицы'
.var
  Р_текущий_раздел:string
  Счетчик : word
.endvar
.begin
  Р_текущий_раздел := ' ';
  Счетчик :=0;
end.
.fields
  Наименование_изделия
  Обозначение_изделия
.endfields
  Наименование сборочной единицы    ^
  Обозначение  сборочной единицы    ^

.{ PS_Razdel CheckEnter 
.{ ?Internal;((Р_текущий_раздел<>Раздел_спецификации)and(Р_текущий_раздел<>'')and(Счетчик <> 0))
.fields
.endfields
------------------------------------------------------------------------------
.} //end of ?Internal
.begin
  Счетчик :=Счетчик +1;
 end.
.{ ?Internal;(Р_текущий_раздел<>Раздел_спецификации)
.fields
  Раздел_спецификации
.endfields

@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
-----------------------------------------------------------------------------------------------
     Обозначение ДСЕ          |      Наименование ДСЕ         |   Ед.измерения  |   Количество
-----------------------------------------------------------------------------------------------
.} //end of ?Internal
.begin
  Р_текущий_раздел := Раздел_спецификации;
end.
.FIELDS
Обозначение
Наименование                               
Ед_измерения
Количество                                                               
.ENDFIELDS
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   @@@@@@@@@@@@@@@  &&&&&&&&&&&.&&
.}
-----------------------------------------------------------------------------------------------
.endform