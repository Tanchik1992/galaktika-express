/*
 ╔═══════════════════════════════════════════════════════════════════════════╗
 ║                     (c) 1994,99 корпорация ГАЛАКТИКА                      ║
 ║ Проект        : ГАЛАКТИКА                                                 ║
 ║ Система       : Поставщики,получатели                                     ║
 ║ Версия        : 5.00                                                      ║
 ║ Назначение    : Договора цесии                                            ║
 ║ Ответственный : Стульский Виталий Евгеньевич                              ║
 ╚═══════════════════════════════════════════════════════════════════════════╝
*/

#doc
Договора цесии
#end
.set name = 'Cessia'
.HIDE
.var
 sDogCessia : string;   //- поле "содержание договора";
 s_NDS      : double;   //- НДС по переданной сумме долга в соответствии с ДО.
.endvar
.Fields
 nmNDE
 myTiDkGal : word
!.{
 nRecDog : comp
 NoDog kontr kontr1 kontr2 stat d dobr sumdolg sumopl namepl1
!.{
 nRecDoc : comp
 num1 d1 s1 s2
!.}
!.}
.Endfields
nmNDE - ^
myTiDkGal - ^
.{
NRecDog ^
NoDog   ^
kontr   ^
kontr1  ^
kontr2  ^
stat    ^
d       ^
dobr    ^
sumdolg ^
sumopl  ^
namepl1 ^
.{
─────────────────────────────────────────────
nRecDoc ^
num1 ^
d1   ^
s1   ^
s2   ^
═════════════════════════════════════════════
.}
.}
.endform

#doc
Уступка долга (продажа)
#end
.linkform 'cessia1' prototype is 'cessia'
.nameinlist 'Уступка долга (продажа)'
.group 'Уступка долга (продажа)'
! доп.лог.таблица
#include locessia.frn
.Fields
 CommonFormHeader
.EndFields
^
.{
.begin
! вычисление sDogCessia, s_NDS
#include cessia.frn
end.
.Fields
 NoDog
 kontr1 //должник
 kontr  //покупатель
 dObr
.EndFields
                               Договор N  Б@@@@@@@@ Б
                        уступки долга контрагента
                         Б@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Б
                        контрагенту
                         Б@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Б

По состоянию на @@@@@@@@@@@ уступаются долги по следующим
документам:
────────┬────────────┬────────────────────┬───────────────────
 Номер  │   Дата     │    Сумма долга     │   Сумма продажи
────────┴────────────┴────────────────────┴───────────────────
.{
.Fields
  num1        d1               s1                   s2
.EndFields
@@@@@@@   @@@@@@@@@@   &#&'&&&&&&&&&&&.&&   &#&'&&&&&&&&&&&.&&
.}
──────────────────────────────────────────────────────────────
.Fields
 sumdolg sumopl
.Endfields
 БИтого:                 &#&'&&&&&&&&&&&.&&   &#&'&&&&&&&&&&&.&& Б
.}


Начальник финансового отдела


Главный бухгалтер
.endform

#doc
Уступка долга (покупка)
#end
.linkform 'cessia2' prototype is 'cessia'
.nameinlist 'Уступка долга (покупка)'
.group 'Уступка долга (покупка)'
.Fields
 CommonFormHeader
.EndFields
^
.{
.Fields
 NoDog
 dObr
 kontr  //продавец
 kontr2 //покупатель
 kontr1 //должник
.EndFields
                             Договор N  Б@@@@@@@@ Б
                                от @@@@@@@@@@

Продавец долга:   Б ^ Б
Покупатель долга: Б ^ Б
Должник:          Б ^ Б

───────────────────────────────────────────────────────────────────────────
.Fields
                           sumdolg      nmNDE                     dObr
.EndFields
Сумма долга:           Б&#&'&&&&&&&&&&&.&& Б @@@@@@ по состоянию на @@@@@@@@@@@
───────────────────────────────────────────────────────────────────────────
.Fields
                           sumopl         nmNDE
.EndFields
Сумма оплаты за долг:  Б&#&'&&&&&&&&&&&.&& Б @@@@@
───────────────────────────────────────────────────────────────────────────

.Fields
 namepl1
.Endfields
Примечание:
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

.{
.}
Начальник финансового отдела


Главный бухгалтер
.}
.endform

#doc
Регистрация уступки собственного долга
#end
.linkform 'cessia3' prototype is 'cessia'
.nameinlist 'Регистрация уступки собственного долга'
.group 'Регистрация уступки собственного долга'
! доп.лог.таблица
#include locessia.frn
.Fields
 CommonFormHeader
.EndFields
^
.{
.begin
! вычисление sDogCessia, s_NDS
#include cessia.frn
end.
.Fields
 NoDog
 kontr2
 kontr1
 kontr
 dObr
.Endfields
                               Договор N  Б@@@@@@@@ Б
                        уступки долга контрагента:
                         Б@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Б
                        контрагентом:
                         Б@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Б
                        контрагенту:
                         Б@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Б

По состоянию на @@@@@@@@@@@ уступаются долги по следующим
документам:
────────┬────────────┬────────────────────┬───────────────────
 Номер  │   Дата     │    Сумма долга     │   Сумма продажи
────────┴────────────┴────────────────────┴───────────────────
.{
.Fields
  num1        d1               s1                 s2
.EndFields
@@@@@@@   @@@@@@@@@@   &#&'&&&&&&&&&&&.&&   &#&'&&&&&&&&&&&.&&
.}
──────────────────────────────────────────────────────────────
.Fields
                               sumdolg               sumopl
.EndFields
 БИтого:                 &#&'&&&&&&&&&&&.&&   &#&'&&&&&&&&&&&.&& Б
.}
.endform

#doc
Реестр
#end
.linkform 'cessia4' prototype is 'cessia'
.nameinlist 'Реестр'
.group 'Реестр'
.var
  strReestrName : string;
.endvar
.begin
  case myTiDkGal of
    90: strReestrName := 'уступки долга (продажа)';
    91: strReestrName := 'уступки долга (покупка)';
    92: strReestrName := 'регистрации уступки собственного долга';
  end; //case
  strReestrName := 'Реестр договоров ' + strReestrName;
end.
.Fields
 CommonFormHeader
 strReestrName
.Endfields
^
 Б@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Б

.Fields
 nmNDE nmNDE
.EndFields
───────┬───────────┬───────────┬──────────┬───────────────┬───────────────┬───────────────┬─────────────────────┬──────────────────────
 Номер │  Cтатус   │Сформирован│ Выполнен │    Продавец   │  Покупатель   │     Должник   │ Сумма долга в @@@@@ │ Сумма продажи в @@@@@
───────┴───────────┴───────────┴──────────┴───────────────┴───────────────┴───────────────┴─────────────────────┴──────────────────────
.{
.{ ?internal; ( myTiDkGal = 90 );
!уступки долга (продажа)
.Fields
   NoDog   stat         d         dobr        kontr2          kontr           kontr1            sumdolg               sumopl
.Endfields
@@@@@@@ @@@@@@@@@@@ @@@@@@@@@@@ @@@@@@@@@@ @@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@ &#&'&&&&&&&&&&&&&&.&&  &#&'&&&&&&&&&&&&&&.&&
.} // ( myTiDkGal = 90 );
.{ ?internal; ( myTiDkGal = 91 );
!уступки долга (покупка)
.Fields
   NoDog    stat        d         dobr        kontr           kontr2          kontr1             sumdolg              sumopl
.Endfields
@@@@@@@ @@@@@@@@@@@ @@@@@@@@@@@ @@@@@@@@@@ @@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@ &#&'&&&&&&&&&&&&&&.&&  &#&'&&&&&&&&&&&&&&.&&
.} // ( myTiDkGal = 91 );
.{ ?internal; ( myTiDkGal = 92 );
!регистрации уступки собственного долга
.Fields
   NoDog    stat        d         dobr        kontr1          kontr           kontr2             sumdolg              sumopl
.Endfields
@@@@@@@ @@@@@@@@@@@ @@@@@@@@@@@ @@@@@@@@@@ @@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@ &#&'&&&&&&&&&&&&&&.&&  &#&'&&&&&&&&&&&&&&.&&
.} // ( myTiDkGal = 92 );
.{
.}
.}
───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────

Начальник финансового отдела


Главный бухгалтер

.endform