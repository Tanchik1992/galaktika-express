//********************************************************************************
//                                                        (c) корпорация Галактика
// Галактика 7.12 - логистика
// Алгоритм при изменении статуса накладной/акта
//
//********************************************************************************

#define OBJINT_ALGORITM
#define OBJINT_SETGETVID
#define OBJINT_ACCOUNT

#include objintpl.vin
#include oCommonFunAlg.vih

//******************************************************************************

#doc
Назначение:
  Алгоритм при изменении статуса накладной/акта

Параметры:
  Параметров нет - используется как объектный интерфейс
#end

//******************************************************************************

VipInterface Algoritm_5003 Implements oAlgoritm_Sys

#ifdef ATL51
Licensed (FREE)
#end
;

//********************************************************************************

Interface Algoritm_5003 '' (, hcNoContext,) EscClose, Cyan;

Create View

Var
  cWorkFormPl
, cOldNote
              : comp;

  wTuneNpp
              : word;

From
  FormPl
, KatSopr
, KatNotes KatNotesSrc
, KatNotes KatNotesDst
, KatNotes KatNotes2

Where
((
  cWorkFormPl == FormPl.NRec
))
;

Function GetNum : word;
{
  GetNum := 5003;
}

Function GetMemo (st : oSteckCommonString) : Boolean;
{
  GetMemo := True;
}

Function GetNameInter : String;
{
  GetNameInter := 'Algoritm_5003';
}

Function GetNameInterPr : String;
{
  GetNameInterPr := 'AlgoritmSetup_5003';
}

Function GetName : String;
{
  GetName := 'Алгоритм при изменении статуса накладной/акта';
}

//********************************************************************************

Function VidDocTuneNpp : word;
Var
  ret : word;
{
  ret := 0;

  case KatSopr.VidSopr of

    101 : ret := 1;
    201 : ret := 2;
    111 : ret := 3;
    211 : ret := 4;
    106, 206 : ret := 5;

  end;

  wTuneNpp := ret;
  VidDocTuneNpp := ret;
}

//********************************************************************************

Function TestKatNotesTune : boolean;
Var
  bSrcOk
, bDstOk
          : boolean;
{
  TestKatNotesTune := false;

  if (  (GetFirst KatNotesSrc where (( cOldNote == KatNotesSrc.nRec )) <> tsOk)
     or (GetFirst KatNotesDst where (( KatSopr.cNote == KatNotesDst.nRec )) <> tsOk)
     )
    exit;

  bSrcOk := false;
  bDstOk := false;

  if ( FormPl.ResComp[wTuneNpp] = KatNotesSrc.nRec )
    {
      bSrcOk := true;
    }
  else
    {
      if ( FormPl.ResWord[wTuneNpp] = 0 )
        exit;

      _loop KatNotes2 where (( KatNotesSrc.cOwner == KatNotes2.NRec ))
        if ( KatNotes2.nRec = FormPl.ResComp[wTuneNpp] )
          {
            bSrcOk := true;
            break;
          }
    }

  if ( FormPl.ResComp[wTuneNpp + 20] = KatNotesDst.nRec )
    {
      bDstOk := true;
    }
  else
    {
      if ( FormPl.ResWord[wTuneNpp + 20] = 0 )
        exit;

      _loop KatNotes2 where (( KatNotesDst.cOwner == KatNotes2.NRec ))
        if ( KatNotes2.nRec = FormPl.ResComp[wTuneNpp + 20] )
          {
            bDstOk := true;
            break;
          }

    }

  TestKatNotesTune := bSrcOk and bDstOk;
}

//********************************************************************************

Function InitInter (var pt : TRecAcc) : Boolean;
{
  InitInter := true;

  if ( GetFirst KatSopr where (( pt.cpNrec == KatSopr.nRec )) <> tsOk )
    {
      InitInter := false;
      Exit;
    }

  cWorkFormPl := pt.cFormPl;
  cOldNote    := pt.stSpMnPlan.NRec;

  if ( not IsValid(#FormPl) )
    {
      message('Пользовательский алгоритм не найден', Error);
      InitInter := false;
    }
}

Function RunInter (wflag: word; var pt : TRecAcc) : Boolean;
{
  RunInter := true;

  if ( (VidDocTuneNpp = 0) or (not TestKatNotesTune) )
    exit;

//
// Действия после изменения статуса документа
//

// KatSopr.nRec - текущий документ
// KatNotesSrc.nRec - 'старый' статус
// KatNotesDst.nRec - 'новый' статус
}

End.