// ╔════════════════════════════════════════╗
// ║ возвращает кол-во знаков после запятой ║
// ╚════════════════════════════════════════╝

function getKolZnak(RRub:double):word;
var lRub:word;
{
  if (RRub = 0.0001)
    {
      lRub := 4;
      getKolZnak := lRub;
      exit;
    }
  if (RRub = 0.001)
    {
      lRub := 3;
      getKolZnak := lRub;
      exit;
    }
  if (RRub = 0.01)
    {
      lRub := 2;
      getKolZnak := lRub;
      exit;
    }
  if (RRub = 0.1)
    {
      lRub := 1;
      getKolZnak := lRub;
      exit;
    }
  lRub := 0;
  getKolZnak := lRub;
}

//-- установить точность вывода сумм в зависимости от настройки
procedure SetRoundFormat(VSopr : word);
var
! --- Для установки точности в классах точности
  wforSopr_Price,
  wforSopr_VPrice : word;
{
  wForSopr_Price  := 2;
  wForSopr_VPrice := 2;

  case (VSopr) of
    101, 108, 110, 111, 114,
    206,
    613 :
      if (wGetTune('Round.Buy') <> 0)
        {
          wForSopr_Price  := getKolZnak(doGetTune('Round.BuyDORub'));
          wForSopr_VPrice := getKolZnak(doGetTune('Round.BuyDOVal'));
        }
    1411, 1421:
      if (wGetTune('Round.Wood') <> 0)
        {
          wForSopr_Price  := getKolZnak(doGetTune('Round.WoodRub'));
          wForSopr_VPrice := getKolZnak(doGetTune('Round.WoodVal'));
        }
    600,601,602,603,605,606 :
      if (wGetTune('Round.Order') <> 0)
        {
          wForSopr_Price  := getKolZnak(doGetTune('Round.OrderRub'));
          wForSopr_VPrice := getKolZnak(doGetTune('Round.OrderVal'));
        }

    102,
    202 :
      if (wGetTune('Round.OtpCons') <> 0)
        {
          wForSopr_Price  := getKolZnak(doGetTune('Round.OtpConsRub'));
          wForSopr_VPrice := getKolZnak(doGetTune('Round.OtpConsVal'));
        }

    103,
    203 :
      if (wGetTune('Round.PrCons') <> 0)
        {
          wForSopr_Price  := getKolZnak(doGetTune('Round.PrConsRub'));
          wForSopr_VPrice := getKolZnak(doGetTune('Round.PrConsVal'));
        }

    106,
    201, 204, 205, 211,
    510, 513, 521, 522,
    523, 550, 553, 554, 557 :
      if (wGetTune('Round.Sell') <> 0)
        {
          wForSopr_Price  := getKolZnak(doGetTune('Round.SellDORub'));
          wForSopr_VPrice := getKolZnak(doGetTune('Round.SellDOVal'));
        }

  end;
  SetPrecisionGroup('brForSopr_Vprice',  wForSopr_VPrice );
  SetPrecisionGroup('brForSopr_price',   wForSopr_Price );
  SetPrecisionGroup('brForSopr_RVprice', if (KatSopr.cVal = 0, wForSopr_Price, wForSopr_VPrice));
}
