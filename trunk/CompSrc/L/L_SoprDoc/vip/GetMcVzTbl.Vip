//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 8.10 - Логистика
// Интерфейс для работы с таблицей в рекламационных накладных
//******************************************************************************

#include GetMcVz2.tbl
#include GetMcVzTbl.vih

Interface GetMcVzTbl;

Var
  EndDate: date; // конечная дата пересканирования данных
  wParam : word; // параметр загрузки

Create View
As Select
  *

From
  MEMTMakeVozvrat
;

//******************************************************************************
#doc
  Параметры загрузки данных
#end

Window  winNastr 'Параметры загрузки данных' gray, doAccept;
  Show At (2,2,70,7)

Screen scrNastr;
Fields
  wParam      ('Выберите тип фильтр',,sci13Esc), NoProtect;
  EndDate     ('Введите конечную дату интервала',,sci13Esc) :[10,'DD/MM/YYYY'], NoProtect;
Buttons
  cmOk,,, 'Подтверждение ввода интервала';

<<

 [.] Сохранять информацию по документам, созданным до` .@@@@@@@@@@@

                  <.   Продолжить   .>
>>
end;

Handleevent
cmOk:
{
  SaveMyDSK(wParam , 'GetMcVzTbl_wParam');
  SaveMyDSK(EndDate, 'GetMcVzTbl_EndDate');
  PutCommand(cmCancel);
}

end;
end; // window

//******************************************************************************
// удаление записей
Procedure DeleteAllRecordTbl;
{
  Delete All MEMTMakeVozvrat;
  Delete all MEMKatSopr;
}

//******************************************************************************
// проверка начилия данных
Function RecordsExistsTbl: boolean;
{
  RecordsExistsTbl := GetFirst MEMTMakeVozvrat = tsOk;
}

//******************************************************************************
// удаление записей до даты пересканирования
Procedure DeleteRecordTblUpDate;
{
  Delete All MEMTMakeVozvrat where (( EndDate <<= MEMTMakeVozvrat.dSopr));
  Delete all MEMKatSopr where (( EndDate <<= MEMKatSopr.dSopr));
}

//******************************************************************************
// загрузка окна с настройкой
Procedure ChangeNastr;
{
  RunWindowModal(winNastr);
}

//******************************************************************************
// Возвращает дату, до которой сохранились данные
Function DateSave: date;
{
  DateSave := if (wParam = 1, EndDate, date (0,0,0));
}

Handleevent
cmOnVipLoad:
{
  if (NOT ReadMyDSK(wParam, 'GetMcVzTbl_wParam', TRUE))
    wParam := 0;

  if (NOT ReadMyDSK(EndDate, 'GetMcVzTbl_EndDate', TRUE))
    EndDate := Cur_Date;
}

cmOnVipUnLoad:
{
}

end;

end.
