//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - логистика
// Печать паспорта качества
//
//******************************************************************************

//******************************************************************************

#doc
Назначение:
  Печать паспорта качества

Параметры:
  pNRec - ссылка на спецификацию сопроводительного документа
#end

//******************************************************************************

Interface PrintPaspKach 'Печать паспорта качества';

Create view

Var

  pNRec
           : comp;

From
  SpSopr
, KatMC
, KatOrg
, KatParty
, Sertific
, ValPokaz
, KatPokaz

Where
((
    pNRec              == SpSopr.NRec
and SpSopr.cMCUsl      == KatMC.NRec
and SpSopr.cParty      == Katparty.NRec
and KatParty.csertific == Sertific.NRec
and Sertific.NRec      == ValPokaz.cOwner
and word(coSertific)   == ValPokaz.wTable
and ValPokaz.cKatPokaz == KatPokaz.NRec
and coGetTune('MyOrg') == KatOrg.NRec
))
;

//******************************************************************************

Parameters
  pNRec;

//******************************************************************************

Form FrmPKach('paspkach.rtf', 'pkach01') with novisual;

//******************************************************************************

HandleEvent

cmInit :
  {
    Abort;

    if ( not IsValid(#SpSopr) )
      {
        Message('Невозможно найти позицию спецификации документа...', CancelButton);
        Exit;
      }

    if ( SpSopr.PRMC <> 1 )
      {
        Message('Паспорт качества выдается только на материальные ценности...', CancelButton);
        Exit;
      }

    if ( not IsValid(#KatMC) )
      {
        Message('Невозможно найти матценность в каталоге...', CancelButton);
        Exit;
      }

    frmPKach.Write(SpSopr.nRec);
    frmPKach.Write(KatOrg.Name);
    frmPKach.Write(KatMC.Name);

    if ( GetFirst KatParty <> tsOk )
      frmPKach.SkipFormat(3);
    else
      {
        frmPKach.Write(KatParty.NSertif);

        if ( katParty.dSertif <> date(0, 0, 0) )
          frmPKach.Write(katParty.dSertif);
        else
          frmPKach.SkipFormat(1);

        if ( KatParty.dAnaliz <> date(0, 0, 0) )
          frmPKach.Write(KatParty.dAnaliz);
        else
          frmPKach.SkipFormat(1);
      }

    if ( GetFirst ValPokaz <> tsOK )
      frmPKach.SkipFormat(3);
    else
      {
        var npp : word;

        npp := 1;

        _loop ValPokaz
          {
            frmPKach.Write(npp);
            npp := npp + 1;

            frmPKach.Write(KatPokaz.Name);
            frmPKach.Write(ValPokaz.Value, 3);
          }
      }

    frmPKach.PutEvent(feBreak);
    frmPKach.Write(DateToStr(Cur_Date, 'DD mon YYYY г.'));

    if ( frmPKach.Error )
      frmPKach.AbortForm;
    else
      frmPKach.ShowFile('');
  }

end;

End.
