//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Логистика - сопроводительные документы
// Акт приемки материала
//******************************************************************************

//******************************************************************************

Function KolDostupOH : double;
var dUsed : double;
{
  KolDostupOH := 0;

  dUsed := 0;

  _LOOP SpecZatr where ((SpSopr2.NRec == SpecZatr.cAddSumTune))
    {
      if (SpecZatr.coTable = 1)
        dUsed := dUsed + double(SpecZatr.cSpec);
    }
  dUsed := SpSopr2.KolFact - 0.000001 * dUsed / fEdIzm.GetKoefOtpEd(SpSopr2.cOtpEd);

  KolDostupOH := if (dUsed < 0, 0, dUsed);
}

//******************************************************************************

Procedure CopyAll;
var i          : longint;
var OldNRec    : comp;
var KolFactSrc : double;
var KolSrc     : double;
var KolOplSrc  : double;
{
  i := 0;

  _LOOP SpSopr2 where ((KatSopr2.NRec == SpSopr2.cSopr)) ordered by index SpSopr02
    {
      SpSopr2.KolFact := KolDostupOH;
      SpSopr2.Kol     := SpSopr2.KolFact;   
      SpSopr2.KolOpl  := SpSopr2.KolFact;

      if (SpSopr2.KolFact < 0.000001)
        Continue;

      OldNRec    := SpSopr2.NRec;
      KolFactSrc := SpSopr2.KolFact;
      KolSrc     := SpSopr2.Kol;
      KolOplSrc  := SpSopr2.KolOpl;
 
      inc(i);
      SpSopr2.NRec    := comp(0);
      SpSopr2.cSopr   := KatSopr.NRec;
      SpSopr2.VidSopr := KatSopr.VidSopr;
      SpSopr2.npp     := i;

      if insert current SpSopr2 <> tsOK
        {}

      ClearBuffer(#SpecZatr);
     
      SpecZatr.coTable     := word(1);
      SpecZatr.cAddSumTune := OldNRec;        // ссылка на спецификацию акта М-7 на ответственном хранении
      SpecZatr.cUKS        := SpSopr2.NRec;   // ссылка на спецификацию акта М-7 снятия с ответ. хранения
      SpecZatr.cSpec       := comp(1000000.0 * SpSopr2.KolFact * fEdIzm.GetKoefOtpEd(SpSopr2.cOtpEd));
    
      insert current SpecZatr;

      var piKatDopEd : iKatDEI; // Интерфейс с public-функциями для ДЕИ

      if (SpSopr.PrMC = 1)
        {
          piKatDopEd.CopyDopEd(word(coSpSopr), OldNRec, word(1), KolFactSrc,
                              word(coSpSopr), SpSopr2.NRec, word(1), SpSopr2.KolFact, SpSopr2.VidSopr);
          piKatDopEd.CopyDopEd(word(coSpSopr), OldNRec, word(2), KolSrc,
                              word(coSpSopr), SpSopr2.NRec, word(2), SpSopr2.Kol    , SpSopr2.VidSopr);
          piKatDopEd.CopyDopEd(word(coSpSopr), OldNRec, word(3), KolOplSrc,
                              word(coSpSopr), SpSopr2.NRec, word(3), SpSopr2.KolOpl , SpSopr2.VidSopr);
        }

#ifndef __MTR_TO_SPEC__
      if (GetFirst SpecZatr where ((coSpSopr == SpecZatr.coTable
                                AND OldNRec  == SpecZatr.cSpec)) = tsOk)
        {
          var cz : comp;  cz := SpecZatr.cAddSumTune;

          ClearBuffer(tnSpecZatr);

          SpecZatr.coTable     := coSpSopr;
          SpecZatr.cSpec       := SpSopr2.nRec;
          SpecZatr.cAddSumTune := cz;

          if (Insert Current SpecZatr = tsOk) {};
        }
#else
        oMTRFun.CopySpecMTR(coSpSopr, OldNRec, coSpSopr, SpSopr2.NRec);
#end
    }
} 
//******************************************************************************

Procedure CopyPickSopr;
var i          : longint;
var OldNRec    : comp;
var KolFactSrc : double;
var KolSrc     : double;
var KolOplSrc  : double;
{
  i := 0;

  _LOOP Pick where ((wPList == Pick.wList))
    if GetFirst SpSopr2 where ((Pick.cRec == SpSopr2.NRec)) = tsOK
      {
        SpSopr2.KolFact := Pick.PickKol;
        SpSopr2.Kol     := SpSopr2.KolFact;   
        SpSopr2.KolOpl  := SpSopr2.KolFact;

        if (SpSopr2.KolFact < 0.000001)
          Continue;

        OldNRec    := SpSopr2.NRec;
        KolFactSrc := SpSopr2.KolFact;
        KolSrc     := SpSopr2.Kol;
        KolOplSrc  := SpSopr2.KolOpl;
   
        inc(i);
        SpSopr2.NRec    := comp(0);
        SpSopr2.cSopr   := KatSopr.NRec;
        SpSopr2.VidSopr := KatSopr.VidSopr;
        SpSopr2.npp     := i;

        if insert current SpSopr2 <> tsOK
          {}

        ClearBuffer(#SpecZatr);
       
        SpecZatr.coTable     := word(1);
        SpecZatr.cAddSumTune := OldNRec;        // ссылка на спецификацию акта М-7 на ответственном хранении
        SpecZatr.cUKS        := SpSopr2.NRec;   // ссылка на спецификацию акта М-7 снятия с ответ. хранения
        SpecZatr.cSpec       := comp(1000000.0 * SpSopr2.KolFact * fEdIzm.GetKoefOtpEd(SpSopr2.cOtpEd));
      
        insert current SpecZatr;

        var piKatDopEd : iKatDEI; // Интерфейс с public-функциями для ДЕИ

        if (SpSopr.PrMC = 1)
          {
            piKatDopEd.CopyDopEd(word(coSpSopr), OldNRec, word(1), KolFactSrc,
                                word(coSpSopr), SpSopr2.NRec, word(1), SpSopr2.KolFact, SpSopr2.VidSopr);
            piKatDopEd.CopyDopEd(word(coSpSopr), OldNRec, word(2), KolSrc,
                                word(coSpSopr), SpSopr2.NRec, word(2), SpSopr2.Kol    , SpSopr2.VidSopr);
            piKatDopEd.CopyDopEd(word(coSpSopr), OldNRec, word(3), KolOplSrc,
                                word(coSpSopr), SpSopr2.NRec, word(3), SpSopr2.KolOpl , SpSopr2.VidSopr);
          }

#ifndef __MTR_TO_SPEC__
        if (GetFirst SpecZatr where ((coSpSopr == SpecZatr.coTable
                                  AND OldNRec  == SpecZatr.cSpec)) = tsOk)
          {
            var cz : comp;  cz := SpecZatr.cAddSumTune;

            ClearBuffer(tnSpecZatr);

            SpecZatr.coTable     := coSpSopr;
            SpecZatr.cSpec       := SpSopr2.nRec;
            SpecZatr.cAddSumTune := cz;

            if (Insert Current SpecZatr = tsOk) {};
          }
#else
          oMTRFun.CopySpecMTR(coSpSopr, OldNRec, coSpSopr, SpSopr2.NRec);
#end
      }
} 
//******************************************************************************
Procedure MakeAkt;
{
  if (IsNew)
    insert current KatSopr;

  ReReadRecord(#KatSopr);

  var cKS : comp; cKS := comp(0);

  if (RunInterface('GetAktM7', cKS, KatSopr.cOrgBase, Katsopr.cDogovor, TRUE) <> cmCancel)
    if (GetFirst KatSopr2 where ((cKS == KatSopr2.NRec)) = tsOk)
      {
        StartNewVisual(vtNumericVisual, vfTimer,''#3'Идет формирование спецификации ...', 1);

        KatSopr.cPodrFrom   := KatSopr2.cPodrTo;
        KatSopr.cMolFrom    := KatSopr2.cMolTo;

        KatSopr.cOrgBase    := KatSopr2.cOrgBase;
        KatSopr.cOrg        := KatSopr2.cOrgBase;
        KatSopr.cVal        := KatSopr2.cVal;

        KatSopr.cDogovor    := KatSopr2.cDogovor;
        KatSopr.cAppDogovor := KatSopr2.cAppDogovor;
        KatSopr.cCalPlan    := KatSopr2.cCalPlan;

        KatSopr.cPerson     := KatSopr2.cPerson;
        KatSopr.cGruzFrom   := KatSopr2.cGruzFrom;
        KatSopr.cGruzTO     := KatSopr2.cGruzTO  ;
        KatSopr.cOtvPodr    := KatSopr2.cOtvPodr;
        KatSopr.cUKS        := KatSopr2.cUKS;

        KatSopr.cTransp     := KatSopr2.cTransp;
        KatSopr.nVagon      := KatSopr2.nVagon ;
        KatSopr.cCellVal    := KatSopr2.cCellVal;
        KatSopr.cNMoveCell  := KatSopr2.cNMoveCell;

        KatSopr.cHozOper    := 0;
        KatSopr.YearSopr    := Year(Cur_Date);
        KatSopr.cSchFact    := 0;
        KatSopr.wADoc       := 0;
        KatSopr.cADoc       := 0;
        KatSopr.dOpr        := ZeroDate;

        KatSopr.VhodNal     := KatSopr2.VhodNal; 

        if (not boGetTune('Doc.ChangeDescr'))
          {
            KatSopr.Descr := sGetTune('User.Descr');
            KatSopr.DesGr := sGetTune('User.DesGr');
          }

        if Update Current KatSopr <> tsOk
          {
            Message('Ошибка создания акта!');
            Exit;
          }

        if (Message('Копировать всю спецификацию из выбранного акта?', Confirmation + YesNo) = cmYes )
          CopyAll;
        else
//          if (RunInterface('GetSSopr', KatSopr2.NRec, word(1), word(1), wPList) <> cmCancel)
          if (RunInterface('GetDocSpSopr', KatSopr2.NRec, word(115), word(1), wPList) <> cmCancel)
            CopyPickSopr;

        StopVisual('',0);
      }

  CheckSumma(TRUE);
  ReReadRecord(#KatSopr);
  ReReadRecord(#SpSopr);
}

