

#ifndef _PICKZATR_VPP
#define _PICKZATR_VPP

Procedure PickcZatr;
{
  var cZatr:comp;
  var ch  : word;   ch  := 0; 
  var ch1 : word;   ch1 := 0;
  
  If runinterface(GetZatrForSp,cZatr) = cmCancel
    exit;
  If cZatr = 0
    exit;
  pushpos(#SpSopr);
  if (KatSopr.VidSopr = 110 or KatSopr.VidSopr = 629)
    ReSetBounds(#SpSopr);
  _LOOP spsopr where ((KatSopr.nRec == SpSopr.cSopr))
    {
      #ifdef _SMETA_IN_SOPR_
      if (not boGetTune('Oper.Smeta.StZatr'))
        if (GetFirst SpDocSmeta = tsOk) 
          { 
            ch1 := ch1 + 1; 
            continue;
          }
      #end
      if (not IsValid(#SpecZatr))
        {
          if (GetFirst SpSopHoz where ((SpSopr.NRec == SpSopHoz.cSpSopr)) = tsOk)
            ch := ch + 1;
          else
            {
              ClearBuffer(#SpecZatr);
              SpecZatr.coTable := coSpSopr;
              SpecZatr.cAddSumTune := cZatr;
              SpecZatr.cUKS := SpSopr.cUKS;
              insert current SpecZatr;
            }
        }
      else
        if (SpecZatr.cAddSumTune = 0)
           update current SpecZatr 
             Set SpecZatr.coTable := coSpSopr,
                 SpecZatr.cAddSumTune := cZatr,
                 SpecZatr.cUKS := SpSopr.cUKS;
    }//_Loop
  if (KatSopr.VidSopr = 110 or KatSopr.VidSopr = 629)
    SetBounds(#SpSopr);
  poppos(#SpSopr);

  ReReadRecord(#SpSopr);
#ifdef _SOPRDOCB_VIP
  pushpos(#SpSopUsl);
  _LOOP spsopUsl
    if (not IsValid(#SpecZatr1))
       if (GetFirst SpSopHoz where ((SpSopUsl.NRec == SpSopHoz.cSpSopr)) = tsOk)
         ch := ch + 1;
       else
         {
           ClearBuffer(#SpecZatr1);
           SpecZatr1.coTable := coSpSopr;
           SpecZatr1.cAddSumTune := cZatr;
           SpecZatr1.cUKS := SpSopUsl.cUKS;
           insert current SpecZatr1;
         }
  poppos(#SpSopUsl);
  ReReadRecord(#SpSopUsl);
#end
  if (ch <> 0)
    Message('Операция выполнена не для всех позиций из-за наличия '+
                'распределенных платежей ('+string(ch)+')', Information);
  if (ch1 <> 0)
    Message('Операция выполнена не для всех позиций из-за наличия '+
                'сметных спецификаций ('+string(ch1)+')', Information);
}
#end
