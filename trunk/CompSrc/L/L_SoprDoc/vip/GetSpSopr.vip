//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 8.1 - Логистика - сопроводительные документы
// Выбор позиции спецификации
//******************************************************************************

//******************************************************************************

#doc
Выбор позиции спецификации

Параметры:
  TypeSopr     - Тип сопроводительного документа
  vwParam      - что показывать: 0 - все, 1 - МЦ, >1 - услуги
  vcSpSoprNRec - nrec позиции спецификации
#end

//******************************************************************************

Interface GetSpSopr 'Выбор позиции спецификации' EscClose, Cyan;
  Show at (, , 110, 31)

//------------------------------------------------------------------------------

var

  TypeSopr
, vwParam
: word;

  vcSpSoprNRec
: comp;

  s_SimvRub
: string;

//------------------------------------------------------------------------------

Create view

As select

  if (KatSopr.TipSopr = 0, '<->', if (KatSopr.TipSopr = 1, '<--', '-->'))
    ( FieldName = fsDirect )
//--------------------------------------------------------------------------------

, if (KatSopr.cVal = 0, SpSopr.Price * SpSopr.KolFact, SpSopr.VPrice * SpSopr.KolFact)
    ( FieldName = SumPrice )
//--------------------------------------------------------------------------------

, if (KatSopr.cVal = 0,
    if (s_SimvRub <> '', s_SimvRub, 'руб.'), KlVal.SimVolV)
        ( FieldName = Valut )
//------------------------------------------------------------------------------

, if (KatSopr.cVal = 0, KatSopr.Summa, KatSopr.SumVal)
        ( FieldName = Sum )
//--------------------------------------------------------------------------------

, *


//------------------------------------------------------------------------------


From
  KatSopr
, SpSopr
, KatParty
, KatOtpEd
, KatStroy
, KatOrg
, KatMC
, KatUsl


//------------------------------------------------------------------------------

where
((
       TypeSopr             ==  KatSopr.VidSopr
  AND  KatSopr.NRec         ==  SpSopr.cSopr

  AND  SpSopr.cMcUsl        ==  KatMc.NRec
  AND  SpSopr.cMCUSL        ==  KatUSL.NRec

  AND  SpSopr.cParty        ==  KatParty.NRec

  AND  SpSopr.cOtpEd        ==  KatOtpEd.NRec

  AND  KatSopr.cUKS         ==  KatStroy.NRec

  AND  KatSopr.cOrgBase     ==  KatOrg.NRec

))

//------------------------------------------------------------------------------

Bounds ForUsl   =     KatSopr.NRec == SpSopr.cSopr
                  AND (SpSopr.PrMC <> 1)

Bounds ForMC    =     KatSopr.NRec == SpSopr.cSopr
                  AND word(1)      == SpSopr.PrMc
;
//------------------------------------------------------------------------------

Parameters
  TypeSopr           // Тип сопроводительного документа
, vwParam            // что показывать: 0 - все, 1 - МЦ, >1 - услуги
, vcSpSoprNRec       // nrec позиции спецификации
;

//******************************************************************************

Browse brKatSopr ('', , sci1EnEsc)
  Show at ( , , , 11);
  Table KatSopr;
Fields

//------------------------------------------------------------------------------

  fsDirect          #3'<->'                  : [3], Skip, NoAutoSize, Centered, { Font = {Bold = TRUE; Color = ColorSysRed; Italic = TRUE }};

//------------------------------------------------------------------------------

  KatSopr.dSopr     #3'Дата'                 : [10,'DD/MM/YYYY'], protect;

//------------------------------------------------------------------------------

  KatSopr.Descr     #3'Дескр.'               : [ 6], Protect, NoAutoSize, Centered;

//------------------------------------------------------------------------------

  KatSopr.NSopr     #3'Номер'                : [15], protect;

//------------------------------------------------------------------------------

  ''+ KatStroy.Name #3'Объект'#13#3'строительства': [22], Protect;

//------------------------------------------------------------------------------

  ''+ KatOrg.Name   #3'Контрагент'           : [28], Protect;

//------------------------------------------------------------------------------

  Sum               #3'Сумма'                : [15], [brForSopr_RVPrice, '\2p[|-]36`666`666`666`666.88'], Protect, LessNull, NoAutoSize;

//------------------------------------------------------------------------------

  '' + Valut        #3'Вал.'                 : [ 3], Skip, NoAutoSize, Centered, { Font = { Italic = TRUE } };

//------------------------------------------------------------------------------

  KatSopr.Name      #3'Примечание акта'      : protect;

//------------------------------------------------------------------------------


end; // Browse brKatSopr


Browse brSpecif ('', , sci1EnEsc)
  Show at (,12)
  Table SpSopr;

Fields
//------------------------------------------------------------------------------
  SpSopr.NPP     #3'№' ('', , sci1Esc): [3], NoAutoSize;
//------------------------------------------------------------------------------
  if (SpSopr.PrMC = 1, 'МЦ', 'Ус')
                'Тип'
                : [2], skip;
//------------------------------------------------------------------------------
  if (SpSopr.PrMC = 1, KatMC.BarKod, KatUsl.Kod)
                'Ном.номер'
                ('Номенклатурный номер')
                :[8] , protect;
//------------------------------------------------------------------------------
  if (SpSopr.PrMC = 1, KatMC.Name, KatUsl.Name)
                'Наименование'
                ('Наименование МЦ/услуги')
                :[19] , protect;
//------------------------------------------------------------------------------
  KatParty.Name
                'Партия'
                : [8], skip;
//------------------------------------------------------------------------------
  KatOtpEd.Name
                'ЕдИзм'
                : [5], skip;
//------------------------------------------------------------------------------
  SpSopr.KolFact
                'Количество'
                : [12,4], skip;
//------------------------------------------------------------------------------
  if (KatSopr.cVal = 0, SpSopr.Price, SpSopr.VPrice)
                'Цена за единицу'
                : [15,2], skip;
//------------------------------------------------------------------------------
  SumPrice
                'Сумма'
                : [15,2], skip;
//------------------------------------------------------------------------------

end; // Browse

//------------------------------------------------------------------------------
//------------------------------------------------------------------------------

HandleEvent

cmInit:
{
  var vcSopr : comp;

  s_SimvRub := sGetTune('NDE.SimvRub');

  if (vcSpSoprNRec <> 0)
  {
    if (Getfirst SpSopr where ((vcSpSoprNRec == SpSopr.NRec)) = tsOk)
    {
      vcSopr := SpSopr.cSopr;
      if (Getfirst KatSopr where ((vcSopr == KatSopr.NRec)) = tsOk)
      {
        if (Getfirst SpSopr where ((vcSpSoprNRec == SpSopr.NRec)) = tsOk)
        {
        }
      }
    }
  }

  if (vwParam <> 0)
  {
    if (vwParam = 1)
      PushBounds(tbForMC);

    if (vwParam > 1)
      PushBounds(tbForUsl);

  }
}

//------------------------------------------------------------------------------

cmDefault:
{
  if (CurTable = #SpSopr)
  {
    vcSpSoprNRec := SpSopr.NRec;

    CloseInterface(cmDefault);
  }
}

//------------------------------------------------------------------------------

End;
//******************************************************************************

End. // Interface