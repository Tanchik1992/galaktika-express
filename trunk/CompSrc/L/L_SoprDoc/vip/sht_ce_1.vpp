Window SHT_CE_KatSopr 'Взвешивание пустого АВТО' EscClose;

Screen scrSHT_CE_Header
  show at (,,,14)
  Table KatSopr ;
Fields
  KatSopr.DesGr     ('Код группы пользователей',hcSoprDocFDesGr,sci13Esc) : skip ;
  KatSopr.Descr     ('Дескриптор(идентификатор) пользователя.',hcSoprDocFDesGr,sci13Esc) : skip ;
  Katsopr.NSopr     ('Номер накладной. Вводится путем набора',,sci1Esc) : skip ;
  KatSopr.dSopr     ('Дата выписки накладной. Вводится путем набора',,sci1Esc) : [,'DD/MM/YYYY'], skip ;

  SHT_CE_BaseDoc    ('Номер документа-основания накладной.',hcSoprDocFBaseDoc,sci13Esc) : skip ;

  (KatSopr.FilialNo and 3)
                    ('Вид накладной навальная или тарная')
                    : [LIST 'не задан','тарная','навальная'] , skip ;

  KatNotes.Name     ('Статус документа',,sci13EnEsc) : protect;

  KatSopr.Name      ('Наименование накладной. Вводится путем набора.',,sci1Esc) : skip ;
  KatOrg.Name       : skip ;
  KatOrgF.Name      : skip ;
  KatOrgT.Name      ('Грузополучатель',,sci13EnEsc) : protect ;

  KatSopr.sDover    ('Доверенное лицо',hcSoprDocFDover,sci13Esc) ;
  KatSopr.nDover    ('Номер доверенности, <Ctrl><F3> - выбор из доверенностей контрагентов',hcSoprDocFTTNnDov,sci13Esc) ;
  KatSopr.dDover    ('Дата доверенности',hcSoprDocFTTNnDov,sci1Esc) ;

  Trp               ('Использование транспорта предприятия в данной операции',hcSoprDocFTrp,sci13Esc) : protect ;

  FIO               ('Лицо, ответственное за проведение операции',hcSoprDocFPerson,sci13Esc) : protect;
  FieldInfo         : skip;
  UserField         ('',hcSoprDocUserField,sci1Esc) ;
Buttons
  cmValue2,,,'Вызов окна с транспортной информацией ',,sci1Esc;
<<
  Группа  Дескр. Номер накладной  Выписана    ДО №             Тип
 .@@@@@@ .@@@@@@  .@@@@@@@@@@@@@ .@@@@@@@@@@@ .@@@@@@@@@@@@@@@ .@@@@@@@@@@
 Примечание       .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@Статус.@@@@@@@@@@
 Контрагент       .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Грузоотправитель .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Грузополучатель  .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Доверенное лицо  .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Доверенность №   .@@@@@@@@@@@@@@@@@@@@@@ Дата доверенности .@@@@@@@@@@@@@

 <.~Т~оварно-транспортная информация.>         Транспорт предприятия .@@@@

 Операцию провел  .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 .@@@@@@@@@@@@@@@@.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
>>
end ; // Screen scrSHT_CE_Header

!--------------------------------------------------------------------------
!--------------------------------------------------------------------------
!--------------------------------------------------------------------------

Browse SHT_CE_brSpecif
  show at (,15,,)
  Table SpSopr ;
Fields
  PrMCUsl        '*'             ('',,sci13Esc):[1]    , skip ;
  NameMCUsl      'Наименование ' ('',,sci13Esc):[20]   , protect ;
  KodMCUsl       'Код'           ('',,sci13Esc):[12]   , protect ;
  OtpEd          'Ед.'           ('',,sci13Esc):[3]    , skip ;
  SpSopr.KolFact 'Количество'    ('',,sci1Esc ):[10,3] , noprotect ;
  SpSopr.Netto   'Вес нетто '    ('',,sci1Esc ):[10.3] , noprotect ;
  SpSopr.mTara   'Вес тары  '    ('',,sci1Esc ):[10.3] , noprotect ;
  TTN_massa_all  'Вес брутто'    ('',,sci13Esc):[10.3] , noprotect ;
  SpSopr.Price   'Цена НДЕ'      ('',,sci1Esc ):[12,2] , protect ;
  SpSopr.VPrice  'В валюте'      ('',,sci1Esc ):[12,2] , skip ;
  KlVal.Simvolv  'Валюта'        ('',,sci13Esc):[3]    , skip ;
  SumPrice       'Стоимость'     ('',,sci1Esc ):[12,2] , skip ;
end; // browse

!--------------------------------------------------------------------------
!--------------------------------------------------------------------------
!--------------------------------------------------------------------------
HandleEvent // Window SHT_CE_KatSopr
cmInit :
  {
    sht_ce_tip := 0;
    SetSemaphore('Штерн-Цемент взвешивание пустого автомобиля',sfAuto) ;
    if boGetTune('Ves.Corr')
      SetFieldOption(#SpSopr.mTara   , ofSelectable) ;
    PutCommand(cmValue1) ;
  }

cmValue1 :
  {
    SHT_CE_nrec := 0 ;
    if (not BoundActive(tbSHT_CE))
      AddBounds(tbSHT_CE) ;
    SelectPanel(#KatSopr) ;
    SelectField(#KatSopr.sDover) ;
    PutCommand(cmInsert) ;
  }
cmDone :
  {
    if (curtable = #SpSopr)
      { // эта часть важна, если происходит выход из окна когда курсор в спецификации
        UpDateTable ;
        SelectPanel(#KatSopr);
      }
    UpdateTable ;

    ClearSemaphore('Штерн-Цемент взвешивание пустого автомобиля') ;
    if (BoundActive(tbSHT_CE))
      SubBounds(tbSHT_CE) ;
  }

cmValue2:
  { //-- интерфейс с товарно-транспортной информацией
    if (CurTable = #SpSopr)
      {
        UpDateTable;
        SelectPanel(#KatSopr);
      }
    if (not UpDateTable)
      EXIT ;
    RunInterface(TTNDoc,word(coKatSopr),KatSopr.NRec);
  }

cmAccording :
  PutCommand (cmValue17) ;

cmValue15 :
  SHT_CE_Init ;

cmValue16 :
  SHT_CE_Zero ;

cmValue17 :
  if (SHT_CE_vid=2) // только для навальных
    SHT_CE_ImportWeigth(1) ;
  else
    Message(''#3'Для тарных накладных не предусмотрено использование '+
            'автоматических весов',CancelButton) ;
cmPrintDoc :
  {
    if (CurTable = #SpSopr)
      {
        UpDateTable;
        SelectPanel(#KatSopr);
      }
    if (UpDateTable)
      if ((SHT_CE_vid > 1) and
          (GetFirst SpSopr
             where ((    KatSopr.nRec  == SpSopr.cSopr
                     and 1             == SpSopr.PrMC
                     and 0             == SpSopr.mTara (NoIndex)
                   )) = tsOk))
        Message(''#3'В накладной не указан вес тары.',CancelButton) ;
      else
        {
          SHT_CE_CheckTTNDoc ;
          RunForm('SC_Rasp',KatSopr.nRec) ;
          RescanPanel(#SpSopr);
          ReReadRecord(#SpSopr);
        }
  }

cmHotKeys :
  if (CurTable=#SpSopr)
    {
      SHT_CE_Menu_My_On ;
      PutHotCommand(RunMenu('mnuSHT_CE_2_SpSopr'));
      SHT_CE_Menu_My_Off ;
    }
  else
    PutHotCommand(RunMenu('mnuSHT_CE_SopDocRashSpSopr'));

cmPlPor33 :
  SHT_CE_nrec := KatSopr.nRec ;

cmValue20 : // Услуга из прайс-листа
  {
    UpDateTable;
    ProcessCommand(cmSetDefault) ;
    set SpSopr.PrMC := 2 ;
    GetMCUSLPrl ;
    ReReadRecord(#SpSopr) ;
  }

cmValue14 : // Улсуга из каталога
  {
    UpDateTable;
    ProcessCommand(cmSetDefault) ;
    set SpSopr.PrMC := 2 ;
    GetMCUSLKat ;
    ReReadRecord(#SpSopr) ;
  }
end ; // HandleEvent Window SHT_CE_KatSopr
end ; // Window SHT_CE_KatSopr