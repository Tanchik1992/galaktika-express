//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - логистика
// Выбор спецификации из накладных
//******************************************************************************
#include ExtAttrClass.vih

//********************************************************************************

#doc
Назначение:
   Выбор спецификации из накладных


Параметры:
  pNRec    - ссылка на документ
  wVidSopr - вид документа, из спецификации которого осуществляется выбор
  wPrMC    - тип спецификации (мц/услуга)
  wPList   - пик
#end

//********************************************************************************

Interface GetDocSpSopr 'Выбор спецификации из документа' doAccept, EscClose, cyan;
Show at (, , 100, 14);

Table STRUCT TmpSpSopr ""
(
  cSpSopr  : comp   "",
  cMCUsl   : comp   "",
  PrMC     : word   "",
  cOtpEd   : comp   "",
  cVal     : comp   "",
  Kol      : double "",
  Price    : double "",
  Dostup   : double "" 
);

Create View
Var
  pNRec       : comp;     // ссылка на документ

  wFiltr
, wPList
, wPrMC
, wVidSOpr    : word;

  s_SimvRub   : string;

As Select
//******************************************************************************
  if (isValid(tnPick), '√', '')
    ( FieldName = isPicked )
//******************************************************************************
, if (TmpSpSopr.PrMC = 1, if (isValid(tnKatMC), KatMC.Name, '? МЦ ' + string(TmpSpSopr.cMCUsl))
                        , if (isValid(tnKatUsl), KatUsl.Name, '? услуга ' + string(TmpSpSopr.cMCUsl)))
    ( FieldName = NameMCUsl )
//******************************************************************************
, if (TmpSpSopr.PrMC = 1, if (isValid(tnKatMC), KatMC.BarKod, '? МЦ ' + string(TmpSpSopr.cMCUsl))
                        , if (isValid(tnKatUsl), KatUsl.Kod, '? услуга ' + string(TmpSpSopr.cMCUsl)))
    ( FieldName = KodMCUsl )
//******************************************************************************
, if(TmpSpSopr.PrMC = 1, 'Т', 'У')
    ( FieldName = PrMCUsl )
//******************************************************************************
, if (TmpSpSopr.cVal = 0,
    if (s_SimvRub <> '', s_SimvRub, 'руб.'), KlVal.SimVolV)
    ( FieldName = Valut )
//******************************************************************************
, if(TmpSpSopr.cOtpEd = 0, KatEd.Name, if (wGetTune('Doc.BrAbbrEd')=0, KatOtpEd.ABBR, KatOtpEd.Name))
    ( FieldName = OtpEd )
//******************************************************************************


From
  KatSopr
, KatSopr   KatSopr2
, SpSopr
, SpSopr    SpSopr2
, SpecZatr
, KatOtpEd
, KatEd
, KatMC
, KatUsl
, KlVal
, Pick
, TmpSpSopr

where
((
     pNRec                   ==  KatSopr.NRec
AND  KatSopr.NRec            ==  SpSopr.cSopr 
AND  word(1)                 ==  SpSopr.PrMC
AND  TmpSpSopr.cMCUsl        ==  KatMC.NRec
AND  TmpSpSopr.cMCUsl        ==  KatUsl.NRec
AND  if(TmpSpSopr.PrMc = 1,
      KatMc.cEd, KatUsl.cEd) ==  KatEd.NRec
AND  TmpSpSopr.cOtpEd        ==  KatOtpEd.NRec
AND  TmpSpSopr.cVal          ==  KlVal.NRec
AND  wPList                  ==  Pick.wList
AND  TmpSpSopr.cSpSopr       ==  Pick.cRec
))

;

Parameters
  pNRec
, wVidSopr
, wPrMC
, wPList
;


Panel pnSpecif
  Table TmpSpSopr;

Browse brSpecif ('', hcSellWNaklEditSpec, sci1EscIns)
  Show at (, , , )
Fields
  {Font = {Color = if (isPicked <> '', ColorMark, 0) }};
  isPicked         #3''                                 : [ 1], NoAutoSize, Protect;
  PrMCUsl          #3'Т'#13#3'У'    ('Товар/услуга')    : [ 1], Protect , NoAutoSize;
  NameMCUsl        #3'Наименование'                     : [25], Protect, NoDel;
  KodMCUsl         #3'Код'                              : [10], Protect, NoDel, NoAutoSize;
  OtpEd            #3'ЕдИзм'                            : [ 5], Protect, NoDel, NoAutoSize;
  TmpSpSopr.Kol    #3'Количество'                       : [10], [prSignsInKol], Protect, NoAutoSize;
  TmpSpSopr.Price  #3'Цена'                             : [12], [brForSopr_RVPrice], Protect, NoAutoSize;
  Valut            #3'Вал.'                             : [ 5], Protect, NoAutoSize;
  TmpSpSopr.Dostup #3'Остаток'  ('Доступное к выбору количество')
                                                        : [10], [prSignsInKol, '\3p[|-]36`666`666`666`666.888'], Protect, NoAutoSize;
  Pick.PickKol     #3'Выбрано'                          : [10], [prSignsInKol, '\3p[|-]36`666`666`666`666.888'], Protect, NoAutoSize;
end;
end;

//******************************************************************************

//******************************************************************************

Procedure KillPick;
{
  ResetBounds(#Pick);
  delete Pick where ((wPList == Pick.wList));
  SetBounds(#Pick);
}

//******************************************************************************

Function KolDostup : double;
var dUsed : double;
{
  case wVidSopr of
  442: 
    {
      dUsed := SpSopr.KolFact;

      _LOOP KatSopr2 where (( KatSopr.nRec == KatSopr2.cKatSopr ))
        {
          if ( KatSopr2.VidSopr = word(115) )
            {
              _LOOP SpSopr2 where (( KatSopr2.NRec == SpSopr2.cSopr  AND
                                     SpSopr.cMCUsl == SpSopr2.cMCUsl AND
                                     word(1)       == SpSopr2.PrMC       ))
                {
                  dUsed := dUsed - SpSopr2.KolFact;
                }
            }
        }
    }
  115:
    {
      dUsed := 0;

      _LOOP SpecZatr where ((SpSopr.NRec == SpecZatr.cAddSumTune))
        {
          if (SpecZatr.coTable = 1)
            dUsed := dUsed + double(SpecZatr.cSpec);
        }

      dUsed := SpSopr.KolFact - 0.000001 * dUsed / fEdIzm.GetKoefOtpEd(SpSopr.cOtpEd);
    }
  end; // case

  KolDostup := if (dUsed < 0, 0, dUsed);
}

//******************************************************************************

HandleEvent
cmInit:
  {
    s_SimvRub := sGetTune('NDE.SimvRub');

    SetTitle('Выбор спецификации по документу ' + PrintNumber(KatSopr.Descr, KatSopr.NSopr));

    ResetBounds(#Pick);
    Delete All Pick where ((wPList == Pick.wList));
    SetBounds(#Pick);

    var TmpKol : double;

    _LOOP SpSopr
      {
        TmpKol := KolDostup;

        if ( TmpKol > 0.000001 )
          {
            insert current TmpSpSopr
               set TmpSpSopr.cSpSopr := SpSopr.nRec
                 , TmpSpSopr.cMCUsl  := SpSopr.cMCUsl 
                 , TmpSpSopr.PrMC    := SpSopr.PrMC
                 , TmpSpSopr.cVal    := SpSopr.cVal   
                 , TmpSpSopr.Kol     := SpSopr.KolFact    
                 , TmpSpSopr.Price   := SpSopr.Price  
                 , TmpSpSopr.Dostup  := TmpKol;
          }
      }

    if (GetFirst TmpSpSopr <> tsOk)
      {
        Message('В спецификации распоряжения на прием-отпуск нет позиций для выбора!');

        Abort;
        Exit;
      }
  }

cmOpenSearch:
  {
    if (CurField <> #Pick.PickKol)
      {
        Stop;
        Exit;
      }

    var Kol_New : double; Kol_New := Pick.PickKol;

    if RunDialog('GetPickKol', Kol_New) <> cmCancel
      {
        if (Kol_New > TmpSpSopr.Dostup)
          Kol_New := TmpSpSopr.Dostup;

        if GetFirst Pick = tsOk
          update current Pick set Pick.PickKol := Kol_New;
        else
          insert Pick set Pick.cRec := TmpSpSopr.cSpSopr, Pick.PickKol := Kol_New, Pick.wList := wPList;
      }

    ReReadRecord(#Pick);
    ReReadRecord(#SpSopr);
  }

cmMarkUnMark:
  {
    if (IsValid(#Pick))
      delete current Pick;
    else
      {
        Pick.wList   := wPList;
        Pick.cRec    := TmpSpSopr.cSpSopr;
        Pick.PickKol := TmpSpSopr.Dostup;
        insert current Pick;
      }

#ifdef ATL51
    RedrawCurrentAndGo (GetCurrentFormat, TRUE);
#else
    if (GetNext TmpSpSopr = tsOk)
      CallToNeighbours(cmPosDown, #TmpSpSopr);

    RescanPanel(#TmpSpSopr);
#endif
  }

cmSelectAll:
  {
    PushPos(#TmpSpSopr);

    StartNewVisual(vtRotateVisual
                   , vfTimer+vfBreak+vfConfirm
                   , 'Помечаю всю спецификацию...'#13#3
                   , 1);

    KillPick;

    _LOOP TmpSpSopr
      if (NOT NextVisual)
        Break;
      else
        {
          Pick.wList   := wPList;
          Pick.cRec    := TmpSpSopr.cSpSopr;
          Pick.PickKol := TmpSpSopr.Dostup;
          insert current Pick;
        }

     PopPos(#TmpSpSopr);
     ReReadRecord;
     RescanPanel(#TmpSpSopr);
     StopVisual('', 0);
   }

cmUnSelectAll:
  {
    KillPick;
    RescanPanel(#TmpSpSopr);
    ReReadRecord;
  }

cmDefault:
  if (UpdateTable)
    {
      ResetBounds(#Pick);

      if (GetFirst Pick where ((wPList == Pick.wList)) <> tsOk)
        {
          ClearBuffer(#Pick);
          Pick.wList   := wPList;
          Pick.cRec    := TmpSpSopr.cSpSopr;
          Pick.PickKol := TmpSpSopr.Dostup;
          insert current Pick;
        }
    }

End; // HandleEvent
End. // interface
