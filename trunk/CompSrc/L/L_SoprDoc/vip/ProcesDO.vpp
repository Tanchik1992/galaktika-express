//-----------------------------------------------------------------------------
//                                                (c) корпорация ГАЛАКТИКА   
// Галактика 5.71 - ... логистика                                             
// Процедуры-функции для интерфейса пакетной обработки ДО                    
//-----------------------------------------------------------------------------

#include DOfuns.var

var WasModified: boolean;

Procedure SetBaseDocStatus(_Status: word; _Note: comp);
{
  WasModified := false;
  oStatLog.StatLog_InsNewStat( word(40)
                             , BaseDoc.nRec
                             , BaseDoc.Status
                             , BaseDoc.cNote
                             , _Status
                             , _Note
                             , 'Пакетная обработка' );
  BaseDoc.Status := _Status;
  BaseDoc.cNote  := _Note;
  WasModified := true;
}; // procedure SetBaseDocStatus

Procedure SetBaseDocdDoc(adDoc: date);
{
  BaseDoc.dDoc := adDoc;
  WasModified := true;
}; // procedure SetBaseDocdDoc

Procedure RunCalcSkidka;
{
  oDOfuns.BaseDoc_CountSkidka(BaseDoc.NRec, false);
  GetTotalSkid(false);
  WasModified := true;
}; // procedure RunCalcSkidka

Procedure RunProcessing;
{
  if ( GetMarkerCount(DOMarker) > 0 ) 
   {
     var i: longint;
     var BaseDocNRec: comp;
     
     StartNewVisual( vtIndicatorVisual, vfTimer+vfBreak+vfConfirm
                   , 'Обрабатываю документы...'#13, GetMarkerCount(DOMarker) );

     for (i := 0; i < GetMarkerCount(DOMarker) and NextVisual; i := i + 1) 
      {
        if (GetMarker(DOMarker, i, BaseDocNRec))
          if (GetFirst BaseDoc where ((BaseDocNRec == BaseDoc.NRec)) = tsOk) then 
           {
             WasModified := false;
             // Модификационные операции
             if ( (ModifOps and 1) <> 0 ) 
               SetBaseDocStatus(ModifStatus, ModifNote);  // статус

             if ( (ModifOps and 2) <> 0 ) 
               SetBaseDocdDoc(ModifdDoc);  // дата

             // Функциональные операции
             if ( (FuncOps and 1) <> 0 ) 
               RunCalcSkidka;    //  расчет скидок/надбавок
             
             if ( WasModified ) 
               Update Current BaseDoc;

             // Внешние операции
           };
      };
     StopVisual('', 0);
   } 
  else
    message('Не выбрано ни одного документа.');
}; // procedure RunProcessing
