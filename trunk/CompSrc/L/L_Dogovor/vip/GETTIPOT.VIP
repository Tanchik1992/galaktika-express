/*
 ╔═══════════════════════════════════════════════════════════════════════════╗
 ║                     (c) 1987,98 корпорация ГАЛАКТИКА                      ║
 ║ Проект        : ГАЛАКТИКА                                                 ║
 ║ Система       : Оперативный контур                                        ║
 ║ Версия        : 5.50                                                      ║
 ║ Назначение    : Выбор типа отгрузки                                       ║
 ║ Ответственный : Володько Вадим Геннадьевич (VVG)                          ║
 ║ Параметры     : есть                                                      ║
 ╚═══════════════════════════════════════════════════════════════════════════╝
*/

#doc
Интерфейс выбора базиса поставки
#end
Interface GetTipOtg 'Выберите базис поставки' (,hcDogBazisPost,sci178Esc)
  doAccept,EscClose,cyan;
  show at (4,5,75,19);
Create View
Var
  c1 : comp;
From
  TipOtg (TipOtg02);

Parameters
  c1;          // NRec типа

QuickChoice qcTipOtgName
 {
  AcceptField = TipOtg.nrec;
  ViewField   = TipOtg.Name;
  SearchField = TipOtg.Name;

  OnClose: c1 := TipOtg.nRec;
 }
QuickChoice qcTipOtgCode
 {
  AcceptField = TipOtg.nrec;
  ViewField   = TipOtg.Code;
  ViewField   = TipOtg.Name;
  SearchField = TipOtg.Code;

  OnClose: c1 := TipOtg.nRec;
 }

!-------------------------------------------------------------------------------

Window Win1 'Базисы поставки' escclose;
  show at (4,5,75,19);

browse brTipOtg ('',,sci178Esc);
Fields
  TipOtg.Name 'Базис поставки':[60];
  TipOtg.Code 'Код':[6];
end;

HandleEvent

cmDone :
  if (not UpdateTable)
    abort;
  else
    if (not isvalid(#TipOtg))
      PutCommand(cmClose);

end;
end; // window

!-------------------------------------------------------------------------------

browse TipOtgbr ('Выберите тип и нажмите ВВОД',hcDogBazisPost,sci147EnEsc)

fields
  TipOtg.Name 'Тип отгрузки':[60],protect;
  TipOtg.Code 'Код':[6],protect;
end;

HandleEvent

cmInit :
  {
    if ( GetFirst TipOtg where ((c1 == TipOtg.NRec)) <> 0 )
      if ( GetFirst TipOtg <> 0 )
        {
          if (message(''#3'Справочник типов отгрузки пуст.'#13#3+
                          'Нажмите ВВОД и заполните его...',
                          OkButton) = cmCancel)
            {
              abort;
              exit;
            }
          PutCommand(cmInsert);
        }
  }

cmSetDefault :
  {
    SelectField(#TipOtg.Name);
    PutCommand(cmEdit);
  }

cmDefault :
  c1 := TipOtg.NRec;

cmEdit :
  RunWindow(Win1);

cmAddNewRec :
  {
    PutCommand(cmInsert);
    RunWindow(Win1);
    ReReadRecord;
  }

cmCheckRecord :
  if (trim(TipOtg.Name) = '')
    {
      message(''#3'Укажите наименование типа или удалите запись...',
                CancelButton);
      abort;
    }

cmInsertRecord :
  insert current TipOtg;

cmUpdateRecord :
  update current TipOtg;

cmDeleteRecord :
  if (Message(''#3'Удалим тип отгрузки ?',YesNo)=Yes)
    if (Message(''#3'Отменить удаление типа отгрузки ?',YesNo)=No)
      delete current TipOtg;
    else abort;
  else abort;

cmPickClass:
  if ((UpdateTable) and (TipOtg.NRec <> 0))
    RunInterface(ExClassifier, word(CurTableCode), TipOtg.NRec);

cmPickAttr:
  if ((UpdateTable) and (TipOtg.NRec <> 0))
    RunInterface(Attribute, word(CurTableCode), TipOtg.NRec);

end;
end.