//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Логистика
//
//******************************************************************************

Procedure MySetFieldDogovor; forward;

#doc
Окно редактирования агентского договора
#end
Window wiDogovorEditAgentMain 'Редактирование договора' ('Редактирование договора', , )
#ifdef ATL51
  LeftToolbar
#end
  show at (,, 88, 25);

#ifdef ComponentVersion
ToolBar
    F_Common::tbPrintDoc
  , tbRunDogExcel
  , F_Common::tbManagerId
  , F_Common::tbAttrClass
  , DogNavigator
;
#else
ToolBar
    tbPrintDoc
  , tbRunDogExcel
  , tbManagerId
  , tbAttrClass
  , DogNavigator
;
#end

Screen scDogovorEditAgentMain
  ('Редактирование договора',hcDogEditDogovor,sci13Esc)
  Show at (,,,14);
  Table Dogovor ;

Fields
  KatNotes.Name      ('Статус договора'): PickButton, protect, {font = {color = ColorByEndDate; bold = (ColorByEndDate<>0)}};
  Dogovor.DesGr      ('Код группы пользователей'): protect;
  Dogovor.Descr      ('Дескриптор(идентификатор) пользователя'): protect;
  Dogovor.NoDoc_Ext  ('Входящий номер договора/соглашения', , sci1Esc): noprotect;
  Dogovor.dInput     ('Дата создания договора/соглашения'): [10, 'DD/MM/YYYY'], noprotect;
  KatVidD.Name       ('Вид договора/соглашения'): Skip;
  Dogovor.NoDoc      ('Номер договора/соглашения'): PickButton, noprotect;
  Dogovor.dDoc       ('Дата заключения договора/соглашения'): [10, 'DD/MM/YYYY'], noprotect;
  Dogovor.TipMoney   ('Тип договора/соглашения'): [LIST 1 'в НДЕ', 'вал-НДЕ', 'валютный'], protect;
  ValDogovor         ('Валюта договора/соглашения'): protect;
  Dogovor.dBeg       ('Дата начала договора/соглашения'): [10, 'DD/MM/YYYY'], noprotect;
  Dogovor.dEnd       ('Дата окончания договора/соглашения', ): [10, 'DD/MM/YYYY'], noprotect, {font = {color = ColorByEndDate; bold = (ColorByEndDate<>0)}};
  OwnerDogInfo       : skip;
  DogZamenaInfo      : skip;
//******************************************************************************
  AttrDog.Subject1   ('Предмет договора/соглашения', , sci1Esc): noprotect;
//******************************************************************************
  AttrDog.Otv_My_Fio  ('Фамилия, имя, отчество ответственного', , sci13Esc) : PickButton, noprotect;
  AttrDog.Otv_My_Post ('Должность ответственного', , sci13Esc) : PickButton, noprotect;
  KuratPodr.Name    : Protect;
//******************************************************************************
  KatVidD.Role1      ('Роль контрагента в договоре/соглашении') : skip;
  KatOrg.Name        ('Наименование контрагента') : protect,QuickChoice;
Buttons
  cmValue2 ,,, 'Просмотр подробной информации по контрагенту';
  cmAttrib ,,, 'Просмотр и изменение расширенной информации по документу';
  cmValue10,,, 'Текстовая информация';
<<
 Группа&  Дескр.&   Входящий номер &   Дата   &    Статус.@@@@@@@@@@@@@@@@@@@@@@@@@
 .@@@@@@  .@@@@@@   .@@@@@@@@@@@   .@@@@@@@@@@@    Вид   .@@@@@@@@@@@@@@@@@@@@@@@@@
        Внутренний №.@@@@@@@@@@@ от.@@@@@@@@@@@    Тип   .@@@@@@@@@@@@@@@@@@@.@@@@@
        На период с .@@@@@@@@@@@ по.@@@@@@@@@@@.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Предмет договора/соглашения
 .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Ответственный .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@Должность.@@@@@@@@@@@@@@@@@@@@@@@@@
 Курирующее подразделение .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

<. Контрагент 1.> .@@@@@@@@@@@@@@.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

<.Расширенная информация.>                               <. Текстовая информация .>
>>
end; // screen scDogovorEditAgentMain

Browse brSpAgentDog (, hcDogSpecification, sci178Esc);
  Show at (,15,,);
  Table SpDocs;

Fields
  SpDocs.CODE    #3'№'#13#3'п/п' ('Порядковый номер')               : [ 4], NoProtect, NoAutoSize;
  Principal.Name #3'Принципал'   ('Принципал',,sci1378Esc)          : [25], Protect, QuickChoice;
//  PlatFor.Name   #3'Плательщик за контрагента'   ('Плательщик (получатель платежей) за контрагента',,sci1378Esc)          : [25], Protect, QuickChoice;
//  IspPol.Name    #3'Исполнитель/получатель'   ('Исполнитель/получатель',,sci1378Esc)          : [25], Protect, QuickChoice;
  SpDocs.Price   #3'%'           ('% агентского вознаграждения'): [6.2, '\2p[|-]36`666.88'], NoProtect;
end;  //browse

!--------- Проверка доступности экранных кнопок -----------------------

HandleEvent // Window wiDogovorEditMain

cmInit:
  {
    var StrTitle: string;
    if Dogovor.cZamena <> 0
      {
        StrTitle := 'Редактирование отменяющего договора';
        if (sGetTune('OPER.Dogovor.UserName.OtmenDogovor') <> '')
          StrTitle := StrTitle + ' (' +
                      sGetTune('OPER.Dogovor.UserName.OtmenDogovor') + ')';
      }
    else
      {
        StrTitle := 'Редактирование договора';
        if (sGetTune('OPER.Dogovor.UserName.Dogovor') <> '')
          StrTitle := StrTitle + ' (' +
                      sGetTune('OPER.Dogovor.UserName.Dogovor') + ')';
      }

    SetWindowTitle(wiDogovorEditAgentMain, StrTitle);

    MySetFieldDogovor;

    if (GetFirst fastfirstrow AttrDog = tsOk)
      {
        OldSubject1 := AttrDog.Subject1;
      }
    else
      {
        ClearBuffer(#AttrDog);
        AttrDog.cDogovor := Dogovor.nRec;
        AttrDog.cPodr    := DefaultBaseCO;
        if (insert current AttrDog <> tsOk) {}
        OldSubject1 := '';
      }

    c1 := -1;

    SelectFormat(scDogovorEditAgentMain);
    SelectFieldInFormat(scDogovorEditAgentMain, #Dogovor.NoDoc_Ext);
  }

cmOpenSearch:
  case CurField of
    #Principal.Name, #PlatFor.Name, #IspPol.Name:
      {
        if NullVipRef(oChkLink)
          GetVipRef(oChkLink, 'KatOrg');

        if oChkLink.CheckLinks(coKatOrgDescr)
          QuickChoiceName := 'qcKatOrgName'
        else
          QuickChoiceName := 'qcKatOrgTreeName';

        ProcessCommand(cmPick);
      }
  else
    Abort;
  end; // case CurField

#include d_d_we.han

end; // HandleEvent Window wiDogovorEditMain

end; // Window wiDogovorEditMain
