/*
 ╔═══════════════════════════════════════════════════════════════════════════╗
 ║                     (c) 1987,2001 корпорация ГАЛАКТИКА                    ║
 ║ Проект        : ГАЛАКТИКА                                                 ║
 ║ Система       : Оперативный контур                                        ║
 ║ Версия        : 5.73                                                      ║
 ║ Назначение    : Инклюд для печати в интерфейсе Просмотр дог-согл-кал      ║
 ║                 плана и привязанных документов                            ║
 ║ Ответственный : Шишов Виктор Васильевич (ShV)                             ║
 ╚═══════════════════════════════════════════════════════════════════════════╝
*/

Procedure PrintDocumentsView;
var
  a       : comp;
  CurTabl : longint;
{
  CurTabl := CurTable;
  TreePushPos(trTreeTables);
  PushPos(CurTabl);

  a := SGetDocs.NRec;
  do
    {
      frm0.PutEventById(feDoLoop, fcMain_Cicle1);
      frm0.write(SGetDocs.cDogovor);
      frm0.write(SGetDocs.cAppDogovor);
      frm0.write(SGetDocs.cCalPlan);
      frm0.write(sDocType);
      frm0.write(SGetDocs.DesGr);
      frm0.write(SGetDocs.Descr);
      frm0.write(SGetDocs.dDoc);
      frm0.write(SGetDocs.NoDoc);
      frm0.write(KatOrg.Name);
      frm0.write(KatNotes.Name);
      frm0.write(sDirect);
      a := SGetDocs.cOwner;
      TreeJumpToRecord(trTreeTables, a);
    }
  while (a <> 0);
  TreePopPos(trTreeTables);

  ReRead_Filters_Only(1); //платежи
  _loop IerDog where (( if(SGetDocs.cAppDogovor = 0, SGetDocs.cDogovor, SGetDocs.cAppDogovor) == IerDog.cUpRec ))
    _loop BaseFin
      {
        frm0.PutEventById(feDoLoop, fcCiclePlat);
        frm0.write(BaseFin.NRec);
        frm0.write(BaseFin.DesGr);
        frm0.write(BaseFin.Descr);
        frm0.write(BaseFin.dDoc);
        frm0.write(BaseFin.NoDoc);
        frm0.write(KatOrg1.Name);
        frm0.write(BaseFin.NamePl);
        frm0.write(BaseFin.Summa);
      }

  ReRead_Filters_Only(2); //ДО
  _loop IerDog where (( if(SGetDocs.cAppDogovor = 0, SGetDocs.cDogovor, SGetDocs.cAppDogovor) == IerDog.cUpRec ))
    _loop BaseDoc
      {
        frm0.PutEventById(feDoLoop, fcCicleDO);
        frm0.write(BaseDoc.NRec);
        frm0.write(BaseDoc.DesGr);
        frm0.write(BaseDoc.Descr);
        frm0.write(BaseDoc.dDoc);
        frm0.write(BaseDoc.NoDoc);
        frm0.write(KatNotes1.Name);
        frm0.write(KatOrg2.Name);
        frm0.write(BaseDoc.Total);
        frm0.write(SimvolB);
      }

  ReRead_Filters_Only(3); //Накладные
  _loop IerDog where (( if(SGetDocs.cAppDogovor = 0, SGetDocs.cDogovor, SGetDocs.cAppDogovor) == IerDog.cUpRec ))
    _loop KatSopr
      {
        frm0.PutEventById(feDoLoop, fcCicleKatSopr);
        frm0.write(KatSopr.NRec);
        frm0.write(KatSopr.DesGr);
        frm0.write(KatSopr.Descr);
        frm0.write(KatSopr.dSopr);
        frm0.write(Katsopr.NSopr);
        frm0.write(KatNotes2.Name);
        frm0.write(KatOrg3.Name);
        frm0.write(Sum);
        frm0.write(Valut);
      }

  ReRead_Filters_Only(4); //Наряд-заказы
  _loop IerDog where (( if(SGetDocs.cAppDogovor = 0, SGetDocs.cDogovor, SGetDocs.cAppDogovor) == IerDog.cUpRec ))
    _loop NZakaz
      {
        frm0.PutEventById(feDoLoop, fcCicleNZakaz);
        frm0.write(NZakaz.NRec);
        frm0.write(NZakaz.DesGr);
        frm0.write(NZakaz.Descr);
        frm0.write(NZakaz.dDoc);
        frm0.write(NZakaz.NoDoc);
        frm0.write(KatNotes3.Name);
        frm0.write(KatOrg4.Name);
        frm0.write(nZakazPrior);
        frm0.write(nZakazTypeShort);
      }

  ReRead_Filters(True); //восстановим
  PopPos(CurTabl);
  ReReadRecord(CurTabl);

  if not frm0.Error
    frm0.ShowFile('')
  else
    frm0.AbortForm;
}

Procedure PrintSpDocs(Row : word; Column : word; WithDirect : boolean);
var _str : string;
{
  xlSetCellStringValue(SpDocs.CODE, Row, Column, Row, Column);
  if WithDirect
    {
      _str := if(SpDocs.Direct = 1, '1->2', '2->1');
      xlSetCellStringValue(_str, Row, Column + 1, Row, Column + 1);
    }
  else
    dec(Column);

  xlSetCellStringValue(SpDogGrName    , Row, Column + 2, Row, Column + 2);
  xlSetCellStringValue(SpDogName      , Row, Column + 3, Row, Column + 3);
  xlSetCellNumberValue(SpDocs.Kol     , Row, Column + 4, Row, Column + 4);
  xlSetCellStringValue(KatOtpEd.Name  , Row, Column + 5, Row, Column + 5);
  xlSetCellNumberValue(SpDocs.Price   , Row, Column + 6, Row, Column + 6);
  xlSetCellNumberValue(SpDocs.Summa   , Row, Column + 7, Row, Column + 7);
}

Procedure PrintDocumentsZamenView;
var
  XlRes               : boolean;
  aFileName, FBasName : string;
  i                   : word;
  StrCount            : array[1..6] of word;
  _str                : string;
  SheetNames          : array[1..6] of string;
  OldSave_Format      : byte;
  CurTabl             : longint;
{
  OldSave_Format := Save_Format;

  SheetNames[1] := 'Статусы';
  SheetNames[2] := 'Договор со спецификацией';
  SheetNames[3] := 'Договор с КП';
  SheetNames[4] := 'Наряд-заказы';
  SheetNames[5] := 'Платежи';
  SheetNames[6] := 'Отгрузки';

  XlRes := true;
  aFileName := GetStringParameter('Files', 'OutputFilesDirectory', 0) + 'dogshist.xls';
  DeleteFile(aFileName);
  XlRes := xlCreateExcel(aFileName, true);
  if XlRes
    XlRes := xlIsExcelValid;
  if not XlRes
    Exit;

  StartNewVisual(vtIndicatorVisual, vfTimer+vfBreak+vfConfirm, ''#3'Формирование заголовков...', RecordsInTable(#SGetDocs));
  FBasName := GetStringParameter('Files', 'OutputFilesDirectory', 0) + 'dogshist.bas';
  DeleteFile(FBasName);
#include dogshist.bas

  if(PutSrvFileToClient(ConvertClientPathToSrv(FBasName), FBasName, False) <> 0) then
    Message('Fill_Excel : Невозможно скопировать файл клиенту, ('+FBasName+')');

  xlImportModule(FBasName);
  xlRunMacro('CreateReportHeaders');

  for(i := 1; i <= 6; inc(i))
    StrCount[i] := 4;
  iNextVisual('Формирование отчета...');

  CurTabl := CurTable;
  TreePushPos(trTreeTables);
  PushPos(CurTabl);

  TreePushPos(trTreeTables);
  if TreeGetFirstEx(trTreeTables)
  do
    {
      if not NextVisual
        Break;
      for(i := 1; i <= 6; inc(i))
        {
          xlSetActiveSheetByName(SheetNames[i]);
          case SGetDocs.wDocType of
            1, 2 : //договор/соглашение
              {
                _str := '№ ' + SGetDocs.NoDoc + DateToStr(SGetDocs.dDoc, ' от DD/MM/YYYY');
                xlSetCellStringValue(_str       , StrCount[i], 1+2*(SGetDocs.wDocType-1), StrCount[i], 1+2*(SGetDocs.wDocType-1));
                xlSetCellStringValue(KatOrg.Name, StrCount[i], 2+2*(SGetDocs.wDocType-1), StrCount[i], 2+2*(SGetDocs.wDocType-1));
                if IsValid(#SGetDocsOwner)
                  {
                    _str := '№ '+SGetDocsOwner.NoDoc + DateToStr(SGetDocsOwner.dDoc, ' от DD/MM/YYYY');
                    xlSetCellStringValue(_str, StrCount[i], 5, StrCount[i], 5);
                  }
                _str := chr(39) + DateToStr(SGetDocs.dBeg, 'DD/MM/YYYY');
                xlSetCellStringValue(_str           , StrCount[i],  6, StrCount[i],  6);
                _str := chr(39) + DateToStr(SGetDocs.dEnd, 'DD/MM/YYYY');
                xlSetCellStringValue(_str           , StrCount[i],  7, StrCount[i],  7);
                xlSetCellStringValue(KatVidD.Name   , StrCount[i],  8, StrCount[i],  8);
                xlSetCellStringValue(KatNazna.Name  , StrCount[i],  9, StrCount[i],  9);
                _str := if (SGetDocs.TipMoney = 1,'НДЕ',
                          if (SGetDocs.TipMoney = 2, 'Вал-НДЕ', 'Вал'));
                xlSetCellStringValue(_str           , StrCount[i], 10, StrCount[i], 10);
                xlSetCellStringValue(SGetDocs.DogVal, StrCount[i], 11, StrCount[i], 11);
                xlSetCellNumberValue(SGetDocs.Summa , StrCount[i], 12, StrCount[i], 12);
                xlSetCellStringValue(OtvPodr.Name   , StrCount[i], 13, StrCount[i], 13);
                xlSetCellStringValue(Persons.FIO    , StrCount[i], 14, StrCount[i], 14);
                case i of
                  1 :
                    {
                      _loop StatLog where (( SGetDocs.nRec     == StatLog.cDoc and
                                             word(399)+
                                             SGetDocs.wDocType == StatLog.DocType ))
                        {
                          _str := DateToStr(StatLog.dOper, 'DD/MM/YYYY ');
                          _str := _str + TimeToStr(StatLog.TimeOper, 'HH:MM');
                          xlSetCellStringValue(_str           , StrCount[i], 15, StrCount[i], 15);
                          xlSetCellStringValue(OldNotes.Name  , StrCount[i], 16, StrCount[i], 16);
                          xlSetCellStringValue(NewNotes.Name  , StrCount[i], 17, StrCount[i], 17);
                          xlSetCellStringValue(StatLog.Comment, StrCount[i], 18, StrCount[i], 18);
                          inc(StrCount[i]);
                        }
                      if (RecordExists StatLog where (( SGetDocs.nRec     == StatLog.cDoc and
                                                        word(399)+
                                                        SGetDocs.wDocType == StatLog.DocType )) = tsOK)
                        dec(StrCount[i]);
                    }
                  2 :
                    {
                      _loop SpDocs where (( SGetDocs.nRec     == SpDocs.cDoc and
                                            word(399)+
                                            SGetDocs.wDocType == SpDocs.TiDk and
                                            SpDocs.cOtvPodr   == OtvPodr.nRec))
                        {
                          _str := if(SpDocs.Direct = 1,'1->2', '2->1');
                          xlSetCellStringValue(_str        , StrCount[i], 16, StrCount[i], 16);
                          PrintSpDocs(StrCount[i], 15, true);
                          xlSetCellStringValue(OtvPodr.Name, StrCount[i], 23, StrCount[i], 23);
                          inc(StrCount[i]);
                        }
                      if (RecordExists SpDocs where (( SGetDocs.nRec     == SpDocs.cDoc and
                                                       word(399)+
                                                       SGetDocs.wDocType == SpDocs.TiDk )) = tsOK)
                        dec(StrCount[i]);
                    }
                  3 :
                    {
                      _loop PKP where (( SGetDocs.NRec     == PKP.cOwner   and
                                         word(4)           == PKP.wDocType and
                                         SGetDocs.cCalPlan == CalPlan.nRec and
                                         CalPlan.cOtvPodr  == OtvPodr.nRec ))
                        {
                          xlSetCellStringValue(PKP.NoDoc   , StrCount[i], 15, StrCount[i], 15);
                          _str := if(PKP.VidPlan = 1, 'МЦ/Услуги','Финансовый');
                          xlSetCellStringValue(_str        , StrCount[i], 16, StrCount[i], 16);
                          xlSetCellNumberValue(PKP.Summa   , StrCount[i], 17, StrCount[i], 17);
                          xlSetCellStringValue(OtvPodr.Name, StrCount[i], 18, StrCount[i], 18);

                          _loop SpDocs where (( PKP.cCalPlan == SpDocs.cDoc and
                                                word(403)    == SpDocs.TiDk ))
                            {
                              _str := if(SpDocs.Direct = 1,'1->2', '2->1');
                              xlSetCellStringValue(_str        , StrCount[i], 20, StrCount[i], 20);
                              PrintSpDocs(StrCount[i], 19, true);
                              inc(StrCount[i]);
                            }
                          if (RecordExists SpDocs where (( PKP.cCalPlan == SpDocs.cDoc and
                                                           word(403)    == SpDocs.TiDk )) = tsOK)
                            dec(StrCount[i]);

                          inc(StrCount[i]);
                        }
                      if RecordExists PKP = tsOK dec(StrCount[i]);
                    }
                  4 :
                    {
                      _loop NZakaz where (( SGetDocs.NRec    == NZakaz.cDogovor   and
                                            NZakaz.cPodrForm == KatPodr.nRec      and
                                            NZakaz.cPunktP   == MarPunkt.nRec ))
                        {
                          case nZakaz.TypeDoc of
                            0 : _str := 'обычный';
                            1 : _str := 'групповой';
                            2 : _str := 'детальный';
                          else
                               _str := '';
                          end;
                          xlSetCellStringValue(_str         , StrCount[i], 15, StrCount[i], 15);
                          xlSetCellStringValue(NZakaz.NoDoc , StrCount[i], 16, StrCount[i], 16);
                          _str := chr(39) + DateToStr(NZakaz.dDoc, 'DD/MM/YYYY ');
                          xlSetCellStringValue(_str         , StrCount[i], 17, StrCount[i], 17);
                          xlSetCellStringValue(KatPodr.Name , StrCount[i], 18, StrCount[i], 18);
                          xlSetCellStringValue(MarPunkt.Name, StrCount[i], 19, StrCount[i], 19);

                          _loop SpDocs where (( NZakaz.nRec     == SpDocs.cDoc and
                                                word(440)       == SpDocs.TiDk and
                                                SpDocs.cResOtgr == ResOtgr.nRec ))
                            {
                              PrintSpDocs(StrCount[i], 20, false);
                              inc(StrCount[i]);
                              xlSetCellStringValue(ResOtgr.Name, StrCount[i], 27, StrCount[i], 27);
                            }
                          if (RecordExists SpDocs where (( NZakaz.nRec == SpDocs.cDoc and
                                                           word(440)   == SpDocs.TiDk )) = tsOK)
                            dec(StrCount[i]);

                        }
                      if (RecordExists NZakaz where (( SGetDocs.NRec == NZakaz.cDogovor )) = tsOK)
                        inc(StrCount[i]);
                    }
                  5 :
                    {
                      ReRead_Filters_Only(1); //платежи

                      _loop IerDog where (( if(SGetDocs.cAppDogovor = 0, SGetDocs.cDogovor, SGetDocs.cAppDogovor) == IerDog.cUpRec AND
                                            if(SGetDocs.cAppDogovor = 0, SGetDocs.cDogovor, SGetDocs.cAppDogovor) == IerDog.cRec
                                         ))
                        _loop BaseFin where (( BaseFin.cPlPor   == PlPor.nRec    and
                                               PlPor.cPlat      == KatOrgF.nRec  and
                                               PlPor.cPol       == KatOrgT.nRec  and
                                               PlPor.cStepDoc   == StepDoc.NRec  and
                                               StepDoc.cBaseDoc == BaseDoc.NRec ))
                        {
                          if ( ( SGetDocs.wDocType = 1 ) AND ( BaseFin.cAppDogovor <> 0 ) )
                            Continue;

                          _str := GetTiDkGalName(BaseFin.TiDkGal);
                          xlSetCellStringValue(_str           , StrCount[i], 15, StrCount[i], 15);
                          xlSetCellStringValue(BaseFin.NoDoc  , StrCount[i], 16, StrCount[i], 16);
                          _str := chr(39)  + DateToStr(BaseFin.dDoc, 'DD/MM/YYYY ');
                          xlSetCellStringValue(_str           , StrCount[i], 17, StrCount[i], 17);
                          xlSetCellStringValue(KatOrgF.Name   , StrCount[i], 18, StrCount[i], 18);
                          xlSetCellStringValue(KatOrgT.Name   , StrCount[i], 19, StrCount[i], 19);
                          _str := if (PlPor.cStepDoc  = 0, '',
                                        GetVidBaseDocName(BaseDoc.VidDoc) +
                                        ' №  ' + BaseDoc.NoDoc +
                                        ' от ' + DateToStr(BaseDoc.dDoc, 'DD/MM/YY г.') +
                                        ' ' + BaseDoc.Name);
                          xlSetCellStringValue(_str           , StrCount[i], 20, StrCount[i], 20);
                          xlSetCellStringValue(BaseFin.NamePl , StrCount[i], 21, StrCount[i], 21);
                          xlSetCellNumberValue(SumFin         , StrCount[i], 22, StrCount[i], 22);
                          xlSetCellStringValue(ValutFin       , StrCount[i], 23, StrCount[i], 23);
                          inc(StrCount[i]);
                        }
                      if (RecordExists BaseFin = tsOK)
                        dec(StrCount[i]);
                    }
                  6 :
                    {
                      ReRead_Filters_Only(3); //Накладные
                      _loop IerDog where (( if(SGetDocs.cAppDogovor = 0, SGetDocs.cDogovor, SGetDocs.cAppDogovor) == IerDog.cUpRec AND
                                            if(SGetDocs.cAppDogovor = 0, SGetDocs.cDogovor, SGetDocs.cAppDogovor) == IerDog.cRec
                                         ))
                        _loop KatSopr where (( KatSopr.cStepDoc  == StepDoc.NRec and
                                               StepDoc.cBaseDoc  == BaseDoc.NRec and
                                               KatSopr.cNZakaz   == NZakaz.NRec  and
                                               KatSopr.cGruzFrom == KatOrgF.NRec and
                                               KatSopr.cGruzTo   == KatOrgT.NRec ))
                        {
                          if ( ( SGetDocs.wDocType = 1 ) AND ( KatSopr.cAppDogovor <> 0 ) )
                            Continue;

                          _str := GetTiDkGalName(KatSopr.VidSopr);
                          xlSetCellStringValue(_str           , StrCount[i], 15, StrCount[i], 15);
                          xlSetCellStringValue(KatSopr.NSopr  , StrCount[i], 16, StrCount[i], 16);
                          _str := chr(39)  + DateToStr(KatSopr.dSopr, 'DD/MM/YYYY ');
                          xlSetCellStringValue(_str           , StrCount[i], 17, StrCount[i], 17);
                          xlSetCellStringValue(BaseDoc.NoDoc  , StrCount[i], 18, StrCount[i], 18);
                          xlSetCellStringValue(NZakaz.NoDoc   , StrCount[i], 19, StrCount[i], 19);
                          xlSetCellStringValue(KatOrgF.Name   , StrCount[i], 20, StrCount[i], 20);
                          xlSetCellStringValue(KatOrgT.Name   , StrCount[i], 21, StrCount[i], 21);
                          _loop SpSopr where (( KatSopr.nRec  == SpSopr.cSopr
                                            and SpSopr.cVal   == SpVal.nRec
                                            and SpSopr.cOtpEd == KatOtpEd.Nrec
                                            and SpSopr.cMCUsl == KatMC.NRec
                                            and SpSopr.cMCUsl == KatUsl.NRec ))
                            {
                              xlSetCellNumberValue(SpSopr.Npp    , StrCount[i], 22, StrCount[i], 22);
                              _str := if (SpSopr.PrMC = 1,KatMC.Name,KatUsl.Name);
                              xlSetCellStringValue(_str          , StrCount[i], 23, StrCount[i], 23);
                              xlSetCellNumberValue(SpSopr.KolFact, StrCount[i], 24, StrCount[i], 24);
                              xlSetCellStringValue(KatOtpEd.Name , StrCount[i], 25, StrCount[i], 25);
                              var _Price, _Summa : double;
#ifdef _RoundInDoc_
                              _Price := if (KatSopr.cVal = 0, SpSopr.rPrice, SpSopr.rVPrice);
#else
                              _Price := if (KatSopr.cVal = 0, SpSopr.Price, SpSopr.VPrice);
#end
                              xlSetCellNumberValue(_Price        , StrCount[i], 26, StrCount[i], 26);
                              _Summa := if (KatSopr.cVal = 0,SpSopr.Price*SpSopr.kolfact,SpSopr.VPrice*SpSopr.kolfact);
                              xlSetCellNumberValue(_Summa        , StrCount[i], 27, StrCount[i], 27);
                              xlSetCellStringValue(Valut         , StrCount[i], 28, StrCount[i], 28);
                              inc(StrCount[i]);
                            }
                          if (RecordExists SpSopr where (( KatSopr.nRec == SpSopr.cSopr )) = tsOK)
                            dec(StrCount[i]);
                          inc(StrCount[i]);
                        }
                      if (RecordExists KatSopr = tsOK)
                        dec(StrCount[i]);
                    }
                end;
                inc(StrCount[i]);
              }
          end;
        }
    }
  while TreeGetNextEx(trTreeTables);
  xlSetActiveSheetByName(SheetNames[1]);
  xlKillExcel;
  StopVisual('', 0);

  TreePopPos(trTreeTables);
  Save_Format := OldSave_Format;
  ReRead_Filters(true);
  PopPos(CurTabl);
  ReReadRecord(CurTabl);
  ReScanPanel(#SGetDocs);
}

Procedure PrintCurrentRec;
{
  if ((DskParams and vcGetDog_Zamena) <> 0)
    PrintDocumentsZamenView
  else
    PrintDocumentsView;
}