//********************************************************************************
//                                                        (c) корпорация Галактика
// Галактика 5.70 - логистика
// Установка фильтров по дескрипторам и группам для договоров
//
//********************************************************************************

#include EditPos.vpp

//-----------------------------------------------------------------------------

Procedure PopCurrentBounds;
{
  if BoundActive(tbMainBound)
    {
      PopBounds(tbMainBound);
      Exit;
    }

    if BoundActive(tbVscBound)
    {
      PopBounds(tbVscBound);
      Exit;
    }

  if BoundActive(tbVscGrBound)
    {
      PopBounds(tbVscGrBound);
      Exit;
    }

  if BoundActive(tbdMainBound)
    {
      PopBounds(tbdMainBound);
      Exit;
    }

   if BoundActive(tbdVscBound)
    {
      PopBounds(tbdVscBound);
      Exit;
    }

  if BoundActive(tbdVscGrBound)
    {
      PopBounds(tbdVscGrBound);
      Exit;
    }

#ifdef __NIOKR__
  if BoundActive(tbPodrBound)
    {
      PopBounds(tbPodrBound);
      Exit;
    }

  if BoundActive(tbdPodrBound)
    {
      PopBounds(tbdPodrBound);
      Exit;
    }
#end
}

//-----------------------------------------------------------------------------

Procedure SetDescrBounds(bUserFilter: boolean);
{
  if bUserFilter
    {
      fltBegDate := dGetTune('User.dFilter1');
      fltEndDate := dGetTune('User.dFilter2');

      d1 := fltBegDate;
      d2 := fltEndDate;

      IsFilter := boGetTune('User.UseFilter');

      if IsFilter
        {
          if ( fltEndDate <= fltBegDate )
            Message('Фильтр в настройке пользователя некорректен!', Information);
        }
    }

  case CurVisible of
    dcAllModif        // все права
   ,dcOwnModifAllRead // свои - изменение, по всем - чтение
   ,dcGrpModifAllRead:// все права в рамках группы, по всем - чтение
      {
        SetFieldSelectable(#Dogovor.Descr, TRUE);
        SetFieldSelectable(#Dogovor.DesGr, TRUE);
      }
    dcOwnOnly:        // только со своим дескриптором
      {
        SetFieldSelectable(#Dogovor.Descr, FALSE);
        SetFieldSelectable(#Dogovor.DesGr, FALSE);
      }
    dcGrpRead         // свои-изменение, по группе - чтение
   ,dcGrpModif:       // все права в рамках группы
      {
        SetFieldSelectable(#Dogovor.Descr, TRUE);
        SetFieldSelectable(#Dogovor.DesGr, FALSE);
      }
  end;

  PopCurrentBounds;
  SetOrder(tiOrd_dDoc);

#ifdef __NIOKR__
  if ( wGetTune('Dogovor.Visible') = 1 )
    {
      if ( IsFilter )
        PushBounds(tbdPodrBound);
      else
        PushBounds(tbPodrBound);


      SetFilterBounds;

      CurOrdBnd := tiOrd_dDoc;

      Exit;
    }
#end
  case CurVisible of

    dcAllModif, dcOwnModifAllRead, dcGrpModifAllRead :
      {
        if ( IsFilter )
          PushBounds(tbdMainBound);
        else
          {
            PushBounds(tbMainBound);
            SetOrder(CurOrdBnd);
          }
      }

    dcOwnOnly :
      {
        if ( IsFilter )
          PushBounds(tbdVscBound);
        else
          PushBounds(tbVscBound);
      }

    dcGrpRead, dcGrpModif :
      {
        if ( IsFilter )
          PushBounds(tbdVscGrBound);
        else
          PushBounds(tbVscGrBound);
      }

  end; // case

  SetFilterBounds;

  case CurVisible of

    dcOwnOnly, dcGrpRead, dcGrpModif :
      CurOrdBnd := tiOrd_dDoc;

  else
    if IsFilter
      CurOrdBnd := tiOrd_dDoc;

  end;
}
