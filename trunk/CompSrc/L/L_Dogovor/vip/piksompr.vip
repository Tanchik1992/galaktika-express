/*
 ╔═══════════════════════════════════════════════════════════════════════════╗
 ║                     (c) 1994,97 корпорация ГАЛАКТИКА                      ║
 ║ Проект        : ГАЛАКТИКА                                                 ║
 ║ Система       : Оперативный контур                                        ║
 ║ Версия        : 5.70                                                      ║
 ║ Назначение    : Перегон выбора из маркера в Pick для GetSomKatNazna       ║
 ║ Ответственный : Глушаков Дмитрий Сергеевич                                ║
 ║ Параметры     : есть                                                      ║
 ║    OkOne      // TRUE - груповой выбор FALSE - только один (+возвращает)  ║
 ║    pKlProg    // входной-выходной - KatNazna.nRec                         ║
 ╚═══════════════════════════════════════════════════════════════════════════╝
*/

#doc
Интерфейс сохранения выбора назначений из маркера в Pick
#end
Interface PickSomKlProg 'Выбор назначений' DoAccept,EscClose,cyan;
  show at (4,2,76,23);
Create View
var
//--параметры
  OkOne        : boolean;
  pKlProg      : comp;
//--
  Marker         : longint; // указатель на маркер
  MarkerCount, i : longint; // счетчики записей в маркере
  pickrec        : comp;
As select
  Pick.wList
from
  Pick
where
  ((
    word(446) == Pick.wList
  ))
;
Parameters
  OkOne,   // TRUE - груповой выбор FALSE - только один/ ЕСЛИ возвращает TRUE-помечены все распоряжения!
  pKlProg  // входной-выходной - KatNazna.nRec
  ;

HandleEvent
cmInit    :
{
var ret:word;
  Marker:=InitMarker('MKatNazna',8,100,10);
  ClearMarker(Marker);
  _loop Pick
  {
    InsertMarker(Marker,Pick.cRec);
  }
  DoneMarker(Marker,'MKatNazna');

  if RunInterface('GetSomKatNazna',
                  OkOne,   // TRUE - груповой выбор FALSE - только один/ ЕСЛИ возвращает TRUE-помечены все распоряжения!
                  pKlProg  // входной-выходной - KatNazna.nRec
                  ) = cmCancel  // если там Cancel - то и здесь Cancel
    {
    Abort;
    Exit;
    }
  delete safe Pick;
  StartNewVisual(vtRotateVisual, vfTimer,
               ''#3'Формирование элементов выбора ...'#13#3, 1);
  Marker:=InitMarker('MKatNazna',8,100,10);
  MarkerCount:=GetMarkerCount(Marker);
  FOR ( i:=0; i<MarkerCount; i:=i+1 )
    {
    if GetMarker(Marker,i,pickrec)
      {
      Pick.wList := 446;
      Pick.PickKol:=i;
      Pick.cRec:=pickrec;
      insert current Pick;
      }
    NextVisual;
    }
  DoneMarker(Marker,'MKatNazna');
  StopVisual('',0);
  PutCommand(cmDefault);
}
end;  // HandleEvent
end.  // Interface