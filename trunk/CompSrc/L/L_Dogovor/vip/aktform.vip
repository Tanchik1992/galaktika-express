//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 5.70 - логистика
// Интерфейс автоматического формирования актов сверки
//
//******************************************************************************

#doc
Интерфейс автоматического формирования актов сверки
#end
Interface AktForm 'Автоформирование акта сверки' (, hci_dogov_Avtoform_Akt_Sverki, ) escClose, gray; // cyan;

  Show at (6, 3, 74, 16);

Create view

Var

  pAktSver
            : comp;

  wDO
, wSopr
, wPlat
, wSF
, wAvans
, wDokument
            : word;

  d1
, d2
, DO_d1
, DO_d2
, Sopr_d1
, Sopr_d2
, Plat_d1
, Plat_d2
            : Date;

  bFormir
            : boolean;

Select
  AktSver.*

From
  AktSver (Normal)

Where
((
  pAktSver  ==  AktSver.NRec
))
;

Parameters
  pAktSver  // ссылка на акт сверки
;

#include aktfun.vpp


//******************************************************************************

Panel pAktSverForm;

Screen scAktSverForm
  Table AktSver;

Fields
//  wDokument   ('Выбор документов для формирования акта сверки', , sci1Esc) : NoProtect;
  wDO         ('Выбор документов для формирования акта сверки', , sci1Esc) : NoProtect;
  DO_d1       ('Начало периода для документов-оснований')                  : [10, 'DD/MM/YYYY'], NoProtect;
  DO_d2       ('Конец периода для документов-оснований')                   : [10, 'DD/MM/YYYY'], NoProtect;
  wSopr       ('Выбор документов для формирования акта сверки', , sci1Esc) : NoProtect;
  Sopr_d1     ('Начало периода для сопроводительных документов')           : [10, 'DD/MM/YYYY'], NoProtect;
  Sopr_d2     ('Конец периода для сопроводительных документов')            : [10, 'DD/MM/YYYY'], NoProtect;
  wSF         ('Выбор документов для формирования акта сверки', , sci1Esc) : NoProtect;
  wPlat       ('Выбор документов для формирования акта сверки', , sci1Esc) : NoProtect;
  Plat_d1     ('Начало периода для платежных документов')                  : [10, 'DD/MM/YYYY'], NoProtect;
  Plat_d2     ('Конец периода для платежных документов')                   : [10, 'DD/MM/YYYY'], NoProtect;
  wAvans      ('Выбор документов для формирования акта сверки', , sci1Esc) : NoProtect;

Buttons
  cmValue1, Default, , 'Сформировать акт сверки', , sci1Esc;
  cmCancel, , , 'Отмена формирования акта сверки', , sci1Esc;

<<

   [.] документы-основания       `  с .@@@@@@@@@@ по .@@@@@@@@@@@

   [.] сопроводительные документы`  с .@@@@@@@@@@ по .@@@@@@@@@@@
       (.) все                        `
       (.) только со счетами-фактурами`

   [.] платежные документы       `  с .@@@@@@@@@@ по .@@@@@@@@@@@
       (.) все                              `
       (.) только распределенные и авансовые`

              <.~С~формировать.>      <.~О~тмена.>

>>
end; // Screen scAktSverForm

//******************************************************************************

HandleEvent // Panel pAktSverForm

cmCheckField :
{
  SetFieldSelectable(#DO_d1  , wDO = 1);
  SetFieldSelectable(#DO_d2  , wDO = 1);
  SetFieldSelectable(#Sopr_d1, wSopr = 1);
  SetFieldSelectable(#Sopr_d2, wSopr = 1);
  SetFieldSelectable(#Plat_d1, wPlat = 1);
  SetFieldSelectable(#Plat_d2, wPlat = 1);

  case CurField of
    #DO_d1, #DO_d2 :
      {
        if (CurField = #DO_d2)
          if (AktSver.dDoc < DO_d2)
            {
              Message('Дата окончания периода для документов-оснований не должна превышать дату акта сверки' +
                      DateToStr(AktSver.dDoc, ' DD/MM/YYYY.'), Warning);
              Abort;
              Exit;
            }

        if (DO_d2 < DO_d1)
          {
            Message('Дата начала периода для документов-оснований не должна превышать дату окончания периода.', Warning);
            Abort;
            Exit;
          }
      }
    #Sopr_d1, #Sopr_d2 :
      {
        if (CurField = #Sopr_d2)
          if (AktSver.dDoc < Sopr_d2)
            {
              Message('Дата окончания периода для сопроводительных документов не должна превышать дату акта сверки' +
                      DateToStr(AktSver.dDoc, ' DD/MM/YYYY.'), Warning);
              Abort;
              Exit;
            }

        if (Sopr_d2 < Sopr_d1)
          {
            Message('Дата начала периода для сопроводительных документов не должна превышать дату окончания периода.', Warning);
            Abort;
            Exit;
          }
      }
    #Plat_d1, #Plat_d2 :
      {
        if (CurField = #Plat_d2)
          if (AktSver.dDoc < Plat_d2)
            {
              Message('Дата окончания периода для платежных документов не должна превышать дату акта сверки' +
                      DateToStr(AktSver.dDoc, ' DD/MM/YYYY.'), Warning);
              Abort;
              Exit;
            }

        if (Plat_d2 < Plat_d1)
          {
            Message('Дата начала периода для платежных документов не должна превышать дату окончания периода.', Warning);
            Abort;
            Exit;
          }
      }
  end;
}

end; // HandleEvent Panel pAktSverForm

end; // Panel pAktSverForm

//******************************************************************************

HandleEvent // Interface AktForm

cmInit :
{
  bFormir := false;

  wDO     := 1;
  wSopr   := 1;
  wSF     := 0;
  wPlat   := 1;
  wAvans  := 0;

  DO_d1   := AktSver.dBeg;
  DO_d2   := AktSver.dEnd;
  Sopr_d1 := AktSver.dBeg;
  Sopr_d2 := AktSver.dEnd;
  Plat_d1 := AktSver.dBeg;
  Plat_d2 := AktSver.dEnd;
}

cmValue1 :
{
  bFormir   := true;
  wDokument := wDO + 2 * wSopr + 4 * wPlat + 8 * wSF + 16 * wAvans;

  AutoFormAktSver(pAktSver, wDokument, DO_d1, DO_d2, Sopr_d1, Sopr_d2, Plat_d1, Plat_d2);

  CloseInterface(cmDefault);
}

end; // HandleEvent Interface AktForm

End. // Interface AktForm