//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Логистика - ДО
// автоинкрементация по SetDefault документа
//******************************************************************************
{
  BaseDoc.dForm    := Cur_Date;
  BaseDoc.Descr    := CurDescr;
  BaseDoc.DesGr    := CurDesGr;
  BaseDoc.dDoc     := Cur_Date;
  BaseDoc.YearDoc  := Year(BaseDoc.dDoc);
  BaseDoc.KolDn    := wGetTune('Oper.SrokSch');
  BaseDoc.dEnd     := Add_Day(BaseDoc.dDoc, BaseDoc.KolDn);
  BaseDoc.Prior    := '99';
  BaseDoc.VhSkid   := 1;
  BaseDoc.FilialNo := lGetTune('Konsolid.FilialNo');

  if (coGetTune('MyBank') <> 0)
    BaseDoc.cMyBank := coGetTune('MyBank')
  else
    if (GetFirst FastFirstRow PickBank where (( coGetTune('MyOrg') == PickBank.cOrg )) ordered by PickBank.Name = tsOk)
      BaseDoc.cMyBank := PickBank.NRec
    else
      BaseDoc.cMyBank := 0;

  case tipDo of
  //-----------
    101:
      {
        BaseDoc.TipMoney := wGetTune('Doc.Buy.Val');
        BaseDoc.VhodNal  := if(wGetTune('Doc.Buy.Nalog') = 1, 1, 2);
        BaseDoc.Status   := if(wGetTune('Doc.Buy.Status') = 0, 2, 1);
        BaseDoc.cOtvPodr := DefaultBaseCO;

        if (coGetTune('Doc.Buy.cPayment') <> 0)
          BaseDoc.cPayment := coGetTune('Doc.Buy.cPayment');
      }
  //-----------
    111:
      {
        BaseDoc.TipMoney := wGetTune('Doc.Buy.ValPO');
        BaseDoc.VhodNal  := if(wGetTune('Doc.Buy.Nalog') = 1, 1, 2);
        BaseDoc.Status   := if(wGetTune('Doc.Buy.StatusPO') = 0, 2, 1);
        BaseDoc.cOtvPodr := DefaultBaseCO;

        if (coGetTune('Doc.Buy.cPaymentPO') <> 0)
          BaseDoc.cPayment := coGetTune('Doc.Buy.cPaymentPO');
      }
  //-----------
    510,
    201:
      {
        BaseDoc.TipMoney      := wGetTune('Doc.Sell.Val');
        BaseDoc.VhodNal       := if(wGetTune('Doc.Sell.Nalog') = 1, 1, 2);
        BaseDoc.Status        := if(wGetTune('Doc.Sell.Status') = 0, 2, 1);
        BaseDoc.wCursForPlPor := wGetTune('Oper.Sell.DO.CursForPlPor');

        if (tipDo <> 510)
          BaseDoc.cOtvPodr := DefaultBaseCO;

        if (coGetTune('Doc.Sell.cPayment') <> 0)
          BaseDoc.cPayment := coGetTune('Doc.Sell.cPayment');
      }
  //-----------
    211:
      {
        BaseDoc.TipMoney      := wGetTune('Doc.Sell.ValPO');
        BaseDoc.VhodNal       := if(wGetTune('Doc.Sell.Nalog') = 1, 1, 2);
        BaseDoc.Status        := if(wGetTune('Doc.Sell.StatusPO') = 0, 2, 1);
        BaseDoc.wCursForPlPor := wGetTune('Oper.Sell.PredOplDO.CursForPlPor');

        if (coGetTune('Doc.Sell.cPaymentPO') <> 0)
          BaseDoc.cPayment := coGetTune('Doc.Sell.cPaymentPO');
      }
  //-----------
    102:
      {
        BaseDoc.TipMoney := wGetTune('Doc.PrCons.Val');
        BaseDoc.VhodNal  := if(wGetTune('Doc.PrCons.Nalog') = 1, 1, 2);
        BaseDoc.Status   := if(wGetTune('Doc.PrCons.Status') = 0, 2, 1);
        if (coGetTune('Doc.PrCons.cPayment') <> 0)
          BaseDoc.cPayment := coGetTune('Doc.PrCons.cPayment');
      }
  //-----------
    202:
      {
        BaseDoc.TipMoney := wGetTune('Doc.OtpCons.Val');
        BaseDoc.VhodNal  := if(wGetTune('Doc.OtpCons.Nalog') = 1, 1, 2);
        BaseDoc.Status   := if(wGetTune('Doc.OtpCons.Status') = 0, 2, 1);
        if (coGetTune('Doc.OtpCons.cPayment') <> 0)
          BaseDoc.cPayment := coGetTune('Doc.OtpCons.cPayment');
      }
  //-----------
    1410:
      {
        BaseDoc.TipMoney := wGetTune('Doc.Wood.NarAkt.Val');
        BaseDoc.VhodNal  := if(wGetTune('Doc.Wood.NarAkt.Nalog') = 1, 1, 2);
        BaseDoc.Status   := if(wGetTune('Doc.Wood.NarAkt.Status') = 0, 2, 1);
        BaseDoc.cOtvPodr := DefaultBaseCO;

        if (coGetTune('Doc.Wood.NarAkt.cPayment') <> 0)
          BaseDoc.cPayment := coGetTune('Doc.Wood.NarAkt.cPayment');
      }
  //-----------
    1420:
      {
        BaseDoc.TipMoney := wGetTune('Doc.Wood.Schet.Val');
        BaseDoc.VhodNal  := if(wGetTune('Doc.Wood.Schet.Nalog') = 1, 1, 2);
        BaseDoc.Status   := if(wGetTune('Doc.Wood.Schet.Status') = 0, 2, 1);

        if (coGetTune('Doc.Wood.Schet.cPayment') <> 0)
          BaseDoc.cPayment := coGetTune('Doc.Wood.Schet.cPayment');
      }
  //-----------
  end;


  BaseDoc.cVal := if (BaseDoc.TipMoney =1, 0 , coGetTune('BaseCurrency'));

  StepDoc.Status := BaseDoc.Status;

  case BaseDoc.VidDoc of
  //--------------------
#ifdef __vschet__
    201,
    1420,
#end
  //--------------------
    101
  , 102
  , 111
  , 1410
  , 202
  , 211:
      if NOT CheckAndSetCurrency
        {
          Abort;
          Exit;
        }
  //--------------------
  end;


  if (BaseDoc.Direct <> 1) // закупка
    case BaseDoc.VidDoc of
    //--------------------
      101
    , 102
    , 1410
    , 111: BaseDoc.NoDoc := '000000';
    //--------------------
    end;

  if (BaseDoc.Direct = 1) // продажа
    {
      BaseDoc.cNalog := wGetTune('Doc.SkidkaDo'); //принцип формирования скидки для ДО

      if ((tipDo = 101) OR (tipDo = 201) OR (tipDo = 211) OR (tipDo = 1420))
        if (( coGetTune('Doc.cCurBaseCO') <> 0 ) AND ( BaseDoc.cOtvPodr = 0 ))
          BaseDoc.cOtvPodr := coGetTune('Doc.cCurBaseCO');
    }

  if (boGetTune('FN.isNumStatDoc') AND (tipDo = 201 OR tipDo = 1420 OR tipDo = 211) AND IsValid(tnStatDoc))
    {
      if (tipDo = 201)
        BaseDoc.Nodoc := GetLastNumD_(101, 'Категория ('+StatDoc.StatCode+')');

      if (tipDo = 1420)
        BaseDoc.Nodoc := GetLastNumD_(1410, 'Категория (' + StatDoc.StatCode + ')');

      if (tipDo = 211)
        BaseDoc.Nodoc := GetLastNumD_(111 , 'Категория ('+StatDoc.StatCode+')');
    }
  else
    if (boGetTune('FN.isNumOtvPodr') AND ((tipDo = 101) OR (tipDo = 201) OR (tipDo = 211)))
      {
        case tipDo of
          101: if boGetTune('Oper.Buy.DO.AvtoNum') BaseDoc.Nodoc := GetLastNumD_(100, string(BaseDoc.cOtvPodr));
          201: BaseDoc.Nodoc := GetLastNumD_(101, string(BaseDoc.cOtvPodr));
          211: BaseDoc.Nodoc := GetLastNumD_(111, string(BaseDoc.cOtvPodr));
         1420: BaseDoc.Nodoc := GetLastNumD_(1410, string(BaseDoc.cOtvPodr));
        end;
      }
    else
      if (wGetTune('FN.AvtoNum') = 1)  // инкрементация по БД
        {
          PushPos(#BaseDoc);
          ResetBounds(#BaseDoc);

          if ((GetLast BaseDoc
                  where (( tipdo          == BaseDoc.VidDoc AND
                           Year(Cur_Date) == BaseDoc.YearDoc )) ordered by BaseDoc.NoDoc) = tsOk)
            {
              StrNum := BaseDoc.Nodoc;
              NextNumStr(StrNum);
            }
          else
            {
              if (tipDo = 201 OR tipDo = 1420)
                {
                  var lNoDoc : word;

                  if (tipDo = 201)
                    lNoDoc := if (ExistTune('Doc.Sell.DO.NoDoc'), wGetTune('Doc.Sell.DO.NoDoc'), 6);

                  if (tipDo = 1420)
                    lNoDoc := if (ExistTune('Doc.Wood.Schet.NoDoc'), wGetTune('Doc.Wood.Schet.NoDoc'), 6);

                  StrNum := LPadCh('1', '0', lNoDoc);
                }
              else
                StrNum := '000001';
            }

          SetBounds(#BaseDoc);
          PopPos(#BaseDoc);
          BaseDoc.NoDoc := StrNum;
        }
      else
        {
          case tipDo of
          //-----------
            101: if boGetTune('Oper.Buy.DO.AvtoNum') BaseDoc.Nodoc := GetLastNumD(100);
            201: BaseDoc.Nodoc := GetLastNumD(101);
            202: BaseDoc.Nodoc := GetLastNumD(102);
            211: BaseDoc.Nodoc := GetLastNumD(111);
            501: BaseDoc.Nodoc := GetLastNumD(103);
            502: BaseDoc.Nodoc := GetLastNumD(105);
            520: BaseDoc.Nodoc := GetLastNumD(215);
            510: BaseDoc.Nodoc := GetLastNumD(104);
            550: BaseDoc.Nodoc := GetLastNumD(550);
           1420: BaseDoc.Nodoc := GetLastNumD(1410);
          //-----------
          end;//case
        }
}
