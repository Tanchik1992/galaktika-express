//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Логистика - ДО
// Пересортировка спецификации ДО по МЦ
//******************************************************************************

{ 
  if (CurTable <> #BaseDoc)
    exit;

  if (GetFirst KatSopr where ((StepDoc.Nrec == KatSopr.cStepDoc)) = tsOk)
    {
      Message('Пересортировка недоступна при наличии накладных по документу!', Information);
      Exit;
    }

  if (not IsValid(#SpStep)) 
    Exit;

  if (message('Пересортировать спецификацию по '+ if (wGetTune('Pick.SortMC') = 0,
              'наименованию?', 'номенклатурному номеру?'), YesNo) <> yes)
    Exit;

  delete all NoVisual tmpReeN;

  var NumRec: longint;  NumRec := 0;

  StartNewVisual(vtNumericVisual, vfTimer+vfBreak+vfConfirm, 'Обработка спецификации: ', 1);

  PushPos(#SpStep);

  _LOOP SpStep 
    {
      if not NextVisual 
        {
          NumRec := 0;
          Break;
        }

      insert tmpReen
        set tmpreen.dSopr := if (SpStep.PrMC = 1, longint(0), longint(1)),
            tmpreen.cRec  := SpStep.Nrec,
            tmpreen.Nomer := if (wGetTune('Pick.SortMC') = 0,
                               if (SpStep.PrMC = 1, KatMC.Name, KatUsl.Name),
                               if (SpStep.PrMC = 1, KatMC.BarKod, KatUsl.Kod));
      NumRec := NumRec + 1;
    }

  StopVisual ( '', 0 );

  if NumRec > 0 
    {
      StartNewVisual(vtIndicatorVisual, vfTimer, 'Сортировка спецификации...', NumRec);
      
      NumRec := 0;
      _LOOP tmpreen ordered by index tmpreen02
        {
          NextVisual;
          Inc(NumRec);
          update NoVisual SpStep where ((tmpReeN.cRec == SpStep.Nrec))
            set SpStep.Npp := NumRec;
        }

      StopVisual ( '', 0 );
    }

  RereadRecord(#BaseDoc);
  PopPos(#SpStep);
  RescanPanel(#SpStep);
}
