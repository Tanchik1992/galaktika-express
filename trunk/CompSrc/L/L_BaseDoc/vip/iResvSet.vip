//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Логистика
// Отчет о резерве по ДО - параметры
//******************************************************************************

#doc
Настройка отчета о резерве по ДО
#end
Interface iReservSetup 'Параметры отчета о резерве по ДО' ( , hcKuzbassIRepNastrList, ) DoAccept, EscClose, Cyan;
  Show at (0, 5, 66, 25);

Create view
var
  NReport: word;
  pNRec: comp;

As select
//------------------------------------------------------------------------------
  if ( UserDeskRep.ResWord[3] = 0
     , UserDeskRep.ResDate[1]
     , Cur_Date
     )
    ( FieldName = RepDate )
//------------------------------------------------------------------------------
, if ( UserDeskRep.ResComp[1] <> 0
     , GroupMC.Name
     , if (UserDeskRep.ResWord[31] > 1, 'множественный выбор', '')
     )
    ( FieldName = fGrMC )
//------------------------------------------------------------------------------
, if ( UserDeskRep.ResComp[2] <> 0
     , KatMC.Name
     , if (UserDeskRep.ResWord[32] > 1, 'множественный выбор', '')
     )
    ( FieldName = fMC )
//------------------------------------------------------------------------------
, if ( UserDeskRep.ResComp[3] <> 0
     , KatPodr.Name
     , if (UserDeskRep.ResWord[33] > 1, 'множественный выбор', '')
     )
    ( FieldName = fSklad )
//------------------------------------------------------------------------------
, if ( UserDeskRep.ResComp[4] <> 0
     , KatMol.Name
     , if (UserDeskRep.ResWord[34] > 1, 'множественный выбор', '')
     )
    ( FieldName = fMOL )
//------------------------------------------------------------------------------
, if ( UserDeskRep.ResComp[5] <> 0
     , KatParty.Name
     , if (UserDeskRep.ResWord[35] > 1, 'множественный выбор', '')
     )
    ( FieldName = fParty )
//------------------------------------------------------------------------------
, if ( UserDeskRep.ResComp[6] <> 0
     , KatOrg.Name
     , if (UserDeskRep.ResWord[36] > 1, 'множественный выбор', '')
     )
    ( FieldName = fKontr )
//------------------------------------------------------------------------------
, if ( UserDeskRep.ResComp[7] <> 0
     , sGetTuneEx('User.Descr', UserOfficeFilial(UserDeskRep.ResComp[7]), UserDeskRep.ResComp[7])
     , if (UserDeskRep.ResWord[37] > 1, 'множественный выбор', '')
     )
    ( FieldName = fDescr )
//------------------------------------------------------------------------------
, if ( UserDeskRep.ResComp[8] <> 0
     , GrDescr.DesGr
     , if (UserDeskRep.ResWord[38] > 1, 'множественный выбор', '')
     )
    ( FieldName = fDesGr )
//------------------------------------------------------------------------------
, if ( UserDeskRep.ResComp[9] <> 0
     , f_sNumDate(BaseDoc.NoDoc, BaseDoc.dDoc)
     , if (UserDeskRep.ResWord[39] > 1, 'множественный выбор', '')
     )
    ( FieldName = fDO )
//------------------------------------------------------------------------------
, *
from
  GroupMC
, KatMC
, KatPodr
, KatMol
, KatParty
, KatOrg
, GrDescr
, StepDoc
, BaseDoc
, Pick
, PickRep
, UserDeskRep
, RepGrLst
, RepGrOne
, RepGroup
, KatOrgDescr

Where
((
  // Настройка отчета
       UserName                 == UserDeskRep.OwnName
  AND  NReport                  == UserDeskRep.NRep
       // Фильтрация
  AND  UserDeskRep.ResComp[1]   == GroupMC.NRec
  AND  UserDeskRep.ResComp[2]   == KatMC.NRec
  AND  UserDeskRep.ResComp[3]   == KatPodr.NRec
  AND  UserDeskRep.ResComp[4]   == KatMol.NRec
  AND  UserDeskRep.ResComp[5]   == KatParty.NRec
  AND  UserDeskRep.ResComp[6]   == KatOrg.NRec
  AND  UserDeskRep.ResComp[8]   == GrDescr.NRec
  AND  UserDeskRep.ResComp[9]   == StepDoc.NRec
  AND  StepDoc.cBaseDoc         == BaseDoc.NRec
       // Группировка
  AND  UserDeskRep.ResComp[21]  == RepGrLst.NRec
  AND  RepGrLst.NRec            == RepGrOne.cRepGrLst
  AND  RepGrOne.cRec            == RepGroup.NRec
))
;

//******************************************************************************

Parameters
  pNRec         // NRec выбранной настройки
;

//******************************************************************************

#include pmarker.vpp

//******************************************************************************

#doc
Окно установки параметров отчета о резерве по ДО
#end
Window winParamResDO 'Резерв по ДО' EscClose, Gray;
  Show at (, , 67, 20);

//******************************************************************************

Panel panParam;
  Table UserDeskRep;

Screen sc0sel  (, hcSellM1SeeRezervDO, sci1378Esc)

Fields
//------------------------------------------------------------------------------
  UserDeskRep.RepName ('Наименование отчета', , sci178Esc): NoProtect,
    {Font = {BackColor = if (UserDeskRep.RepName = '', ColorNeed, 0)}};
//------------------------------------------------------------------------------
  UserDeskRep.ResWord[3] ('Формировать отчет на дату', , ):
    [LIST 0 'указанную дату', 'текущую дату'], Protect;
//------------------------------------------------------------------------------
  RepDate ('Укажите дату формирования отчета', , ): [, 'DD/MM/YYYY'], NoProtect;
  UserDeskRep.ResWord[2] ('Вывод только просроченных на дату формирования отчета резервов', , sci178Esc): NoProtect;
  UserDeskRep.ResWord[1] ('Установите фильтры', , sci178Esc): NoProtect;
  fGrMC  ('Установите фильтр по группе МЦ', , )          : PickButton, Protect;
  fMC    ('Установите фильтр по номенклатуре', , )       : PickButton, Protect;
  fSklad ('Установите фильтр по складу', , )             : PickButton, Protect;
  fMOL   ('Установите фильтр по МОЛ', , )                : PickButton, Protect;
  fParty ('Установите фильтр по партии', , )             : PickButton, Protect;
  fKontr ('Установите фильтр по контрагенту', , )        : PickButton, Protect;
  fDescr ('Установите фильтр по дескриптору', , )        : PickButton, Protect;
  fDesGr ('Установите фильтр по группе дескрипторов', , ): PickButton, Protect;
  fDO    ('Установите фильтр по ДО', , )                 : PickButton, Protect;
  RepGrLst.Name ('Группировка документов', , )           : PickButton, Protect;
  UserDeskRep.ResName[3] ('Содержание группировки документов', , sci178Esc): Skip;
//------------------------------------------------------------------------------
<<

`Название: `.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

`Сформировать отчет на: ` .@@@@@@@@@@@@@@@@ .@@@@@@@@@@

  [.] только просроченные резервы`

`Фильтр: `
  [.] Группа МЦ          ` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
  [.] Матценность        ` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
  [.] Склад              ` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
  [.] МОЛ                ` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
  [.] Партия             ` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
  [.] Контрагент         ` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
  [.] Дескриптор         ` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
  [.] Группа дескрипторов` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
  [.] ДО                 ` .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@

`Порядок группировки документов:`.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
.@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
>>
end; // Screen sc0sel


//******************************************************************************

Procedure SetFields;
var
  wMask: word;
{
  SetFieldVisible(#fGrMC    , (UserDeskRep.ResWord[1] AND   1) > 0);
  SetFieldVisible(#fMC      , (UserDeskRep.ResWord[1] AND   2) > 0);
  SetFieldVisible(#fSklad   , (UserDeskRep.ResWord[1] AND   4) > 0);
  SetFieldVisible(#fMOL     , (UserDeskRep.ResWord[1] AND   8) > 0);
  SetFieldVisible(#fParty   , (UserDeskRep.ResWord[1] AND  16) > 0);
  SetFieldVisible(#fKontr   , (UserDeskRep.ResWord[1] AND  32) > 0);
  SetFieldVisible(#fDescr   , (UserDeskRep.ResWord[1] AND  64) > 0);
  SetFieldVisible(#fDesGr   , (UserDeskRep.ResWord[1] AND 128) > 0);
  SetFieldVisible(#fDO      , (UserDeskRep.ResWord[1] AND 256) > 0);
  SetFieldVisible(#RepDate  , UserDeskRep.ResWord[3] = 0);

  wMask := 0;
  // ДО
  if ((UserDeskRep.ResWord[1] AND 256) = 256)
    wMask := Word(0FFh);
  else
    {
      if ((UserDeskRep.ResWord[1] AND Word(0FFh)) >= 1)
        wMask := Word(0100h);

      // Группа МЦ
      if ((UserDeskRep.ResWord[1] AND 1) = 1)
        wMask := wMask OR Word(2);
      else
        // МЦ
        if ((UserDeskRep.ResWord[1] AND 2) = 2)
          wMask := wMask OR Word(1);
    }

  ClusterDisabledField(#UserDeskRep.ResWord[1], wMask);
}

//******************************************************************************

HandleEvent

cmExprFieldChanged: // меняем вычисляемое поле
  if (CurField = #RepDate)
    set UserDeskRep.ResDate[1] := StrToDate(ExprFieldValue, 'DD/MM/YYYY'); // запоминаем значение SelKol

cmSetDefault:
  {
    UserDeskRep.RepName    := 'Без имени';
    UserDeskRep.dRep       := Cur_Date;
    UserDeskRep.ResDate[1] := Cur_Date;
    ClusterDisabledField(#UserDeskRep.ResWord[1], 0);
  }

cmInsertRecord:
  Insert current UserDeskRep;

cmDeleteRecord:
  if (Message('Удалить текущую настройку?', YesNo + mfSwapButtons) <> cmYes)
    Abort;
  else
    Delete current UserDeskRep;

cmUpdateRecord:
  Update current UserDeskRep;

cmPositionChanged:
  SetFields;

cmCheckField:
  case CurField of
  //--------------
    #UserDeskRep.ResWord[1]
  , #UserDeskRep.ResWord[3]:
      SetFields;
  //--------------
    #UserDeskRep.ResDate[1]:
      if (LongInt(UserDeskRep.ResDate[1]) = 0)
        {
          Message('Необходимо указать дату!', Warning);
          Abort;
          Exit;
        }
  //--------------
  end;

cmPick:
  {
    var NumPick: word;

    case CurField of
    //------------------------------------------------------------------------------
      #fGrMC:
        {
          UpdateTable;
          DelPick(Word(2));
          PickRep2Pick(NReport*10+1, Word(2), UserDeskRep.ResComp[1]);
          if (RunInterface('GetGrMCS', comp(0), '', TRUE) <> cmCancel)
            {
              DelPickRep(NReport*10+1);
              Pick2PickRep(Word(2), NReport*10+1, UserDeskRep.ResComp[1], NumPick);
              if (NumPick > 1)
                Set UserDeskRep.ResWord[31] := NReport*10+1
              else
                Set UserDeskRep.ResWord[31] := 0;
            }

        }
    //------------------------------------------------------------------------------
      #fMC:
        {
          UpdateTable;
          DelPick(Word(7));
          PickRep2Pick(NReport*10+2, Word(7), UserDeskRep.ResComp[2]);
          if (RunInterface('GetSomeMC', Comp(-12347)) <> cmCancel)
            {
             DelPickRep(NReport*10+2);
             Pick2PickRep(Word(7), NReport*10+2, UserDeskRep.ResComp[2], NumPick);
             if (NumPick > 1)
               Set UserDeskRep.ResWord[32] := NReport*10+2
             else
               Set UserDeskRep.ResWord[32] := 0;

             RescanPanel(#UserDeskRep);
            }
        }
    //------------------------------------------------------------------------------
      #fSklad:
        {
          UpdateTable;
          DelPick(Word(11));
          PickRep2Pick(NReport*10+3, Word(11), UserDeskRep.ResComp[3]);
          if (RunInterface('GetSomeSklad') <> cmCancel)
            {
              DelPickRep(NReport*10+3);
              Pick2PickRep(Word(11), NReport*10+3, UserDeskRep.ResComp[3], NumPick);
              if (NumPick > 1)
                Set UserDeskRep.ResWord[33] := NReport*10+3
              else
                Set UserDeskRep.ResWord[33] := 0;

              RescanPanel(#UserDeskRep);
            }
        }
    //------------------------------------------------------------------------------
      #fMOL:
        {
          UpdateTable;
          DelPick(Word(8));
          PickRep2Pick(NReport*10+4, Word(8), UserDeskRep.ResComp[4]);
          if (RunInterface('GetSomeMOL') <> cmCancel)
            {
              DelPickRep(NReport*10+4);
              Pick2PickRep(Word(8), NReport*10+4, UserDeskRep.ResComp[4], NumPick);
              if (NumPick > 1)
                Set UserDeskRep.ResWord[34] := NReport*10+4
              else
                Set UserDeskRep.ResWord[34] := 0;

              RescanPanel(#UserDeskRep);
            }
        }
    //------------------------------------------------------------------------------
      #fParty:
        {
          UpdateTable;
          DelPick(Word(10));
          PickRep2Pick(NReport*10+5, Word(10), UserDeskRep.ResComp[5]);
          if (RunInterface('GetSomeParty', Word(0)) <> cmCancel)
            {
              DelPickRep(NReport*10+5);
              Pick2PickRep(Word(10), NReport*10+5, UserDeskRep.ResComp[5], NumPick);
              if (NumPick > 1)
                Set UserDeskRep.ResWord[35] := NReport*10+5
              else
                Set UserDeskRep.ResWord[35] := 0;

              RescanPanel(#UserDeskRep);
            }
        }
    //------------------------------------------------------------------------------
      #fKontr:
        {
          UpdateTable;
          PickRep2Marker('KatOrg', NReport*10+6, UserDeskRep.ResComp[6]);
          if (RunInterface('GetSomeKontrPrim', FALSE) <> cmCancel)
            {
              DelPickRep(NReport*10+6);
              Marker2PickRep('KatOrg', NReport*10+6, UserDeskRep.ResComp[6], NumPick);
              if (NumPick > 1)
                Set UserDeskRep.ResWord[36] := NReport*10+6
              else
                Set UserDeskRep.ResWord[36] := 0;
            }
        }
    //------------------------------------------------------------------------------
      #fDescr:
        {
          UpdateTable;
          PickRep2Marker('UserTuneDescr', NReport*10+7, UserDeskRep.ResComp[7]);
          if (RunInterface('GetSomeDescr', TRUE) <> cmCancel)
            {
              DelPickRep(NReport*10+7);
              Marker2PickRep('UserTuneDescr', NReport*10+7, UserDeskRep.ResComp[7], NumPick);
              if (NumPick > 1)
                Set UserDeskRep.ResWord[37] := NReport*10+7
              else
                {
                  Set UserDeskRep.ResWord[37] := 0;
                  UserDeskRep.ResName[1] := sGetTuneEx('User.Descr', UserOfficeFilial(UserDeskRep.ResComp[7]), UserDeskRep.ResComp[7])
                }
            }
        }
    //------------------------------------------------------------------------------
      #fDesGr:
        {
          UpdateTable;
          PickRep2Marker('UserTuneGrDescr', NReport*10+8, UserDeskRep.ResComp[8]);
          if (RunInterface('GetGrDescr', UserDeskRep.ResName[2], TRUE, TRUE, comp(0)) <> cmCancel)
            {
              DelPickRep(NReport*10+8);
              Marker2PickRep('UserTuneGrDescr', NReport*10+8, UserDeskRep.ResComp[8], NumPick);
              if (NumPick > 1)
                Set UserDeskRep.ResWord[38] := NReport*10+8
              else
                Set UserDeskRep.ResWord[38] := 0;
            }
        }
    //------------------------------------------------------------------------------
      #fDO:
        {
          UpdateTable;
          DelPick(Word(32));
          PickRep2Pick(NReport*10+9, Word(32), UserDeskRep.ResComp[9]);
          if (RunInterface('GetDBase', FALSE, Word(1), Word(201),
                           Comp(0), Comp(0), Comp(0), Double(0)) <> cmCancel)
            {
              DelPickRep(NReport*10+9);
              Pick2PickRep(Word(32), NReport*10+9, UserDeskRep.ResComp[9], NumPick);
              if (NumPick > 1)
                Set UserDeskRep.ResWord[39] := NReport*10+9
              else
                Set UserDeskRep.ResWord[39] := 0;

              RescanPanel(#UserDeskRep);
            }
        }
    //------------------------------------------------------------------------------
      #RepGrLst.Name:  // Группировка
        {
          var Sod: string;

          UpdateTable;
          if (RunInterface(GetRepGruppa, NReport, UserDeskRep.ResComp[21], '') <> cmCancel)
            {
              Set UserDeskRep.ResComp[21] := UserDeskRep.ResComp[21];

              Sod := '';
              if (GetFirst RepGrLst = tsOk)
                _LOOP RepGrOne
                  Sod := Sod + RepGroup.Name + '/';

              Set UserDeskRep.ResName[3] := SubStr(Sod, 1, Length(Sod) - 1);
            }
        }
    //------------------------------------------------------------------------------
    end; // case

    RescanPanel(#UserDeskRep);
  }

cmDelOnProtect:
  {
    case CurField of
    //------------------------------------------------------------------------------
      #fGrMC:
        {
          DelPickRep(NReport*10+1);
          UserDeskRep.ResComp[1]  := 0;
          UserDeskRep.ResWord[31] := 0;
        }
    //------------------------------------------------------------------------------
      #fMC:
        {
          DelPickRep(NReport*10+2);
          UserDeskRep.ResComp[2]  := 0;
          UserDeskRep.ResWord[32] := 0;
        }
    //------------------------------------------------------------------------------
      #fSklad:
        {
          DelPickRep(NReport*10+3);
          UserDeskRep.ResComp[3]  := 0;
          UserDeskRep.ResWord[33] := 0;
        }
    //------------------------------------------------------------------------------
      #fMOL:
        {
          DelPickRep(NReport*10+4);
          UserDeskRep.ResComp[4]  := 0;
          UserDeskRep.ResWord[34] := 0;
        }
    //------------------------------------------------------------------------------
      #fParty:
        {
          DelPickRep(NReport*10+5);
          UserDeskRep.ResComp[5]  := 0;
          UserDeskRep.ResWord[35] := 0;
        }
    //------------------------------------------------------------------------------
      #fKontr:
        {
          DelPickRep(NReport*10+6);
          UserDeskRep.ResComp[6]  := 0;
          UserDeskRep.ResWord[36] := 0;
        }
    //------------------------------------------------------------------------------
      #fDescr:
        {
          DelPickRep(NReport*10+7);
          UserDeskRep.ResComp[7]  := 0;
          UserDeskRep.ResWord[37] := 0;
          UserDeskRep.ResName[1]  := '';
        }
    //------------------------------------------------------------------------------
      #fDesGr:
        {
          DelPickRep(NReport*10+8);
          UserDeskRep.ResComp[8]  := 0;
          UserDeskRep.ResWord[38] := 0;
          UserDeskRep.ResName[2]  := '';
        }
    //------------------------------------------------------------------------------
      #fDO:
        {
          DelPickRep(NReport*10+9);
          UserDeskRep.ResComp[9]  := 0;
          UserDeskRep.ResWord[39] := 0;
        }
    //------------------------------------------------------------------------------
      #RepGrLst.Name:  // Группировка
        {
          UserDeskRep.ResName[3]  := '';
          UserDeskRep.ResComp[21] := 0;
        }
    //------------------------------------------------------------------------------
    end; // case

    RescanPanel(#UserDeskRep);
  }

end; // HandleEvent
end; // Panel

//******************************************************************************

HandleEvent

cmInit:
  SetFields;

cmDone:
  if (NOT UpdateTable)
    Abort;
  else
    if (NOT IsValid(#UserDeskRep))
      PutCommand(cmClose);

end;
end; // Window


//******************************************************************************

Panel pReservDOSetup;
  Table UserDeskRep;

//******************************************************************************

Browse brUserDeskRep ('Enter - сформировать отчет ', , sci1478Esc);
  show  at (,,,16);

Fields
  UserDeskRep.RepName #3'Наименование отчета' : [45], Protect;
  UserDeskRep.dRep    #3'Дата'                : [10], Protect, NoAutoSize;
  if(UserDeskRep.ResWord[40] = 0, '', '√')    : [ 1], Skip;
end;

Screen scUserDeskRep
  Show At (,17,,);

Buttons
  cmValue10, Default ,, 'Формирование отчета',, sci1Esc;
  cmValue11,         ,, 'Настройка отчета',, sci1Esc;
  cmValue12,         ,, 'Отказ от формирования отчета',, sci1Esc;
<<

   <. Сформировать .>   <.  Настройка   .>   <.    Отмена    .>
>>
end;

//******************************************************************************

HandleEvent

cmSetDefault:
  {
    SelectPanel(#UserDeskRep);
    PutCommand(cmEdit);
  }
end; // HandleEvent For Panel
end; // Panel

//******************************************************************************

HandleEvent

cmInit:
  {
    pNRec   := Comp(0);
    NReport := cgReport_90;
  }

cmEdit: // Редактирование настройки
  {
    RunWindowModal(winParamResDO);
    RescanPanel(#UserDeskRep);
  }

cmDefault:
  {
    if (NOT UpdateTable)
      Exit;

    pNRec := UserDeskRep.NRec;
    PushPos(#UserDeskRep);
    Update UserDeskRep Set ResWord[40] := 0;
    PopPos(#UserDeskRep);
    Set UserDeskRep.ResWord[40] := 1;
    if (NOT UpdateTable)
      Exit;

    RescanPanel(#UserDeskRep);
  }

cmClose:
  pNRec := Comp(0);

cmCancel:
  pNRec := Comp(0);

cmValue10 :
{
  PutCommand(cmDefault);
}

cmValue11 :
{
  PutCommand(cmEdit);
}

cmValue12 :
{
  PutCommand(cmCancel);
}

end; // HandleEvent
End. // Interface

//******************************************************************************