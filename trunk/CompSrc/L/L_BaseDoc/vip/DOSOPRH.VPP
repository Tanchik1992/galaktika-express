//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Догистика - документы-основания
// Функции работы с таблицей SoprHoz
//******************************************************************************

#ifNdef _DOSOPRH_VPP
#define _DOSOPRH_VPP

//******************************************************************************

#include DOfuns.var

//******************************************************************************

Procedure FillSoprHoz;
{
  var MyTipHoz: word;

  myTipHoz := oDOfuns.GetTipHozOnVidDoc(BaseDoc.VidDoc);

  SoprHoz.TipDoc      := myTipHoz;
  SoprHoz.TiDkGal     := myTipHoz;
  SoprHoz.cDogovor    := BaseDoc.cDogovor;
  SoprHoz.cAppDogovor := BaseDoc.cAppDogovor;
  SoprHoz.cSoprDoc    := BaseDoc.NRec;
  SoprHoz.Direct      := BaseDoc.Direct;
  SoprHoz.NoDoc       := BaseDoc.NoDoc;
  SoprHoz.DatOb       := BaseDoc.dDoc;
  SoprHoz.cStepDoc    := StepDoc.NRec;
  SoprHoz.FilialNo    := BaseDoc.FilialNo;
  SoprHoz.DesGr       := BaseDoc.DesGr;
  SoprHoz.Descr       := BaseDoc.Descr;
  SoprHoz.cFilKor     := comp(0);
  if (isvalid(#KatSopr) and (isvalid(#Filial)))
    SoprHoz.cFilKor     := if(KatSopr.cOrg <> 0, Filial.NRec, comp(0));

  if (BaseDoc.cVal = 0)
    {
      SoprHoz.Summa    := BaseDoc.Total;
      SoprHoz.KodValut := 0;
      SoprHoz.SumValut := 0;
      Exit;
    }

  SoprHoz.Summa := oValFunc.GetAnyCurrency(BaseDoc.cVal
                                         , BaseDoc.Total
                                         , if(BaseDoc.dValCurse = ZeroDate, BaseDoc.dDoc, BaseDoc.dValCurse)
                                         , comp(0));

  if (BaseDoc.TipMoney <> 3)
    {
      SoprHoz.SumValut := 0;
      SoprHoz.KodValut := 0;
    }
  else
    {
      SoprHoz.SumValut := BaseDoc.Total;
      SoprHoz.KodValut := BaseDoc.cVal;
    }
}

//******************************************************************************

#ifNdef __var_iSHoz_isdef__
var iSHoz: ObjSHmanager;
#end

//******************************************************************************

Function UpDateDoSoprHoz: word;
{
  if (GetFirst FastFirstRow SoprHoz <> tsOk)
    {
      UpDateDoSoprHoz := tsInvalidPositioning;
      Exit;
    }

  FillSoprHoz;

#ifNdef __var_iSHoz_isdef__
  if NullVipRef(iSHoz)
    if (not GetVipRef(iSHoz, 'SHmanager'))
      Message('Не загружен интерфейс для работы с хоз. операциями! ', Error);
#end
  UpDateDoSoprHoz := iSHoz.UpdByHan(SoprHoz.BufferP)
}

//******************************************************************************

Function InsertDoSoprHoz: word;
{
  if ( RecordExists SoprHoz = tsOk )
    {
      UpDateDoSoprHoz;
      Exit;
    }

  ClearBuffer(#SoprHoz);

  FillSoprHoz;

  SoprHoz.NRec     := 0;
  SoprHoz.cHozOper := 0;
  SoprHoz.NameSHO  := '';
  SoprHoz.prObor   := 0;

#ifNdef __var_iSHoz_isdef__
  if NullVipRef(iSHoz)
    if (not GetVipRef(iSHoz, 'SHmanager'))
      Message('Не загружен интерфейс для работы с хозоперациями! ', Error);
#end
  InsertDoSoprHoz := iSHoz.InsByHan(SoprHoz.BufferP)
}

//******************************************************************************

#end // _DOSOPRH_VPP

//******************************************************************************