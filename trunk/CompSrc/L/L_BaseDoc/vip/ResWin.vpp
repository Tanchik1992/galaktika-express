//******************************************************************************
//                                                      (c) корпорация Галактика
// Галактика 7.12 - Логистика - ДО
// Окно резервов по позиции документа-основания
//******************************************************************************

#doc
Окно просмотра резерва по позиции спецификации документа
#end
Window wReserv 'Размещение резерва текущей позиции' escClose, cyan;
  Show at (, , 108, 8);

//******************************************************************************

Panel p1;
  Table DoRes;

Screen scShRes01;
  Show at (, , , 2);

Fields
  KatMC.Name+''              : Skip, {Font = {Bold = TRUE}};
  KatMC.BarKod+''            : Skip, {Font = {Bold = TRUE}};
  'отпускная единица: '      : Skip, right,  {Font = {color = ColorSysBlack; Italic = TRUE}};
  string(OtpEd)              : Skip,         {Font = {Bold = TRUE}};
  string(SpStep.SrokRes)+''  : Skip, {Font = {Bold = TRUE}};
<<
  МЦ .@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
  Код.@@@@@@@@@@@@@@@@@@ .@@@@@@@@@@@@@@@ .@@@@@@@@@@@@@@@@@@@@@@  зарезервировано до.@@@@@@@@@@@@@@@
>>
end;

Browse brResByPodr (, , sci1Esc);
  Show at (, 3, , );

Fields
  KatPodr1.Name  #3'Склад'  : [60], Skip;
  DoRes.Kol      #3'Резерв' : [11],[prSignsInKol], Protect, NoAutoSize;
End;

Browse brResByFullRazrez (, , sci1Esc);
  Show at (, 3, , );

Fields
  KatPodr1.Name  #3'Склад'  : [25], Skip;
  KatMol.Name    #3'МОЛ'    : [15], Skip;
  KatParty1.Name #3'Партия' : [25], Skip;
  DoRes.Kol      #3'Резерв' : [11],[prSignsInKol], Protect, NoAutoSize;
End;

End;// Panel

//******************************************************************************

Procedure CallReReserv;
{
  if (wGetTune('Reserv.PrSchSklad') = 2)
    {
      RunInterface( Rperenos, comp(0), comp(0), SpStep.NRec, comp(0));
      RereadRecord(#DoRes);
      RescanPanel(#DoRes);
    }
  else
    Message('Функция доступна только при резервировании по складскому разрезу!'#13+
            'см.' + GetTuneName('Reserv.PrSchSklad'), Information);
}

//******************************************************************************

HandleEvent

cmInit:
  {
    SetFormat(if (wGetTune('Reserv.PrSchSklad') = 2, brResByFullRazrez, brResByPodr));

    SetFieldSelectable(#DOres.Kol, wGetTune('Reserv.PrSchSklad') = 2);
  }

//------------------------------------------------------------------------------

cmOpenSearch:
  {
    Stop;
    if (message('Перейти в режим перенаправления резерва по данной позиции?', YesNo) = Yes)
      CallReReserv;
  }

//------------------------------------------------------------------------------

#ifdef _RESPROC_VPP
cmHotKeys:
  if EditDOPosEnabled(true, true)
    if (wGetTune('Reserv.PrSchSklad') = 2)
      case RunMenu('mnuEditReservOnSpStep') of
        cmValue1:
          {
            EditMakeReservOnSpStep;
            RereadRecord(#DoRes);
            RescanPanel(#DoRes);
          }

        cmValFld11: // вызов функции перенаправления резервов
          CallReReserv;
      end;
#end
//------------------------------------------------------------------------------

end;
end; // Window

//******************************************************************************