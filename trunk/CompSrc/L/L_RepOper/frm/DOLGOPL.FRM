/*
╔═══════════════════════════════════════════════════════════════════════════╗
║                     (c) 1994,97 корпорация ГАЛАКТИКА                      ║
║ Проект        : ГАЛАКТИКА                                                 ║
║ Система       : Оперативный контур                                        ║
║ Версия        : 5.00                                                      ║
║ Назначение    : Отчеты для Довгани                                        ║
║ Ответственный : Володько Анатолий Владимирович (Anatoly)                  ║
║ Параметры     : нет                                                       ║
╚═══════════════════════════════════════════════════════════════════════════╝
*/
#doc
Отчеты для Довгани
#end
.set name='DolgOpl'
!.test
.hide
.fields
  dd1:date          //   1
  dd2:date          //   2
  dd3:date          //   3
  NameTypes :string
  AllKontr:word     //   4
  pDescr:string     //   5
  pDescrFIO:string  //   6
  pDesGr:string     //   7
  pDesGrFIO:string  //   8
.endfields
123 45678
^^^^^^^^^
.{CheckEnter dKontr
.fields
  Kontr
.endfields
1
^
.{CheckEnter DKatSopr
.fields
  nRecBD  :comp
  nRecKS  :comp
  DOpr    :date        //  1
  Sum     :double      //  2
  Dolg    :double      //  3
  Srok    :word        //  4
  DOpl    :date        //  5
  PDolg   :double      //  6
  PrDn    :longint     //  7
  Descr   :string      //  8
  Manager :string      //  9
  dOpl_BaseDoc : date  // 10
  Srok_BaseDoc : word  // 11
.endfields
  12345678901
^^^^^^^^^^^^^
.{CheckEnter DKatSopr1
.fields
  nRecKS1  :comp
  DOpr1    :date       //  1
  Sum1     :double     //  2
  Dolg1    :double     //  3
  Descr1   :string     //  4
  Manager1 :string     //  5
.endfields
 12345
^^^^^^
.}
.}
.}
.endform

!--------------------------------------------------------------------------------------------------------------------
#doc
Задолженности по накладным
Отчет о просроченной дебиторской задолженности
#end
.linkform 'DolgOpl_01' prototype is 'DolgOpl'
.NameInList 'Отчет о просроченной дебиторской задолженности'
.Group 'Задолженности по накладным'
.var
  KDolg  :double
  KPDolg :double
  IDolg  :double
  IPDolg :double
.endvar
.fields
  CommonFormHeader
  DateToStr(dd3,'DD/MM/YYYY')
  'по '+NameTypes+' за период с '+DateToStr(dd1,'DD/MM/YYYY')+' по '+DateToStr(dd2,'DD/MM/YYYY')
.endfields
.begin
  IDolg  :=0.0;
  IPDolg :=0.0;
end.
 Б^

                                      ОТЧЕТ
                      О ПРОСРОЧЕННОЙ ДЕБИТОРСКОЙ ЗАДОЛЖЕННОСТИ
                                  НА @#@@@@@@@@

!                      по накладным с @#@@@@@@@@ по @#@@@@@@@@
@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Б
 И┌────┬──────────┬──────────────┬──────────┬────────┬──────────────┬────────┬────────────────┐
│Дол-│   Дата   │ Задолженность│   Срок   │Отсрочка│ Просроченная │Просроч.│    Менеджер и  │
│жник│ отгрузки │   по оплате  │  оплаты  │платежа │ задолженность│ дней   │номер накладной │
├────┼──────────┼──────────────┼──────────┼────────┼──────────────┼────────┼────────────────┤
│(1) │   (2)    │      (3)     │    (4)   │   (5)  │      (6)     │   (7)  │      (8)       │
├────┴──────────┴──────────────┴──────────┴────────┴──────────────┴────────┴────────────────┤ И
!    │01/01/1997│99'999'999'999│01/01/1997│   30   │99'999'999'999│   10   │                │
!Итого по должнику:
!Итого:
.{CheckEnter DKontr
.fields
  Kontr
.endfields
.begin
  KDolg  :=0.0;
  KPDolg :=0.0;
end.
 И│ Б@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Б│
│    ┌──────────┬──────────────┬──────────┬────────┬──────────────┬────────┬────────────────┤ И
.{CheckEnter DKatSopr
.fields
  DateToStr(DOpr,'DD/MM/YYYY') Dolg DateToStr(DOpl,'DD/MM/YYYY') Srok   PDolg  PrDn  Manager
.endfields
 И│    │@#@@@@@@@@│&'&&&&&&&&&.&&│@#@@@@@@@@│&#&&&.  │&#'&&&&&&&&.&&│&#&&&.  │@#@@@@@@@@@@@@@@│ И
.{CheckEnter DKatSopr1
.}
.begin
  KDolg  :=KDolg  + Dolg ;
  KPDolg :=KPDolg + PDolg;
end.
.}
.fields
                    KDolg                               KPDolg
.endfields
 И├────┴──────────┼──────────────┼──────────┼────────┼──────────────┼────────┼────────────────┤
│ БПо должнику:    Б│ Б&'&&&&&&&&&.&& Б│          │        │ Б&#'&&&&&&&&.&& Б│        │                │
├───────────────┴──────────────┴──────────┴────────┴──────────────┴────────┴────────────────┤ И
.begin
  IDolg  :=KDolg  + IDolg ;
  IPDolg :=KPDolg + IPDolg;
end.
.}
.fields
                    IDolg                               IPDolg
.endfields
 И├───────────────┬──────────────┬──────────┬────────┬──────────────┬────────┬────────────────┤
│ БИтого:          Б│ Б&'&&&&&&&&&.&& Б│          │        │ Б&#'&&&&&&&&.&& Б│        │                │
└───────────────┴──────────────┴──────────┴────────┴──────────────┴────────┴────────────────┘ И

.endform

!--------------------------------------------------------------------------------------------------------------------

#doc
Накладные с последующими отгрузками
Отчет по группам дебиторской задолженности
#end
.linkform 'DolgOpl_02' prototype is 'DolgOpl'
.NameInList 'Отчет по группам дебиторской задолженности'
.Group 'Накладные с последующими отгрузками'
.var
  KDolg  :double
  KPDolg :double
  KSum1  :double
  IDolg  :double
  IPDolg :double
  ISum1  :double
.endvar
.fields
  CommonFormHeader
  DateToStr(dd3,'DD/MM/YYYY')
  'по '+NameTypes+' за период с '+DateToStr(dd1,'DD/MM/YYYY')+' по '+DateToStr(dd2,'DD/MM/YYYY')
.endfields
.begin
  IDolg  :=0.0;
  IPDolg :=0.0;
  ISum1  :=0.0;
end.
 Б^

                                      ОТЧЕТ
                      ПО ГРУППАМ ДЕБИТОРСКОЙ ЗАДОЛЖЕННОСТИ
                                  НА @@@@@@@@@@

!                      по накладным с @#@@@@@@@@ по @#@@@@@@@@
@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
                                (в миллионах рублей) Б
 И┌────┬────────┬─────────────┬─────────────┬──────────┬────────────────────┬────────────────┐
│Дол-│Отсрочка│Задолженность│Просроченная │Просрочено│Последующие отгрузки│    Менеджер    │
│жник│платежа │  по оплате  │задолженность│   дней   ├──────────┬─────────┤    и номер     │
│    │        │             │             │          │   Дата   │  Сумма  │   накладной    │
├────┼────────┼─────────────┼─────────────┼──────────┼──────────┼─────────┼────────────────┤
│(1) │   (2)  │     (3)     │     (4)     │    (5)   │    (6)   │   (7)   │      (8)       │
├────┴────────┴─────────────┴─────────────┴──────────┴──────────┴─────────┴────────────────┤ И
!    │   30   │999'999'999  │99'999'999   │ >30 дней │21/02/1997│ 999'999 │                │
!Итого по должнику:
!Итого:
.{CheckEnter dKontr
.fields
  Kontr
.endfields
.begin
  KDolg  :=0.0;
  KPDolg :=0.0;
  KSum1  :=0.0;
end.
 И│ Б@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Б│
│    ┌────────┬─────────────┬─────────────┬──────────┬──────────┬─────────┬────────────────┤ И
.{CheckEnter DKatSopr
.fields
       Srok Dolg/1000000.0 PDolg/1000000.0 If(PrDn<=5,'<=5',
                                             If(PrDn<=10,'> 5',
                                             If(PrDn<=15,'>10',
                                             If(PrDn<=20,'>15',
                                             If(PrDn<=30,'>20','>30'))))+' дней')
                                                                            Manager
.endfields
 И│    │&#&&&.  │&'&&&&&&&&.&&│&#'&&&&&&&.&&│@#~@@@@@@@│          │         │@#@@@@@@@@@@@@@@│ И
.{CheckEnter DKatSopr1
.fields
                                 DateToStr(DOpr1,'DD/MM/YYYY') Sum1/1000000.0  Manager1
.endfields
 И│    │        │             │             │          │@@@@@@@@@@│&'&&&&.&&│@#@@@@@@@@@@@@@@│ И
.begin
  KSum1  :=KSum1  + Sum1;
end.
.}
.begin
  KDolg  :=KDolg  + Dolg;
  KPDolg :=KPDolg + PDolg;
end.
.}
.fields
            KDolg/1000000.0   KPDolg/1000000.0               KSum1/1000000.0
.endfields
 И├────┴────────┼─────────────┼─────────────┼──────────┼──────────┼─────────┼────────────────┤
│ БПо должнику:  Б│ Б&#'&&&&&.&&   Б│ Б&#'&&&&&.&&   Б│          │          │ Б&#'&.&&   Б│                │
├─────────────┴─────────────┴─────────────┴──────────┴──────────┴─────────┴────────────────┤ И
.begin
  IDolg  :=KDolg  + IDolg ;
  IPDolg :=KPDolg + IPDolg;
  ISum1  :=KSum1  + ISum1;
end.
.}
.fields
            IDolg/1000000.0 IPDolg/1000000.0                   ISum1/1000000.0
.endfields
 И├─────────────┬─────────────┬─────────────┬──────────┬──────────┬─────────┬────────────────┤
│ БИтого:        Б│ Б&'&&&&&&.&&   Б│ Б&#'&&&&&.&&   Б│          │          │ Б&#'&&&.   Б│                │
└─────────────┴─────────────┴─────────────┴──────────┴──────────┴─────────┴────────────────┘ И

.endform

!--------------------------------------------------------------------------------------------------------------------

#doc
Прогноз на дату
Прогноз поступления денежных средств
#end
.linkform 'DolgOpl_03' prototype is 'DolgOpl'
.NameInList 'Прогноз поступления денежных средств'
.Group 'Прогноз на дату'
.var
  KDolg  :double
  IDolg  :double
.endvar
.fields
  CommonFormHeader
  DateToStr(dd3,'DD/MM/YYYY')
  'по '+NameTypes+' за период с '+DateToStr(dd1,'DD/MM/YYYY')+' по '+DateToStr(dd2,'DD/MM/YYYY')
.endfields
.begin
  IDolg  :=0.0;
end.
 Б^

                       ПРОГНОЗ ПОСТУПЛЕНИЯ ДЕНЕЖНЫХ СРЕДСТВ
                                  НА @#@@@@@@@@

!                      по накладным с @#@@@@@@@@ по @#@@@@@@@@
@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
 Б
┌─────────────────────────────────────────────────────────┬────────────────┐
│ Наименование покупателя                                 │ Сумма к оплате │
├─────────────────────────────────────────────────────────┼────────────────┤
│                            (1)                          │      (2)       │
├─────────────────────────────────────────────────────────┼────────────────┤
!                                                         │999'999'999'999 │
.{CheckEnter dKontr
.begin
  KDolg  :=0.0;
end.
.{CheckEnter DKatSopr
.{CheckEnter DKatSopr1
.}
.begin
  If (dOpl=dd3) KDolg  :=KDolg  + Dolg;
end.
.}
.fields
  Kontr                                                        KDolg
.endfields
.{?Internal;KDolg>=1.0
│ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│&'&&&&&&&&&&.&& │
.}
.begin
  IDolg  :=KDolg  + IDolg ;
end.
.}
.fields
                                                               IDolg
.endfields
├─────────────────────────────────────────────────────────┼────────────────┤
│ БИтого прогноз поступлений:                                Б│ Б&'&&&&&&&&&&.&&  Б│
└─────────────────────────────────────────────────────────┴────────────────┘

.endform

!--------------------------------------------------------------------------------------------------------------------
