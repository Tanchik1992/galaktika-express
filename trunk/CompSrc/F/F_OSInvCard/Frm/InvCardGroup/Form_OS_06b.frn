#declare CountLineInTab2(NumberMove)
{
  // 15
  КолСтрок :=    LineAmount(Length(Массив_ПеремещениеВыбытие_документ_дата_номер      [#NumberMove]), 21);

  // 16
  if (КолСтрок < LineAmount(Length(Массив_ПеремещениеВыбытие_Стуктурное_подразделение [#NumberMove]), 45))
  {
    КолСтрок :=  LineAmount(Length(Массив_ПеремещениеВыбытие_Стуктурное_подразделение [#NumberMove]), 45);
  }

  // 17
  if (КолСтрок < LineAmount(Length(Массив_ПеремещениеВыбытие_Ответственное_лицо       [#NumberMove]), 45))
  {
    КолСтрок :=  LineAmount(Length(Массив_ПеремещениеВыбытие_Ответственное_лицо       [#NumberMove]), 45);
  }

  // 18
  if (КолСтрок < LineAmount(Length(Массив_ПеремещениеВыбытие_Причина_Выбытия          [#NumberMove]), 34))
  {
    КолСтрок :=  LineAmount(Length(Массив_ПеремещениеВыбытие_Причина_Выбытия          [#NumberMove]), 34);
  }
}
#end // CommissionBody
!
.linkform 'Form_OS_06b' prototype is 'FormOS6a' // FormGroupOS6a
.NameInList 'Форма N ОС-6б'
.group 'ОС-6б'
!.hide
.defo landscape
.p 62
.var
  /*
  01 Номер по порядку
  02 Наименование объекта основных средств
  03 Инвентарный номер

   * Поступило
  04 документ, дата, номер
  05 дата принятия к бухгалтерскому учету
  06 стуктурное подразделение
  07 ответственное лицо
  08 Первоначальная стоимость, руб.
  09 Срок полезного использования
  10 Сумма начисленной амортизации (износа), руб.

  11 Остаточная стоимость, руб.

   * Переоценка
  12 дата
  13 коэффициент пересчета (переоценки)
  14 восстановительная стоимость, руб.

   * Внутренние перемещения, выбытие (списание)
  15 документ, дата, номер
  16 структурное подразделение
     ?? Если перемещение, то куда. А если выбытие, от куда?
  17 ответственное лицо
  18 причина выбытия (списания)
  */

  // ***** Объявление вспомогательных переменных *****

  НомерИК         : word;
  НомерИК_Max     : word;
  НомерПоПорядку  : word;

  НомерПереоценки : word;
  НомерПеремещенияВыбытия : word;

  NN              : word;
  Страница        : word;

  КолНомер_в_ИК   : word;

  Country         : word;

  // ***** Переменные проверки отличий общих полей *****
  Flag                           : boolean;
  Flag_Структурное_подразделение : boolean;

  // ***** Объявление массивов *****
  Массив_ИК_Количество                                 : array[1..2] of double; //    Количество в конкретной ИК

  Массив_ИК_Наименование                               : array[1..2] of string; // 02 Наименование объекта основных средств
  Массив_ИК_Инвентарный_номер                          : array[1..2] of string; // 03 Инвентарный номер

  // * Поступило
  Массив_ИК_Поступление_документ_дата_номер            : array[1..2] of string; // 04 документ, дата, номер
  Массив_ИК_Поступление_Дата_принятия_к_бух_учету      : array[1..2] of date;   // 05 дата принятия к бухгалтерскому учету
  Массив_ИК_Поступление_Стуктурное_подразделение       : array[1..2] of string; // 06 стуктурное подразделение
  Массив_ИК_Поступление_Ответственное_лицо             : array[1..2] of string; // 07 ответственное лицо

  Массив_ИК_Первоначальная_стоимость                   : array[1..2] of double; // 08 Первоначальная стоимость, руб.
  Массив_ИК_Срок_полезного_использования               : array[1..2] of string; // 09 Срок полезного использования
  Массив_ИК_Сумма_начисленной_амортизации              : array[1..2] of double; // 10 Сумма начисленной амортизации (износа), руб.

  Массив_ИК_Остаточная_стоимость                       : array[1..2] of double; // 11 Остаточная стоимость, руб.

  // * Переоценка
  Массив_ИК_КоличествоПереоценок                       : array[1..2] of word;   //    Количество переоценок для данной ИК

  Массив_Переоценка_Дата                               : array[1..2] of date;   // 12 дата
  Массив_Тип_операции                                  : array[1..2] of word;
  Массив_Переоценка_коэффициент_пересчета              : array[1..2] of double; // 13 коэффициент пересчета (переоценки)
  Массив_Переоценка_Восстановительная_стоимость        : array[1..2] of double; // 14 восстановительная стоимость, руб.

  // * Внутренние перемещения, выбытие (списание)
  Массив_ИК_КоличествоПеремещенийВыбытий               : array[1..2] of word;   //    Количество перемещений или выбытий для данной ИК

  Массив_ПеремещениеВыбытие_документ_дата_номер        : array[1..2] of string; // 15 документ, дата, номер
  Массив_ПеремещениеВыбытие_Стуктурное_подразделение   : array[1..2] of string; // 16 структурное подразделение
  Массив_ПеремещениеВыбытие_Ответственное_лицо         : array[1..2] of string; // 17 ответственное лицо
  Массив_ПеремещениеВыбытие_Причина_Выбытия            : array[1..2] of string; // 18 причина выбытия (списания)

  СтрокаN         : word;
  КолСтрокВТаб1   : word;
  КолСтрокВТаб2   : word;
  КолСтрок        : word;

  Массив_ИК_Количество_доп_строк_в_Таблице1            : array[1..2] of word;
  Массив_ИК_Количество_доп_строк_в_Таблице2            : array[1..2] of word;
.endvar
.function LineAmount (LengthLine : word; AmountInField : word) : word;
begin
  LineAmount := 0;

  if (LengthLine = 0)
  {
    LineAmount := 1;
  }
  else
  {
    if (LengthLine / AmountInField < 1)
    {
      LineAmount := 1;
    }
    else
    {
      if (Trunc(LengthLine / AmountInField) < (LengthLine / AmountInField))
      {
        LineAmount := Word(Trunc(LengthLine / AmountInField) + 1);
      }
      else
      {
        LineAmount := Word(Trunc(LengthLine / AmountInField));
      }
    }
  }
end.
#include funcOs.frn
end.  // funcOs.frn - вынес из файла чтобы не создавалась пустая строка перед формой
.begin
  // ***** Обнуление переменных *****

  НомерИК         := 0;
  НомерИК_Max     := 0;

  НомерПереоценки := 0;
  НомерПеремещенияВыбытия := 0;

  Country := wGetTune('Country');

  // ***** Обнуление переменных проверки отличий общих полей *****
  Flag                           := true;
  Flag_Структурное_подразделение := true;

  // ***** Обнуление массивов по таблице *****
  for (NN := 1; NN <= Count(Массив_ИК_Наименование); NN := NN + 1)
  {
    Массив_ИК_Количество                               [NN] := 0;

    Массив_ИК_Наименование                             [NN] := '';
    Массив_ИК_Инвентарный_номер                        [NN] := '';

    // * Поступило
    Массив_ИК_Поступление_документ_дата_номер          [NN] := '';
    Массив_ИК_Поступление_Дата_принятия_к_бух_учету    [NN] := date (0,0,0);
    Массив_ИК_Поступление_Стуктурное_подразделение     [NN] := '';
    Массив_ИК_Поступление_Ответственное_лицо           [NN] := '';

    Массив_ИК_Первоначальная_стоимость                 [NN] := 0;
    Массив_ИК_Срок_полезного_использования             [NN] := '';
    Массив_ИК_Сумма_начисленной_амортизации            [NN] := 0;

    Массив_ИК_Остаточная_стоимость                     [NN] := 0;

    Массив_ИК_КоличествоПереоценок                     [NN] := 0;

    Массив_ИК_КоличествоПеремещенийВыбытий             [NN] := 0;

    Массив_ИК_Количество_доп_строк_в_Таблице1          [NN] := 0;
    Массив_ИК_Количество_доп_строк_в_Таблице2          [NN] := 0;
  }

  // ***** Обнуление массивов переоценок *****
  for (NN := 1; NN <= Count(Массив_Переоценка_Дата); NN := NN + 1)
  {
    Массив_Переоценка_Дата                             [NN] := date (0,0,0);
    Массив_Тип_операции                                [NN] := 0;
    Массив_Переоценка_коэффициент_пересчета            [NN] := 0;
    Массив_Переоценка_Восстановительная_стоимость      [NN] := 0;
  }

  // ***** Обнуление массивов внутренних перемещений, выбытий (списаний) *****
  for (NN := 1; NN <= Count(Массив_ПеремещениеВыбытие_документ_дата_номер); NN := NN + 1)
  {
    Массив_ПеремещениеВыбытие_документ_дата_номер      [NN] := '';
    Массив_ПеремещениеВыбытие_Стуктурное_подразделение [NN] := '';
    Массив_ПеремещениеВыбытие_Ответственное_лицо       [NN] := '';
    Массив_ПеремещениеВыбытие_Причина_Выбытия          [NN] := '';
  }
end.
.{
.{ CheckEnter CycleByIK
.begin
  НомерИК     := НомерИК + 1;
  НомерИК_Max := НомерИК_Max + 1;

  Массив_ИК_Количество                                 [НомерИК] := Количество;
  Массив_ИК_Наименование                               [НомерИК] := Наименование;
  Массив_ИК_Инвентарный_номер                          [НомерИК] := Инвентарный_номер;

  // ***** Проверка отличий общих полей *****
  if (Общее_Структурное_подразделение_nRec <> Структурное_подразделение_nRec)
  {
    Flag := false;
    Flag_Структурное_подразделение := false;
  }

  // * Поступило
                                                                    // ?? + 'Акт'
  Массив_ИК_Поступление_документ_дата_номер            [НомерИК] := if (longint(Поступления_дата) <> 0, DateToStr(Поступления_дата, 'DD/MM/YYYY'), '')
                                                                      + if(longint(Поступления_дата) <> 0 and Поступления_номер <> '', ' ','')
                                                                      + Поступления_номер;
  Массив_ИК_Поступление_Дата_принятия_к_бух_учету      [НомерИК] := Дата_принятия_к_бух_учету;
  Массив_ИК_Поступление_Стуктурное_подразделение       [НомерИК] := Поступления_Стуктурное_подразделение;
  Массив_ИК_Поступление_Ответственное_лицо             [НомерИК] := Поступления_Ответственное_лицо;

  Массив_ИК_Первоначальная_стоимость                   [НомерИК] := Первоначальная_стоимость;
  Массив_ИК_Срок_полезного_использования               [НомерИК] := SrokIspEdIzmWord (Срок_полезного_использования);
  Массив_ИК_Сумма_начисленной_амортизации              [НомерИК] := Сумма_начисленной_амортизации;

  Массив_ИК_Остаточная_стоимость                       [НомерИК] := Остаточная_стоимость;

  // Создание нулевой записи, чтобы при обращении к массиву не было ошибки...
  // И обнуление перед подсчетом
  Массив_ИК_КоличествоПеремещенийВыбытий               [НомерИК] := 0;

  КолСтрокВТаб1 := 0;

  КолСтрок      := 0;
  КолСтрокВТаб2 := 0;

  // 02 - Наименование
  КолСтрокВТаб1 :=    LineAmount(Length(Массив_ИК_Наименование                         [НомерИК]), 37);

  // 03 - Инвентарный номер
  if (КолСтрокВТаб1 < LineAmount(Length(Массив_ИК_Инвентарный_номер                    [НомерИК]), 23))
  {
    КолСтрокВТаб1 :=  LineAmount(Length(Массив_ИК_Инвентарный_номер                    [НомерИК]), 23);
  }

  // 04 - документ, дата, номер
  if (КолСтрокВТаб1 < LineAmount(Length(Массив_ИК_Поступление_документ_дата_номер      [НомерИК]), 15))
  {
    КолСтрокВТаб1 :=  LineAmount(Length(Массив_ИК_Поступление_документ_дата_номер      [НомерИК]), 15);
  }

  // 06 - стуктурное подразделение
  if (КолСтрокВТаб1 < LineAmount(Length(Массив_ИК_Поступление_Стуктурное_подразделение [НомерИК]), 28))
  {
    КолСтрокВТаб1 :=  LineAmount(Length(Массив_ИК_Поступление_Стуктурное_подразделение [НомерИК]), 28);
  }

  // 07 - ответственное лицо
  if (КолСтрокВТаб1 < LineAmount(Length(Массив_ИК_Поступление_Ответственное_лицо       [НомерИК]), 28))
  {
    КолСтрокВТаб1 :=  LineAmount(Length(Массив_ИК_Поступление_Ответственное_лицо       [НомерИК]), 28);
  }

  // 09 - Срок полезного использования
  if (КолСтрокВТаб1 < LineAmount(Length(Массив_ИК_Срок_полезного_использования         [НомерИК]), 18))
  {
    КолСтрокВТаб1 :=  LineAmount(Length(Массив_ИК_Срок_полезного_использования         [НомерИК]), 18);
  }
end.
.{ CheckEnter CycleByOverAppreciated
.begin
  // * Переоценка
  Массив_ИК_КоличествоПереоценок                       [НомерИК] := Массив_ИК_КоличествоПереоценок [НомерИК] + 1;

  НомерПереоценки := НомерПереоценки + 1;

  Массив_Переоценка_Дата                               [НомерПереоценки] := Переоценка_дата;
  Массив_Тип_операции                                  [НомерПереоценки] := Тип_операции;
  Массив_Переоценка_коэффициент_пересчета              [НомерПереоценки] := Переоценка_коэффициент_пересчета_переоценки;
  Массив_Переоценка_Восстановительная_стоимость        [НомерПереоценки] := Переоценка_восстановительная_стоимость;
end.
.}
.{ CheckEnter CycleByAmortization
.}
.{ CheckEnter CycleByOperation
.begin
  if (Операции_Тип = 2)
  {
    НомерПеремещенияВыбытия := НомерПеремещенияВыбытия + 1;
    Массив_ИК_КоличествоПеремещенийВыбытий             [НомерИК] := Массив_ИК_КоличествоПеремещенийВыбытий [НомерИК] + 1;

    // * Внутренние перемещения, выбытие (списание)
                                                                                    // ?? + 'Акт'
    Массив_ПеремещениеВыбытие_документ_дата_номер      [НомерПеремещенияВыбытия] := if(longint(Операции_Дата) <> 0, DateToStr(Операции_Дата, 'DD/MM/YYYY'), '')
                                                                                      + if(longint(Операции_Дата) <> 0 and Операции_Номер <> '', ' ','')
                                                                                      + Операции_Номер;

    Массив_ПеремещениеВыбытие_Стуктурное_подразделение [НомерПеремещенияВыбытия] := Операции_Подр;
    Массив_ПеремещениеВыбытие_Ответственное_лицо       [НомерПеремещенияВыбытия] := Операции_МОЛ;

    // Т.к. в консультации к проблеме бля Белорусии сказали, что выводить только для выбытия.
    // А в Российской проблеме сказали - если есть для перемещения, то выводить и для перемещения...
    Массив_ПеремещениеВыбытие_Причина_Выбытия          [НомерПеремещенияВыбытия] := if (Country <> 1, Операции_Основание, '');

    // Выисление количество строк для данной ИК во второй таблице
    КолСтрок := 0;
    #CountLineInTab2 (НомерПеремещенияВыбытия)
    КолСтрокВТаб2 := КолСтрокВТаб2 + КолСтрок;
  }
end.
.}
.begin
  if (Выбыло)
  {
    НомерПеремещенияВыбытия := НомерПеремещенияВыбытия + 1;

    Массив_ИК_КоличествоПеремещенийВыбытий             [НомерИК] := Массив_ИК_КоличествоПеремещенийВыбытий [НомерИК] + 1;

                                                                                    // ?? + 'Акт'
    Массив_ПеремещениеВыбытие_документ_дата_номер      [НомерПеремещенияВыбытия] := if(longint(Выбытия_дата) <> 0, DateToStr(Выбытия_дата, 'DD/MM/YYYY'), '')
                                                                                      + if(longint(Выбытия_дата) <> 0 and Выбытия_номер <> '', ' ','')
                                                                                      + Выбытия_номер;
    Массив_ПеремещениеВыбытие_Стуктурное_подразделение [НомерПеремещенияВыбытия] := '';
    Массив_ПеремещениеВыбытие_Ответственное_лицо       [НомерПеремещенияВыбытия] := '';
    Массив_ПеремещениеВыбытие_Причина_Выбытия          [НомерПеремещенияВыбытия] := Причина_выбытия;

    // Выисление количество строк для данной ИК во второй таблице
    КолСтрок := 0;
    #CountLineInTab2 (НомерПеремещенияВыбытия)
    КолСтрокВТаб2 := КолСтрокВТаб2 + КолСтрок;
  }

  // **** Вычисление количество строк
  Массив_ИК_Количество_доп_строк_в_Таблице1 [НомерИК] := 0;
  Массив_ИК_Количество_доп_строк_в_Таблице2 [НомерИК] := 0;

  // Т.к. переоценки все умещаются в одну строку, а перемещения выбытия нет,
  // то если переоценок больше, то прибовляем на сколько их больше.
  // А если меньше или равно, то это максимальное кличество...
  if (Массив_ИК_КоличествоПереоценок [НомерИК] > Массив_ИК_КоличествоПеремещенийВыбытий [НомерИК])
  {
    КолСтрокВТаб2 := КолСтрокВТаб2 + (Массив_ИК_КоличествоПереоценок [НомерИК] - Массив_ИК_КоличествоПеремещенийВыбытий [НомерИК])
  }

  // Т.е. если не было ни переоценок, не перемещений - выбытий, то не заходили в макроопределение, а следовательно не вызвали функцию,
  // а онабы вернула единицу....
  if (КолСтрокВТаб2 = 0)
  {
    КолСтрокВТаб2 := 1;
  }

  // Непосредственно вычисления колстиства строк...
  if (КолСтрокВТаб1 <> КолСтрокВТаб2)
  {
    if (КолСтрокВТаб1 < КолСтрокВТаб2)
    {
      Массив_ИК_Количество_доп_строк_в_Таблице1 [НомерИК] := КолСтрокВТаб2 - КолСтрокВТаб1;
      Массив_ИК_Количество_доп_строк_в_Таблице2 [НомерИК] := 0;
    }
    else
    {
      Массив_ИК_Количество_доп_строк_в_Таблице1 [НомерИК] := 0;
      Массив_ИК_Количество_доп_строк_в_Таблице2 [НомерИК] := КолСтрокВТаб1 - КолСтрокВТаб2;
    }
  }
  else
  {
    Массив_ИК_Количество_доп_строк_в_Таблице1 [НомерИК] := 0;
    Массив_ИК_Количество_доп_строк_в_Таблице2 [НомерИК] := 0;
  }
end.
.}
! ***** Вывод сообщения о отличий общих полей *****
.begin
  if (not Flag)
  {
    Message ('Значения в карточках для поля:'#13 +
             if (not Flag_Структурное_подразделение , 'Наименование структурного подразделения,'#13 , '') +
             'отличаются.'                          , Information+CancelButton);
  }
end.
.fields
  // ***** Общая информация *****
  '(Метод учета: ' +Метод_Учета +')'

  // Код - Коды
  if (Country = 1, 'ы', '')

  // ОКУД
  if (Country = 1, '0501381', '0306035')

  Наименование_организации
  if (Country = 1, 'ОКЮЛП', 'ОКПО')
  ОКПО
  if (Flag_Структурное_подразделение, Общее_Структурное_подразделение, '')

  if (LongInt(Начало_периода) <> 0, Day(Начало_периода), '')
  if (LongInt(Начало_периода) <> 0, DateToStr(Начало_периода,'mon'), '')
  if (LongInt(Начало_периода) <> 0, Year(Начало_периода), '')

  if (LongInt(Конец_периода) <> 0, Day(Конец_периода), '')
  if (LongInt(Конец_периода) <> 0, DateToStr(Конец_периода,'mon'), '')
  if (LongInt(Конец_периода) <> 0, Year(Конец_периода), '')

  ЛОзаВК_Должность
  ЛОзаВК
  string(ЛОзаВК_Табельный_номер)
  'true' : 's'
.endfields
 Р@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
.{?INTERNAL; (Country = 1)
! Беларусь
                                                                                                                                                                                Приложение 10
                                                                                                                                                                              к постановлению
                                                                                                                                                                        Министерства финансов
                                                                                                                                                                          Республики Беларусь
                                                                                                                                                                             08.12.2003 № 168
                                                                                                                                                                                  Форма ОС-6б
.}
.{?INTERNAL; (Country <> 1)
! Россия
                                                                                                                                                                Унифицированная форма № ОС-6б

                                                                                                                                                                                   Утверждена
                                                                                                                                                                               Постановлением
                                                                                                                                                                           Госкомстата России
                                                                                                                                                                            от 21.01.2003 № 7
.}

                                                                                                                                                                               ┌─────────────┐
                                                                                                                                                                               │     Код@    │
                                                                                                                                                                               ├─────────────┤
                                                                                                                                                                Форма по ОКУД  │@~@@@@@@@@@@@│
                                                                                                                                                                               ├─────────────┤
    Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е      по @@@@@ │@~@@@@@@@@@@@│
                                                                                  И(наименование организации)  И                                                                       ├─────────────┤
    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д               │             │
                                                                            И(наименование структурного подразделения)       И                                                            └─────────────┘
 Р Б


                                              ИНВЕНТАРНАЯ КНИГА
                                      УЧЕТА ОБЪЕКТОВ ОСНОВНЫХ СРЕДСТВ
 Б
                        за период с " Д&& Д"  Д@~@@@@@@ Д  Д&&&& Д г. по " Д&& Д"  Д@~@@@@@@ Д  Д&&&& Д г.
 Р
                                                                                                                                                                             ┌───────────────┐
                                                                                                                                                                             │Табельный номер│
                                                                                                                                                                             ├───────────────┤
    Лицо, ответственное за ведение книги   Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д     Д@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д│@~@@@@@@@@@@@@@│
    Материально ответственное лицо                                  И(должность) И                                                   И  (фамилия, инициалы)   И                           └───────────────┘
 Р Ш
^
                                                                                                                                                                                                  2-я страница формы № ОС-6б
   ┌───────┬─────────────────────────────────────┬───────────────────────┬───────────────────────────────────────────────────────────────────────────────────────────┬──────────────────┬──────────────────┬──────────────────┐
   │       │                                     │                       │                                         Поступило                                         │                  │                  │      Сумма       │
   │ Номер │         Наименование объекта        │      Инвентарный      ├───────────────┬─────────────────┬────────────────────────────┬────────────────────────────┤  Первоначальная  │       Срок       │   начисленной    │
   │  по   │           основных средств          │         номер         │   документ,   │ дата принятия к │         стуктурное         │       ответственное        │    стоимость,    │    полезного     │   амортизации    │
   │порядку│                                     │                       │  дата, номер  │  бухгалтерскому │       подразделение        │           лицо             │        руб.      │  использования   │    (износа),     │
   │       │                                     │                       │               │      учету      │                            │                            │                  │                  │       руб.       │
   ├───────┼─────────────────────────────────────┼───────────────────────┼───────────────┼─────────────────┼────────────────────────────┼────────────────────────────┼──────────────────┼──────────────────┼──────────────────┤
   │   1   │                  2                  │           3           │       4       │        5        │              6             │              7             │         8        │         9        │       10         │
!  ├───────┼─────────────────────────────────────┼───────────────────────┼───────────────┼─────────────────┼────────────────────────────┼────────────────────────────┼──────────────────┼──────────────────┼──────────────────┤
! ***** Цикл по ИК *****
.begin
  НомерИК         := 1;
  НомерПоПорядку  := 0;
  КолНомер_в_ИК   := 0;
  Страница        := 2;
end.
.fields
  Страница
.endfields
.{While (НомерИК <= НомерИК_Max)
.[h skip
.begin
  Страница := Страница + 2;
end.

                                                                                                                                                                                               &&&&-я страница формы № ОС-6б
   ┌───────┬─────────────────────────────────────┬───────────────────────┬───────────────────────────────────────────────────────────────────────────────────────────┬──────────────────┬──────────────────┬──────────────────┐
   │       │                                     │                       │                                         Поступило                                         │                  │                  │      Сумма       │
   │ Номер │         Наименование объекта        │      Инвентарный      ├───────────────┬─────────────────┬────────────────────────────┬────────────────────────────┤  Первоначальная  │       Срок       │   начисленной    │
   │  по   │           основных средств          │         номер         │   документ,   │ дата принятия к │         стуктурное         │       ответственное        │    стоимость,    │    полезного     │   амортизации    │
   │порядку│                                     │                       │  дата, номер  │  бухгалтерскому │       подразделение        │           лицо             │        руб.      │  использования   │    (износа),     │
   │       │                                     │                       │               │      учету      │                            │                            │                  │                  │       руб.       │
   ├───────┼─────────────────────────────────────┼───────────────────────┼───────────────┼─────────────────┼────────────────────────────┼────────────────────────────┼──────────────────┼──────────────────┼──────────────────┤
   │   1   │                  2                  │           3           │       4       │        5        │              6             │              7             │         8        │         9        │       10         │
   ├───────┼─────────────────────────────────────┼───────────────────────┼───────────────┼─────────────────┼────────────────────────────┼────────────────────────────┼──────────────────┼──────────────────┼──────────────────┤
.]h
.begin
  НомерПоПорядку := НомерПоПорядку + 1;
  КолНомер_в_ИК := КолНомер_в_ИК + 1;
end.
.fields
  НомерПоПорядку                                                                                                              // 01

  Массив_ИК_Наименование                         [НомерИК]                                                                    // 02

  Массив_ИК_Инвентарный_номер                    [НомерИК]
    + if (AddNumIKToInvNum and DuplicateRecordInGroupIK and (Массив_ИК_Количество [НомерИК] > 1), ' / ' + КолНомер_в_ИК, '')  // 03

  Массив_ИК_Поступление_документ_дата_номер      [НомерИК]                                                                    // 04

  if (longint(Массив_ИК_Поступление_Дата_принятия_к_бух_учету [НомерИК]) <> 0,
    DateToStr(Массив_ИК_Поступление_Дата_принятия_к_бух_учету [НомерИК],'DD/MM/YYYY'), '')                                    // 05

  Массив_ИК_Поступление_Стуктурное_подразделение [НомерИК]                                                                    // 06

  Массив_ИК_Поступление_Ответственное_лицо       [НомерИК]                                                                    // 07

  Массив_ИК_Первоначальная_стоимость             [НомерИК] * if (DuplicateRecordInGroupIK, 1, Массив_ИК_Количество [НомерИК]) // 08

  Массив_ИК_Срок_полезного_использования         [НомерИК]                                                                    // 09

  Массив_ИК_Сумма_начисленной_амортизации        [НомерИК] * if (DuplicateRecordInGroupIK, 1, Массив_ИК_Количество [НомерИК]) // 10
.endfields
   ├───────┼─────────────────────────────────────┼───────────────────────┼───────────────┼─────────────────┼────────────────────────────┼────────────────────────────┼──────────────────┼──────────────────┼──────────────────┤
   │&&&&&&&│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@│@~@@@@@@@@@@@@@│@~@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@@@│&&&&&&&&&&&&&&&.&&│@~@@@@@@@@@@@@@@@@│&&&&&&&&&&&&&&&.&&│
.begin
  СтрокаN := 1;
end.
.{While (СтрокаN <= Массив_ИК_Количество_доп_строк_в_Таблице1 [НомерИК])
   │       │                                     │                       │               │                 │                            │                            │                  │                  │                  │
.begin
  СтрокаN := СтрокаN + 1;
end.
.}
.begin
  if (DuplicateRecordInGroupIK)
  {
    if (КолНомер_в_ИК >= Массив_ИК_Количество [НомерИК])
    {
      НомерИК := НомерИК + 1;
      КолНомер_в_ИК := 0;
    }
  }
  else
  {
    НомерИК := НомерИК + 1;
  }
end.
.[f
   └───────┴─────────────────────────────────────┴───────────────────────┴───────────────┴─────────────────┴────────────────────────────┴────────────────────────────┴──────────────────┴──────────────────┴──────────────────┘
.]f
.}
.fields
  'true' : 's'
.endfields
^
                                                                                                                                                                                                  3-я страница формы № ОС-6б
   ┌─────────────────────┬───────────────────────────────────────────────┬────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┐
   │                     │                   Переоценка                  │                    Внутренние  перемещения, выбытие (списание)                                                                                     │
   │      Остаточная     ├──────────┬──────────────┬─────────────────────┼─────────────────────┬─────────────────────────────────────────────┬─────────────────────────────────────────────┬──────────────────────────────────┤
   │      стоимость,     │          │  коэффициент │  восстановительная  │       документ,     │                                             │                                             │                                  │
   │         руб.        │   дата   │   пересчета  │      стоимость,     │     дата, номер     │          структурное подразделение          │              ответственное лицо             │    причина выбытия (списания)    │
   │                     │          │ (переоценки) │         руб.        │                     │                                             │                                             │                                  │
   ├─────────────────────┼──────────┼──────────────┼─────────────────────┼─────────────────────┼─────────────────────────────────────────────┼─────────────────────────────────────────────┼──────────────────────────────────┤
   │          11         │    12    │      13      │         14          │          15         │                     16                      │                     17                      │                18                │
!  ├─────────────────────┼──────────┼──────────────┼─────────────────────┼─────────────────────┼─────────────────────────────────────────────┼─────────────────────────────────────────────┼──────────────────────────────────┤
! ***** Цикл по ИК *****
.begin
  НомерИК         := 1;
  НомерПоПорядку  := 0;
  НомерПереоценки := 0;
  КолНомер_в_ИК   := 0;
  НомерПеремещенияВыбытия := 0;
  Страница        := 3;
end.
.fields
  Страница
.endfields
.{While (НомерИК <= НомерИК_Max)
.[h skip
.begin
  Страница := Страница + 2;
end.

                                                                                                                                                                                               &&&&-я страница формы № ОС-6б
   ┌─────────────────────┬───────────────────────────────────────────────┬────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┐
   │                     │                   Переоценка                  │                    Внутренние  перемещения, выбытие (списание)                                                                                     │
   │      Остаточная     ├──────────┬──────────────┬─────────────────────┼─────────────────────┬─────────────────────────────────────────────┬─────────────────────────────────────────────┬──────────────────────────────────┤
   │      стоимость,     │          │  коэффициент │  восстановительная  │       документ,     │                                             │                                             │                                  │
   │         руб.        │   дата   │   пересчета  │      стоимость,     │     дата, номер     │          структурное подразделение          │              ответственное лицо             │    причина выбытия (списания)    │
   │                     │          │ (переоценки) │         руб.        │                     │                                             │                                             │                                  │
   ├─────────────────────┼──────────┼──────────────┼─────────────────────┼─────────────────────┼─────────────────────────────────────────────┼─────────────────────────────────────────────┼──────────────────────────────────┤
   │          11         │    12    │      13      │         14          │          15         │                     16                      │                     17                      │                18                │
   ├─────────────────────┼──────────┼──────────────┼─────────────────────┼─────────────────────┼─────────────────────────────────────────────┼─────────────────────────────────────────────┼──────────────────────────────────┤
.]h
.begin
  НомерПоПорядку := НомерПоПорядку + 1;
  КолНомер_в_ИК := КолНомер_в_ИК + 1;
  NN := 1;
end.
.fields
  Массив_ИК_Остаточная_стоимость     [НомерИК] // 11

  // ***** Вывод первой переоценки *****
  if (Массив_ИК_КоличествоПереоценок [НомерИК] >= NN,
      DateToStr(Массив_Переоценка_Дата [НомерПереоценки + NN], 'DD/MM/YYYY') ,'') // 12

  if((Массив_ИК_КоличествоПереоценок [НомерИК] >= NN) and (Массив_Тип_операции [НомерПереоценки + NN] = 6),
     iExcelFormat.DoubleToStrFormatExcel (Массив_Переоценка_коэффициент_пересчета [НомерПереоценки + NN], 4) ,'') // 13

  if (Массив_ИК_КоличествоПереоценок [НомерИК] >= NN,
    iExcelFormat.DoubleToStrFormatExcel (Массив_Переоценка_Восстановительная_стоимость [НомерПереоценки + NN]
                * if (DuplicateRecordInGroupIK, 1, Массив_ИК_Количество [НомерИК]), 2)
    , '') // 14

  // ***** Вывод первого перемещенрия или выбытия *****
  if(Массив_ИК_КоличествоПеремещенийВыбытий [НомерИК] >= NN,
    Массив_ПеремещениеВыбытие_документ_дата_номер      [НомерПеремещенияВыбытия + NN], '') // 15

  if(Массив_ИК_КоличествоПеремещенийВыбытий [НомерИК] >= NN,
    Массив_ПеремещениеВыбытие_Стуктурное_подразделение [НомерПеремещенияВыбытия + NN], '') // 16


  if(Массив_ИК_КоличествоПеремещенийВыбытий [НомерИК] >= NN,
    Массив_ПеремещениеВыбытие_Ответственное_лицо       [НомерПеремещенияВыбытия + NN], '') // 17

  if(Массив_ИК_КоличествоПеремещенийВыбытий [НомерИК] >= NN,
    Массив_ПеремещениеВыбытие_Причина_Выбытия          [НомерПеремещенияВыбытия + NN], '') // 18
.endfields
   ├─────────────────────┼──────────┼──────────────┼─────────────────────┼─────────────────────┼─────────────────────────────────────────────┼─────────────────────────────────────────────┼──────────────────────────────────┤
   │&&&&&&&&&&&&&&&&&&.&&│@@@@@@@@@@│&&&&&&&.&&&&&&│&&&&&&&&&&&&&&&&&&.&&│@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│
! ***** Цикл по переоценке *****
.begin
end.
.{While ((NN < Массив_ИК_КоличествоПереоценок [НомерИК]) or (NN < Массив_ИК_КоличествоПеремещенийВыбытий [НомерИК]))
.begin
 NN := NN + 1;
end.
.fields
  // ***** Вывод первой переоценки *****
  if (Массив_ИК_КоличествоПереоценок [НомерИК] >= NN,
      DateToStr(Массив_Переоценка_Дата [НомерПереоценки + NN], 'DD/MM/YYYY') ,'') // 12

  if((Массив_ИК_КоличествоПереоценок [НомерИК] >= NN) and (Массив_Тип_операции [НомерПереоценки + NN] = 6),
     iExcelFormat.DoubleToStrFormatExcel (Массив_Переоценка_коэффициент_пересчета [НомерПереоценки + NN], 4) ,'') // 13

  if (Массив_ИК_КоличествоПереоценок [НомерИК] >= NN,
    iExcelFormat.DoubleToStrFormatExcel (Массив_Переоценка_Восстановительная_стоимость [НомерПереоценки + NN]
                * if (DuplicateRecordInGroupIK, 1, Массив_ИК_Количество [НомерИК]), 2)
    , '') // 14

  // ***** Вывод первого перемещенрия или выбытия *****
  if(Массив_ИК_КоличествоПеремещенийВыбытий [НомерИК] >= NN,
    Массив_ПеремещениеВыбытие_документ_дата_номер      [НомерПеремещенияВыбытия + NN], '') // 15

  if(Массив_ИК_КоличествоПеремещенийВыбытий [НомерИК] >= NN,
    Массив_ПеремещениеВыбытие_Стуктурное_подразделение [НомерПеремещенияВыбытия + NN], '') // 16


  if(Массив_ИК_КоличествоПеремещенийВыбытий [НомерИК] >= NN,
    Массив_ПеремещениеВыбытие_Ответственное_лицо       [НомерПеремещенияВыбытия + NN], '') // 17

  if(Массив_ИК_КоличествоПеремещенийВыбытий [НомерИК] >= NN,
    Массив_ПеремещениеВыбытие_Причина_Выбытия          [НомерПеремещенияВыбытия + NN], '') // 18
.endfields
!  │                     ├──────────┼──────────────┼─────────────────────┤                     │                                             │                                             │                                  │
   │                     │@@@@@@@@@@│&&&&&&&.&&&&&&│&&&&&&&&&&&&&&&&&&.&&│@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│
.}
.begin
  СтрокаN := 1;
end.
.{While (СтрокаN <= Массив_ИК_Количество_доп_строк_в_Таблице2 [НомерИК])
   │                     │          │              │                     │                     │                                             │                                             │                                  │
.begin
  СтрокаN := СтрокаN + 1;
end.
.}
.begin
  if (DuplicateRecordInGroupIK)
  {
    if (КолНомер_в_ИК >= Массив_ИК_Количество [НомерИК])
    {
      НомерПереоценки := НомерПереоценки + Массив_ИК_КоличествоПереоценок [НомерИК];

      НомерПеремещенияВыбытия := НомерПеремещенияВыбытия + Массив_ИК_КоличествоПеремещенийВыбытий [НомерИК];

      НомерИК := НомерИК + 1;
      КолНомер_в_ИК := 0;
    }
  }
  else
  {
    НомерПереоценки := НомерПереоценки + Массив_ИК_КоличествоПереоценок [НомерИК];

    НомерПеремещенияВыбытия := НомерПеремещенияВыбытия + Массив_ИК_КоличествоПеремещенийВыбытий [НомерИК];

    НомерИК := НомерИК + 1;
  }
end.
.[f
   └─────────────────────┴──────────┴──────────────┴─────────────────────┴─────────────────────┴─────────────────────────────────────────────┴─────────────────────────────────────────────┴──────────────────────────────────┘
.]f
.}
 Ш
.} // Конец формы
.endform
