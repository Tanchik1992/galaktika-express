.linkform 'Form_OS_06a_Rus' prototype is 'FormOS6a' // FormGroupOS6a
.NameInList 'Форма N ОС-6а << Россия >>'
.group 'ОС-6а <<Россия>>'
.hide
.defo landscape
.p 62
.var
  // ***** Объявление вспомогательных переменных *****

  НомерИК         : word;                                                     // 01 - Текущий номер элемента в цикле состава
  НомерИК_Max     : word;                                                     //      Количество ИК
  НомерПоПорядку  : word;

  НомерПереоценки : word;                                                     // Номер текущей переоценки

  NN              : word;

  КолНомер_в_ИК   : word;

  // ***** Переменные проверки отличий общих полей *****
  Flag                                : boolean;
  Flag_Счет                           : boolean;
  Flag_СубСчет                        : boolean;
! Flag_Код_аналитического_учета       : boolean;
  Flag_Номер_амортизационной_группы   : boolean;

  Flag_Структурное_подразделение      : boolean;
  Flag_Материально_ответственное_лицо : boolean;

  // ***** Объявление массивов *****

  // ***** Данные по таблице *****
  Массив_ИК_Количество                               : array[1..2] of double; // Количество в конкретной ИК

  Массив_ИК_Наименование                             : array[1..2] of string; // 02  Наименование объекта основных средств
  Массив_ИК_Инвентарный_номер                        : array[1..2] of string; // 03  Инвентарный номер
  Массив_ИК_Код_ОКОФ                                 : array[1..2] of string; // 04  Код по ОКОФ
  Массив_ИК_Дата_выпуска                             : array[1..2] of date;   // 05  Дата выпуска постройки
                                                                              //     Поступил в организацию
! Массив_ИК_Поступления_документ                     : array[1..2] of string; // 06  документ,
  Массив_ИК_Поступления_дата                         : array[1..2] of date;   //     дата
  Массив_ИК_Поступления_номер                        : array[1..2] of string; //     номер

  Массив_ИК_Дата_принятия_к_бух_учету                : array[1..2] of date;   // 07  дата принятия к бухгалтерскому учету
  Массив_ИК_Первоначальная_стоимость                 : array[1..2] of double; // 08  Первоначальная стоимость на дату принятия к бухгалтерскому учету
  Массив_ИК_Срок_полезного_использования             : array[1..2] of string; // 09  Срок полезного использования
  Массив_ИК_Сумма_начисленной_амортизации            : array[1..2] of double; // 10  Сумма начисленной амортизации износа
  Массив_ИК_Остаточная_стоимость                     : array[1..2] of double; // 11  Остаточная стоимость

                                                                              //     Выбыл (передан)
! Массив_ИК_Выбытия_документ                         : array[1..2] of string; // 15  документ,
  Массив_ИК_Выбытия_дата                             : array[1..2] of date;   //     дата
  Массив_ИК_Выбытия_номер                            : array[1..2] of string; //     номер

  Массив_ИК_Причина_выбытия                          : array[1..2] of string; // 16  причина выбытия

  Массив_ИК_КоличествоПереоценок                     : array[1..2] of word;

  // ***** Цикл по переоценке *****
  Массив_Переоценка_Дата                             : array[1..2] of date;   // 12
  Массив_Тип_операции                                : array[1..2] of word;
  Массив_Переоценка_коэффициент_пересчета_переоценки : array[1..2] of double; // 13
  Массив_Переоценка_восстановительная_стоимость      : array[1..2] of double; // 14

.endvar
#include funcOs.frn
end.  // funcOs.frn - вынес из файла чтобы не создавалась пустая строка перед формой
.begin

  // ***** Обнуление переменных *****

  НомерИК         := 0;
  НомерИК_Max     := 0;
  НомерПереоценки := 0;
  КолНомер_в_ИК   := 0;
  НомерПоПорядку  := 0;

  // ***** Обнуление переменных проверки отличий общих полей *****
  Flag                                := true;
  Flag_Счет                           := true;
  Flag_СубСчет                        := true;
! Flag_Код_аналитического_учета       := true;
  Flag_Номер_амортизационной_группы   := true;

  Flag_Структурное_подразделение      := true;
  Flag_Материально_ответственное_лицо := true;

  // ***** Обнуление массивов по таблице *****
  for (NN := 1; NN <= Count(Массив_ИК_Наименование); NN := NN + 1)
  {
    Массив_ИК_Количество                               [NN] := 0;

    Массив_ИК_Наименование                             [NN] := '';            // 02  Наименование объекта основных средств
    Массив_ИК_Инвентарный_номер                        [NN] := '';            // 03  Инвентарный номер
    Массив_ИК_Код_ОКОФ                                 [NN] := '';            // 04  Код по ОКОФ
    Массив_ИК_Дата_выпуска                             [NN] := date (0,0,0);  // 05  Дата выпуска постройки
                                                                              //     Поступил в организацию
!   Массив_ИК_Поступления_документ                     [NN] := '';            // 06  документ,
    Массив_ИК_Поступления_дата                         [NN] := date (0,0,0);  //     дата
    Массив_ИК_Поступления_номер                        [NN] := '';            //     номер

    Массив_ИК_Дата_принятия_к_бух_учету                [NN] := date (0,0,0);  // 07  дата принятия к бухгалтерскому учету
    Массив_ИК_Первоначальная_стоимость                 [NN] := 0;             // 08  Первоначальная стоимость на дату принятия к бухгалтерскому учету
    Массив_ИК_Срок_полезного_использования             [NN] := '';            // 09  Срок полезного использования
    Массив_ИК_Сумма_начисленной_амортизации            [NN] := 0;             // 10  Сумма начисленной амортизации износа
    Массив_ИК_Остаточная_стоимость                     [NN] := 0;             // 11  Остаточная стоимость

!   Массив_ИК_Выбытия_документ                         [NN] := '';            // 15  документ,
    Массив_ИК_Выбытия_дата                             [NN] := date (0,0,0);  //     дата
    Массив_ИК_Выбытия_номер                            [NN] := '';            //     номер

    Массив_ИК_Причина_выбытия                          [NN] := '';            // 16  причина выбытия

    Массив_ИК_КоличествоПереоценок                     [NN] := 0;             //     Количество переоценок для данной ИК
  }

  // ***** Обнуление массивов переоценок *****
  for (NN := 1; NN <= Count(Массив_Переоценка_Дата); NN := NN + 1)
  {
    Массив_Переоценка_Дата                             [NN] := date (0,0,0);  // 12
    Массив_Тип_операции                                [NN] := 0;
    Массив_Переоценка_коэффициент_пересчета_переоценки [NN] := 0;             // 13
    Массив_Переоценка_восстановительная_стоимость      [NN] := 0;             // 14
  }
end.
.{
! ***** Заполнение массивов *****
.{ CheckEnter CycleByIK
.begin

  НомерИК     := НомерИК + 1;
  НомерИК_Max := НомерИК_Max + 1;

  // ***** Проверка отличий общих полей *****
  if (Общий_Счет <> Счет)
  {
    Flag := false;
    Flag_Счет := false;
  }

  if (Общий_СубСчет <> СубСчет)
  {
    Flag := false;
    Flag_СубСчет := false;
  }

! if (Общий_Код_аналитического_учета <> Код_аналитического_учета)
! {
!   Flag := false;
!   Flag_Код_аналитического_учета := false;
! }

  if (Общий_Номер_амортизационной_группы <> Номер_амортизационной_группы)
  {
    Flag := false;
    Flag_Номер_амортизационной_группы := false;
  }

  if (Общее_Структурное_подразделение_nRec <> Структурное_подразделение_nRec)
  {
    Flag := false;
    Flag_Структурное_подразделение := false;
  }

  if (Общее_Материально_ответственное_лицо_nRec <> Материально_ответственное_лицо_nRec)
  {
    Flag := false;
    Flag_Материально_ответственное_лицо := false;
  }

  // ***** Заполнение массивов *****

  Массив_ИК_Количество                    [НомерИК] := Количество;

  Массив_ИК_Наименование                  [НомерИК] := Наименование;                                    // 02  Наименование объекта основных средств
  Массив_ИК_Инвентарный_номер             [НомерИК] := Инвентарный_номер;                               // 03  Инвентарный номер
  Массив_ИК_Код_ОКОФ                      [НомерИК] := Код_ОКОФ;                                        // 04  Код по ОКОФ
  Массив_ИК_Дата_выпуска                  [НомерИК] := Дата_выпуска;                                    // 05  Дата выпуска постройки
                                                                                                        //     Поступил в организацию
! Массив_ИК_Поступления_документ          [НомерИК] := Поступления_документ;                            // 06  документ,
  Массив_ИК_Поступления_дата              [НомерИК] := Поступления_дата;                                //     дата
  Массив_ИК_Поступления_номер             [НомерИК] := Поступления_номер;                               //     номер

  Массив_ИК_Дата_принятия_к_бух_учету     [НомерИК] := Дата_принятия_к_бух_учету;                       // 07  дата принятия к бухгалтерскому учету
  Массив_ИК_Первоначальная_стоимость      [НомерИК] := Первоначальная_стоимость;                        // 08  Первоначальная стоимость на дату принятия к бухгалтерскому учету
  Массив_ИК_Срок_полезного_использования  [НомерИК] := SrokIspEdIzmWord (Срок_полезного_использования); // 09  Срок полезного использования
  Массив_ИК_Сумма_начисленной_амортизации [НомерИК] := Сумма_начисленной_амортизации;                   // 10  Сумма начисленной амортизации износа
  Массив_ИК_Остаточная_стоимость          [НомерИК] := Остаточная_стоимость;                            // 11  Остаточная стоимость

! Массив_ИК_Выбытия_документ              [НомерИК] := Выбытия_документ;                                // 15  документ,
  Массив_ИК_Выбытия_дата                  [НомерИК] := Выбытия_дата;                                    //     дата
  Массив_ИК_Выбытия_номер                 [НомерИК] := Выбытия_номер;                                   //     номер

  Массив_ИК_Причина_выбытия               [НомерИК] := Причина_выбытия;                                 // 16  причина выбытия
end.
.{ CheckEnter CycleByOverAppreciated
.begin
  Массив_ИК_КоличествоПереоценок          [НомерИК] := Массив_ИК_КоличествоПереоценок [НомерИК] + 1;

  НомерПереоценки := НомерПереоценки + 1;

  Массив_Переоценка_Дата                             [НомерПереоценки] := Переоценка_дата;                            // 12
  Массив_Тип_операции                                [НомерПереоценки] := Тип_операции;
  Массив_Переоценка_коэффициент_пересчета_переоценки [НомерПереоценки] := Переоценка_коэффициент_пересчета_переоценки;// 13
  Массив_Переоценка_восстановительная_стоимость      [НомерПереоценки] := Переоценка_восстановительная_стоимость;     // 14
end.
.}
.{ CheckEnter CycleByAmortization
.}
.{ CheckEnter CycleByOperation
.}
.}
! ***** Вывод сообщения о отличий общих полей *****
.begin
  if (not Flag)
  {
    Message ('Значения в карточках для полей:'#13 +
             if (not Flag_Счет                          , 'Счет,'#13                                            , '') +
             if (not Flag_СубСчет                       , 'СубСчет,'#13                                         , '') +
!            if (not Flag_Код_аналитического_учета      , 'Код аналитического учета,'#13                        , '') +
             if (not Flag_Номер_амортизационной_группы  , 'Номер амортизационной группы,'#13                    , '') +
             if (not Flag_Структурное_подразделение     , 'Структурное подразделение,'#13                       , '') +
             if (not Flag_Материально_ответственное_лицо, 'Материально ответственное лицо,'#13                  , '') +
             'отличаются.'                              , Information+CancelButton);
  }
end.
! ***** Вывод в форму *****
.fields
  // ***** Общая информация *****

  '(Метод учета: ' +Метод_Учета +')'

  Наименование_организации                                                               // Организация
  ОКПО

  if (Flag_Структурное_подразделение     , Общее_Структурное_подразделение       , '')   // наименование структурного подразделения

  if (Flag_Счет, Общий_Счет + if (Flag_СубСчет, ' ' + Общий_СубСчет, '')         , '')   // + ' ' +
! if (Flag_Код_аналитического_учета      , Общий_Код_аналитического_учета        , '')

  if (Flag_Номер_амортизационной_группы  , Общий_Номер_амортизационной_группы    , '')

  Номер_документа
  Дата_составления

  if (Flag_Структурное_подразделение     , Общее_Структурное_подразделение       , '')   // Структурное подразделение получателя
  if (Flag_Материально_ответственное_лицо, Общее_Материально_ответственное_лицо  , '')
  if (Flag_Материально_ответственное_лицо, Общий_Табельный_номер                 , '')

.endfields
 Ш@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                                                                                            Унифицированная форма № ОС-6а

                                                                                                                                                                                                                    Утверждена
                                                                                                                                                                                                                Постановлением
                                                                                                                                                                                                            Госкомстата России
                                                                                                                                                                                                             от 21.01.2003 № 7

                                                                                                                                                                                                                 ┌────────────┐
                                                                                                                                                                                                                 │     Код    │
                                                                                                                                                                                                                 ├────────────┤
                                                                                                                                                                                                   Форма по ОКУД │   0306034  │
                                                                                                                                                                                                                 ├────────────┤
    Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е       по ОКПО │@~@@@@@@@@@@│
                                                                                       наименование организации                                                                                                  │            │
    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д               ├────────────┤
                                                                               наименование структурного подразделения                                                                                           │            │
                                                                                                                                                                                                                 ├────────────┤
                                                                                                                                                                         Счет, субсчет, код аналитического учета │@~@@@@@@@@@@│
                                                                                                                                                                                                                 ├────────────┤
                                                                                                                                                                                    Номер амортизационной группы │@~@@@@@@@@@@│
                                                                                                                                                                                                                 └────────────┘

                                                     ┌──────────────────────────┬──────────────────┐
                                                     │      Номер документа     │ Дата составления │
                 ИНВЕНТАРНАЯ КАРТОЧКА                ├──────────────────────────┼──────────────────┤
                                                     │ @~@@@@@@@@@@@@@@@@@@@@@@ │ @~@@@@@@@@@@@@@@ │
     ГРУППОВОГО УЧЕТА ОБЪЕКТОВ ОСНОВНЫХ СРЕДСТВ      └──────────────────────────┴──────────────────┘


                                                                                                                                                                                                                 ┌────────────┐
   Структурное подразделение получателя  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д│            │
                                                                                                                       наименование                                                                              ├────────────┤
   Материально ответственное лицо  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д Табельный номер │@~@@@@@@@@@@│
                                                                                                         фамилия, инициалы                                                                                       └────────────┘

   ┌──────┬────────────────┬───────────────┬─────────┬──────────┬───────────────────────────┬────────────────┬───────────────┬────────────────┬────────────────┬─────────────────────────────────────┬────────────────────────┐
   │Номер │  Наименование  │  Инвентарный  │   Код   │   Дата   │  Поступил в организацию   │ Первоначальная │     Срок      │      Сумма     │   Остаточная   │              Переоценка             │     Выбыл (передан)    │
   │по по-│     объекта    │     номер     │ по ОКОФ │ выпуска  ├────────────────┬──────────┤  стоимость на  │   полезного   │   начисленной  │   стоимость,   ├──────────┬─────────┬────────────────┼──────────┬─────────────┤
   │рядку │    основных    │               │         │(построй- │    документ,   │   дата   │ дату принятия к│    исполь-    │   амортизации  │      руб.      │   дата   │ коэффи- │   восстанови-  │ документ,│   причина   │
   │      │     средств    │               │         │   ки)    │   дата, номер  │принятия к│ бухгалтерскому │    зования    │    (износа),   │                │          │  циент  │    тельная     │   дата,  │   выбытия   │
   │      │                │               │         │          │                │бухгалтер-│   учету, руб.  │               │       руб.     │                │          │пересчета│   стоимость,   │   номер  │             │
   │      │                │               │         │          │                │  скому   │                │               │                │                │          │ (пере-  │      руб.      │          │             │
   │      │                │               │         │          │                │  учету   │                │               │                │                │          │ оценки) │                │          │             │
   ├──────┼────────────────┼───────────────┼─────────┼──────────┼────────────────┼──────────┼────────────────┼───────────────┼────────────────┼────────────────┼──────────┼─────────┼────────────────┼──────────┼─────────────┤
   │   1  │        2       │       3       │    4    │     5    │        6       │     7    │        8       │       9       │       10       │       11       │    12    │   13    │       14       │    15    │     16      │
! ***** Цикл по ИК *****
.begin
  НомерИК         := 1;
  НомерПоПорядку  := 0;
  НомерПереоценки := 0;
  КолНомер_в_ИК   := 0;
end.
.{While (НомерИК <= НомерИК_Max)
.[h skip
                                                                                                                                                                                              Лист @np@
                                                                                                                                                                                              Оборотная сторона формы № ОС-6а
   ┌──────┬────────────────┬───────────────┬─────────┬──────────┬───────────────────────────┬────────────────┬───────────────┬────────────────┬────────────────┬─────────────────────────────────────┬────────────────────────┐
   │Номер │  Наименование  │  Инвентарный  │   Код   │   Дата   │  Поступил в организацию   │ Первоначальная │     Срок      │      Сумма     │   Остаточная   │              Переоценка             │     Выбыл (передан)    │
   │по по-│     объекта    │     номер     │ по ОКОФ │ выпуска  ├────────────────┬──────────┤  стоимость на  │   полезного   │   начисленной  │   стоимость,   ├──────────┬─────────┬────────────────┼──────────┬─────────────┤
   │рядку │    основных    │               │         │(построй- │    документ,   │   дата   │ дату принятия к│    исполь-    │   амортизации  │      руб.      │   дата   │ коэффи- │   восстанови-  │ документ,│   причина   │
   │      │     средств    │               │         │   ки)    │   дата, номер  │принятия к│ бухгалтерскому │    зования    │    (износа),   │                │          │  циент  │    тельная     │   дата,  │   выбытия   │
   │      │                │               │         │          │                │бухгалтер-│   учету, руб.  │               │       руб.     │                │          │пересчета│   стоимость,   │   номер  │             │
   │      │                │               │         │          │                │  скому   │                │               │                │                │          │ (пере-  │      руб.      │          │             │
   │      │                │               │         │          │                │  учету   │                │               │                │                │          │ оценки) │                │          │             │
   ├──────┼────────────────┼───────────────┼─────────┼──────────┼────────────────┼──────────┼────────────────┼───────────────┼────────────────┼────────────────┼──────────┼─────────┼────────────────┼──────────┼─────────────┤
   │   1  │        2       │       3       │    4    │     5    │        6       │     7    │        8       │       9       │       10       │       11       │    12    │   13    │       14       │    15    │     16      │
   ├──────┼────────────────┼───────────────┼─────────┼──────────┼────────────────┼──────────┼────────────────┼───────────────┼────────────────┼────────────────┼──────────┼─────────┼────────────────┼──────────┼─────────────┤
.]h
   ├──────┼────────────────┼───────────────┼─────────┼──────────┼────────────────┼──────────┼────────────────┼───────────────┼────────────────┼────────────────┼──────────┼─────────┼────────────────┼──────────┼─────────────┤
.begin
  НомерПоПорядку := НомерПоПорядку + 1;
  КолНомер_в_ИК := КолНомер_в_ИК + 1;
  NN := 1;
end.
.fields
  // ***** Вывод информаци по карточке *****
  НомерПоПорядку                                                                                                    // 01
  Массив_ИК_Наименование                  [НомерИК]                                                                 // 02
  Массив_ИК_Инвентарный_номер             [НомерИК]
    + if (AddNumIKToInvNum and DuplicateRecordInGroupIK and (Массив_ИК_Количество [НомерИК] > 1), ' / ' + КолНомер_в_ИК, '') // 03
  Массив_ИК_Код_ОКОФ                      [НомерИК]                                                                 // 04
  if (longint(Массив_ИК_Дата_выпуска [НомерИК]) <> 0, DateToStr(Массив_ИК_Дата_выпуска [НомерИК],'DD/MM/YYYY'), '') // 05

  // Поступил в организацию                                                                                         // 06
! if ( Массив_ИК_Поступления_документ     [НомерИК]  <> '' , ' '     + Массив_ИК_Поступления_документ [НомерИК] , '') +
  if ( longint(Массив_ИК_Поступления_дата [НомерИК]) <> 0  , DateToStr(Массив_ИК_Поступления_дата     [НомерИК] , 'DD/MM/YYYY') , '') +
  if ( Массив_ИК_Поступления_номер        [НомерИК]  <> '' , ' '     + Массив_ИК_Поступления_номер    [НомерИК] , '')

  if ( longint(Массив_ИК_Дата_принятия_к_бух_учету [НомерИК]) <> 0, DateToStr( Массив_ИК_Дата_принятия_к_бух_учету [НомерИК], 'DD/MM/YYYY') , '')// 07
  Массив_ИК_Первоначальная_стоимость      [НомерИК] * if (DuplicateRecordInGroupIK, 1, Массив_ИК_Количество [НомерИК]) // 08
  Массив_ИК_Срок_полезного_использования  [НомерИК]                                                                    // 09
  Массив_ИК_Сумма_начисленной_амортизации [НомерИК] * if (DuplicateRecordInGroupIK, 1, Массив_ИК_Количество [НомерИК]) // 10
  Массив_ИК_Остаточная_стоимость          [НомерИК] * if (DuplicateRecordInGroupIK, 1, Массив_ИК_Количество [НомерИК]) // 11

  // ***** Вывод первой переоценки *****
  if (Массив_ИК_КоличествоПереоценок [НомерИК] <> 0, DateToStr(Массив_Переоценка_Дата [НомерПереоценки + NN], 'DD/MM/YYYY') ,'') // 12
  if((Массив_ИК_КоличествоПереоценок [НомерИК] <> 0) and (Массив_Тип_операции [НомерПереоценки + NN] = 6),
     iExcelFormat.DoubleToStrFormatExcel (Массив_Переоценка_коэффициент_пересчета_переоценки [НомерПереоценки + NN], 4) ,'') // 13
  if (Массив_ИК_КоличествоПереоценок [НомерИК] <> 0, DoubleToStr(Массив_Переоценка_восстановительная_стоимость [НомерПереоценки + NN] * if (DuplicateRecordInGroupIK, 1, Массив_ИК_Количество [НомерИК]), efSumFormatExcel), '') // 14

  // Выбыл (передан)                                                                                                // 15
! if ( Массив_ИК_Выбытия_документ         [НомерИК]  <> '' , ' '     + Массив_ИК_Выбытия_документ     [НомерИК] , '') +
  if ( longint(Массив_ИК_Выбытия_дата     [НомерИК]) <> 0  , DateToStr(Массив_ИК_Выбытия_дата         [НомерИК] , 'DD/MM/YYYY') , '') +
  if ( Массив_ИК_Выбытия_номер            [НомерИК]  <> '' , ' '     + Массив_ИК_Выбытия_номер        [НомерИК] , '')

  Массив_ИК_Причина_выбытия               [НомерИК]                                                                 // 16
.endfields
   │&&&&&&│@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@│@@@@@@@@@│@@@@@@@@@@│@@@@@@@@@@@@@@@@│@@@@@@@@@@│&&&&&&&&&&&&&.&&│@@@@@@@@@@@@@@@│&&&&&&&&&&&&&.&&│&&&&&&&&&&&&&.&&│@@@@@@@@@@│&&&&&&&&&│&&&&&&&&&&&&&.&&│@@@@@@@@@@│@@@@@@@@@@@@@│
! ***** Цикл по переоценке *****
.{While (NN < Массив_ИК_КоличествоПереоценок [НомерИК])
.begin
 NN := NN + 1;
end.
.fields
  if (Массив_ИК_КоличествоПереоценок [НомерИК] <> 0, DateToStr(Массив_Переоценка_Дата [НомерПереоценки + NN], 'DD/MM/YYYY') ,'') // 12
  if((Массив_ИК_КоличествоПереоценок [НомерИК] <> 0) and (Массив_Тип_операции [НомерПереоценки + NN] = 6),
     iExcelFormat.DoubleToStrFormatExcel (Массив_Переоценка_коэффициент_пересчета_переоценки [НомерПереоценки + NN], 4) ,'') // 13
  if (Массив_ИК_КоличествоПереоценок [НомерИК] <> 0, DoubleToStr(Массив_Переоценка_восстановительная_стоимость [НомерПереоценки + NN] * if (DuplicateRecordInGroupIK, 1, Массив_ИК_Количество [НомерИК]), efSumFormatExcel), '') // 14
.endfields
!  │      │                │               │         │          │                │          │                │               │                │                ├──────────┼─────────┼────────────────┤          │             │
   │      │                │               │         │          │                │          │                │               │                │                │@@@@@@@@@@│&&&&&&&&&│&&&&&&&&&&&&&.&&│          │             │
.}
.begin
  if (DuplicateRecordInGroupIK)
  {
    if (КолНомер_в_ИК >= Массив_ИК_Количество [НомерИК])
    {
      НомерПереоценки := НомерПереоценки + Массив_ИК_КоличествоПереоценок [НомерИК];
      НомерИК := НомерИК + 1;
      КолНомер_в_ИК := 0;
    }
  }
  else
  {
    НомерПереоценки := НомерПереоценки + Массив_ИК_КоличествоПереоценок [НомерИК];
    НомерИК := НомерИК + 1;
  }
end.
.[f
   └──────┴────────────────┴───────────────┴─────────┴──────────┴────────────────┴──────────┴────────────────┴───────────────┴────────────────┴────────────────┴──────────┴─────────┴────────────────┴──────────┴─────────────┘
.]f
.}

.fields
  ЛОзаВК_Должность
  ЛОзаВК
.endfields
   Лицо, ответственное за ведение инвентарной карточки  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д    __________________________      Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                                                     должность                        подпись                                 расшифровка подписи
 Ш
.} // Конец формы
.endform
