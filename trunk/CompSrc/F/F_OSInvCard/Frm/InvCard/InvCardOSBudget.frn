.linkform 'InvCardOSBudget' prototype is 'FormOS6'
.NameInList 'Инвентарная карточка (для бюджетных организаций) <<Россия>>'
!.group 'OS_Form6_Rus'
.group 'Бюджетная организация'
.group 'Бюджетная организация НМА'
.hide
.defo landscape
.P 60
.var
  NN   : word;
  NMax : word;
  bWriteAmortInYear                : boolean;               // Выводить амортизацию за год, или за месяц.
  boFormOS6Amortization            : boolean;
  // параметры диалога
  wWriteAmortInYear                : word;
  wWriteAllIzmAmort                : word;
  iDialog                          : longint;

  Количество_Номеров_1             : word;
  Массив_Номеров_1                 : array[1..2] of string;

  Количество_Номеров_3             : word;
  Массив_Номеров_3                 : array[1..2] of string;

  Количество_Дата_Номер_Ввода      : word;
  Массив_Дата_и_Номер_Ввода        : array[1..2] of string;

  Количество_Амортизаци            : word;
  Массив_Амортизация_Дата          : array[1..2] of date;
  Массив_Амортизация_СПИ           : array[1..2] of double;
  Массив_Амортизация_За_Месяц      : array[1..2] of double; // 09
  Массив_Амортизация_За_Год        : array[1..2] of double; // 10
  Массив_Амортизация_Всего         : array[1..2] of double; // 11
  Массив_Амортизация_ТипОперации   : array[1..2] of word;
  Массив_Амортизация_Коэф          : array[1..2] of double;

  wOSSrokIspEdIzm                  : word // Равно значению настройки wGetTune('OS.SrokIspEdIzm'). Для того чтобы не читать каждый раз её из базы...

  КоличествоМесяцев                : word;
  ГодПредыдущегоНачисления         : word;

  Начислено_с_начала_эксплуатации  : double;                 // 11
  Сумма_За_Год                     : double;                 // 10

  bНеОтображатьНули                : boolean;

  Количество_Изменений             : word;
  Массив_Изменений_Дата            : array[1..2] of date;
  Массив_Изменений_Сумма           : array[1..2] of double;

  Количество_Переоценок            : word;
  Массив_Переоценок_Дата           : array[1..2] of date;
  Массив_Переоценок_Сумма          : array[1..2] of double;

  Количество_Характеристик         : word;
  Массив_Характеристика            : array[1..2] of string;

  Количество_ДрагМеталлов          : word;
  Массив_ДрагМеталлов_Наименование : array[1..2] of string;
  Массив_ДрагМеталлов_ЕдИзм        : array[1..2] of string;
  Массив_ДрагМеталлов_Код          : array[1..2] of string;
  Массив_ДрагМеталлов_Масса        : array[1..2] of double;

  Количество_Перемещений           : word;
  Массив_Перемещений_Дата          : array[1..2] of date;
  Массив_Перемещений_Номер         : array[1..2] of string;
  Массив_Перемещений_Подразделение : array[1..2] of string;
  Массив_Перемещений_МОЛ           : array[1..2] of string;

  Количество_Выбытий               : word;
  Массив_Выбытий_Дата              : array[1..2] of date;
  Массив_Выбытий_Причина           : array[1..2] of string;

!  Количество_Составляющих          : word;
!  Массив_Наименование_Составляющей : array[1..2] of string;
.endvar
#include funcOs.frn
end.  // funcOs.frn - вынес из файла чтобы не создавалась пустая строка перед формой
.begin
  wWriteAmortInYear := 0;
  wWriteAllIzmAmort := 0;
  iDialog :=0;
  RunDialog(DialogInvCardOSBudget, wWriteAmortInYear, wWriteAllIzmAmort, iDialog);
  bWriteAmortInYear := Boolean(wWriteAmortInYear);
end.
.{
.begin
  // Обнуление переменных
  NN   := 0;
  NMax := 0;
  Количество_Номеров_1     := 0;
  Количество_Номеров_3     := 0;
  Количество_Дата_Номер_Ввода := 0;
  Количество_Амортизаци    := 0;
  Количество_Изменений     := 0;
  Количество_Переоценок    := 0;
  Количество_Характеристик := 0;
  Количество_ДрагМеталлов  := 0;
! Количество_Составляющих  := 0;
  Количество_Перемещений   := 0;
  Количество_Выбытий       := 0;
  Начислено_с_начала_эксплуатации := double(0);

  wOSSrokIspEdIzm := wGetTune('OS.SrokIspEdIzm'); // в месяцах = 0, в годах = 1

  // Обнуление массивов
  for (NN := 1; NN <= Count(Массив_Номеров_1); NN := NN + 1)
  {
    Массив_Номеров_1 [NN] := '';
  }

  for (NN := 1; NN <= Count(Массив_Номеров_3); NN := NN + 1)
  {
    Массив_Номеров_3 [NN] := '';
  }

  for (NN := 1; NN <= Count(Массив_Дата_и_Номер_Ввода); NN := NN + 1)
  {
    Массив_Дата_и_Номер_Ввода [NN] := '';
  }

  for (NN := 1; NN <= Count(Массив_Амортизация_Дата); NN := NN + 1)
  {
    Массив_Амортизация_Дата      [NN] := date(0,0,0);
    Массив_Амортизация_СПИ       [NN] := double(0);
    Массив_Амортизация_За_Месяц  [NN] := double(0);
    Массив_Амортизация_За_Год    [NN] := double(0);
    Массив_Амортизация_Всего     [NN] := double(0);
    Массив_Амортизация_ТипОперации[NN]:= word(0);
    Массив_Амортизация_Коэф       [NN]:= double(0);
  }

  for (NN := 1; NN <= Count(Массив_Изменений_Дата); NN := NN + 1)
  {
    Массив_Изменений_Дата  [NN] := date(0,0,0);
    Массив_Изменений_Сумма [NN] := double(0);
  }

  for (NN := 1; NN <= Count(Массив_Переоценок_Дата); NN := NN + 1)
  {
    Массив_Переоценок_Дата  [NN] := date(0,0,0);
    Массив_Переоценок_Сумма [NN] := double(0);
  }

  for (NN := 1; NN <= Count(Массив_ДрагМеталлов_Наименование); NN := NN + 1)
  {
    Массив_ДрагМеталлов_Наименование [NN] := '';
    Массив_ДрагМеталлов_ЕдИзм        [NN] := '';
    Массив_ДрагМеталлов_Код          [NN] := '';
    Массив_ДрагМеталлов_Масса        [NN] := double(0);
  }

  for (NN := 1; NN <= Count(Массив_Характеристика); NN := NN + 1)
  {
    Массив_Характеристика [NN] := '';
  }

!  for (NN := 1; NN <= Count(Массив_Наименование_Составляющей); NN := NN + 1)
!  {
!    Массив_Наименование_Составляющей [NN] := '';
!  }

  for (NN := 1; NN <= Count(Массив_Перемещений_Дата); NN := NN + 1)
  {
    Массив_Перемещений_Дата          [NN] := date(0,0,0);
    Массив_Перемещений_Номер         [NN] := '';
    Массив_Перемещений_Подразделение [NN] := '';
    Массив_Перемещений_МОЛ           [NN] := '';
  }

  for (NN := 1; NN <= Count(Массив_Выбытий_Дата); NN := NN + 1)
  {
    Массив_Выбытий_Дата              [NN] := date(0,0,0);
    Массив_Выбытий_Причина           [NN] := '';
  }


  // * Заполнение массивов номеров
  /*
  1. Инвентарный номер     - Инвентарный_Номер     - loOS6.KatOs.InNum
  2. Номер Паспорта        - Номер_Паспорта        - loOS6.KatOs.PsNum
  3. Заводской Номер       - Заводской_Номер       - loOS6.KatOs.ZavNom
  4. Модель, тип, марка    - Тип_Марка             - loOS6.KatOs.TipMark

  5. Номенклатурный номер  - Номенклатурный_Номер  - loOS6.KatOs.BarKod
  6. Гаражный номер        - Гаражный_Номер        - loOS6.Transp.GarNom
  7. Регистрационный номер - Регистрационный_Номер - loOS6.KatOs.REGNUM
  8. Имущественный номер   - Имущественный_Номер   - loOS6.KatOs.DOPINNUM
  */

  // * Номера колонки 1
  // Консультация АЦ:
  // В системе поле: "Модель, тип, марка" надо использовать для заполнения этой графы, и
  //   Паспорт, свидетельство чертеж (проект) пусть заполняют в этом же поле
  if (Номер_Паспорта <> '')
  {
    Количество_Номеров_1 := Количество_Номеров_1 + 1;
    Массив_Номеров_1 [Количество_Номеров_1] := Номер_Паспорта;
  }

  if (Тип_Марка <> '')
  {
    Количество_Номеров_1 := Количество_Номеров_1 + 1;
    Массив_Номеров_1 [Количество_Номеров_1] := Тип_Марка;
  }

  // * Номера колонки 3
  // Консультация АЦ: все заполненные в столбик:
  //   номенклатурный, гаражный, регистрационный, имущественный,
  //   номер паспорта (но т.к. номер паспорта уже выводим в графе 1, не дублируем)
  if (Номенклатурный_Номер <> '')
  {
    Количество_Номеров_3 := Количество_Номеров_3 + 1;
    Массив_Номеров_3 [Количество_Номеров_3] := Номенклатурный_Номер;
  }

  if (Гаражный_Номер <> '')
  {
    Количество_Номеров_3 := Количество_Номеров_3 + 1;
    Массив_Номеров_3 [Количество_Номеров_3] := Гаражный_Номер;
  }

  if (Регистрационный_Номер <> '')
  {
    Количество_Номеров_3 := Количество_Номеров_3 + 1;
    Массив_Номеров_3 [Количество_Номеров_3] := Регистрационный_Номер;
  }

  if (Имущественный_Номер <> '')
  {
    Количество_Номеров_3 := Количество_Номеров_3 + 1;
    Массив_Номеров_3 [Количество_Номеров_3] := Имущественный_Номер;
  }

  // * Дата и номер ввода
  if (iExcelFormat.DateStandartToStrFormatExcelNotNul(Дата_Документа_Ввода) <> '')
  {
    Количество_Дата_Номер_Ввода := Количество_Дата_Номер_Ввода + 1;
    Массив_Дата_и_Номер_Ввода [Количество_Дата_Номер_Ввода] := iExcelFormat.DateStandartToStrFormatExcelNotNul(Дата_Документа_Ввода);
  }

  if (Номер_Документа_Ввода <> '')
  {
    Количество_Дата_Номер_Ввода := Количество_Дата_Номер_Ввода + 1;
    Массив_Дата_и_Номер_Ввода [Количество_Дата_Номер_Ввода] := Номер_Документа_Ввода;
  }

  // 102.67453 - Колонка 11 заполняется с учетом износа при поступлении.
  Начислено_с_начала_эксплуатации := Сумма_Амортизации_получения;
end.
.fields
  '(Метод учета: ' +Метод_Учета +')'

  // Номер (на данный момент в галактике не хранится данная информация)
  // По консультации АЦ: Нужно выводить порядковый номер карточки

  DateToStr(Отчетный_период,'DD')
  DateToStr(Отчетный_период,'Mon')
  DateToStr(Отчетный_период,'YYYY')

  // Дата
  Отчетный_период

  // Учреждение
  Организация_Наименование
  // ОКПО
  Организация_ОКПО

  // Структурное подразделение
  Подразделение
  // Код подразделения с права
  Код_Подразделения

  Наименование_ОС                                           // Наименование объекта (полное)
  Код_Оборудования_ОКОФ                                     // по ОКОФ

  // Назначение объекта - Наименование_ОС
  Вид_Оборудования
  Код_Оборудования

  // Организация-изготовитель
  Наименование_Изготовителя

  // Местонахождение объекта
  Подразделение
  // Код подразделения с права
  Код_Подразделения

  // Материально ответственное лицо
  МОЛ
  МОЛ_Табельный_номер

  // Инвентарный номер объекта
  Инвентарный_Номер
  // Номер амортизационной группы
  Амортизационная_Группа

  // Срок полезного использования
  //   По консультации АЦ: срок использования у нового владельца.
  //   Если приобретено ОС новое, то выводится срок эксплуатации полный
  SrokIspEdIzmWord(Срок_Использования)

  // Номер счета
  Счет_Субсчет
.endfields
 Ш ^
                                                                                                                                                                                                        ┌─────────────────────┐
                                                     ЩИНВЕНТАРНАЯ КАРТОЧКА УЧЕТА ОСНОВНЫХ СРЕДСТВ № ________________ Щ                        │        КОДЫ         │
                                                                                                                                                                                                        ├─────────────────────┤
                                                                                                                                                                                          Форма по ОКУД │       0504031       │
                                                                                                                                                                                                        ├─────────────────────┤
                                                                         Шот " Д&& Д"  Д@~@@@@@@@@@@@@@@@@@ Д &&&& г. Ш                                                     Дата │@~@@@@@@@@@@@@@@@@@@@│
                                                                                                                                                                                                        ├─────────────────────┤
   Учреждение  Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е по ОКПО │@~@@@@@@@@@@@@@@@@@@@│
                                                                                                                                                                                                        ├─────────────────────┤
   Структурное подразделение  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д │@~@@@@@@@@@@@@@@@@@@@│
                                                                                                                                                                                                        ├─────────────────────┤
   Наименование объекта (полное)  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д по ОКОФ │@~@@@@@@@@@@@@@@@@@@@│
                                                                                                                                                                                                        ├─────────────────────┤
   Назначение объекта  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д │@~@@@@@@@@@@@@@@@@@@@│
                                                                                                                                                                                                        ├─────────────────────┤
   Организация-изготовитель  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д по ОКПО │                     │
                                                                                                                                                                                                        ├─────────────────────┤
   Местонахождение объекта  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д │@~@@@@@@@@@@@@@@@@@@@│
                                                                                                                                                                                                        ├─────────────────────┤
   Материально ответственное лицо  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д │@~@@@@@@@@@@@@@@@@@@@│
                                                                                                                                                                                                        └─────────────────────┘
                      Инвентарный    ┌────────────────┐            Номер амортизационной  ┌─────────────────────┐            Срок полезного  ┌────────────────────────┐            Номер  ┌──────────────────────────────┐
                      номер объекта  │@~@@@@@@@@@@@@@@│            группы                 │@~@@@@@@@@@@@@@@@@@@@│            использования   │@~@@@@@@@@@@@@@@@@@@@@@@│            счета  │@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@│
                                     └────────────────┘                                   └─────────────────────┘                            └────────────────────────┘                   └──────────────────────────────┘
.begin
  Количество_Амортизаци    := 0;
  КоличествоМесяцев        := 0;
  ГодПредыдущегоНачисления := 0;

  bНеОтображатьНули := False;

  if (Сумма_Амортизации_получения <> 0)
  {
    Количество_Амортизаци := Количество_Амортизаци + 1;
    Начислено_с_начала_эксплуатации := Сумма_Амортизации_получения;
    Массив_Амортизация_Всего  [Количество_Амортизаци] := Начислено_с_начала_эксплуатации;
    Массив_Амортизация_Дата     [Количество_Амортизаци] := date(0,0,0);
    Массив_Амортизация_СПИ      [Количество_Амортизаци] := double(0);
    Массив_Амортизация_За_Месяц [Количество_Амортизаци] := double(0);
    Массив_Амортизация_За_Год   [Количество_Амортизаци] := double(0);
    Массив_Амортизация_ТипОперации[Количество_Амортизаци] := word(0);
    Массив_Амортизация_Коэф       [Количество_Амортизаци] := double(0);

    bНеОтображатьНули := True;
  }
end.
.{ FormOS6Amortization CheckEnter
.begin
  boFormOS6Amortization := True;

  if (wWriteAllIzmAmort = 0)
    if (Амортизация_тип_операции <> word(5))
      boFormOS6Amortization := False;

  if boFormOS6Amortization
  {
    if(not bWriteAmortInYear) or (ГодПредыдущегоНачисления <> Year(Амортизация_Дата))
    {
      Количество_Амортизаци := Количество_Амортизаци + 1;
    }

    Массив_Амортизация_Дата  [Количество_Амортизаци] := Амортизация_Дата ;
    Массив_Амортизация_СПИ   [Количество_Амортизаци] := Амортизация_СПИ;
    Массив_Амортизация_ТипОперации[Количество_Амортизаци] := Амортизация_тип_операции;
    Массив_Амортизация_Коэф       [Количество_Амортизаци] := Амортизация_Норма;

    // 11 Начислено с начала эксплуатации, руб
    Начислено_с_начала_эксплуатации := Начислено_с_начала_эксплуатации + Амортизация_Сумма;
    Массив_Амортизация_Всего [Количество_Амортизаци] := Начислено_с_начала_эксплуатации;

    // 10 Амортизация за год
    if (ГодПредыдущегоНачисления = Year(Амортизация_Дата))
    {
      if (round(Амортизация_Сумма, 2) <> 0)
      {
        КоличествоМесяцев := КоличествоМесяцев + 1;
      }

      Сумма_За_Год := Сумма_За_Год + Амортизация_Сумма;
    }
    else
    { // Первый месяц года
      if (round(Амортизация_Сумма, 2) <> 0)
      {
        КоличествоМесяцев := 1;
      }
      else
      {
        КоличествоМесяцев := 0;
      }

      Сумма_За_Год := Амортизация_Сумма;
    }
    Массив_Амортизация_За_Год [Количество_Амортизаци] := Сумма_За_Год;

    // 9 Амортизация за месяц
    if((not bWriteAmortInYear) or (ГодПредыдущегоНачисления <> Year(Амортизация_Дата)))
    { // Если помесячно или первый месяц года
      Массив_Амортизация_За_Месяц [Количество_Амортизаци] := Амортизация_Сумма;
    }
    else
    {
      Массив_Амортизация_За_Месяц [Количество_Амортизаци] := Сумма_За_Год / КоличествоМесяцев;
    }

    ГодПредыдущегоНачисления := Year(Амортизация_Дата);
  }
end.
.}
.begin
  NMax := Количество_Номеров_1

  if (NMax < Количество_Номеров_3)
    NMax := Количество_Номеров_3;

  if (NMax < Количество_Амортизаци)
    NMax := Количество_Амортизаци;
end.
.fields
  // Заголовок
  // (дата окончания начисления)
  if (Longint(Дата_окончания_начисления)<>0, DateToStr(Дата_окончания_начисления, 'DD/MM/YYYY'),'') // Дата_последней_начисленной_амортизации
.endfields
   ──────────────────────────────┬──────────────────────────────────────┬──────────┬──────────┬─────────────────────╥─────────────────────────────────────────────────────────────────╔═══════════════════════════════════════╗
               Паспорт,          │             Номер объекта            │   Дата   │  Дата и  │    Первоначальная   ║             Амортизация           (дата окончания начисления)   ║@~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@║
            свидетельство,       │                (детали)              │  выпуска │номер акта│ (восстановительная),╟─────────────────────┬───────────────────────────────────────────╚═════════════════════╤═════════════════╝
           чертеж (проект,       ├─────────────────────┬────────────────┤ (изготов-│ о вводе в│     кадастровая     ║       норма, %      │                сумма, руб                 │      начислено      │       Дата
         модель, тип, марка)     │      заводской      │      иной      │  ления) *│ эксплуа- │    стоимость, руб   ╟──────────┬──────────┼─────────────────────┬─────────────────────┤       с начала      │      (год)
                                 │                     │                │          │ тацию ** │                     ║ за месяц │  за год  │       за месяц      │        за год       │  эксплуатации, руб  │    начисления
   ──────────────────────────────┼─────────────────────┼────────────────┼──────────┼──────────┼─────────────────────╫──────────┼──────────┼─────────────────────┼─────────────────────┼─────────────────────┼──────────────────
                  1              │          2          │        3       │     4    │     5    │          6          ║     7    │    8     │          9          │         10          │          11         │        12
.begin
  NN := 0;
end.
.{While (NN < NMax)
.begin
  NN := NN + 1;
end.
.fields
  // Таблица 1
  // 01 Паспорт, свидетельство, чертеж (проект, модель, тип, марка)
  if (NN > Количество_Номеров_1, '',
    (Массив_Номеров_1 [NN]) )

  // * Номер объекта (детали)
  // 02 заводской
  // Консультация АЦ: заводской, Номер объекта
  if (NN = 1, Заводской_Номер, '')

  // 03 иной
  if (NN > Количество_Номеров_3, '',
    (Массив_Номеров_3 [NN]) )

  // 04 Дата выпуска (изготовления) *
  if (NN = 1, iExcelFormat.DateStandartToStrFormatExcelNotNul(Дата_Выпуска), '')

  // 05 Дата и номер акта о вводе в эксплуатацию **
  if (NN > Количество_Дата_Номер_Ввода, '',
    (Массив_Дата_и_Номер_Ввода [NN]) )

  // 06 Первоначальная (восстановительная), кадастровая стоимость, руб - Денис сказал Эту 100 %
  if (NN = 1, iExcelFormat.DoubleToStrFormatExcel(Стоимость, 2), '')

  // * Амортизация (последняя дата начисления)
  // * норма, %
  // 07 за месяц
  if ( NN > Количество_Амортизаци or bНеОтображатьНули
     , ''
     , if ( ((Not bWriteAmortInYear) AND (Массив_Амортизация_ТипОперации[NN] <> word(5)))
          , ''
          , String(100 / (Массив_Амортизация_СПИ [NN] / if(wOSSrokIspEdIzm = 0, 12, 1)) / 12, 10, 6)
          )
     )

  // 08 за год
  if (NN > Количество_Амортизаци or bНеОтображатьНули
    , ''
    , if ( ((Not bWriteAmortInYear) AND (Массив_Амортизация_ТипОперации[NN] <> word(5)))
         , ''
         , String(100 / (Массив_Амортизация_СПИ [NN] / if(wOSSrokIspEdIzm = 0, 12, 1)), 10, 6)
         )
     )

  // * сумма, руб
  // 09 за месяц
  if (NN > Количество_Амортизаци or bНеОтображатьНули, '',
    iExcelFormat.DoubleToStrFormatExcel(Массив_Амортизация_За_Месяц [NN], 2))

  // 10 за год
  if (NN > Количество_Амортизаци or bНеОтображатьНули, '',
    iExcelFormat.DoubleToStrFormatExcel(Массив_Амортизация_За_Год [NN], 2))

  // 11 начислено с начала эксплуатации, руб
  // 102.67453 - Колонка 11 заполняется с учетом износа при поступлении.
  if (NN > Количество_Амортизаци or bНеОтображатьНули, '',
    iExcelFormat.DoubleToStrFormatExcel(Массив_Амортизация_Всего [NN], 2))

  // 12 Дата (год) начисления
  if (NN > Количество_Амортизаци or bНеОтображатьНули or (LongInt(Массив_Амортизация_Дата [NN]) = 0), '',
    if(bWriteAmortInYear,  DateToStr(Массив_Амортизация_Дата [NN],'YYYY'),
                           DateToStr(Массив_Амортизация_Дата [NN],'DD/MM/YYYY')
      )
    )
.endfields
   ──────────────────────────────┼─────────────────────┼────────────────┼──────────┼──────────┼─────────────────────╫──────────┼──────────┼─────────────────────┼─────────────────────┼─────────────────────┼──────────────────
    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@│@@@@@@@@@@│@@@@@@@@@@│&&&&&&&&&&&&&&&&&&.&&║&&&.&&&&&&│&&&.&&&&&&│&&&&&&&&&&&&&&&&&&.&&│&&&&&&&&&&&&&&&&&&.&&│&&&&&&&&&&&&&&&&&&.&&│@~@@@@@@@@@@@@@@@
.begin
  bНеОтображатьНули := False;
end.
.}
   ──────────────────────────────┴─────────────────────┴────────────────┴──────────┴──────────┴─────────────────────╫──────────┼──────────┼─────────────────────┼─────────────────────┼─────────────────────┼──────────────────
.{ FormOS6Pereoc CheckEnter
.begin
    Количество_Переоценок := Количество_Переоценок + 1;
    Массив_Переоценок_Дата  [Количество_Переоценок] := Дата_Переоценка;
    Массив_Переоценок_Сумма [Количество_Переоценок] := Восст_Стоимость_Переоценка;
end.
.}
.{ FormOS6Oper CheckEnter
.begin
  if (Тип_Операции = 2)
  {
    Количество_Перемещений := Количество_Перемещений + 1;
    Массив_Перемещений_Дата          [Количество_Перемещений] := Дата_Операции;
    Массив_Перемещений_Номер         [Количество_Перемещений] := Номер_Операции;
    Массив_Перемещений_Подразделение [Количество_Перемещений] := Структ_Подраздел_Операции;
    Массив_Перемещений_МОЛ           [Количество_Перемещений] := МОЛ_Операции;
  }

  if (Тип_Операции = 4)
  {
    Количество_Выбытий := Количество_Выбытий + 1;
    Массив_Выбытий_Дата    [Количество_Выбытий] := Дата_Операции;
    Массив_Выбытий_Причина [Количество_Выбытий] := Основание_Операции;
  }
end.
.}
.{ FormOS6ModernNew CheckEnter
.begin
  if (Mode = 0) OR ((Mode = 2) AND wGetTune('Country')=ccBel) // Модернизация // 0 - модернизация, 1 - ремонт
  {
    Количество_Изменений := Количество_Изменений + 1;
    Массив_Изменений_Дата  [Количество_Изменений] := Дата_Документа_Изменения;
    Массив_Изменений_Сумма [Количество_Изменений] := Сумма_Изменения;
  }
end.
.}
.begin
  if (Количество_Изменений > Количество_Переоценок)
    NMax := Количество_Изменений;
  else
    NMax := Количество_Переоценок;
  NN := 0;
end.
   ─────────────────────────────────────────────────────────╥───────────────────────────────────────────────────────╢          │          │                     │                     │                     │
         Реконструкция, модернизация, достройка             ║                 Результаты переоценки                 ╟──────────┼──────────┼─────────────────────┼─────────────────────┼─────────────────────┼──────────────────
         дооборудование, частичная ликвидация               ║                                                       ║          │          │                     │                     │                     │
   ──────────────┬─────────┬────────────────────────────────╫────────────┬─────────┬────────────────────────────────╫──────────┼──────────┼─────────────────────┼─────────────────────┼─────────────────────┼──────────────────
          дата   │  номер  │             сумма,             ║    дата    │  номер  │             сумма,             ║          │          │                     │                     │                     │
                 │  ж / о  │              руб               ║            │  ж / о  │              руб               ║          │          │                     │                     │                     │
   ──────────────┼─────────┼────────────────────────────────╫────────────┼─────────┼────────────────────────────────╫──────────┼──────────┼─────────────────────┼─────────────────────┼─────────────────────┼──────────────────
           1     │    2    │               3                ║      4     │    5    │               6                ║          │          │                     │                     │                     │
.{While (NN < NMax)
.begin
  NN := NN + 1;
end.
.fields
  // Таблица 2

  // Реконструкция, модернизация, достройка дооборудование, частичная ликвидация
  // 01 - дата
  if (NN > Количество_Изменений, '',
    iExcelFormat.DateStandartToStrFormatExcelNotNul(Массив_Изменений_Дата [NN]) )

  // 02 - номер ж/о

  // 03 - сумма, руб
  if (NN > Количество_Изменений, '',
    iExcelFormat.DoubleToStrFormatExcelNotNul(Массив_Изменений_Сумма [NN], 2) )

  // Результаты переоценки
  // 04 - дата
  if (NN > Количество_Переоценок, '',
    iExcelFormat.DateStandartToStrFormatExcelNotNul(Массив_Переоценок_Дата [NN]) )

  // 05 - номер ж/о

  // 06 - сумма, руб
  if (NN > Количество_Переоценок, '',
    iExcelFormat.DoubleToStrFormatExcelNotNul(Массив_Переоценок_Сумма [NN], 2) )
.endfields
   ──────────────┼─────────┼────────────────────────────────╫────────────┼─────────┼────────────────────────────────╫──────────┼──────────┼─────────────────────┼─────────────────────┼─────────────────────┼──────────────────
      @@@@@@@@@@ │         │&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&║ @@@@@@@@@@ │         │&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&&║          │          │                     │                     │                     │
.}
.{?INTERNAL;(NN = 0)
   ──────────────┼─────────┼────────────────────────────────╫────────────┼─────────┼────────────────────────────────╫──────────┼──────────┼─────────────────────┼─────────────────────┼─────────────────────┼──────────────────
                 │         │                                ║            │         │                                ║          │          │                     │                     │                     │
.}
   ──────────────┴─────────┴────────────────────────────────╨────────────┴─────────┴────────────────────────────────╨──────────┴──────────┴─────────────────────┴─────────────────────┴─────────────────────┴──────────────────

   *  Для животных, многолетних насаждений, земельных участков - соответственно дата рождения, дата закладки, дата регистрации.
   ** Для животных, многолетних насаждений, земельных участков - дата и номер акта приемки (решение).

                                                                                                                                                                                                 Оборотная сторона ф. 0504031
   ─────────────────────┬────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┬────────────────────┬────────────────────────────────────────────
         Отметка об     │                                                   Внутреннее перемещение объекта                                                   │      Отметка о     │
       оприходовании    ├──────────────────────────────────┬─────────┬───────────────────────────────────────────┬───────────────────────────────────────────┤       выбытии      │                  Причина
          объекта       │             накладная            │         │              местонахождение              │                материально                │       объекта      │                  выбытия
   ───────────┬─────────┼──────────┬───────────────────────┼─────────┤                  объекта                  │               ответственное               ├──────────┬─────────┤
       дата   │  номер  │   дата   │         номер         │  номер  │                                           │                    лицо                   │   дата   │  номер  │
              │  ж / о  │          │                       │  ж / о  │                                           │                                           │          │  ж / о  │
   ───────────┼─────────┼──────────┼───────────────────────┼─────────┼───────────────────────────────────────────┼───────────────────────────────────────────┼──────────┼─────────┼────────────────────────────────────────────
        1     │    2    │    3     │           4           │    5    │                     6                     │                     7                     │     8    │    9    │                     10
.begin
  if (Количество_Перемещений > Количество_Выбытий)
    NMax := Количество_Перемещений;
  else
    NMax := Количество_Выбытий;
  NN := 0;
end.
.{While (NN < NMax)
.begin
  NN := NN + 1;
end.
.fields
  // Таблица 3

  // * Отметка об оприходовании объекта
  // 01 - дата
  if (NN = 1, iExcelFormat.DateStandartToStrFormatExcelNotNul(Дата_Документа_Получения), '')

  // 02 - номер ж/о

  // * Внутреннее перемещение объекта
  // накладная
  // 03 дата
  if (NN > Количество_Перемещений, '',
    iExcelFormat.DateStandartToStrFormatExcelNotNul(Массив_Перемещений_Дата [NN]) )

  // 04 номер
  if (NN > Количество_Перемещений, '',
    Массив_Перемещений_Номер [NN] )

  // 05 номер ж/о

  // 06 местонахождение объекта
  if (NN > Количество_Перемещений, '',
    Массив_Перемещений_Подразделение [NN] )

  // 07 материально ответственное лицо
  if (NN > Количество_Перемещений, '',
    Массив_Перемещений_МОЛ [NN] )

  // Отметка о выбытии объекта

!  Дата_Документа_Выбытия ???
!  Номер_Документа_Выбытия ???
!  Причина_Выбытия ???

  // 08 дата
  if (NN > Количество_Выбытий, '',
    iExcelFormat.DateStandartToStrFormatExcelNotNul(Массив_Выбытий_Дата [NN]) )

  // 09 номер ж/о

  // 10 Причина выбытия
  if (NN > Количество_Выбытий, '',
    Массив_Выбытий_Причина [NN] )
.endfields
   ───────────┼─────────┼──────────┼───────────────────────┼─────────┼───────────────────────────────────────────┼───────────────────────────────────────────┼──────────┼─────────┼────────────────────────────────────────────
    @@@@@@@@@@│         │@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@│         │@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@│         │@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
.}
.{?INTERNAL;(NN = 0)
.fields
  // Отметка об оприходовании объекта
  if (NN = 1, iExcelFormat.DateStandartToStrFormatExcelNotNul(Дата_Документа_Получения), '')  // 01 - дата
.endfields
   ───────────┼─────────┼──────────┼───────────────────────┼─────────┼───────────────────────────────────────────┼───────────────────────────────────────────┼──────────┼─────────┼────────────────────────────────────────────
    @@@@@@@@@@│         │          │                       │         │                                           │                                           │          │         │
.}
   ───────────┴─────────┴──────────┴───────────────────────┴─────────┴───────────────────────────────────────────┴───────────────────────────────────────────┴──────────┴─────────┴────────────────────────────────────────────

.{ FormOS6OsMc   CheckEnter
!.begin
!  Количество_Составляющих := Количество_Составляющих + 1;
!  Массив_Наименование_Составляющей [Количество_Составляющих] := Наименование_Составляющей;
!end.
.}
.{ FormOS6Drag   CheckEnter
.begin
  Количество_ДрагМеталлов := Количество_ДрагМеталлов + 1;
  Массив_ДрагМеталлов_Наименование [Количество_ДрагМеталлов] := Наименование_ДрагМеталлов;
  Массив_ДрагМеталлов_ЕдИзм        [Количество_ДрагМеталлов] := ЕдИзм_ДрагМеталлов;
  Массив_ДрагМеталлов_Код          [Количество_ДрагМеталлов] := Номенклат_Номер_ДрагМеталлов;
  Массив_ДрагМеталлов_Масса        [Количество_ДрагМеталлов] := Масса_ДрагМеталлов;
end.
.}
.{ FormOS6Modern CheckEnter
.}
.{ FormOS6Memo CheckEnter
.begin
  Количество_Характеристик := Количество_Характеристик + 1;
  Массив_Характеристика [Количество_Характеристик] := InfoMemo;
end.
.}
.begin
  NMax := 0;
  if (Количество_Характеристик > Количество_ДрагМеталлов)
    NMax := Количество_Характеристик;
  else
    NMax := Количество_ДрагМеталлов;

  NN := 0;
end.
                                                                                      Б Краткая индивидуальная характеристика объекта  Б
   ──────────────────────────┬──────────────────────────────────────────────────────────────────────┬──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────
                             │                 Материалы, размеры и прочие сведения                 │                          Содержание драгоценных материалов
          Наименование       ├──────────────────────┬───────────────────────────────────────────────┤                              (металлов, камней и т.п.)
            признаков,       │                      │        наименование важнейших пристроек,      ├───────────────────────────────────────────────────────────────┬──────────────────────────────────┬───────────────────────
         характеризующих     │       основной       │        приспособлений и принадлежностей,      │                           наименование                        │       единица измерения          │
            объект *         │        объект        │        относящихся к основному объекту        ├──────────────────────────────┬────────────────────────────────┼───────────────────┬──────────────┤      количество
                             │                      ├───────────┬───────────┬───────────┬───────────┤            объекта           │          драгоценного          │    наименование   │    код по    │        (масса)
                             │                      │           │           │           │           │           (детали)           │            материала           │                   │     ОКЕИ     │
   ──────────────────────────┼──────────────────────┼───────────┼───────────┼───────────┼───────────┼──────────────────────────────┼────────────────────────────────┼───────────────────┼──────────────┼───────────────────────
                1            │           2          │     3     │     4     │     5     │     6     │               7              │                8               │         9         │      10      │          11
.{While (NN < NMax)
.begin
  NN := NN + 1;
end.
.fields
  // Таблица 4
  // 01 - Наименование признаков, характеризующих объект *
  if (NN > Количество_Характеристик, '',
    Массив_Характеристика [NN] )

  // Материалы, размеры и прочие сведения
  // 02 основной объект
  if (NN = 1, Наименование_ОС, '')

!  if (NN > Количество_Составляющих, '',
!    Массив_Наименование_Составляющей [NN] )
  // наименование важнейших пристроек, приспособлений и принадлежностей, относящихся к основному объекту

  // 03
  // 04
  // 05
  // 06

  // * Содержание драгоценных материалов (металлов, камней и т.п.)
  // * наименование
  // 07 объекта (детали)

  // 08 драгоценного материала
  if (NN > Количество_ДрагМеталлов, '',
    Массив_ДрагМеталлов_Наименование [NN] )

  // * единица измерения
  // 09 наименование
  if (NN > Количество_ДрагМеталлов, '',
    Массив_ДрагМеталлов_ЕдИзм        [NN] )

  // 10 код по ОКЕИ
  if (NN > Количество_ДрагМеталлов, '',
    Массив_ДрагМеталлов_Код          [NN] )

  // 11 количество (масса)
  if (NN > Количество_ДрагМеталлов, '',
    iExcelFormat.DoubleToStrFormatExcelNotNul(Массив_ДрагМеталлов_Масса [NN], 12) )
.endfields
   ──────────────────────────┼──────────────────────┼───────────┼───────────┼───────────┼───────────┼──────────────────────────────┼────────────────────────────────┼───────────────────┼──────────────┼───────────────────────
   @@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@│           │           │           │           │                              │@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@~@@@@@@@@@@@@@@@@@│@~@@@@@@@@@@@@│&&&&&&&&&.&&&&&&&&&&&&
.}
.{?INTERNAL;(NN = 0)
.fields
  // 02 основной объект
  Наименование_ОС
.endfields
   ──────────────────────────┼──────────────────────┼───────────┼───────────┼───────────┼───────────┼──────────────────────────────┼────────────────────────────────┼───────────────────┼──────────────┼───────────────────────
                             │@@@@@@@@@@@@@@@@@@@@@@│           │           │           │           │                              │                                │                   │              │
.}
   ──────────────────────────┴──────────────────────┴───────────┴───────────┴───────────┴───────────┴──────────────────────────────┴────────────────────────────────┴───────────────────┴──────────────┴───────────────────────

   * Для животных, многолетних насаждений, земельных участков - порода (породность), кличка, масть, приметы, количество деревьев (кустов), номер участка (полосы), площадь в квадратных метрах.

.fields
  ЛОзаВК_Должность
  ЛОзаВК
.endfields
   Карточку заполнил  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д  ____________    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                  (должность)                    (подпись)                          (расшифровка подписи)
 Ш
 
.}
.endform
