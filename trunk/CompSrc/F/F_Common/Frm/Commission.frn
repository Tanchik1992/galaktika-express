!-------------------------------------------------------------------------------
!                                                       (c) корпорация Галактика
!  Галактика 7.12 - Макроопределения для вывода в поток состава комиссии,
!  на базе объекта комиссий.
!-------------------------------------------------------------------------------
#ifndef __CommissionForm__
#define __CommissionForm__

! Макроопределение для вставки в секции .Fields базовой формы
#declare CommissionFields
  Комиссия                               : boolean     // True, если комиссия выбрана.
  cSelectionCommissionNRec               : comp        // Ссылка на выбранную комиссию
  Имя_комиссии                                         // имя выбранной комиссии

  // * Списки в формате установленном пользователем:
  Информация_о_председателе                            // Вывод информации о председателе в формате установленном пользователем
  Список_членов_комиссии                               // Вывод списка членов комиссии, одной строкой, в формате установленном пользователем

  // * Списки членов комиссии, одной строкой, с жёстко установленной структурой:
  ФИО_членов_комиссии                                  // вывод только ФИО членов комиссии
  Должность_и_ФИО_членов_комиссии                      // вывод Должности и ФИО членов комиссии, в такой последовательности
  ФИО_и_Должность_членов_комиссии                      // вывод ФИО и Должности членов комиссии, в такой последовательности

  // * Председатель_комиссии:
  Председатель_комиссии_nRec             : comp
  Председатель_комиссии_ФИО
  Председатель_комиссии_Табельный_Номер  : LongInt     // tTabN
  Председатель_комиссии_Должность

  // * Члены комиссии:
  Количество_членов_комиссии             : word

  // * Цикл по членам комиссии:
  Член_комиссии_nRec                     : comp
  Член_комиссии_ФИО
  Член_комиссии_Табельный_Номер          : LongInt     // tTabN
  Член_комиссии_Должность
#end // CommissionFields

! Макроопределение для вставки в секции вывода данных базовой формы
#declare CommissionBody(FormName)

  Комиссия                               - ^
  cSelectionCommissionNRec               - ^
  Имя_комиссии                           - ^

  *** Списки в формате установленном пользователем: ***
  Информация_о_председателе              - ^
  Список_членов_комиссии                 - ^

  *** Списки членов комиссии, одной строкой, с жёстко установленной структурой: ***
  ФИО_членов_комиссии                    - ^
  Должность_и_ФИО_членов_комиссии        - ^
  ФИО_и_Должность_членов_комиссии        - ^

  *** Председатель_комиссии: ***
  Председатель_комиссии_nRec             - ^
  Председатель_комиссии_ФИО              - ^
  Председатель_комиссии_Табельный_Номер  - ^
  Председатель_комиссии_Должность        - ^

  *** Члены комиссии: ***
  Количество_членов_комиссии             - ^

  { ***** цикл по членам комиссии CommissionMember_#FormName *****
.{ CheckEnter CommissionMember_#FormName
    { -------------------
      Член_комиссии_nRec                 - ^
      Член_комиссии_ФИО                  - ^
      Член_комиссии_Табельный_Номер      - ^
      Член_комиссии_Должность            - ^
    } -------------------
.}
  } ***** цикл по членам комиссии *****
#end // CommissionBody

! Макроопределение для вставки в секции .var присоединенной формы
#declare CommissionLinkVar
.var
  // *** Объявление вспомогательных переменных ***
  Номер_члена_комиссии : word;

  // *** Объявление массивов *****
  Массив_Член_комиссии_nRec            : array[1..2] of comp;
  Массив_Член_комиссии_ФИО             : array[1..2] of string;
  Массив_Член_комиссии_Табельный_Номер : array[1..2] of string; // tTabN; LongInt;
  Массив_Член_комиссии_Должность       : array[1..2] of string;
.endvar
#end // CommissionBody

! Макроопределение для вставки в секции вывода данных присоединенной формы
#declare CommissionLinkInit(FormName)
.begin
  // *** Обнуление переменной ***
  Номер_члена_комиссии := 0;

  // *** Обнуление массивов по таблице ***
  for (Номер_члена_комиссии := 1; Номер_члена_комиссии <= Count(Массив_Член_комиссии_nRec); Номер_члена_комиссии := Номер_члена_комиссии + 1)
  {
    Массив_Член_комиссии_nRec            [Номер_члена_комиссии] := comp(0);
    Массив_Член_комиссии_ФИО             [Номер_члена_комиссии] := '';
    Массив_Член_комиссии_Табельный_Номер [Номер_члена_комиссии] := ''; // LongInt(0)
    Массив_Член_комиссии_Должность       [Номер_члена_комиссии] := '';
  }

  Номер_члена_комиссии := 0;
end.
.{ CheckEnter CommissionMember_#FormName
.begin
  Номер_члена_комиссии := Номер_члена_комиссии + 1;
  Массив_Член_комиссии_nRec            [Номер_члена_комиссии] := Член_комиссии_nRec;
  Массив_Член_комиссии_ФИО             [Номер_члена_комиссии] := Член_комиссии_ФИО;
  Массив_Член_комиссии_Табельный_Номер [Номер_члена_комиссии] := string(Член_комиссии_Табельный_Номер);
  Массив_Член_комиссии_Должность       [Номер_члена_комиссии] := Член_комиссии_Должность;
end.
.}
.begin
  Номер_члена_комиссии := 0;
end.
#end // CommissionLinkInit
#end // __CommissionForm__
