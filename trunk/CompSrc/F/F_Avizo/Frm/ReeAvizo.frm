//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 5.71 - Бухгалтерский контур
// Реестр входящих/исходящих авизо по документу
//------------------------------------------------------------------------------

#doc
Реестр входящих/исходящих авизо по документу
#end
.set name = 'ReestrAvizo'
.NameInList 'Базовая форма'
.hide
.fields
  Титл
  ФинДок_Сумма         : double
  ФинДок_Контрагент
  Исходящее_Номер
  Исходящее_Сформировано
  Исходящее_Проведено
  Исходящее_Сумма      : double
  Исходящее_Контрагент
  Входящее_Номер
  Входящее_Проведено
  Входящее_Сумма       : double
  Входящее_Контрагент
.endfields
^^^
.{CycleOfAVIZOI CheckEnter
^^^^^
.}
.{CycleOfAVIZOV CheckEnter
^^^^
.}
.endform

!------------------------------------------------------------------------------------------------------------------------------------------------------
.linkform 'ReestrAvizo_01' prototype is 'ReestrAvizo'
.NameInList 'Реестр исходящих АВИЗО'
.P 50
.set thousand separator = "`"
.var
  ИТОГО_Сумма_Исходящих    : double
  ИТОГО_Подлежит_Извещению : double
.endvar
.fields
  CommonFormHeader
  Титл
  ФинДок_Сумма
  ФинДок_Контрагент
  Исходящее_Номер
  Исходящее_Сформировано
  Исходящее_Проведено
  Исходящее_Сумма
  Исходящее_Контрагент
  ИТОГО_Сумма_Исходящих
  ИТОГО_Подлежит_Извещению
.endfields
.[H Skip
 Р                                                                                    лист @np@
────────────────────┬───────────┬──────────┬───────────────┬─────────────────────────────────
   Номер документа  │Сформирован│ Проведен │     Сумма     │           Получатель
────────────────────┴───────────┴──────────┴───────────────┴───────────────────────────────── Р
.]H
.begin
  ИТОГО_Сумма_Исходящих := 0;
end.
^
 Б                  Реестр Исходящих АВИЗО
 Р^ на сумму &&&&&&&&&&&&.&&
Контрагент : @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ С
 Р                                                                                    лист @np@
────────────────────┬───────────┬──────────┬───────────────┬─────────────────────────────────
   Номер документа  │Сформирован│ Проведен │     Сумма     │            Получатель
────────────────────┴───────────┴──────────┴───────────────┴───────────────────────────────── Р
.{CycleOfAVIZOI CheckEnter
 Р@@@@@@@@@@@@@@@@@@@@│ @@@@@@@@@@│@@@@@@@@@@│&&&&&&&&&&&&.&&│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Р
.begin
  ИТОГО_Сумма_Исходящих := ИТОГО_Сумма_Исходящих + Исходящее_Сумма;
end.
.}
.{CycleOfAVIZOV CheckEnter
.}
.begin
  ИТОГО_Подлежит_Извещению := ФинДок_Сумма - ИТОГО_Сумма_Исходящих;
end.
 С─────────────────────────────────────────────────────────────────────────────────────────────
             Извещено о сумме               &&&&&&&&&&&&.&&
             Подлежит извещению             &&&&&&&&&&&&.&& С
.endform

!------------------------------------------------------------------------------------------------------------------------------------------------------
.linkform 'ReestrAvizo_02' prototype is 'ReestrAvizo'
.NameInList 'Реестр входящих АВИЗО'
.P 50
.set thousand separator = "`"
.var
  ИТОГО_Сумма_Входящих     : double
.endvar
.fields
  CommonFormHeader
  Титл
  ФинДок_Сумма
  ФинДок_Контрагент
  Входящее_Номер
  Входящее_Проведено
  Входящее_Сумма
  Входящее_Контрагент
  ИТОГО_Сумма_Входящих
.endfields
.[H Skip
 Р                                                                        лист @np@
────────────────────┬──────────┬───────────────┬─────────────────────────────────
   Номер документа  │ Проведен │     Сумма     │            Составитель
────────────────────┴──────────┴───────────────┴───────────────────────────────── Р
.]H
.begin
  ИТОГО_Сумма_Входящих := 0;
end.
^
 Б              Реестр входящих АВИЗО
 Р^ на сумму &&&&&&&&&&&&.&&
Контрагент : @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ С
 Р                                                                        лист @np@
────────────────────┬──────────┬───────────────┬─────────────────────────────────
   Номер документа  │ Проведен │     Сумма     │            Составитель
────────────────────┴──────────┴───────────────┴───────────────────────────────── Р
.{CycleOfAVIZOI CheckEnter
.}
.{CycleOfAVIZOV CheckEnter
 Р@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@│&&&&&&&&&&&&.&&│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Р
.begin
  ИТОГО_Сумма_Входящих := ИТОГО_Сумма_Входящих + Входящее_Сумма;
end.
.}
 Р─────────────────────────────────────────────────────────────────────────────────
 Б  Получено извещений на сумму   &&&&&&&&&&&&.&& С
.endform

!------------------------------------------------------------------------------------------------------------------------------------------------------
.linkform 'ReestrAvizo_03' prototype is 'ReestrAvizo'
.NameInList 'Реестр всех привязанных АВИЗО'
.P 50
.set thousand separator = "`"
.var
  ИТОГО_Сумма_Исходящих    : double
  ИТОГО_Сумма_Входящих     : double
  ИТОГО_Подлежит_Извещению : double
.endvar
.fields
  CommonFormHeader
  Титл
  ФинДок_Сумма
  ФинДок_Контрагент
  Исходящее_Номер
  Исходящее_Сформировано
  Исходящее_Проведено
  Исходящее_Сумма
  Исходящее_Контрагент
  Входящее_Номер
  Входящее_Проведено
  Входящее_Сумма
  Входящее_Контрагент
  ИТОГО_Сумма_Исходящих
  ИТОГО_Сумма_Входящих
  ИТОГО_Подлежит_Извещению
.endfields
.[H Skip
 Р                                                                                    лист @np@
────────────────────┬───────────┬──────────┬───────────────┬─────────────────────────────────
   Номер документа  │Сформирован│ Проведен │     Сумма     │            Контрагент
────────────────────┴───────────┴──────────┴───────────────┴───────────────────────────────── Р
.]H
.begin
  ИТОГО_Сумма_Исходящих := 0;
  ИТОГО_Сумма_Входящих  := 0;
end.
^
 Б              Реестр всех привязанных АВИЗО
 Р^ на сумму &&&&&&&&&&&&.&&
Контрагент : @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ С
 Р                                                                                    лист @np@
────────────────────┬───────────┬──────────┬───────────────┬─────────────────────────────────
   Номер документа  │Сформирован│ Проведен │     Сумма     │            Контрагент
────────────────────┴───────────┴──────────┴───────────────┴───────────────────────────────── Р
 БИсходящие АВИЗО : Б
.{CycleOfAVIZOI CheckEnter
 Р@@@@@@@@@@@@@@@@@@@@│ @@@@@@@@@@│@@@@@@@@@@│&&&&&&&&&&&&.&&│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Р
.begin
  ИТОГО_Сумма_Исходящих := ИТОГО_Сумма_Исходящих + Исходящее_Сумма;
end.
.}
 БВходящие  АВИЗО : Б
.{CycleOfAVIZOV CheckEnter
 Р@@@@@@@@@@@@@@@@@@@@│           │@@@@@@@@@@│&&&&&&&&&&&&.&&│@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Р
.begin
  ИТОГО_Сумма_Входящих  := ИТОГО_Сумма_Входящих  + Входящее_Сумма;
end.
.}
.begin
  ИТОГО_Подлежит_Извещению := ФинДок_Сумма - ИТОГО_Сумма_Исходящих;
end.
 Р─────────────────────────────────────────────────────────────────────────────────────────────
 Б             Извещено о сумме               &&&&&&&&&&&&.&&
             Получено извещений на сумму    &&&&&&&&&&&&.&&
             Подлежит извещению             &&&&&&&&&&&&.&& С
.endform

