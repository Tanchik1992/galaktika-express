//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 6.00 - Основные средства
// Ведомости переоценки
// Внимание! Существует slk-отчет в формате Excel
//------------------------------------------------------------------------------

#include Commission.frn // комиссии

#doc
Ведомости переоценки
#end

.FORM OSPereoc
.HIDE
.var
#include Fld_Amort.var
.endvar
.begin
#include Fld_Amort.vpp
end.

.fields
  Формат_Сумм : string // Формат содержащийся в рфыекщйках пользователя UserDeskRep.ResName[1]

  nRec_MoveOs : comp
  Date_Pereoc : date

  nRec_NastrOs : comp
  NastrOsName
  Divider
  sDivider
  nRec_KlVal : comp
  CurrName
  nRec_UserDeskRep : comp
  sGrpName
  sSrtName

#CommissionFields

!---------------- FilterLoop
  sFltName

  CodeName

!---------------- GroupLoop
  Group_nRec : comp
  Head_TempDescr_TableCode : word // код таблицы по которой постоена данная группировка
  Head_TempDescr_CRecKat   : comp // номер записи в таблице по которой построена группировка
  GroupName
  GroupLevel : word // Уровень группировки
  NeedSum1 : boolean
  Head_StoimGrp
  Head_IznosGrp
  Head_OstStoimGrp
  Head_StoimGrpNew
  Head_IznosGrpNew
  Head_OstStoimGrpNew
  Head_BaseStoimGrp     // амортизируемая стоимость (до переоценки)
  Head_BaseStoimGrpNew  //           -//-           (после переоценки)

!---------------- DataLoop
  KatOs_nRec
  KatOs_cMc
  SpKatOs_nRec
  cHigherOS
  isLeaf
  NameIzg
  TipMark
  BarKod
  Kod_Part
  DatGar
  StoimPs
  RegNum
  IznosPs
  Poprkoef
  SumOtkl
  SumFld
  StoimL

  fInnum
  OSName
  Kod_nRec : comp
  Kod
  Date_sost : date
  nRec_TempDescr : comp
  Stoim
  SumIzn
  NewStoim
  NewSumIzn
  NewIznMes
  OstStoim
  NewOstStoim
  IznPerc
  Coeff
  BaseStoim
  NewBaseStoim

!---------------- ItogLoop
    ItgGroup_nRec : comp
    Tail_TempDescr_TableCode : word // код таблицы по которой постоена данная группировка
    Tail_TempDescr_CRecKat   : comp // номер записи в таблице по которой построена группировка
    ItgGroupName
    StoimGrp
    IznosGrp
    OstStoimGrp
    StoimGrpNew
    IznosGrpNew
    OstStoimGrpNew
    BaseStoimGrp     // амортизируемая стоимость (до переоценки)
    BaseStoimGrpNew  //           -//-           (после переоценки)
.endfields
! Подкачка Excel форматов
! Эта строчка нужна пока не будет решена 102.31867
#include ExcelFormat.frn

^ ^
Дата переоценки ^

Метод учета:          ^ ^
Денежная единица:     ^ (^) ^ ^
^
Группировка:          ^
Сортировка:           ^

#CommissionBody(OSPereoc)

Фильтры:
.{ CheckEnter FilterLoop
 ^
.}
^
.{ MainLoop
.{ CheckEnter GroupLoop
^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^
.}
.{ CheckEnter DataLoop
^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^
^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^
.}
.{ CheckEnter ItogLoop
^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^
.}
.}
.ENDFORM

!===================================================
.linkform 'OsPereoc_01' prototype is 'OsPereoc'
!===================================================
.NameInList 'Стандартная ведомость переоценки'
.p 60
.Group '<<Общие>>'
.defo portrait
#CommissionLinkVar
.[h
 Щ                                                                                                                                              Лист @Np@
 Щ
.]h
#CommissionLinkInit(OSPereoc)

.Fields
  Информация_о_председателе
  Список_членов_комиссии
.EndFields
 Й         Предприятие ___________________________________
        Ведомость переоценки основных фондов по состоянию на ____________ 20  г.
        Комиссия в составе:
        председателя:  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д,
        и членов:
         Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
        назначенная приказом руководителя предприятия от "__"___________20  г.
        N____________ произвела переоценку основных фондов по состоянию
                  на "__"____________20  г.  Й
 Щ
.fields
  NastrOsName
  Divider
  sDivider
  CurrName
  sGrpName
  sSrtName
.endfields
Метод учета:          ^
Денежная единица:     ^ (^) ^
Группировка:          ^
Сортировка:           ^
Фильтры:
.{ CheckEnter  FilterLoop
.fields
  sFltName
.endfields
 ^
.}
 Щ
.{ MainLoop
.fields
  CodeName
  Divider
  CurrName
.endfields
.[h
 Ш┌───────────────┬──────────────────────────┬─────────┬──────────┬──────────────────┬─────────────────────────┬───────┬──────────────────┬──────────────────┐
│               │                          │         │  Дата    │    Первонача-    │   Остаточная стоимость  │Коэффи-│    Восстанови-   │    Остаточная    │
│  Инвентарный  │       Наименование       │   Код   │  ввода   │       льная      ├──────────────────┬──────┤ циент │    тельная       │     стоимость    │
│     номер     │         объекта          │  @@@@@  │    в     │     стоимость    │        в         │  в   │ перес-│    стоимость     │      после       │
│               │                          │         │эксплуат. │                  │@~@@@@@@@@@@@@@@@@│ проц │ чета  │    после         │    переоценки    │
│               │                          │         │          │                  │@~@@@@@@@@@@@@@@@@│      │       │    переоценки    │                  │
└───────────────┴──────────────────────────┴─────────┴──────────┴──────────────────┴──────────────────┴──────┴───────┴──────────────────┴──────────────────┘ Ш
.]h
.{ CheckEnter GroupLoop
.fields
  GroupName
  if (NeedSum1, DoubleToStr(double(Head_StoimGrp), Формат_Сумм), '')
  if (NeedSum1, DoubleToStr(double(Head_OstStoimGrp), Формат_Сумм), '')
  if (NeedSum1, DoubleToStr(double(Head_StoimGrpNew), Формат_Сумм), '')
  if (NeedSum1, DoubleToStr(double(Head_OstStoimGrpNew), Формат_Сумм), '')
.endfields

 Щ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&&                &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& Щ
.}
.{ CheckEnter DataLoop
.fields
  fInnum           OSName                   Kod       Date_sost    DoubleToStr(double(Stoim), Формат_Сумм)
                                                                                      DoubleToStr(double(OstStoim), Формат_Сумм)
                                                                                                         IznPerc
                                                                                                                DoubleToStr(double(Coeff), '66.8888')
                                                                                                                        DoubleToStr(double(NewStoim), Формат_Сумм)
                                                                                                                                           DoubleToStr(double(NewOstStoim), Формат_Сумм)
.endfields
 Ш @@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@ @@@@@@@@@@ &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& &&&&&& &&.&&&& &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& Ш
.}
.{ CheckEnter ItogLoop
.fields
            ItgGroupName                                           DoubleToStr(double(StoimGrp), Формат_Сумм)
                                                                                      DoubleToStr(double(OstStoimGrp), Формат_Сумм)
                                                                                                                        DoubleToStr(double(StoimGrpNew), Формат_Сумм)
                                                                                                                                           DoubleToStr(double(OstStoimGrpNew), Формат_Сумм)
.endfields

 Щ Итого по @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&&                &'&&&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& Щ
.}
.}
! End MainLoop ----------------------------------------------------------------

 ЩБухгалтер Щ
 ЩОтветственный за выпуск Щ
.endform

!===================================================
.linkform 'OsPereoc_02' prototype is 'OsPereoc'
!===================================================
.NameInList 'Ведомость переоценки с месячным износом'
.p 60
.Group '<<Общие>>'
.defo portrait
#CommissionLinkVar
.[h
 Щ                                                                                                                                              Лист @Np@
 Щ
.]h
#CommissionLinkInit(OSPereoc)

.Fields
  Информация_о_председателе
  Список_членов_комиссии
.EndFields
 Й         Предприятие ___________________________________
        Ведомость переоценки основных фондов по состоянию на ____________ 20  г.
        Комиссия в составе:
        председателя:  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д,
        и членов:
         Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
        назначенная приказом руководителя предприятия от "__"___________20  г.
        N____________ произвела переоценку основных фондов по состоянию
                       на "__"____________20  г.  Й
 Щ
.fields
  NastrOsName
  Divider
  sDivider
  CurrName
  sGrpName
  sSrtName
.endfields
Метод учета:          ^
Денежная единица:     ^ (^) ^
Группировка:          ^
Сортировка:           ^
Фильтры:
.{ CheckEnter  FilterLoop
.fields
  sFltName
.endfields
 ^
.}
 Щ
.{ MainLoop
.fields
  sNameField_Iznosa
  CodeName
  Divider
  CurrName
.endfields
.[h
 Ш┌───────────────┬──────────────────────────┬─────────┬──────────┬──────────────┬─────────────────────┬───────┬──────────────┬──────────────┬──────────────┐
│               │                          │         │  Дата    │  Первонача-  │ Остаточная стоимость│Коэффи-│  Восстанови- │  Остаточная  │    Сумма     │
│  Инвентарный  │       Наименование       │   Код   │  ввода   │     льная    ├──────────────┬──────┤ циент │  тельная     │   стоимость  │@~@@@@@@@@@@@@│
│     номер     │         объекта          │  @@@@@  │    в     │   стоимость  │      в       │  в   │ перес-│  стоимость   │    после     │  за  месяц   │
│               │                          │         │эксплуат. │              │ @~@@@@@@@@@@@│ проц │ чета  │  после       │  переоценки  │    после     │
│               │                          │         │          │              │ @~@@@@@@@@@@@│      │       │  переоценки  │              │ переоценки   │
└───────────────┴──────────────────────────┴─────────┴──────────┴──────────────┴──────────────┴──────┴───────┴──────────────┴──────────────┴──────────────┘ Ш
.]h
.{ CheckEnter GroupLoop
.fields
  GroupName
  if (NeedSum1, DoubleToStr(double(Head_StoimGrp), Формат_Сумм), '')
  if (NeedSum1, DoubleToStr(double(Head_OstStoimGrp), Формат_Сумм), '')
  if (NeedSum1, DoubleToStr(double(Head_StoimGrpNew), Формат_Сумм), '')
  if (NeedSum1, DoubleToStr(double(Head_OstStoimGrpNew), Формат_Сумм), '')
.endfields

 Щ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ &'&&&&&&&&&&&& &'&&&&&&&&&&&&                &'&&&&&&&&&&&& &'&&&&&&&&&&&& Щ
.}
.{ CheckEnter DataLoop
.fields
  fInnum           OSName                   Kod       Date_sost    DoubleToStr(double(Stoim), Формат_Сумм)
                                                                                  DoubleToStr(double(OstStoim), Формат_Сумм)
                                                                                                 IznPerc
                                                                                                        DoubleToStr(double(Coeff), '66.8888')
                                                                                                                DoubleToStr(double(NewStoim), Формат_Сумм)
                                                                                                                               DoubleToStr(double(NewOstStoim), Формат_Сумм)
                                                                                                                                              DoubleToStr(double(NewIznMes), Формат_Сумм)
.endfields

 Ш @@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@ @@@@@@@@@@ &'&&&&&&&&&&&& &'&&&&&&&&&&&& &&&&&& &&.&&&& &'&&&&&&&&&&&& &'&&&&&&&&&&&& &'&&&&&&&&&&&& Ш
.}
.{ CheckEnter ItogLoop
.fields
            ItgGroupName                                           DoubleToStr(double(StoimGrp), Формат_Сумм)
                                                                                  DoubleToStr(double(OstStoimGrp), Формат_Сумм)
                                                                                                               DoubleToStr(double(StoimGrpNew), Формат_Сумм)
                                                                                                                               DoubleToStr(double(OstStoimGrpNew), Формат_Сумм)
.endfields

 Щ Итого по @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ &'&&&&&&&&&&&& &'&&&&&&&&&&&&                &'&&&&&&&&&&&& &'&&&&&&&&&&&& Щ
.}
.}
! End MainLoop ----------------------------------------------------------------

 ЩБухгалтер Щ
 ЩОтветственный за выпуск Щ
.endform

!===================================================
.linkform 'OsPereoc_03' prototype is 'OsPereoc'
!===================================================
.NameInList 'Ведомость переоценки'
.p 40
.Group '<<Беларусь>>'
.defo landscape
.var
   nPop   : word
   Dat1   : string
   Dat2   : string
   Dat3   : string
   nMonth : byte
   nYear  : word
   sMonth : string
.endvar
#CommissionLinkVar
.[h
 Р                                                                                                                                                                                   Лист @Np@ Р
.]h
#CommissionLinkInit(OSPereoc)
.fields
  Dat1
  Dat1
  Dat2
  Dat2
.endfields
.begin
  var tdPer : date;
  tdPer := if(Day(Date_Pereoc)=Last_Day(Date_Pereoc),Add_Months(Date_Pereoc,1),Date_Pereoc);
  nMonth:=Month(tdPer);
  nYear:=Year(tdPer);
  case nMonth of
    0:sMonth :=' ';
    1:sMonth := 'января ';
    2:sMonth := 'февраля ';
    3:sMonth := 'марта ';
    4:sMonth := 'апреля ';
    5:sMonth := 'мая ';
    6:sMonth := 'июня ';
    7:sMonth := 'июля ';
    8:sMonth := 'августа ';
    9:sMonth := 'сентября ';
    10:sMonth:= 'октября ';
    11:sMonth:= 'ноября ';
    12:sMonth:= 'декабря ';
  end;
  Dat1 := 'на 1 ' + sMonth + if(nYear <> 0, String(nYear), ' ') + ' г.';
  Dat2 := 'на 1 ' + sMonth;
  Dat3 := if(nYear <> 0, String(nYear), ' ');
end.
 Р
                                                                                                                                                                               Приложение 2
                                                                                                                                                           к Постановлению Минэкономики РБ,
                                                                                                                                                    Минфина РБ, Минстатистики и анализа РБ,
                                                                                                                                                          Минархитектуры и строительства РБ
                                                                                                                                                          от 20.11.2006 г. № 199/139/185/34 Р

 Б                                            В Е Д О М О С Т Ь
                            переоценки основных средств по состоянию ^ Б

.{ CheckEnter  FilterLoop
.}
.{ MainLoop
.[h
 Р┌────┬───────────────┬──────────────────────────┬──────────┬──────────┬──────────────┬───────┬──────────────┬────────────────────────┬──────────────┬─────────────────────────────┬────────┐
│    │               │                          │Шифр групп│   Дата   │Первоначальная│Коэффи-│ Восстанови-  │   Амортизация(износ)   │ Остаточная   │   Амортизируемая стоимость  │ Коэффи-│
│ №  │  Инвентарный  │       Наименование       │ основных │ ввода  в │   стоимость  │ циент │   тельная    ├─────────┬──────────────┤ стоимость    │   @~@@@@@@@@@@@@@@@@@@@@@@  │ циент  │
│п/п │     номер     │         объекта          │средств по│эксплуата-│@~@@@@@@@@@@@@│ пере- │  стоимость   │удельный │  сумма после │   после      ├──────────────┬──────────────┤  пере- │
│    │               │                          │Временному│   цию    │              │ счета │@~@@@@@@@@@@@@│вес (%) в│  переоценки  │ переоценки   │      до      │    после     │ оценки │
│    │               │                          │ классифи-│  (год,   │              │       │              │первонач.│  (графа 8 х  │ (графа 8 -   │  переоценки  │  переоценки  │ аморти-│
│    │               │                          │  катору  │  месяц)  │              │       │              │стоимости│х графа 9:100)│ - графа 10)  │              │              │зируемой│
│    │               │                          │          │          │              │       │              │ до пере-│              │              │              │              │стоимос-│
│    │               │                          │          │          │              │       │              │ оценки  │              │              │              │              │   ти   │
├────┼───────────────┼──────────────────────────┼──────────┼──────────┼──────────────┼───────┼──────────────┼─────────┼──────────────┼──────────────┼──────────────┼──────────────┼────────┤
│ 1  │       2       │            3             │     4    │     5    │       6      │   7   │       8      │    9    │      10      │      11      │      12      │      13      │   14   │
└────┴───────────────┴──────────────────────────┴──────────┴──────────┴──────────────┴───────┴──────────────┴─────────┴──────────────┴──────────────┴──────────────┴──────────────┴────────┘ Р
.]h
.{ CheckEnter GroupLoop
.fields
  GroupName
  if (NeedSum1, DoubleToStr(double(Head_StoimGrp), Формат_Сумм), '')
  if (NeedSum1, DoubleToStr(double(Head_StoimGrpNew), Формат_Сумм), '')
  if (NeedSum1, DoubleToStr(double(Head_OstStoimGrpNew), Формат_Сумм), '')
  if (NeedSum1, DoubleToStr(double(Head_BaseStoimGrp), Формат_Сумм), '')
  if (NeedSum1, DoubleToStr(double(Head_BaseStoimGrpNew), Формат_Сумм), '')
.endfields
 С @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@       &'&&&&&&&&&&&&         &'&&&&&&&&&&&&                          &'&&&&&&&&&&&& &'&&&&&&&&&&&& &'&&&&&&&&&&&& С
.}
.{ CheckEnter DataLoop
.fields
 nPop
 fInnum
 OSName
 Kod
 Date_sost
 DoubleToStr(double(Stoim), Формат_Сумм)
 Coeff
 DoubleToStr(double(NewStoim), Формат_Сумм)
 100-Double(IznPerc)
 DoubleToStr(double(NewSumIzn), Формат_Сумм)
 DoubleToStr(double(NewOstStoim), Формат_Сумм)
 DoubleToStr(double(BaseStoim), Формат_Сумм)
 DoubleToStr(double(NewBaseStoim), Формат_Сумм)
 if(round(double(BaseStoim),2)<>0, double(NewBaseStoim)/double(BaseStoim), 1.00)
.endfields
.begin
  nPop := nPop +1
end.
 Р @@@@ @@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@  @@@@@@@@@@ &'&&&&&&&&&&&& &&.&&&& &'&&&&&&&&&&&&  &&&.&&   &'&&&&&&&&&&&& &'&&&&&&&&&&&& &'&&&&&&&&&&&& &'&&&&&&&&&&&& &&.&&&& Р
.}
.{ CheckEnter ItogLoop
.fields
 ItgGroupName
 DoubleToStr(double(StoimGrp), Формат_Сумм)
 DoubleToStr(double(StoimGrpNew), Формат_Сумм)
 DoubleToStr(double(OstStoimGrpNew), Формат_Сумм)
 DoubleToStr(double(BaseStoimGrp), Формат_Сумм)
 DoubleToStr(double(BaseStoimGrpNew), Формат_Сумм)
.endfields
 С Итого по @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@       &'&&&&&&&&&&&&         &'&&&&&&&&&&&&                          &'&&&&&&&&&&&& &'&&&&&&&&&&&& &'&&&&&&&&&&&& С
.}
.}
! End MainLoop ----------------------------------------------------------------

.Fields
!  Председатель_комиссии_Должность
  Председатель_комиссии_ФИО
.EndFields
 Р   Председатель комиссии ___________________________    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                 (подпись)                                                   (И.О. Фамилия)

.{While (Номер_члена_комиссии < Количество_членов_комиссии)
.begin
  Номер_члена_комиссии := Номер_члена_комиссии + 1;
end.
.fields
  if (Номер_члена_комиссии = word(1), 'Члены комиссии:', '')
!  Массив_Член_комиссии_Должность [Номер_члена_комиссии]
  Массив_Член_комиссии_ФИО       [Номер_члена_комиссии]
.endfields
   @@@@@@@@@@@@@@@       ___________________________    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                 (подпись)                                                   (И.О. Фамилия)

.}
.{?INTERNAL;(Количество_членов_комиссии = 0)
   Члены комиссии:       ___________________________   __________________________________________________________________________________________
                                 (подпись)                                                   (И.О. Фамилия)

                         ___________________________   __________________________________________________________________________________________
                                 (подпись)                                                   (И.О. Фамилия)

.}
.endform

!===================================================
.linkform 'OsPereoc_04' prototype is 'OsPereoc'
!===================================================
.NameInList 'Расширенная ведомость переоценки'
.p 60
.Group '<<Общие>>'
.defo landscape
#CommissionLinkVar
.[h
 И                                                                                                                                                                                                                       Лист @Np@ И
.]h
#CommissionLinkInit(OSPereoc)
.Fields
  'по состоянию на ' +
  '1 '+ DateToStr(if(Day(Date_Pereoc) = Last_Day(Date_Pereoc), Add_Months(Date_Pereoc, 1), Date_Pereoc), 'mon') +
  ' ' + Year(if(Day(Date_Pereoc) = Last_Day(Date_Pereoc), Add_Months(Date_Pereoc, 1), Date_Pereoc)) + ' г.'
  Информация_о_председателе
  Список_членов_комиссии
  DateToStr(if(Day(Date_Pereoc) = Last_Day(Date_Pereoc), Add_Months(Date_Pereoc, 1), Date_Pereoc), 'mon')
  Year(if(Day(Date_Pereoc) = Last_Day(Date_Pereoc), Add_Months(Date_Pereoc, 1), Date_Pereoc))
.EndFields
  Предприятие _____________________________________________________________________________________________________________________________________________________________________




 Щ                                     Ведомость переоценки основных фондов
              @~@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Щ



      Комиссия в составе:
         председателя:  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
         и членов:
                        Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д,

      назначенная приказом руководителя предприятия от "__"___________20  г. N____________,  произвела переоценку основных фондов по состоянию на " Д 1  Д"  Д ^  Д ^ г.
.fields
  NastrOsName
  Divider
  sDivider
  CurrName
  sGrpName
  sSrtName
.endfields
 И
Метод учета:          ^
Денежная единица:     ^ (^) ^
Группировка:          ^
Сортировка:           ^
Фильтры:
.{ CheckEnter  FilterLoop
.fields
  sFltName
.endfields
  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
.}
.{ MainLoop
.fields
  CodeName
  'в ' + Divider + ' ' + CurrName
.endfields
.[h
 Б┌───────────────┬──────────────────────────┬─────────┬──────────┬────────────────┬───────┬────────────────┬────────────────┬────────────────┬───────┬────────────────┬────────────────┬─────────────────────────┬────────────────┐
│               │                          │         │  Дата    │ Первоначальная │Коэффи-│ Первоначальная │   Величина     │  Амортизация   │Коэффи-│  Амортизация   │   Величина     │   Остаточная стоимость  │   Остаточная   │
│  Инвентарный  │       Наименование       │   Код   │  ввода   │   стоимость    │ циент │   стоимость    │   изменения    │    (износ)     │ циент │    (износ)     │   изменения    │      до переоценки      │   стоимость    │
│     номер     │         объекта          │  @@@@@  │    в     │       до       │ перес-│     после      │ первоначальной │       до       │ перес-│     после      │  амортизации   ├──────────────────┬──────┤     после      │
│               │                          │         │эксплуат. │   переоценки   │ чета  │   переоценки   │   стоимости    │   переоценки   │ чета  │   переоценки   │    (износа)    │@~@@@@@@@@@@@@@@@@│ в %  │   переоценки   │
└───────────────┴──────────────────────────┴─────────┴──────────┴────────────────┴───────┴────────────────┴────────────────┴────────────────┴───────┴────────────────┴────────────────┴──────────────────┴──────┴────────────────┘ Б
.]h
.{ CheckEnter GroupLoop
.fields
  GroupName
  if (NeedSum1, DoubleToStr(double(Head_StoimGrp), Формат_Сумм), '')
  if (NeedSum1, DoubleToStr(double(Head_StoimGrpNew), Формат_Сумм), '')
  if (NeedSum1, DoubleToStr(double(Head_StoimGrpNew) - double(Head_StoimGrp), Формат_Сумм), '')

  if (NeedSum1, DoubleToStr(double(Head_IznosGrp), Формат_Сумм), '')
  if (NeedSum1, DoubleToStr(double(Head_IznosGrpNew), Формат_Сумм), '')
  if (NeedSum1, DoubleToStr(double(Head_IznosGrpNew) - double(Head_IznosGrp), Формат_Сумм), '')

  if (NeedSum1, DoubleToStr(double(Head_OstStoimGrp), Формат_Сумм), '')
  if (NeedSum1, DoubleToStr(double(Head_OstStoimGrpNew), Формат_Сумм), '')
.endfields
 Б @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ &'&&&&&&&&&&&&&&         &'&&&&&&&&&&&&&& &'&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&         &'&&&&&&&&&&&&&& &'&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&&        &'&&&&&&&&&&&&&& Б
.}
.{ CheckEnter DataLoop
.fields
  fInnum
  OSName
  Kod
  Date_sost
  DoubleToStr(double(Stoim), Формат_Сумм)
  DoubleToStr(double(Coeff), '66.8888')
  DoubleToStr(double(NewStoim), Формат_Сумм)
  DoubleToStr(double(NewStoim) - double(Stoim), Формат_Сумм)

  DoubleToStr(double(SumIzn), Формат_Сумм)
  DoubleToStr(double(NewSumIzn) / double(SumIzn), '66.8888')
  DoubleToStr(double(NewSumIzn), Формат_Сумм)
  DoubleToStr(double(NewSumIzn) - double(SumIzn), Формат_Сумм)

  DoubleToStr(double(OstStoim), Формат_Сумм)
  IznPerc
  DoubleToStr(double(NewOstStoim), Формат_Сумм)
.endfields
 @@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@ @@@@@@@@@@ &'&&&&&&&&&&&&&& &&.&&&& &'&&&&&&&&&&&&&& &'&&&&&&&&&&&&&& &'&&&&&&&&&&&&&& &&.&&&& &'&&&&&&&&&&&&&& &'&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&& &&&&&& &'&&&&&&&&&&&&&&
.}
.{ CheckEnter ItogLoop
.fields
  ItgGroupName
  DoubleToStr(double(StoimGrp), Формат_Сумм)
  DoubleToStr(double(StoimGrpNew), Формат_Сумм)
  DoubleToStr(double(StoimGrpNew) - double(StoimGrp), Формат_Сумм)

  DoubleToStr(double(IznosGrp), Формат_Сумм)
  DoubleToStr(double(IznosGrpNew), Формат_Сумм)
  DoubleToStr(double(IznosGrpNew) - double(IznosGrp), Формат_Сумм)

  DoubleToStr(double(OstStoimGrp), Формат_Сумм)
  DoubleToStr(double(OstStoimGrpNew), Формат_Сумм)
.endfields
 Б Итого по @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ &'&&&&&&&&&&&&&&         &'&&&&&&&&&&&&&& &'&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&         &'&&&&&&&&&&&&&& &'&&&&&&&&&&&&&& &'&&&&&&&&&&&&&&&&        &'&&&&&&&&&&&&&& Б
.}
.}
! End MainLoop ----------------------------------------------------------------

Бухгалтер
Ответственный за выпуск И
.endform

!===================================================
.linkform 'AktPereoc_01' prototype is 'OsPereoc'
!===================================================
.NameInList 'Акт по результатам переоценки'
.P 70
.Group '<<Общие>>'
.defo portrait
.var
   Dat1   : string
   nMonth : byte
   nYear  : word
   sMonth : string

   Уровень              : word
   Itog_StoimGrp        : double
   Itog_OstStoimGrp     : double
   Itog_StoimGrpNew     : double
   Itog_OstStoimGrpNew  : double
.endvar
#CommissionLinkVar
.begin
  Уровень := 65535;
  Itog_StoimGrp       := 0;
  Itog_OstStoimGrp    := 0;
  Itog_StoimGrpNew    := 0;
  Itog_OstStoimGrpNew := 0;

  nMonth:=Month(Date_Pereoc);
  nYear:=Year(Date_Pereoc);
  case nMonth of
    0:sMonth:=' ';
    1:sMonth:='января ';
    2:sMonth:='февраля ';
    3:sMonth:='марта ';
    4:sMonth:='апреля ';
    5:sMonth:='мая ';
    6:sMonth:='июня ';
    7:sMonth:='июля ';
    8:sMonth:='августа ';
    9:sMonth:='сентября ';
    10:sMonth:='октября ';
    11:sMonth:='ноября ';
    12:sMonth:='декабря ';
  end;
  Dat1:='на 1 '+sMonth+if(nYear<>0,String(nYear),' ')+' г.';
end.
#CommissionLinkInit(OSPereoc)
.{ CheckEnter  FilterLoop
.}
.{ MainLoop
.{ CheckEnter GroupLoop
.begin
  if (GroupLevel < Уровень)
  {
    Уровень := GroupLevel;
    // GroupName
    Itog_StoimGrp       := double(Head_StoimGrp);
    Itog_OstStoimGrp    := double(Head_OstStoimGrp);
    Itog_StoimGrpNew    := double(Head_StoimGrpNew);
    Itog_OstStoimGrpNew := double(Head_OstStoimGrpNew);
  }
end.
.}
.{ CheckEnter DataLoop
.}
.{ CheckEnter ItogLoop
.}
.}
.fields
  Информация_о_председателе
  Список_членов_комиссии
  DoubleToStr(double(Itog_StoimGrp), Формат_Сумм)
  CurrName
  DoubleToStr(double(Itog_StoimGrpNew), Формат_Сумм)
  CurrName
  DoubleToStr(double(Itog_OstStoimGrp), Формат_Сумм)
  CurrName
  DoubleToStr(double(Itog_OstStoimGrpNew), Формат_Сумм)
  CurrName
  CurrName
  CurrName
.endfields
 Р


 Б                                                                                 УТВЕРЖДАЮ Б

                                           _______________________________________________
                                                       Руководитель организации

                                           "____"________________20  г.


                             БАКТ ПО РЕЗУЛЬТАТАМ ПЕРЕОЦЕНКИ Б

   Комиссия в составе:
   председателя  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д ,

   членов комиссии
    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д ,

   действовала на основании приказа от "____"________________ 20   г. №_________________ ,

   произвела переоценку основных фондов и установила следующее.

   1. Результаты переоценки основных средств.

     Первоначальная стоимость основных фондов до переоценки
      Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д @@@@@@@@@@@@@

     Восстановительная стоимость основных фондов после переоценки
      Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д @@@@@@@@@@@@@

     Остаточная стоимость основных фондов до переоценки
      Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д @@@@@@@@@@@@@

     Остаточная стоимость основных фондов после переоценки
      Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д @@@@@@@@@@@@@

   2. При проведении инвентаризации основных фондов выявлены:

     а) недостачи на сумму ____________________________________________________ @@@@@@@@@

     б) излишки на сумму ______________________________________________________ @@@@@@@@@

   3. К настоящему акту прилагается следующая документация по переоценке основных фондов:

     а) __________________________________________________________________________________

     б) __________________________________________________________________________________

     в) __________________________________________________________________________________


.Fields
!  Председатель_комиссии_Должность
  Председатель_комиссии_ФИО
.EndFields
   Председатель комиссии ___________________________    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                 (подпись)                    (И.О. Фамилия)

.{While (Номер_члена_комиссии < Количество_членов_комиссии)
.begin
  Номер_члена_комиссии := Номер_члена_комиссии + 1;
end.
.fields
  if (Номер_члена_комиссии = word(1), 'Члены комиссии:', '')
!  Массив_Член_комиссии_Должность [Номер_члена_комиссии]
  Массив_Член_комиссии_ФИО       [Номер_члена_комиссии]
.endfields
   @@@@@@@@@@@@@@@       ___________________________    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                 (подпись)                    (И.О. Фамилия)

.}
.{?INTERNAL;(Количество_членов_комиссии = 0)
   Члены комиссии:       ___________________________   ___________________________________
                                 (подпись)                    (И.О. Фамилия)

                         ___________________________   ___________________________________
                                 (подпись)                    (И.О. Фамилия)

.}
.endform

!===================================================
.linkform 'AktPereoc_02' prototype is 'OsPereoc'
!===================================================
.NameInList 'Акт по результатам переоценки'
.P 70
.Group '<<Беларусь>>'
.defo portrait
.var
   Dat1   : string
   nMonth : byte
   nYear  : word
   sMonth : string

   Уровень              : word
   Itog_StoimGrp        : double
   Itog_OstStoimGrp     : double
   Itog_StoimGrpNew     : double
   Itog_OstStoimGrpNew  : double
.endvar
#CommissionLinkVar
.begin
  Уровень := 65535;
  Itog_StoimGrp       := 0;
  Itog_OstStoimGrp    := 0;
  Itog_StoimGrpNew    := 0;
  Itog_OstStoimGrpNew := 0;

  nMonth:=Month(Date_Pereoc);
  nYear:=Year(Date_Pereoc);
  case nMonth of
    0:sMonth:=' ';
    1:sMonth:='января ';
    2:sMonth:='февраля ';
    3:sMonth:='марта ';
    4:sMonth:='апреля ';
    5:sMonth:='мая ';
    6:sMonth:='июня ';
    7:sMonth:='июля ';
    8:sMonth:='августа ';
    9:sMonth:='сентября ';
    10:sMonth:='октября ';
    11:sMonth:='ноября ';
    12:sMonth:='декабря ';
  end;
  Dat1:='на 1 '+sMonth+if(nYear<>0,String(nYear),' ')+' г.';
end.
#CommissionLinkInit(OSPereoc)
.{ CheckEnter  FilterLoop
.}
.{ MainLoop
.{ CheckEnter GroupLoop
.begin
  if (GroupLevel < Уровень)
  {
    Уровень := GroupLevel;
    // GroupName
    Itog_StoimGrp       := double(Head_StoimGrp)/1000;
    Itog_OstStoimGrp    := double(Head_OstStoimGrp)/1000;
    Itog_StoimGrpNew    := double(Head_StoimGrpNew)/1000;
    Itog_OstStoimGrpNew := double(Head_OstStoimGrpNew)/1000;
  }
end.
.}
.{ CheckEnter DataLoop
.}
.{ CheckEnter ItogLoop
.}
.}
.fields
  Dat1
  Информация_о_председателе
  Список_членов_комиссии
  DoubleToStr(double(Itog_StoimGrp),    '\+2p[|-]36`666`666`666.88')
  DoubleToStr(double(Itog_StoimGrpNew), '\+2p[|-]36`666`666`666.88')
  DoubleToStr(double(Itog_OstStoimGrp), '\+2p[|-]36`666`666`666.88')
  DoubleToStr(double(Itog_OstStoimGrpNew), '\+2p[|-]36`666`666`666.88')
.endfields
 Р
                                                                              Приложение 3
                                                          к Постановлению Минэкономики РБ,
                                                   Минфина РБ, Минстатистики и анализа РБ,
                                                         Минархитектуры и строительства РБ
                                                         от 20.11.2006 г. № 199/139/185/34

                                                   УТВЕРЖДАЮ

                                                   ______________________________________
                                                              (руководитель организации)

                                                   ___________   ________________________
                                                    (подпись)      (фамилия, инициалы)

                                                   "____"________________20  г.


                                          БАКТ Б
                        Брезультатов переоценки основных средств
                           по состоянию ^ Б


   Комиссия в составе:
   председателя  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д ,

   членов комиссии
    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д ,

   действующая на основании приказа от "____"________________ 20   г. №_________________,

   произвела переоценку основных средств и установила следующее.

   1. Результаты переоценки основных средств.

     Первоначальная стоимость основных средств до переоценки
      Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д тыс. руб.

     Восстановительная стоимость основных средств после переоценки
      Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д тыс. руб.

     Остаточная стоимость основных средств до переоценки
      Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д тыс. руб.

     Остаточная стоимость основных средств после переоценки
      Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д тыс. руб.

   2. К настоящему акту прилагается следующая документация по переоценке основных средств:

   _______________________________________________________________________________________

   _______________________________________________________________________________________

.Fields
!  Председатель_комиссии_Должность
  Председатель_комиссии_ФИО
.EndFields
   Председатель комиссии ___________________________    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                 (подпись)                    (И.О. Фамилия)

.{While (Номер_члена_комиссии < Количество_членов_комиссии)
.begin
  Номер_члена_комиссии := Номер_члена_комиссии + 1;
end.
.fields
  if (Номер_члена_комиссии = word(1), 'Члены комиссии:', '')
!  Массив_Член_комиссии_Должность [Номер_члена_комиссии]
  Массив_Член_комиссии_ФИО       [Номер_члена_комиссии]
.endfields
   @@@@@@@@@@@@@@@       ___________________________    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                 (подпись)                    (И.О. Фамилия)

.}
.{?INTERNAL;(Количество_членов_комиссии = 0)
   Члены комиссии:       ___________________________   ___________________________________
                                 (подпись)                    (И.О. Фамилия)

                         ___________________________   ___________________________________
                                 (подпись)                    (И.О. Фамилия)

.}
 Р
.endform
