#include Commission.frn
.linkform 'GroupOs_04b_Bel' prototype is 'GroupOs'
.NameInList 'Форма N ОС-4б << Беларусь >>'
.group 'Выбытие ОС <<Беларусь>>'
.group 'Выбытие ОС для НМА <<Беларусь>>'
.f "OS-4b.out"
.hide
.defo landscape
.p 62
.var
  // ***** Объявление массивов *****

  Массив_ИК_Количество                        : array[1..2] of double; // Количество в конкретной ИК

  Массив_ИК_Наименование                      : array[1..2] of string; // 02 - Объект, наименование
  Массив_ИК_Инвентарный_Номер                 : array[1..2] of string; // 03 - Объект, инвентарный номер
  Массив_ИК_Фактический_срок_эксплуатации     : array[1..2] of string; // 04 - Фактический срок эксплуатации: double  SrokIspEdIzmWord (Фактический_срок_эксплуатации) : string;
  Массив_ИК_Восстановительная_стоимость       : array[1..2] of double; // 05 - Первоначальная или восстановительная стоимость,руб.
  Массив_ИК_Сумма_начисленной_амортизации     : array[1..2] of double; // 06 - Сумма начисленной амортизации (износа), руб.
  Массив_ИК_Остаточная_стоимость              : array[1..2] of double; // 07 - Остаточная стоимость, руб.
  Массив_ИК_Причина_списания                  : array[1..2] of string; // 08 - Причина списания

  Массив_ИК_КоличествоДрагМеталлов            : array[1..2] of word;   //      Количество Даг Металлов в ИК

  Массив_ДрагМеталл_Наименование              : array[1..2] of string; // 09 - наименование драгоценных материалов
  Массив_ДрагМеталл_ЕдИзм                     : array[1..2] of string; // 11 - единица измерения
  Массив_ДрагМеталл_Масса                     : array[1..2] of double; // 13 - масса

  // ***** Переменные проверки отличий общих полей *****
  Flag                                        : boolean;
  Flag_Структурное_подразделение_Поставщика   : boolean;
  Flag_Сдал                                   : boolean;

  // ***** Переменные общих полей *****
  // Выводим, только если у всех одинаковое значение
  Итог_Структурное_подразделение_Поставщика   : string;

  Итог_Сдал_nRec                              : comp;
  Итог_Сдал                                   : string;
  Итог_Сдал_Табельный_номер                   : string;

  // ***** Объявление вспомогательных переменных *****

  НомерПоПорядку                              : word;                  // 01 - Текущий номер элемента в цикле состава

  НомерИК                                     : word;
  НомерИК_Max                                 : word;                  //      Количество элементов   в цикле состава

  КолНомер_в_ИК                               : word;

  НомерДрагМеталла                            : word;                  // Текущий номер элемента в общем цикле драгметаллов

  i                                           : word;
  Maxi                                        : word;
.endvar
#CommissionLinkVar
#include funcOs.frn
end.  // funcOs.frn - вынес из файла чтобы не создавалась пустая строка перед формой
.begin

  // ***** Обнуление *****
  for (i := 1; i <= Count(Массив_ИК_Наименование); i := i + 1)
  {
    Массив_ИК_Количество                        [i] := 0;                // Количество в конкретной ИК
    Массив_ИК_Наименование                      [i] := '';               // 02 - Объект, наименование
    Массив_ИК_Инвентарный_Номер                 [i] := '';               // 03 - Объект, инвентарный номер
    Массив_ИК_Фактический_срок_эксплуатации     [i] := '';               // 04 - Фактический срок эксплуатации: double  SrokIspEdIzmWord (SrokIspNumber : double) : string;
    Массив_ИК_Восстановительная_стоимость       [i] := 0;                // 05 - Первоначальная или восстановительная стоимость, руб.
    Массив_ИК_Сумма_начисленной_амортизации     [i] := 0;                // 06 - Сумма начисленной амортизации (износа), руб.
    Массив_ИК_Остаточная_стоимость              [i] := 0;                // 07 - Остаточная стоимость, руб.
    Массив_ИК_Причина_списания                  [i] := '';               // 08 - Причина списания

    Массив_ИК_КоличествоДрагМеталлов            [i] := 0;                //      Количество Даг Металлов в ИК
  }

  for (i := 1; i <= Count(Массив_ДрагМеталл_Наименование); i := i + 1)
  {
    Массив_ДрагМеталл_Наименование              [i] := '';
    Массив_ДрагМеталл_ЕдИзм                     [i] := '';
    Массив_ДрагМеталл_Масса                     [i] := 0;
  }

  // ***** Обнуление переменных проверки отличий общих полей *****
  Flag                                        := true;

  Flag_Структурное_подразделение_Поставщика   := true;

  Flag_Сдал                                   := true;

  Итог_Структурное_подразделение_Поставщика   := '';

  Итог_Сдал_nRec                              := comp(0);
  Итог_Сдал                                   := '';
  Итог_Сдал_Табельный_номер                   := '';

  НомерПоПорядку   := 0;

  НомерИК          := 0;
  НомерИК_Max      := 0;

  НомерДрагМеталла := 0;

  i                := 0;
end.
.{
! ***** Заполнение массивов *****
#CommissionLinkInit(GroupOs)
.{ CheckEnter CycleByGroupIK
.begin
  // ***** Заполнение массивов *****
  НомерИК            := НомерИК     + 1;
  НомерИК_Max        := НомерИК_Max + 1;

  Массив_ИК_Количество                        [НомерИК] := Количество;                                             // Количество в конкретной ИК

  Массив_ИК_Наименование                      [НомерИК] := Наименование;                                           // 02 - Объект, наименование
  Массив_ИК_Инвентарный_Номер                 [НомерИК] := Инвентарный_Номер;                                      // 03 - Объект, инвентарный номер
  Массив_ИК_Фактический_срок_эксплуатации     [НомерИК] := SrokIspEdIzmWord (Фактический_срок_эксплуатации);       // 04 - Фактический срок эксплуатации
  Массив_ИК_Восстановительная_стоимость       [НомерИК] := Восстановительная_стоимость;                            // 05 - Первоначальная или восстановительная стоимость,руб.
  Массив_ИК_Сумма_начисленной_амортизации     [НомерИК] := Сумма_начисленной_амортизации;                          // 06 - Сумма начисленной амортизации (износа), руб.
  Массив_ИК_Остаточная_стоимость              [НомерИК] := Остаточная_стоимость;                                   // 07 - Остаточная стоимость, руб.
  Массив_ИК_Причина_списания                  [НомерИК] := Причина_списания;                                       // 08 - Причина списания

  Массив_ИК_КоличествоДрагМеталлов             [НомерИК] := 0;

  if (НомерИК = 1)
  {
    Итог_Структурное_подразделение_Поставщика := Структурное_подразделение_Поставщика;

    Итог_Сдал_nRec                            := Сдал_nRec;
    Итог_Сдал                                 := Сдал;
    Итог_Сдал_Табельный_номер                 := Сдал_Табельный_номер;
  }
  else
    if (НомерИК > 1)
    {
      if (Итог_Структурное_подразделение_Поставщика <> Структурное_подразделение_Поставщика)
      {
        Flag := false;
        Итог_Структурное_подразделение_Поставщика := '';
        Flag_Структурное_подразделение_Поставщика := false;
      }

      if (Итог_Сдал_nRec <> Сдал_nRec)
      {
        Flag := false;
        Flag_Сдал := false;
        Итог_Сдал := '';
        Итог_Сдал_Табельный_номер := '';
      }
    }

end.
! ***** Заполнение массива Краткая индивидуальная характеристика объекта *****
.{ CheckEnter FormGroupOsMemo
!  СтрокаХарактеристики ^
.}
! ***** Заполнение массива ДрагМеталлов *****
.{ CheckEnter FormGroupOs_NalDrag // Os_NalDrag
.begin
  НомерДрагМеталла := НомерДрагМеталла + 1;

  Массив_ИК_КоличествоДрагМеталлов [НомерИК] := Массив_ИК_КоличествоДрагМеталлов [НомерИК] + 1;

  Массив_ДрагМеталл_Наименование [НомерДрагМеталла] := ДрагМеталл_Наименование;
  Массив_ДрагМеталл_ЕдИзм        [НомерДрагМеталла] := ДрагМеталл_ЕдИзм;
  Массив_ДрагМеталл_Масса        [НомерДрагМеталла] := ДрагМеталл_Масса;
end.
.}
.}
! ***** Вывод в форму *****
.begin
  if (not Flag)
  {
    Message ('Значения в карточках для полей:'#13 +
             if (not Flag_Структурное_подразделение_Поставщика, 'Структурное подразделение поставщика,'#13, '') +
             if (not Flag_Сдал,           'МОЛ сдавшее объекты основных средств и его табельный номер,'#13, '') +
             'отличаются.', Information+CancelButton);
  }
end.
.fields
  // ***** Вывод общей для операции информации *****
  '(Метод учета: ' +Метод_Учета +')'
  Поставщик                                                                                                       // 01
  Поставщик_ОКПО                                                                                                  // 02
  Итог_Структурное_подразделение_Поставщика                                                                       // 03
  if (Longint(Дата_выбытия) <>0, DateToStr(Дата_выбытия, 'DD/MM/YYYY'), '') // Дата списания с бухгалтерского учета // 04
  Итог_Сдал_Табельный_номер                                                                                       // 05
  Итог_Сдал                                                                                                       // 06
  Номер_документа                                                                                                 // 07
  Дата_документа                                                                                                  // 08
  Руководитель_Поставщика_Должность
  Руководитель_Поставщика
.endfields
 Ш@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                                                                                                             Приложение 7
                                                                                                                                                                                                         к постановлению МФ РБ
                                                                                                                                                                                                              08.12.2003 № 168
                                                                                                                                                                                                                 Б Форма № ОС-4б Б

                                                                                                                                                                                                                 ┌────────────┐
                                                                                                                                                                                                                 │     Коды   │
                                                                                                                                                                                                                 ├────────────┤
                                                                                                                                                                                                   Форма по ОКУД │   0501035  │
                                                                                                                                                                                                                 ├────────────┤
    Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е по ОКЮЛП │@~@@@@@@@@@@│
                                                                                              наименование организации                                                                                           ├────────────┤
    Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д│            │
                                                                                              структурное подразделение                                                                                          ├────────────┤
                                                                                                                                                                            Дата списания с бухгалтерского учета │@~@@@@@@@@@@│
                                                                                                                                                                                                                 ├────────────┤
                                                                                                                                                                                                 Табельный номер │@~@@@@@@@@@@│
                                                                                                                                                                                                                 └────────────┘
   Ответственное лицо  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                                                                                           фамилия, инициалы

                                      ┌──────────────────────────┬──────────────────┐        УТВЕРЖДАЮ
                                      │      Номер документа     │ Дата составления │
                                      ├──────────────────────────┼──────────────────┤
                              Б АКТ  Б    │ @~@@@@@@@@@@@@@@@@@@@@@@ │ @~@@@@@@@@@@@@@@ │        Руководитель  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д  __________________________   Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                      └──────────────────────────┴──────────────────┘                                  должность                     подпись                              расшифровка подписи

          Б О СПИСАНИИ ГРУПП ОБЪЕКТОВ ОСНОВНЫХ СРЕДСТВ  Б                                          "_____" _______________ 20___ г.

   1. Сведения о состоянии объектов основных средств на дату списания

   ┌─────┬────────────────────────────┬───────────────┬───────────────┬────────────────┬────────────────┬────────────────┬────────────────┬───────────────────────────────────────────────────────────────────────────────────┐
   │Номер│    Наименование объекта    │  Инвентарный  │  Фактический  │ Первоначальная │      Сумма     │   Остаточная   │    Причина     │                               Драгоценные материалы                               │
   │  по │      основных средств      │     номер     │      срок     │  или восстано- │   начисленной  │   стоимость,   │    списания    ├────────────────────────┬──────────────┬───────────┬────────────┬──────────────────┤
   │ по- │                            │               │     службы    │    вительная   │   амортизации  │      руб.      │                │      наименование      │номенклатурный│  единица  │ количество │       масса      │
   │рядку│                            │               │               │ стоимость,руб. │       руб.     │                │                │                        │     номер    │ измерения │            │                  │
   ├─────┼────────────────────────────┼───────────────┼───────────────┼────────────────┼────────────────┼────────────────┼────────────────┼────────────────────────┼──────────────┼───────────┼────────────┼──────────────────┤
   │  1  │              2             │       3       │       4       │        5       │       6        │       7        │        8       │            9           │      10      │     11    │     12     │        13        │
.begin
  НомерПоПорядку   := 0;
  НомерИК          := 1;
  НомерДрагМеталла := 0;
end.
.{While (НомерИК <= НомерИК_Max)
   ├─────┼────────────────────────────┼───────────────┼───────────────┼────────────────┼────────────────┼────────────────┼────────────────┼────────────────────────┼──────────────┼───────────┼────────────┼──────────────────┤
.begin
  НомерПоПорядку := НомерПоПорядку + 1;
  КолНомер_в_ИК  := КолНомер_в_ИК  + 1;
  i := 1;
end.
.fields
  // ***** Вывод информации по карточке *****
  НомерПоПорядку // 01
  Массив_ИК_Наименование [НомерИК] // 02 - Объект, наименование

  // 03 - Объект, инвентарный номер
  Массив_ИК_Инвентарный_Номер [НомерИК]
    + if (AddNumIKToInvNum and DuplicateRecordInGroupIK and (Массив_ИК_Количество [НомерИК] > 1), ' / ' + КолНомер_в_ИК, '')

  // 04 - Фактический срок эксплуатации: double  SrokIspEdIzmWord (SrokIspNumber : double) : string;
  Массив_ИК_Фактический_срок_эксплуатации [НомерИК]

  // 05 - Первоначальная или восстановительная стоимость,руб.
  iExcelFormat.DoubleToStrFormatExcelNotNul (Массив_ИК_Восстановительная_стоимость [НомерИК]
    * if (DuplicateRecordInGroupIK, 1, Массив_ИК_Количество [НомерИК]), 2)

  // 06 - Сумма начисленной амортизации (износа), руб.
  iExcelFormat.DoubleToStrFormatExcelNotNul (Массив_ИК_Сумма_начисленной_амортизации [НомерИК]
    * if (DuplicateRecordInGroupIK, 1, Массив_ИК_Количество [НомерИК]), 2)

  // 07 - Остаточная стоимость, руб.
  iExcelFormat.DoubleToStrFormatExcelNotNul (Массив_ИК_Остаточная_стоимость [НомерИК]
    * if (DuplicateRecordInGroupIK, 1, Массив_ИК_Количество [НомерИК]), 2)

  // 08 - Причина списания
  Массив_ИК_Причина_списания [НомерИК]

  // ***** Вывод информации по драгметаллам в карточке *****
  if (Массив_ИК_КоличествоДрагМеталлов [НомерИК] <> 0, Массив_ДрагМеталл_Наименование [НомерДрагМеталла + i],'')     // 09
  if (Массив_ИК_КоличествоДрагМеталлов [НомерИК] <> 0, Массив_ДрагМеталл_ЕдИзм        [НомерДрагМеталла + i],'')     // 11
  if (Массив_ИК_КоличествоДрагМеталлов [НомерИК] <> 0, iExcelFormat.DoubleToStrFormatExcel (Массив_ДрагМеталл_Масса [НомерДрагМеталла + i], 12),'') // 13
.endfields
   │&&&&&│@@@@@@@@@@@@@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@│&&&&&&&&&&&&&.&&│&&&&&&&&&&&&&.&&│&&&&&&&&&&&&&.&&│@@@@@@@@@@@@@@@@│@@@@@@@@@@@@@@@@@@@@@@@@│              │@@@@@@@@@@@│            │&&&&&&&&&&&&&&&&&&│
.{While (i < Массив_ИК_КоличествоДрагМеталлов [НомерИК])
.begin
 i := i + 1;
end.
.fields
  // ***** Вывод информации по драгметаллам в карточке, если драгметаллов больше одного *****
  Массив_ДрагМеталл_Наименование      [НомерДрагМеталла + i]                                                        // 09
  Массив_ДрагМеталл_ЕдИзм             [НомерДрагМеталла + i]                                                        // 11
  iExcelFormat.DoubleToStrFormatExcel (Массив_ДрагМеталл_Масса [НомерДрагМеталла + i], 12)                          // 13
.endfields
   │     │                            │               │               │                │                │                │                │@@@@@@@@@@@@@@@@@@@@@@@@│              │@@@@@@@@@@@│            │&&&&&&&&&&&&&&&&&&│
.}
.begin
  if (DuplicateRecordInGroupIK)
  {
    if (КолНомер_в_ИК >= Массив_ИК_Количество [НомерИК])
    {
      НомерДрагМеталла := НомерДрагМеталла + Массив_ИК_КоличествоДрагМеталлов [НомерИК];

      НомерИК := НомерИК + 1;
      КолНомер_в_ИК := 0;
    }
  }
  else
  {
    НомерДрагМеталла := НомерДрагМеталла + Массив_ИК_КоличествоДрагМеталлов [НомерИК];

    НомерИК := НомерИК + 1;
  }
end.
.}
   └─────┴────────────────────────────┴───────────────┴───────────────┴────────────────┴────────────────┴────────────────┴────────────────┴────────────────────────┴──────────────┴───────────┴────────────┴──────────────────┘

                                                                                                                                                                                               Оборотная сторона формы № ОС-4б

   Заключение комиссии:

   В результате осмотра указанного в настоящем акте объекта основных средств с комплектацией установлено: _____________________________________________________________________________________________________________________

   ____________________________________________________________________________________________________________________________________________________________________________________________________________________________

   ____________________________________________________________________________________________________________________________________________________________________________________________________________________________

   Перечень прилагаемых документов ____________________________________________________________________________________________________________________________________________________________________________________________


.fields
  Председатель_комиссии_Должность
  Председатель_комиссии_ФИО
.endfields
   Председатель комиссии  Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д    __________________________      Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                     должность                          подпись                                 расшифровка подписи

.{While (Номер_члена_комиссии < Количество_членов_комиссии) OR (Номер_члена_комиссии < 2)
.begin
  Номер_члена_комиссии := Номер_члена_комиссии + 1;
end.
.fields
  if (Номер_члена_комиссии = word(1), 'Члены комиссии:', '')
  if (Номер_члена_комиссии <= Количество_членов_комиссии, Массив_Член_комиссии_Должность [Номер_члена_комиссии], '')
  if (Номер_члена_комиссии <= Количество_членов_комиссии, Массив_Член_комиссии_ФИО       [Номер_члена_комиссии], '')
.endfields
   @@@@@@@@@@@@@@@        Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д    __________________________      Д@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Д
                                     должность                          подпись                                 расшифровка подписи

.}

   2. Сведения о поступлении материальных ценностей от списания объектов основных средств
   ┌─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┬────────────┐
   │                                                                                            Поступило от списания                                                                                            │ Порядковый │
   ├────────────────────────────────────────────────────────────────────┬──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┬─────────────┤    номер   │
   │                              документ                              │                                                            материальные   ценности                                       │Корреспонден-│  записи в  │
   ├─────────────────────────────────────────┬──────────┬───────────────┼──────────────────────────────────────────┬────────────────┬───────────┬────────────────┬─────────────────────────────────┤  ция счетов │  карточке  │
   │                                         │          │               │                                          │ номенклатурный │  единица  │                │         стоимость, руб.         ├──────┬──────┤    учета   │
   │               наименование              │   дата   │     номер     │               наименование               │     номер      │ измерения │   количество   ├────────────────┬────────────────┤ дебет│кредит│ материалов │
   │                                         │          │               │                                          │                │           │                │     единицы    │      всего     │      │      │            │
   ├─────────────────────────────────────────┼──────────┼───────────────┼──────────────────────────────────────────┼────────────────┼───────────┼────────────────┼────────────────┼────────────────┼──────┼──────┼────────────┤
   │                    1                    │     2    │       3       │                     4                    │        5       │     6     │        7       │        8       │        9       │  10  │  11  │     12     │
   ├─────────────────────────────────────────┼──────────┼───────────────┼──────────────────────────────────────────┼────────────────┼───────────┼────────────────┼────────────────┼────────────────┼──────┼──────┼────────────┤
   │                                         │          │               │                                          │                │           │                │                │                │      │      │            │
   ├─────────────────────────────────────────┼──────────┼───────────────┼──────────────────────────────────────────┼────────────────┼───────────┼────────────────┼────────────────┼────────────────┼──────┼──────┼────────────┤
   │                                         │          │               │                                          │                │           │                │                │                │      │      │            │
   ├─────────────────────────────────────────┼──────────┼───────────────┼──────────────────────────────────────────┼────────────────┼───────────┼────────────────┼────────────────┼────────────────┼──────┼──────┼────────────┤
   │                                         │          │               │                                          │                │           │                │                │                │      │      │            │
   └─────────────────────────────────────────┴──────────┴───────────────┴──────────────────────────────────────────┴────────────────┴───────────┴────────────────┴────────────────┴────────────────┴──────┴──────┴────────────┘

   Результаты списания ________________________________________________________________________________________________________________________________________________________________________________________________________

   Выручка от реализации _________________________________________________________________________________________________________________________________________________________________________________________________ руб.

   В инвентарной карточке учета объектов основных средств списание отмечено.

.fields
  Главный_бухгалтер_Поставщика
.endfields
    БГлавный бухгалтер Б ____________________      Е@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ Е
                             подпись                          расшифровка подписи
 Ш
.} // Конец формы
.endform
