//------------------------------------------------------------------------------
//                                                      (c) корпорация Галактика
// Галактика 6.00 - Векселя и кредиты
// Печать реестра вексельных соглашений
//------------------------------------------------------------------------------

#doc
Печать реестра вексельных соглашений
#end

.FORM SoglVeksReestr
.HIDE

! ==============================================================================
.fields

  fltStDate
  fltEndDate
  fltOtv
  fltStat
  fltCurrency

  fltExClassName
!{
  fltExClassSeg
!}
! ------------------------------------------------------------------------------
!{
  SoglVeksNRec  : comp
  SoglVeksCStat : comp      // Ссылка на KatKlass (статус)
  SoglVeksCOrg  : comp      // Ссылка на каталог организаций
  SoglVeksCOtv  : comp      // Ссылка на каталог МОЛ
  SoglVeksCVal  : comp      // Ссылка на каталог валют

  Номер_Внешний
  Номер_Внутренний
  Дата_Составления
  Статус_Соглашения
  Наименование_Организации
  Ответственный
  Сумма_Плановая_НДЕ
  Сумма_Плановая_Вал
  Сумма_Фактическая_НДЕ
  Сумма_Фактическая_Вал
  Валюта

  Примечание_1
  Примечание_2
  Примечание_3
!}
.endfields
! ==============================================================================

.if FilterIsSet
.if DateFilterIsSet
^ ^
.end
^ ^ ^
.if KlassFilterIsSet
^
.{
  ^
.}
.end
.end

.{
  ^ ^ ^ ^ ^
  ^ ^ ^ ^ ^ ^ ^ ^ ^ ^ ^
  ^ ^ ^
.}
.ENDFORM

.LINKFORM SoglVeksReestr_00 PROTOTYPE IS SoglVeksReestr
.nameinlist 'Реестр вексельных соглашений'
.p 80
.fields
  CommonFormHeader

  fltStDate
  fltEndDate
  fltOtv
  fltStat
  fltCurrency
  fltExClassName
  fltExClassSeg
.endfields
^

                         Реестр вексельных соглашений

.if FilterIsSet
.if DateFilterIsSet
                          с @@@@@@@@@@ по @@@@@@@@@@

.end
Ответственный ^
Статус        ^
Валюта        ^

.if KlassFilterIsSet
Наименование классификации ^
.{
Значение классификатора ^
.}

.end
.end
.fields
  Номер_Внешний
  Номер_Внутренний
  Дата_Составления

  if( SoglVeksCVal = comp(0), Сумма_Плановая_НДЕ,    Сумма_Плановая_Вал)
  if( SoglVeksCVal = comp(0), Сумма_Фактическая_НДЕ, Сумма_Фактическая_Вал)
  Валюта
.endfields
.{
.[h
─────────────┬───────────┬──────────┬─────────────────┬─────────────────┬──────
  Входящий   │Внутренний │   Дата   │ Плановая сумма  │Фактическая сумма│Валюта
   номер     │  номер    │          │                 │                 │
─────────────┴───────────┴──────────┴─────────────────┴─────────────────┴──────
.]h
@@@@@@@@@@@@@ @@@@@@@@@@@ @@@@@@@@@@ &'&&&&&&&&&&&&.&& &'&&&&&&&&&&&&.&& @@@@@@
.}
.ENDFORM